MODULE ApplicationPosition;

REQUIRE ApplicationInterview;

NAMESPACE HumanResources;

countApplication (Position p) = GROUP SUM 1 IF position(Application a) = p AND [FILTER applications.a](a);
countApplication (ApplicationStatus s) = GROUP SUM 1 IF status(Application a) = s AND [FILTER applications.a](a);
countApplication (Position p, ApplicationStatus s) = GROUP SUM 1 IF position(Application a) = p AND status(a) = s AND [FILTER applications.a](a);

EXTEND FORM applications
    OBJECTS pas = ApplicationStatus
    
    OBJECTS p = Position
    PROPERTIES(p) READONLY name
    PROPERTIES READONLY countApplication(p, pas) COLUMNS (pas) HEADER name(pas)
    FILTERS countApplication(p)
    
    OBJECTS pa = Application
    PROPERTIES(pa) READONLY
                        dateTime, subject, imagedNameStatus BACKGROUND colorStatus(pa),
                        firstName, lastName, middleName,
                        phone, email,
                        nameRecruiter, dateTimeLastInterview, interviewers, availability, tags,
                        nameDepartment,
                        expectedSalary, proposedSalary
    PROPERTIES(pa) NEWSESSION EDIT
    
    FILTERS position(pa) = p,
            [FILTER applications.a](pa)
    
    FILTERGROUP positionStatus
        FILTER 'By status' status(pa) = pas DEFAULT
;

DESIGN applications {
    tabbedPane {
        NEW position {
            caption = 'By position';
            NEW positionPane {
                fill = 1;
                horizontal = TRUE;
                MOVE BOX(p);
                MOVE BOX(pa) {
                    PROPERTY(interviewers(a)) { valueHeight = 18; }
                }
            }
        }
    }
    
}