MODULE BarCodeImport;

REQUIRE BarCode;

NAMESPACE MasterData;

barCodeExport 'Экспорт штрихкодов' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF BarCode b AND b IS BarCode) THEN {
            NEW b = BarCode {
                id(b) <- '123';
                item(b) <- GROUP LAST Item i ORDER dataName(i) IF i IS Item;
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код номенклатуры' = idItem(BarCode b), 'Штрихкод' = id(b) TO f;        
        open(f());
    }
}

barCodeImport 'Импорт штрихкодов' () {
    LOCAL id = STRING (INTEGER);
    LOCAL idItem = ISTRING[50] (INTEGER);

    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, id = B;
            
            FOR [GROUP SUM 1 BY idItem(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'Отсутствует номенклатура в базе данных с кодом ' + num + '. Импорт отменен';
                RETURN;
            }
            
            FOR imported(INTEGER i) DO NEW b = BarCode {
                id(b) <- id(i);
                item(b) <- item(idItem(i));
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES barCodeExport(), barCodeImport()
;

DESIGN migrationData {
    commons {
        NEW barCode {
            type = CONTAINERH;
            caption = 'Штрихкоды';
            MOVE PROPERTY(barCodeImport());
            MOVE PROPERTY(barCodeExport());
        }
    }
}

