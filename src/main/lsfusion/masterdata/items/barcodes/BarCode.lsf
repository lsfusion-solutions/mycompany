MODULE BarCode;

REQUIRE MasterData, Item, Product;

NAMESPACE MasterData;

CLASS BarCode 'Barcode';

item '{Item}' = DATA Item (BarCode) NONULL DELETE;
idItem 'Item code' (BarCode b) = id(item(b)) IN id;
nameItem '{Item}' (BarCode b) = name(item(b)) IN id;

multiple = ABSTRACT BOOLEAN (BarCode) MATERIALIZED;

@defineID(barCode, 'Barcodes', '', 13);
itemBarCode (STRING[50] id) = item(barCode(id));

singleBarCode (STRING[50] id) = GROUP MAX BarCode b IF NOT multiple(b) BY id(b);
itemSingleBarCode (STRING[50] id) = OVERRIDE item(singleBarCode(id)), item(id);

dataDefaultBarCode = DATA BarCode (Item);
calcDefaultBarCode (Item i) = GROUP LAST BarCode b ORDER DESC b WHERE item(b) = i;

defaultBarCode (Item i) = OVERRIDE dataDefaultBarCode(i), calcDefaultBarCode(i);
default 'Default' (BarCode b) = defaultBarCode(item(b)) = b CHARWIDTH 10;

barCode (Item i) = defaultBarCode(i);

idBarCode 'Barcode' (Item i) = id(barCode(i)) MATERIALIZED INDEXED;

changeDefault (BarCode b) {
    INPUT c = BOOLEAN DO {
        IF c THEN
            defaultBarCode(Item i) <- b WHERE i = item(b);
    }
}

setBarCode(Item i) {
    INPUT id = STRING DO {
        IF NOT barCode(i) THEN NEW b = BarCode {
            item(b) <- i;
        }
        id(BarCode b) <- id WHERE b = barCode(i); 
    }
}

EXTEND FORM item
    PROPERTIES(i) idBarCode AFTER canonicalNameCategory(i) ON CHANGE setBarCode(i)
    
    OBJECTS b = BarCode
    PROPERTIES(b) id, default ON CHANGE changeDefault(b)
    PROPERTIES(b) NEW, DELETE
    FILTERS item(b) = i
;

DESIGN item {
    column1 {
        MOVE PROPERTY(idBarCode(i));
    }
    tabs {
        MOVE BOX(b) { caption = 'Barcodes'; }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLYIF isReadonly() idBarCode AFTER name(i)
;

EXTEND FORM products
    PROPERTIES(p) idBarCode AFTER name(p)
;

consumedBarCode = DATA LOCAL BOOLEAN ();
barCode 'Barcode' = DATA LOCAL STRING () CHANGEKEY 'F4' EVENTID 'SCANNER';

doNotActivateQuantityAfterScanning 'Do not activate quantity after scanning' = DATA BOOLEAN();
EXTEND FORM options PROPERTIES() doNotActivateQuantityAfterScanning;
DESIGN options { items { MOVE PROPERTY(doNotActivateQuantityAfterScanning()); } }

META defineBarCodeSearch(doc, docLine, let, line, quantity)
    processBarCode ABSTRACT LIST (###doc, docLine, STRING);
    scanBarCode (###doc d, docLine l) {
        INPUT id = STRING DO {
            IF itemSingleBarCode(id) THEN {
                set###quantity(d, itemSingleBarCode(id), quantity(d, itemSingleBarCode(id)) (+) 1);
                SEEK ##doc.line = last##docLine(d, itemSingleBarCode(id));
                IF NOT doNotActivateQuantityAfterScanning() THEN
                    ACTIVATE PROPERTY ##doc.quantity(line); 
            } ELSE {
                consumedBarCode() <- NULL;
                processBarCode(d, l, id);
                IF NOT consumedBarCode() THEN
                    MESSAGE 'Barcode not found' + ' (' + id + ')';
            }
        }
    }
    
    EXTEND FORM ##doc
        PROPERTIES READONLYIF readonly(##let) barcode###line = barCode() DRAW line TOOLBAR ON CHANGE scanBarCode(##let, line)
    ;
END

META defineBarCodeSearch(doc, let, line, quantity)
    @defineBarCodeSearch(doc, ###doc##Line, let, line, quantity);
END

META defineBarCodeSeek(form, obj, gobj)
    scanBarCode###form () {
        INPUT id = STRING DO {
            IF itemBarCode(id) THEN
                SEEK form.obj = itemBarCode(id);
            ELSE
                MESSAGE 'Barcode not found' + ' (' + id + ')';
        }
    }
    
    EXTEND FORM form
        PROPERTIES() barCode DRAW gobj TOOLBAR ON CHANGE scanBarCode###form()
    ;
END

META defineBarCodeSeek(form, obj)
    @defineBarCodeSeek(form, obj, obj);
END

// add barcode item form
changeBarCode (Item i) {
    INPUT s = STRING DO {
        IF s THEN {
            IF barCode(s) THEN
                MESSAGE 'The barcode is already in the database.';
            ELSE NEW b = BarCode {
                item(b) <- i;
                id(b) <- s;
            }
        } 
    }
}

EXTEND FORM item
    PROPERTIES() barCode DRAW b TOOLBAR ON CHANGE changeBarCode(i)
;

// auto create
dataAutoCreateBarCode 'Automatically generate barcode' = DATA BOOLEAN (Category);
autoCreateBarCode (Category c) = GROUP LAST dataAutoCreateBarCode(Category cp) ORDER level(c, cp);

EXTEND FORM category
    PROPERTIES(c) dataAutoCreateBarCode
;

DESIGN category {
    params {
        MOVE PROPERTY(dataAutoCreateBarCode(c));
    }
}

WHEN SET(Item i IS Item) AND autoCreateBarCode(category(i)) AND NOT defaultBarCode(i) DO NEW b = BarCode {
    item(b) <- i;
}