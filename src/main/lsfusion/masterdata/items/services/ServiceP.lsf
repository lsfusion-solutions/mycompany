MODULE ServiceP;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS Service 'Service' : Item;

newService 'Service' (Category c) {
    NEWSESSION {
        NEW s = Service {
            category(s) <- c;
            DIALOG item OBJECTS i = s DOCKED DO seek(s);
        }
   }
} IMAGE 'add.png';

copy 'Copy' (Service s) {
    NEWSESSION {
        NEW ns = Service {
            copy(ns, s);
            SHOW item OBJECTS i = ns;
        }
    }
}

EXTEND FORM item 
    PROPERTIES copyService = copy(i)
;
DESIGN item {
    secondaryActions {
        MOVE PROPERTY(copyService) { valueClass = 'btn-secondary'; }
    }
}

EXTEND FORM items
    PROPERTIES newService(c) DRAW i TOOLBAR 
;


