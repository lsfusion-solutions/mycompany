MODULE Product;

REQUIRE MasterData, Item;

NAMESPACE MasterData;

CLASS Product 'Товар' : Item;
weight 'Вес единицы, кг' = DATA NUMERIC[11,3] (Product);
volume 'Объем единицы, м3' = DATA NUMERIC[11,3] (Product);

copyProduct 'Копировать' (Product p) {
    NEWSESSION {
        NEW np = Product {
            weight(np) <- weight(p);
            volume(np) <- volume(p);
            copy(np, p);
            SHOW item OBJECTS i = np;
        }
    }
}

EXTEND FORM item
    PROPERTIES(i) weight, volume
;

DESIGN item {
    tabs {
        NEW product {
            caption = 'Склад';
            NEW box1 {
                MOVE PROPERTY(weight(i)) { charWidth = 5; };
                MOVE PROPERTY(volume(i)) { charWidth = 5; };
            }  
        }
    }
}

newProduct 'Товар' (Category c) {
    NEWSESSION {
        NEW p = Product {
            category(p) <- c;
            SHOW item OBJECTS i = p;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM items
    PROPERTIES newProduct(c) DRAW i TOOLBAR, copyProduct(i) DRAW i TOOLBAR 
;

// dialog product
FORM products 'Товары'
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)

    OBJECTS p = Product
    PROPERTIES(p) READONLY name, id, nameCategory, nameUom
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    FILTERS level(category(p), c)
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F8' DEFAULT 
        FILTER 'Неактивный' archived(p) 'F7'
        
    LIST Product OBJECT p
;

DESIGN products {
    OBJECTS {
        NEW pane {
            type = SPLITH;
            fill = 1;
            NEW categories {
                fill = 1;
                MOVE BOX(TREE categories);
            }
            MOVE BOX(p) {
                fill = 2; 
                GRID(p) { defaultComponent = TRUE; }
            }
        }
    }
}