MODULE ItemVariant;

REQUIRE ItemAttribute, BarCode, Product, ServiceP;

NAMESPACE MasterData;

variantOf 'Основная номенклатура' = DATA Item (Item) INDEXED;
nameVariantOf 'Основная номенклатура' (Item i) = name(variantOf(i));

primary (Item i) = OVERRIDE variantOf(i), (i AS Item) MATERIALIZED INDEXED;

copy(Item n, Item o) + {
     variantOf(n) <- variantOf(o);
}

readonly (Item i) += TRUE IF variantOf(i);
readonly (Item i, ItemAttribute a) += TRUE IF attribute(variantOf(i), a);

WHEN LOCAL CHANGED(attribute(variantOf(Item i), ItemAttribute a)) DO {
     attribute(i, a) <- attribute(variantOf(i), a);
}

WHEN LOCAL CHANGED(dataName(variantOf(Item i))) DO {
    dataName(i) <- dataName(variantOf(i));
}

WHEN LOCAL CHANGED(category(variantOf(Item i))) DO {
    category(i) <- category(variantOf(i));
}

WHEN LOCAL CHANGED(uom(variantOf(Item i))) DO {
    uom(i) <- uom(variantOf(i));
}

WHEN LOCAL CHANGED(archived(variantOf(Item i))) DO {
    archived(i) <- archived(variantOf(i));
}

EXTEND FORM item
    PROPERTIES(i) nameVariantOf
;

addVariant 'Разновидность' (Item ii) {
    NEWSESSION {
        IF ii IS Product THEN NEW p = Product {
                variantOf(p) <- ii;
                SHOW item OBJECTS i = p;
            }
        ELSE NEW s = Service {
            variantOf(s) <- ii;
            SHOW item OBJECTS i = s;
        }
    }
}IMAGE 'add.png';

EXTEND FORM item 
    OBJECTS ii = Item
    PROPERTIES(ii) name READONLY, idBarCode ON CHANGE setBarCode(ii), id
    PROPERTIES(ii) NEWSESSION EDIT, DELETE
    PROPERTIES addVariant(i) DRAW ii TOOLBAR 
    FILTERS variantOf(ii) = i
;

EXTEND FORM items
    FILTERGROUP variants
        FILTER 'Основные' NOT variantOf(i) 'F10'
        FILTER 'Разновидности' variantOf(i) 'F9'
;

DESIGN item {
    header {
        MOVE PROPERTY(nameVariantOf(i)) AFTER PROPERTY(name(i));
    }
    tabs {
        NEW variants {
            caption = 'Разновидности';
            MOVE BOX(ii);
        }
    }
}
