MODULE ItemPrint;

REQUIRE ItemPrintType, ItemAttribute, BarCode;

NAMESPACE MasterData;

addressLinesCompany (PrintItemType t) = addressLines(company(t));
addressCsvCompany (PrintItemType t) = addressCsv(company(t));
phoneCompany (PrintItemType t) = phone(company(t));
emailCompany (PrintItemType t) = email(company(t));
websiteCompany (PrintItemType t) = website(company(t));

count 'Qty' = DATA LOCAL INTEGER (Item);
countItem (PrintItemType t) = GROUP SUM 1 IF count(Item i) AND set(t, i);

FORM printItem '{Label}'
    OBJECTS pt = PrintItemType PANEL
    PROPERTIES(pt) name, nameCompany, addressLinesCompany, addressCsvCompany, phoneCompany, emailCompany, websiteCompany
    REPORT OVERRIDE file(pt), file(RAWFILE(template(pt)), 'path')

    OBJECTS i = Item
    PROPERTIES(i) count, id, name, nameUom, idBarCode
    ORDERS name(i)
    FILTERS count(i), set(pt, i)
;

@defineAttributeForm(printItem, i);

print (PrintItemType pt) {
    CASE EXCLUSIVE
        WHEN format(pt) = TemplateFormat.pdf THEN
            PRINT printItem OBJECTS pt = pt PDF;
        WHEN format(pt) = TemplateFormat.docx THEN
            PRINT printItem OBJECTS pt = pt DOCX;
        WHEN format(pt) = TemplateFormat.xlsx THEN
            PRINT printItem OBJECTS pt = pt XLSX;
        WHEN format(pt) = TemplateFormat.rtf THEN
            PRINT printItem OBJECTS pt = pt RTF;
        WHEN format(pt) = TemplateFormat.html THEN
            PRINT printItem OBJECTS pt = pt HTML;
        WHEN format(pt) = TemplateFormat.noPreview THEN
            PRINT printItem OBJECTS pt = pt NOPREVIEW;
        ELSE
            PRINT printItem OBJECTS pt = pt;
}

FORM printItemParams 'Print options for labels'
    OBJECTS t = PrintItemType
    PROPERTIES(t) READONLY name
    FILTERS countItem(t)
    
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id, nameCategory
    PROPERTIES(i) count
    ORDERS name(i)
    FILTERS set(t, i), active(i)

    FILTERGROUP print
        FILTER 'To print' count(i) DEFAULT 
;

DESIGN printItemParams {
    OBJECTS {
        NEW pane {
            fill = 1;
            MOVE BOX(t);
            MOVE BOX(i) { 
                fill = 4; 
            }
        }
    }
}

printItem () {
    DIALOG printItemParams OBJECTS t INPUT DOCKED DO {
        print(t);
    }
}

print '{Label}' (Item i) {
    count(Item ii) <- 1 IF i = ii;
    printItem();
}

EXTEND FORM item
    PROPERTIES(i) print SHOWIF countPrintType(i)
;

DESIGN item {
    secondaryActions {
        MOVE PROPERTY(print(i));
    } 
}