MODULE ItemPrint;

REQUIRE ItemPrintType, ItemAttribute, BarCode;

NAMESPACE MasterData;

addressCompany (PrintItemType t) = linesAddress(company(t));
phoneCompany (PrintItemType t) = phone(company(t));
emailCompany (PrintItemType t) = email(company(t));
websiteCompany (PrintItemType t) = website(company(t));

count 'Кол-во' = DATA LOCAL INTEGER (Item);

FORM printItem 'Этикетка'
    OBJECTS pt = PrintItemType PANEL
    PROPERTIES(pt) name, nameCompany, addressCompany, phoneCompany, emailCompany, websiteCompany
    REPORT OVERRIDE file(pt), setExtension(RAWFILE(template(pt)), 'path')

    OBJECTS i = Item
    PROPERTIES(i) count, id, name, nameUom, idBarCode
    ORDERS name(i)
    FILTERS count(i), set(pt, i)
;

@defineAttributeForm(printItem, i);

print (PrintItemType pt) {
    CASE EXCLUSIVE
        WHEN format(pt) = TemplateFormat.pdf THEN
            PRINT printItem OBJECTS pt = pt PDF;
        WHEN format(pt) = TemplateFormat.docx THEN
            PRINT printItem OBJECTS pt = pt DOCX;
        WHEN format(pt) = TemplateFormat.xlsx THEN
            PRINT printItem OBJECTS pt = pt XLSX;
        WHEN format(pt) = TemplateFormat.rtf THEN
            PRINT printItem OBJECTS pt = pt RTF;
        WHEN format(pt) = TemplateFormat.html THEN
            PRINT printItem OBJECTS pt = pt HTML;
        WHEN format(pt) = TemplateFormat.noPreview THEN
            PRINT printItem OBJECTS pt = pt NOPREVIEW;
        ELSE
            PRINT printItem OBJECTS pt = pt;
}

FORM printItemParams 'Параметры печати этикеток'
    OBJECTS t = PrintItemType
    PROPERTIES(t) READONLY name
    FILTERS GROUP SUM 1 IF count(Item i) AND set(t, i)
    
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id, nameCategory
    PROPERTIES(i) count
    ORDERS name(i)
    FILTERS set(t, i), active(i)

    FILTERGROUP print
        FILTER 'На печать' count(i) DEFAULT 
;

DESIGN printItemParams {
    OBJECTS {
        NEW pane {
            fill = 1;
            type = SPLITV;
            MOVE BOX(t);
            MOVE BOX(i) { 
                fill = 4; 
            }
        }
    }
}

printItem () {
    DIALOG printItemParams OBJECTS t INPUT DOCKED DO {
        print(t);
    }
}

print 'Этикетка' (Item i) {
    count(Item ii) <- 1 IF i = ii;
    printItem();
}

EXTEND FORM item
    PROPERTIES(i) print SHOWIF countPrintType(i)
;

DESIGN item {
    secondaryActions {
        MOVE PROPERTY(print(i));
    } 
}