MODULE ItemPrintType;

REQUIRE Item, Company, DocTemplate;

NAMESPACE MasterData;

CLASS PrintItemType 'Label template';

name '{Name}' = DATA STRING[50] (PrintItemType);

company = DATA Company (PrintItemType);
nameCompany 'Company' (PrintItemType t) = name(company(t)); 

WHEN LOCAL SET(PrintItemType t IS PrintItemType) AND NOT CHANGED(company(t)) DO company(t) <- defaultCompany();

template 'Template' = DATA STRING (PrintItemType) CHARWIDTH 50;

file = DATA FILE (PrintItemType);
hasFile (PrintItemType t) = TRUE IF file(t);

hasTemplate (PrintItemType t) = template(t) OR hasFile(t);

openFile 'Open' (PrintItemType t) { 
    IF hasFile(t) THEN {
        open(file(t));
    } ELSE IF template(t) THEN {
        readResource(template(t));
        open(resourceFile());
    } 
}
loadFile 'Upload' (PrintItemType t) { INPUT = file(t) CHANGE; }
dropFile 'Reset' (PrintItemType t) { file(t) <- NULL; } 

format = DATA TemplateFormat (PrintItemType);
nameFormat 'Format' (PrintItemType t) = name(format(t));

// category
set 'Incl.' = DATA BOOLEAN (PrintItemType, Category);

countCategory (PrintItemType t) = GROUP SUM 1 IF set(t, Category c) MATERIALIZED;
set 'Incl.' (PrintItemType t, Item i) = (GROUP LAST set(t, Category pc) ORDER level(category(i), pc)) OR (i IS Item AND t IS PrintItemType AND NOT countCategory(t));

countPrintType 'Number of templates' (Item i) = GROUP SUM 1 IF set(PrintItemType pt, i); 

FORM printItemType 'Label template'
    OBJECTS t = PrintItemType PANEL
    PROPERTIES(t) name, nameCompany, 
                  template SHOWIF NOT hasFile(t), openFile, 
                  loadFile, dropFile SHOWIF hasFile(t), 
                  nameFormat
    
    TREE categories cat = Category PARENT parent(cat) 
    PROPERTIES name(cat) READONLY, set(t, cat) BACKGROUND (GROUP SUM 1 IF set(t, Category cc) AND level(cc, cat))
    ORDERS name(cat)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(cat) DEFAULT
    
    EDIT PrintItemType OBJECT t
;

DESIGN printItemType {
    OBJECTS {
        NEW header {
            NEW general {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(name(t));
                MOVE PROPERTY(nameCompany(t));
            }
            NEW format {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(template(t));
                MOVE PROPERTY(openFile(t));
                MOVE PROPERTY(loadFile(t));
                MOVE PROPERTY(dropFile(t));
                MOVE PROPERTY(nameFormat(t));
            }
        }
        MOVE BOX(TREE categories);
    }
}

FORM printItemTypes 'Label templates'
    OBJECTS t = PrintItemType
    PROPERTIES READONLY name(t)
    
    LIST PrintItemType OBJECT t
;

EXTEND FORM options
    OBJECTS pp = PrintItemType
    
    PROPERTIES READONLY name(pp)
    PROPERTIES(pp) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(pp);
    }
}

countPrintItemTypes 'Number of templates' () = GROUP SUM 1 IF r IS PrintItemType; 