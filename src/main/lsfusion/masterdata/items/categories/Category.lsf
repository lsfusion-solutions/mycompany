MODULE Category;

REQUIRE Hierarchy, MetaNumerator;

NAMESPACE MasterData;

CLASS Category 'Category';

@defineID(category, 'Categories', 'CT', 6);

name '{Name}' = DATA ISTRING[50] (Category) IN id NONULL;

archived 'Archived' = DATA BOOLEAN (Category);
active '{Active}' (Category c) = NOT archived(c);

// hierarchy
parent = DATA Category (Category) INDEXED;
idParent 'Parent ID' (Category c) = id(parent(c));
nameParent 'Parent' (Category c) = name(parent(c));

rootCategory() = GROUP MIN Category c AS Category IF NOT parent(c);

CONSTRAINT [GROUP SUM 1 IF Category l IS Category AND NOT parent(l)]() > 1
    MESSAGE 'Parent category is not specified';

CONSTRAINT DROPPED(Category c IS Category) AND PREV(parent(Category child) = c)
        MESSAGE 'It is forbidden to delete categories that are referenced by other categories';

level 'Level' (Category child, Category parent) =
   RECURSION 1l IF child IS Category AND parent = child
        STEP 2l IF parent = parent($parent) MATERIALIZED;

canonicalName 'Canonical name' (Category c) =
   GROUP CONCAT name(Category parent), ' / ' ORDER DESC level(c, parent) CHARWIDTH 50 IN id;
canonicalNameParent 'Parent' (Category c) = canonicalName(parent(c));

levelRoot '{hierarchy.level}' (Category child, Category parent) = PARTITION SUM 1 IF level(child, parent)
                                                                  ORDER DESC level(child, parent)
                                                                  BY child MATERIALIZED;
categoryRoot (Category child, level) = GROUP NAGGR Category parent BY levelRoot(child, parent);  

category1 (Category child) = categoryRoot(child, 1) MATERIALIZED;    
nameCategory1 'Category 1' (Category child) = name(category1(child));

category2 (Category child) = categoryRoot(child, 2) MATERIALIZED;
nameCategory2 'Category 2' (Category child) = name(category2(child));
    
category3 (Category child) = categoryRoot(child, 3) MATERIALIZED;
nameCategory3 'Category 3' (Category child) = name(category3(child));
    
category4 (Category child) = categoryRoot(child, 4) MATERIALIZED; 
nameCategory4 'Category 4' (Category child) = name(category4(child));
   
category5 (Category child) = categoryRoot(child, 5) MATERIALIZED; 
nameCategory5 'Category 5' (Category child) = name(category5(child));
   
category6 (Category child) = categoryRoot(child, 6) MATERIALIZED;
nameCategory6 'Category 6' (Category child) = name(category6(child));    
        
FORM category 'Category'
   OBJECTS c = Category PANEL
   PROPERTIES(c) name, id, archived, canonicalNameParent
  
   EDIT Category OBJECT c
;

DESIGN category {
    OBJECTS {
        NEW pane {
            NEW id {
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(name(c));
                MOVE PROPERTY(id(c));
                MOVE PROPERTY(archived(c));
            }
            MOVE PROPERTY(canonicalNameParent(c)) { alignment = STRETCH; notNull = TRUE; };
            NEW params {
                alignment = STRETCH;
            }
        }
        NEW tabs {
            fill = 1;
            tabbed = TRUE;
            NEW default {
                caption = 'Default values';
            }
        }
    }
}

newCategory 'Category' (Category cc) {
    NEWSESSION {
        NEW cat = Category {
            parent(cat) <- cc;
            DIALOG category OBJECTS c = cat DOCKED DO seek(cat);
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM categories 'Categories'
    OBJECTS c = Category
    PROPERTIES(c) READONLY name, id, canonicalNameParent
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
    FILTERGROUP active
        FILTER '{Active}' active(c) 'F8' DEFAULT
        FILTER 'Archived' archived(c) 'F7'
    
    TREE categories cc = Category PARENT parent(cc)
    PROPERTIES READONLY name(cc)
    PROPERTIES(cc) NEWSESSION EDIT, DELETE
    PROPERTIES(cc) newCategory DRAW cc TOOLBAR 
    ORDERS name(cc)
    FILTERGROUP activeC
        FILTER '{Active}' active(cc) 'F8' DEFAULT
        FILTER 'Archived' archived(cc) 'F7'
    
    LIST Category OBJECT c
    
    EVENTS ON CHANGE cc { SEEK categories.c = cc; }
;

DESIGN categories {
    OBJECTS {
        fill = 1;
        tabbed = TRUE;
        NEW list {
            caption = 'List';
            MOVE BOX(c);
        }
        NEW tree {
            caption = 'Tree';
            MOVE BOX(TREE categories);
        }
    }
}

NAVIGATOR {
    masterData {
        NEW categories;
    }
}