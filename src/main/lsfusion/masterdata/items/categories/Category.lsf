MODULE Category;

REQUIRE Hierarchy, MetaNumerator;

NAMESPACE MasterData;

CLASS Category 'Категория';

@defineID(category, 'Категории', 'КТ', 4);

name 'Наименование' = DATA ISTRING[50] (Category) IN id NONULL;

// hierarchy
parent = DATA Category (Category) INDEXED;
idParent 'Код родителя' (Category c) = id(parent(c));
nameParent 'Родитель' (Category c) = name(parent(c));

CONSTRAINT DROPPED(Category c IS Category) AND PREV(parent(Category child) = c)
        MESSAGE 'Запрещено удалять категории, на которые ссылаются другие категории';

level 'Уровень' (Category child, Category parent) =
   RECURSION 1l IF child IS Category AND parent = child
        STEP 2l IF parent = parent($parent) MATERIALIZED;

canonicalName 'Каноническое имя' (Category c) =
   GROUP CONCAT name(Category parent), ' / ' ORDER DESC level(c, parent) CHARWIDTH 50 IN id;
canonicalNameParent 'Родитель' (Category c) = canonicalName(parent(c));

levelRoot '{hierarchy.level}' (Category child, Category parent) = PARTITION SUM 1 IF level(child, parent)
                                                                  ORDER DESC level(child, parent)
                                                                  BY child MATERIALIZED;
categoryRoot (Category child, level) = GROUP NAGGR Category parent BY levelRoot(child, parent);  

category1 (Category child) = categoryRoot(child, 1) MATERIALIZED;    
nameCategory1 'Группа 1' (Category child) = name(category1(child));

category2 (Category child) = categoryRoot(child, 2) MATERIALIZED;
nameCategory2 'Группа 2' (Category child) = name(category2(child));
    
category3 (Category child) = categoryRoot(child, 3) MATERIALIZED;
nameCategory3 'Группа 3' (Category child) = name(category3(child));
    
category4 (Category child) = categoryRoot(child, 4) MATERIALIZED; 
nameCategory4 'Группа 4' (Category child) = name(category4(child));
   
category5 (Category child) = categoryRoot(child, 5) MATERIALIZED; 
nameCategory5 'Группа 5' (Category child) = name(category5(child));
   
category6 (Category child) = categoryRoot(child, 6) MATERIALIZED;
nameCategory6 'Группа 6' (Category child) = name(category6(child));    
        
FORM category 'Категория'
   OBJECTS c = Category PANEL
   PROPERTIES(c) name, id, canonicalNameParent
  
   EDIT Category OBJECT c
;

DESIGN category {
    OBJECTS {
        NEW pane {
            NEW id {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(name(c));
                MOVE PROPERTY(id(c));
            }
            MOVE PROPERTY(canonicalNameParent(c)) { alignment = STRETCH; };
            NEW params {
                alignment = STRETCH;
            }
        }
        NEW tabs {
            fill = 1;
            type = TABBED;
            NEW default {
                caption = 'Значения по умолчанию';
            }
        }
    }
}

newCategory 'Категория' (Category cc) {
    NEWSESSION {
        NEW cat = Category {
            parent(cat) <- cc;
            SHOW category OBJECTS c = cat;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM categories 'Категории'
    OBJECTS c = Category
    PROPERTIES(c) READONLY name, id, canonicalNameParent
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
    
    TREE categories cc = Category PARENT parent(cc)
    PROPERTIES READONLY name(cc)
    PROPERTIES(cc) NEWSESSION EDIT, DELETE
    PROPERTIES(cc) newCategory DRAW cc TOOLBAR 
    ORDERS name(cc)
    
    LIST Category OBJECT c
    
    EVENTS ON CHANGE cc { SEEK categories.c = cc; }
;

DESIGN categories {
    OBJECTS {
        fill = 1;
        type = TABBED;
        NEW list {
            caption = 'Список';
            MOVE BOX(c);
        }
        NEW tree {
            caption = 'Дерево';
            MOVE BOX(TREE categories);
        }
    }
}

NAVIGATOR {
    masterData {
        NEW categories;
    }
}