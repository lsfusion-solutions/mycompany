MODULE CategoryImport;

REQUIRE Category, Item;

NAMESPACE MasterData;

categoryExport 'Export categories' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Category c AND c IS Category) THEN {
            NEW c = Category {
                id(c) <- '123';
                name(c) <- 'My category';
            }
        }
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(Category c), '{Name}' = name(c), 'ParentID' = idParent(c), 'Prefix' = namePrefix(c) TO f;        
        open(f());
    }
}

categoryImport 'Import categories' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    LOCAL parent = ISTRING[50] (INTEGER);
    LOCAL namePrefix = STRING (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B, parent = C, namePrefix = D;
            FOR imported(INTEGER i) AND NOT category(id(i)) DO NEW c = Category {
                id(c) <- id(i);
            }
            
            FOR imported(INTEGER i) AND id(Category c) = id(i) DO {
                name(c) <- name(i);
                parent(c) <- category(parent(i));
                namePrefix(c) <- namePrefix(i);
            }
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES categoryExport(), categoryImport()
;

DESIGN migrationData {
    commons {
        NEW category {
            horizontal = TRUE;
            caption = 'Categories';
            MOVE PROPERTY(categoryImport());
            MOVE PROPERTY(categoryExport());
        }
    }
}
