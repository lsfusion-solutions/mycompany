MODULE ItemImage;

REQUIRE Item;

NAMESPACE MasterData;

image 'Изображение' = DATA IMAGEFILE (Item);
loadImage 'Загрузить' (Item i)  { INPUT = image(i) CHANGE; }
openImage 'Открыть' (Item i)  { open(image(i)); }
dropImage 'Сбросить' (Item i) { image(i) <- NULL; }

copy(Item n, Item o) + {
    image(n) <- image(o);
}

EXTEND FORM item
    PROPERTIES(i) image, loadImage, openImage, dropImage SHOWIF TRUE IF image(i) 
;

DESIGN item {
    tabs {
        NEW image {
            caption = 'Изображение';
            MOVE PROPERTY(image(i)) { fill = 1; caption = ''; }
            NEW imageActions {
                type = CONTAINERH;
                MOVE PROPERTY(loadImage(i));
                MOVE PROPERTY(openImage(i));
                MOVE PROPERTY(dropImage(i));
            }
        }
    }
}

EXTEND FORM items
    PROPERTIES(i) READONLY image PANEL
;

DESIGN items {
    tabs {
        NEW image {
            caption = 'Изображение';
            MOVE PROPERTY(image(i)) { fill = 1; caption = ''; }
        }
    }
}
