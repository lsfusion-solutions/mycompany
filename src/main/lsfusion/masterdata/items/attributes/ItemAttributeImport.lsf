MODULE ItemAttributeImport;

REQUIRE ItemAttribute, Item, System;

NAMESPACE MasterData;

attributeExport 'Export attributes' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM
            'ItemID' = id(Item i), '{ID}' = id(ItemAttribute a), 'Value' = value(attribute(i, a))
                WHERE value(attribute(i, a)) TO f
        ; 
        open(f());
    }
}

attributeImport 'Import attributes' () {
    LOCAL idItem = STRING[50] (INTEGER);
    LOCAL idItemAttribute = STRING[50] (INTEGER);
    LOCAL value = STRING (INTEGER);
       
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idItem = A, idItemAttribute = B, value = C; 

            FOR [GROUP SUM 1 BY idItemAttribute(INTEGER i)](STRING num) AND NOT itemAttribute(num) DO {
                MESSAGE 'Missing attribute in database with code' + ' ' + num + '... Import canceled';
                RETURN;
            }  
            
            FOR [GROUP SUM 1 BY idItemAttribute(INTEGER i), value(i)](STRING a, STRING v) AND NOT value(itemAttribute(a), v) 
                NEW av = ItemAttributeValue DO {
                    itemAttribute(av) <- itemAttribute(a);
                    value(av) <- v;
                }
            
            FOR id(Item i) = idItem(INTEGER j) AND id(ItemAttribute a) = idItemAttribute(j) DO
                attribute(i, a) <- value(a, value(j));
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES attributeExport(), attributeImport()
;

DESIGN migrationData {
    commons {
        NEW attribute {
            horizontal = TRUE;
            caption = 'Product attributes';
            MOVE PROPERTY(attributeImport());
            MOVE PROPERTY(attributeExport());
        }
    }
}