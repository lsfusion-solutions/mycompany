MODULE ItemAttribute;

REQUIRE Item;

NAMESPACE MasterData;

CLASS ItemAttribute 'Attribute';

id '{ID}' = DATA STRING[50] (ItemAttribute) IN id CHARWIDTH 8;
itemAttribute (STRING[50] id) = GROUP AGGR ItemAttribute i BY id(i);

index 'â„–' (ItemAttribute a) = PARTITION SUM 1 ORDER a AS ItemAttribute MATERIALIZED CHARWIDTH 2;
itemAttribute (INTEGER i) = GROUP LAST ItemAttribute a ORDER a BY index(a);  

nonull 'Required' = DATA BOOLEAN (ItemAttribute);

name '{Name}' = DATA ISTRING[50] (ItemAttribute) IN id NONULL;

// available categories
set 'Incl.' = DATA BOOLEAN (ItemAttribute, Category);

// recursive
valid (ItemAttribute a, Category c) = GROUP LAST set(a, Category cc) ORDER DESC level(c, cc); 

// available values
CLASS ItemAttributeValue 'Value';

itemAttribute 'Attribute' = DATA ItemAttribute (ItemAttributeValue) NONULL DELETE;
nameAttribute 'Attribute' (ItemAttributeValue v) = name(itemAttribute(v));

value 'Value' = DATA STRING (ItemAttributeValue) NONULL IN id;
value (ItemAttribute a, STRING v) = GROUP AGGR ItemAttributeValue av BY itemAttribute(av), value(av);

FORM value 'Value'
    OBJECTS v = ItemAttributeValue PANEL 
    PROPERTIES(v) value 
;

newValue 'Value' (ItemAttribute a) {
    NEWSESSION {
        NEW v = ItemAttributeValue {
            itemAttribute(v) <- a;
            DIALOG value OBJECTS v = v DOCKED DO seek(v);
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT';

// available values
set 'Incl.' = DATA BOOLEAN (ItemAttributeValue, Category);
valid (ItemAttributeValue v, Category c) = GROUP LAST set(v, Category cc) ORDER DESC level(c, cc); 

countCategory 'Number of categories' (ItemAttributeValue v) = GROUP SUM 1 IF set(v, Category c) MATERIALIZED;

CONSTRAINT set(ItemAttributeValue v, Category c) AND NOT valid(itemAttribute(v), c)
    MESSAGE 'The value category must match the attribute category';

FORM values 'Values'
    OBJECTS a = ItemAttribute PANEL, c = Category PANEL 

    OBJECTS v = ItemAttributeValue
    PROPERTIES READONLY value(v)
    PROPERTIES newValue(a) DRAW v TOOLBAR 
    PROPERTIES(v) NEWSESSION EDIT, DELETE
    FILTERS itemAttribute(v) = a,
            valid(v, c) OR NOT countCategory(v)
;

matches (ItemAttributeValue v, Category c) =
    valid(v, c) OR (GROUP SUM 1 IF set(v, Category cc) AND level(cc, c)) OR NOT countCategory(v);

FORM childValues 'Values'
    OBJECTS a = ItemAttribute PANEL, c = Category PANEL 

    OBJECTS v = ItemAttributeValue
    PROPERTIES READONLY value(v)
    PROPERTIES newValue(a) DRAW v TOOLBAR 
    PROPERTIES(v) NEWSESSION EDIT, DELETE
    FILTERS itemAttribute(v) = a,
            matches(v, c)
;

FORM attributes 'Attributes'
    OBJECTS a = ItemAttribute
    PROPERTIES(a) READONLY index, id, name, nonull
    PROPERTIES(a) NEWSESSION NEW, EDIT, DELETE 
    
    LIST ItemAttribute OBJECT a
;

FORM attribute 'Attribute'
    OBJECTS a = ItemAttribute PANEL
    PROPERTIES(a) name, id, nonull
    
    TREE categories c = Category PARENT parent(c) 
    PROPERTIES name(c) READONLY, set(a, c) BACKGROUND (GROUP SUM 1 IF set(a, Category cc) AND level(cc, c))
    ORDERS name(c)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT
           
    OBJECTS v = ItemAttributeValue
    PROPERTIES(v) value, NEW, DELETE
    FILTERS itemAttribute(v) = a
    
    TREE cat cc = Category PARENT parent(cc)
    PROPERTIES name(cc) READONLY, set(v, cc) BACKGROUND (GROUP SUM 1 IF set(v, Category ccc) AND level(ccc, cc))
    FILTERS (GROUP LAST set(a, Category pcc) ORDER level(cc, pcc)) OR (GROUP SUM 1 IF set(a, Category ccc) AND level(ccc, cc)) 
    ORDERS name(cc)
    FILTERGROUP activeCategoryC
        FILTER '{Active}' active(cc) DEFAULT
    
    EDIT ItemAttribute OBJECT a
;

DESIGN attribute {
    OBJECTS {
        NEW pane {
            fill = 1;
            horizontal = TRUE;
            NEW attribute {
                fill = 1;
                NEW name {
                    caption = 'Settings';
                    MOVE PROPERTY(name(a));
                    MOVE PROPERTY(id(a));
                    MOVE PROPERTY(nonull(a));
                }
            }
            NEW details {
                tabbed = TRUE;
                fill = 5;
                MOVE BOX(TREE categories) {
                    caption = 'Categories';
                }
                NEW values {
                    horizontal = TRUE;
                    caption = 'Values';
                    fill = 2;
                    MOVE BOX(v);
                    MOVE BOX(TREE cat) {
                        caption = 'Value categories';
                        fill = 1;
                    }    
                }
            }
        }
    }
}

NAVIGATOR {
    masterData {
        NEW attributes;
    }
}

// attribute value
attribute = DATA ItemAttributeValue (Item, ItemAttribute) INDEXED;
value 'Value' (Item i, ItemAttribute a) = value(attribute(i, a));

CONSTRAINT nonull(ItemAttribute a) AND NOT value(Item i, a) AND valid(a, category(i)) AND NOT SET(nonull(a))
    MESSAGE 'The attribute is required';

CONSTRAINT attribute(Item i, ItemAttribute a) AND (countCategory(attribute(i, a)) AND NOT valid(attribute(i, a), category(i)))
    CHECKED BY attribute[Item, ItemAttribute]
    MESSAGE 'The item category must match the value category';

CONSTRAINT attribute(Item i, ItemAttribute a) AND NOT itemAttribute(attribute(i, a)) = a
    CHECKED BY attribute[Item, ItemAttribute]
    MESSAGE 'An invalid value was selected for this item';
    
CONSTRAINT DROPPED(ItemAttributeValue v IS ItemAttributeValue) AND PREV(attribute(Item i, ItemAttribute a)) = v
    MESSAGE 'Can\'t delete value that is in use';

readonly = ABSTRACT BOOLEAN (Item, ItemAttribute);

EXTEND FORM item
    OBJECTS a = ItemAttribute
    PROPERTIES name(a) READONLY, 
               value(i, a) ON CHANGE { DIALOG values OBJECTS a = a, c = category(i), v = attribute(i, a) CHANGE LIST value(v); } 
                           READONLYIF readonly(i, a) BACKGROUND
                                IF readonly(i, a) THEN RGB(223,223,223) ELSE
                                                      (RGB(245,184,184) IF NOT value(i, a) AND nonull(a))
    FILTERS valid(a, category(i))
;

DESIGN item {
    tabs {
        MOVE BOX(a) { caption = 'Attributes'; }
    }
}

// full value
beforeName 'Before name' = DATA BOOLEAN (ItemAttribute);
sequence 'Sequence' = DATA INTEGER (ItemAttribute);
prefix 'Prefix' = DATA STRING[15] (ItemAttribute);
suffix 'Suffix' = DATA STRING[15] (ItemAttribute);

fullValue 'Full value' (Item i, ItemAttribute a) = (CONCAT '', prefix(a), value(i, a), suffix(a)) IF value(i, a);
prefixName(Item i) += STRING(GROUP CONCAT fullValue(i, ItemAttribute a) IF sequence(a) AND beforeName(a), ' ' ORDER sequence(a), a);
suffixName(Item i) += STRING(GROUP CONCAT fullValue(i, ItemAttribute a) IF sequence(a) AND NOT beforeName(a), ' ' ORDER sequence(a), a);

EXTEND FORM attributes PROPERTIES READONLY sequence(a);
EXTEND FORM attribute PROPERTIES(a) beforeName, sequence, prefix, suffix;

DESIGN attribute {
    attribute {
        NEW itemValues {
            caption = 'Formation of the name';
            MOVE PROPERTY(beforeName(a));
            MOVE PROPERTY(sequence(a));
            MOVE PROPERTY(prefix(a));
            MOVE PROPERTY(suffix(a));
        }
    }
}

// filters
attributeFilter = DATA LOCAL ItemAttributeValue (ItemAttribute);
value 'Value' (ItemAttribute a) = value(attributeFilter(a));

notMatchAttributeFilter(Item i) = GROUP SUM 1 IF attributeFilter(ItemAttribute a) AND NOT attributeFilter(a) = attribute(i, a);  

CONSTRAINT attributeFilter(ItemAttribute a) AND NOT itemAttribute(attributeFilter(a)) = a
    CHECKED BY attributeFilter MESSAGE '';

META defineItemAttributeForm(form, obj, gobj)
    EXTEND FORM form
        OBJECTS ia = ItemAttribute BEFORE gobj
        PROPERTIES name(ia) READONLY, value(ia) ON CHANGE { DIALOG childValues OBJECTS a = ia, c = c, v = attributeFilter(ia) CHANGE LIST value(v); }
        FILTERS NOT notMatchAttributeFilter(obj)
        FILTERS GROUP SUM 1 IF set(ia, Category cc) AND (level(cc, c) OR level(c, cc)) 
    ;
END

META defineItemAttributeForm(form, obj)
    @defineItemAttributeForm(form, obj, obj);
END

@defineItemAttributeForm(items, i);
DESIGN items {
    tabs {
        NEW filterAttributes {
            fill = 1;
            caption = 'Filters';
            MOVE BOX(ia) { 
                caption = ''; 
                GRID(ia) { height = 100; } 
            }
        }
    }
}

copy(Item n, Item o) + {
   FOR attribute(o, ItemAttribute a) DO {
       attribute(n, a) <- attribute(o, a);
   }
}

// index values

value (Item i, INTEGER index) = value(i, itemAttribute(index));

attribute1(Item i) = value(i, 1);
attribute2(Item i) = value(i, 2);
attribute3(Item i) = value(i, 3);
attribute4(Item i) = value(i, 4);
attribute5(Item i) = value(i, 5);
attribute6(Item i) = value(i, 6);
attribute7(Item i) = value(i, 7);
attribute8(Item i) = value(i, 8);
attribute9(Item i) = value(i, 9);

META defineAttributeForm (form, obj)
    EXTEND FORM form
        PROPERTIES(obj) READONLY attribute1, attribute2, attribute3, attribute4, attribute5, attribute6, attribute7, attribute8, attribute9   
    ;
END
 