MODULE ItemImageMobile;

REQUIRE Input, Utils, Reflection, Security, Authentication,
        MasterDataSettings, ItemImage, 
        CategoryMobile, BarCodeMobile, Mobile;

NAMESPACE MasterData;

loadImageApply 'Загрузить' (Item i)  { NEWSESSION { INPUT = image(i) CHANGE DO APPLY; } }
dropImageApply 'Сбросить' (Item i) { NEWSESSION { APPLY { image(i) <- NULL; } } } CONFIRM;

FORM itemImageLoad 'Загрузка изображений'
    OBJECTS i = Item PAGESIZE 50
    PROPERTIES(i) READONLY 'Имя' = name, id
    PROPERTIES(i) READONLY PANEL pName '' = name, '' = idBarCode, '' = nameUom 
    PROPERTIES(i) '' = image READONLY PANEL
    PROPERTIES(i) loadImageApply, dropImageApply
    ORDERS name(i)
    
    FILTERGROUP noImage
        FILTER 'Без изображения' TRUE IF NOT image(i);
;

DESIGN itemImageLoad {
    caption = 'Изображения';
    OBJECTS {
        NEW filters {
            alignment = STRETCH;
            NEW groupFilter {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE FILTERGROUPS(i);
            }
        }
        MOVE GRID(i) {
            headerHeight = 24; 
        }
        REMOVE TOOLBARBOX(i);
        MOVE PROPERTY(pName) { alignment = STRETCH; }
        NEW details {
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(idBarCode(i));
            MOVE PROPERTY(nameUom(i));
        }
        NEW image {
            fill = 0.5;
            type = CONTAINERH;
            MOVE PROPERTY(image(i)) { fill = 1; }
            NEW imageActions {
                alignment = STRETCH;
                MOVE PROPERTY(loadImageApply(i)) { fill = 1; }
                MOVE PROPERTY(dropImageApply(i)) { alignment = STRETCH; };
            }
        }
    }
    REMOVE TOOLBARBOX;
}

@defineNameFilter(itemImageLoad, i, filters);
@defineBarCodeFilter(itemImageLoad, i, filters);
@defineCategoryFilter(itemImageLoad, i, filters);

NAVIGATOR {
    dashboards {
        NEW itemImageLoad;
    }
}

showMobileForms() + { 
    IF permit(currentUser(), navigatorElementCanonicalName('MasterData.itemImageLoad')) THEN
        SHOW itemImageLoad NOWAIT; 
}