MODULE ItemImageMobile;

REQUIRE Input, Utils, Reflection, Security, Authentication,
        ItemImage, CategoryMobile, BarCodeMobile;

NAMESPACE MasterData;

loadImageApply 'Upload' (Item i)  { NEWSESSION { INPUT = image(i) CHANGE DO APPLY; } }
dropImageApply 'Reset' (Item i) { NEWSESSION { APPLY { image(i) <- NULL; } } } CONFIRM;

FORM itemImageLoad 'Images'
    OBJECTS i = Item PAGESIZE 50
    PROPERTIES(i) READONLY '{First_name}' = name, id
    PROPERTIES(i) READONLY PANEL pName '' = name, '' = idBarCode, '' = nameUom 
    PROPERTIES(i) '' = image READONLY PANEL
    PROPERTIES(i) loadImageApply, dropImageApply
    ORDERS name(i)
    
    FILTERGROUP noImage
        FILTER 'No picture' TRUE IF NOT image(i);
;

DESIGN itemImageLoad {
    OBJECTS {
        class = '';
        border = FALSE;
        NEW filters {
            alignment = STRETCH;
            NEW groupFilter {
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE FILTERGROUPS(i);
            }
        }
        MOVE GRID(i);
        REMOVE BOX(i);
        MOVE PROPERTY(pName) { alignment = STRETCH; }
        NEW details {
            horizontal = TRUE;
            alignment = STRETCH;
            MOVE PROPERTY(idBarCode(i));
            MOVE PROPERTY(nameUom(i));
        }
        NEW image {
            fill = 0.5;
            horizontal = TRUE;
            NEW imagePane {
                fill = 1;
                MOVE PROPERTY(image(i)) {
                    size = (100, 100); 
                    fill = 1; 
                }
            }
            NEW imageActions {
                alignment = STRETCH;
                MOVE PROPERTY(loadImageApply(i)) { fill = 1; }
                MOVE PROPERTY(dropImageApply(i)) { alignment = STRETCH; };
            }
        }
    }
    REMOVE TOOLBARBOX;
}

@defineNameFilter(itemImageLoad, i, filters);
@defineBarCodeFilter(itemImageLoad, i, filters);
@defineCategoryFilter(itemImageLoad, i, filters);

NAVIGATOR {
    masterData {
        NEW itemImageLoad AFTER items;
    }
}