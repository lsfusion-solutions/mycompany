MODULE ItemUom;

REQUIRE Item;

NAMESPACE MasterData;

ratio 'Соотношение' = DATA NUMERIC[20,10] (Item, Uom);

EXTEND FORM item
    OBJECTS u = Uom
    PROPERTIES(u) READONLY name, id
    PROPERTIES ratio(i, u)
    
    FILTERS u != uom(i)
    
    FILTERGROUP ratio
        FILTER 'Используются' ratio(i, u)
;

DESIGN item {
    tabs {
        MOVE BOX(u) {
            caption = 'Единицы измерения';
        }
    }
}

// partner
META defineDocPartnerUom(doc)
    partnerUom = DATA Uom (doc##Line);
    namePartnerUom 'Ед. изм. контрагента' (doc##Line l) = name(partnerUom(l));

    WHEN LOCAL CHANGED(item(doc##Line l)) AND NOT ratio(item(l), partnerUom(l)) DO 
        partnerUom(l) <- NULL;
        
    CONSTRAINT partnerUom(doc##Line l) AND NOT ratio(item(l), partnerUom(l)) CHECKED BY partnerUom[doc##Line]
        MESSAGE 'Выбрана единица измерения контрагента, для которой не задано соотношение';
END

META defineDocPartnerQuantity(doc)
    partnerQuantity 'Кол-во контрагента' = DATA NUMERIC[16,3] (doc##Line);
    WHEN LOCAL (CHANGED(quantity(doc##Line l)) OR CHANGED(partnerUom(l))) AND NOT CHANGED(partnerQuantity(l)) DO
        partnerQuantity(l) <- NUMERIC[16,3](quantity(l) / ratio(item(l), partnerUom(l)));
    
    changePartnerQuantity (doc##Line l) {
        INPUT n = partnerQuantity(l) CHANGE DO {
            quantity(l) <- NUMERIC[16,3](partnerQuantity(l) * ratio(item(l), partnerUom(l)));
        }
    }
END

META defineDocPartnerPrice(doc)
    partnerPrice 'Цена контрагента' = DATA NUMERIC[10,2] (doc##Line);
    WHEN LOCAL (CHANGED(price(doc##Line l)) OR CHANGED(partnerUom(l))) AND NOT CHANGED(partnerPrice(l)) DO
        partnerPrice(l) <- NUMERIC[10,2](price(l) * ratio(item(l), partnerUom(l)));
    
    changePartnerPrice (doc##Line l) {
        INPUT n = partnerPrice(l) CHANGE DO {
            price(l) <- NUMERIC[10,2](partnerPrice(l) / ratio(item(l), partnerUom(l)));
        }
    }
END