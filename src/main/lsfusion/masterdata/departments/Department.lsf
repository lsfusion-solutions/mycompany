MODULE Department;

REQUIRE Partner;

NAMESPACE MasterData;

CLASS Department 'Department';

@defineID(department, 'Departments', 'DP', 6);

partner 'Partner' = DATA Partner (Department) NONULL DELETE;

countDepartment (Partner p) = GROUP SUM 1 IF partner(Department d) = p;  

name '{Name}' = DATA ISTRING[100] (Department) CHARWIDTH 20;

address 'Address' = DATA ISTRING[150] (Department) CHARWIDTH 30;
city 'City' = DATA ISTRING[50] (Department) CHARWIDTH 12;
state 'State' = DATA ISTRING[50] (Department) CHARWIDTH 12;
zip 'Postcode' = DATA ISTRING[10] (Department);

addressCsv 'Address' (Department a) = CONCAT ', ', address(a), city(a), state(a) IF NOT state(a) = city(a), zip(a) CHARWIDTH 20;
addressLines 'Address' (Department a) = CONCAT '\n ', address(a), city(a), state(a) IF NOT state(a) = city(a), zip(a);

FORM department 'Department'
    OBJECTS d = Department PANEL
    PROPERTIES(d) id, name, address, city, state, zip 
    
    EDIT Department OBJECT d
;

DESIGN department {
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            NEW headerLeft {
                NEW params {
                    caption = 'Parameters';
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE PROPERTY(id(d));
                    MOVE PROPERTY(name(d));
                }
                NEW address {
                    caption = 'Address';
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE PROPERTY(address(d));
                    MOVE PROPERTY(city(d));
                    MOVE PROPERTY(state(d));
                    MOVE PROPERTY(zip(d));
                }
            }
            NEW headerRight;
        }
        NEW tabbedPane {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

FORM dialogDepartments 'Departments'
    OBJECTS d = Department
    PROPERTIES(d) READONLY name, address, city, state, zip
    
    LIST Department OBJECT d
;

META defineDocDepartment (doc, property)
    department = DATA Department (###doc) INDEXED;
    nameDepartment 'Department' (###doc i) = name(department(i));
    
    CONSTRAINT department(###doc i) AND NOT property(i) = partner(department(i))
        CHECKED BY department[###doc]
        MESSAGE 'The partner of the document does not match the partner of the department';

    WHEN LOCAL CHANGED(property(###doc i)) AND NOT property(i) = partner(department(i)) DO
        department(i) <- NULL;  
END