MODULE LegalEntityImport;

REQUIRE LegalEntity, PartnerPurchase, PartnerSales;

NAMESPACE MasterData;

legalEntityExport 'Export legal entities' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF LegalEntity l IS LegalEntity) THEN {
            NEW l = LegalEntity {
                id(l) <- '123';
                name(l) <- 'Legal entity';
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(LegalEntity l), '{Name}' = name(l),
            'Address' = address(l), 'City' = city(l), 'State' = state(l), 'Zip' = zip(l), 
            'Phone' = phone(l), 'Email' = email(l), 'Website' = website(l),
            'isVendor' = isVendor(l), 'isCustomer' = isCustomer(l) WHERE l IS LegalEntity TO f;   
        open(f());
    }
}

legalEntityImport 'Import legal entities' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[92] (INTEGER);
    LOCAL website = ISTRING[30] (INTEGER);
    LOCAL address = ISTRING[50] (INTEGER);
    LOCAL city = ISTRING[30] (INTEGER);
    LOCAL state = ISTRING[30] (INTEGER);
    LOCAL zip = ISTRING[10] (INTEGER);
    LOCAL phone = ISTRING[100] (INTEGER);
    LOCAL email = ISTRING[30] (INTEGER);
    LOCAL isVendor = BOOLEAN (INTEGER);
    LOCAL isCustomer = BOOLEAN (INTEGER);
       
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id, name, address, city, state, zip, phone, email, website, isVendor, isCustomer; 
            
            FOR imported(INTEGER j) AND (id(j) OR name(j)) DO NEW l = LegalEntity {
                id(l) <- id(j);
                name(l) <- name(j);
                website(l) <- website(j);
                address(l) <- address(j);
                city(l) <- city(j);
                state(l) <- state(j);
                zip(l) <- zip(j);
                phone(l) <- phone(j);
                email(l) <- email(j);
                isVendor(l) <- isVendor(j);
                isCustomer(l) <- isCustomer(j);
            }
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES legalEntityImport(), legalEntityExport()
;

DESIGN migrationData {
    commons {
        NEW legalEntity {
            horizontal = TRUE;
            caption = 'Legal entities';
            MOVE PROPERTY(legalEntityImport());
            MOVE PROPERTY(legalEntityExport());
        }
    }
}
