MODULE Partner;

REQUIRE Utils, MetaNumerator, Input, Icon, Comments, CountryP;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Partner';

@defineID(partner, 'Partners', 'KN', 6);

name '{Name}' = ABSTRACT ISTRING[92] (Partner) IN id CHARWIDTH 15 MATERIALIZED INDEXED MATCH;
fullName 'Full name' = ABSTRACT ISTRING[200] (Partner) IN id CHARWIDTH 25;

address 'Address' = DATA ISTRING[150] (Partner) CHARWIDTH 30;
city 'City' = DATA ISTRING[50] (Partner) CHARWIDTH 12;
state 'State' = DATA ISTRING[50] (Partner) CHARWIDTH 12;
zip 'Postcode' = DATA ISTRING[10] (Partner);
zipState (Partner p) = CONCAT ', ', zip(p), city(p), state(p) IF NOT state(p) = city(p);

country 'Country' = DATA Country (Partner);
nameCountry 'Country' (Partner p) = name(country(p));

addressCsv 'Address' (Partner p) = CONCAT ', ', address(p), zipState(p) CHARWIDTH 20;
addressLines 'Address' (Partner p) = CONCAT '\n', address(p), zipState(p);

phone 'Phone' = DATA ISTRING[100] (Partner) CHARWIDTH 12;
email 'Email' = DATA ISTRING[400] (Partner) CHARWIDTH 20 REGEXP '[\\-!#-\'*+\\/-9=?A-Z^-~]+(\\.[\\-!#-\'*+\\/-9=?A-Z^-~]+)*@[0-9A-Za-z]([0-9A-Za-z\\-]\{0,61\}[0-9A-Za-z])?(\\.[0-9A-Za-z]([0-9A-Za-z\\-]\{0,61\}[0-9A-Za-z])?)+' '{authentication.email.contact.incorrect}';

match (STRING phone, STRING number) = regexpReplace(phone, '[+() -]', '', 'g') = regexpReplace(number, '[+() -]', '', 'g');
partnerPhone (ISTRING[100] phone) = GROUP LAST Partner p ORDER p WHERE match(phone(p), phone);
partnerEmail (ISTRING[400] email) = GROUP LAST Partner p ORDER p WHERE lower(email(p)) = email;

archived 'Archived' = DATA BOOLEAN (Partner);
active '{Active}' (Partner p) = NOT archived(p);

logo = DATA IMAGEFILE (Partner);

// type
CLASS PartnerType 'Partner type';

name '{First_name}' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

EXTEND FORM options
    OBJECTS prtp = PartnerType
    PROPERTIES(prtp) READONLY name
;

DESIGN options {
    tabbedPane {
        MOVE BOX(prtp);
    }
}

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Partner type' (Partner p) = name(type(p));

FORM partner 'Partner'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) id, nameType, logo, 
                  address, city, state, zip, nameCountry,
                  phone, email,
                  archived
    
    EDIT Partner OBJECT p
;

DESIGN partner {
    OBJECTS {
        NEW statusPane FIRST {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW statusActions {
                horizontal = TRUE;
                flex = 1;
                NEW primaryActions {
                    horizontal = TRUE;
                }
                NEW secondaryActions {
                    horizontal = TRUE;
                }
            }
            NEW status {
                horizontal = TRUE;
            }
        }
        NEW header {
            horizontal = TRUE;            
            NEW headerLeft {
                NEW headerMain {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    NEW headerMainLeft {
                        fill = 1;
                        NEW headerTop {
                            horizontal = TRUE;
                            alignment = STRETCH;
                            MOVE PROPERTY(id(p));
                            MOVE PROPERTY(nameType(p));
                        }
                        NEW headerDetails { alignment = STRETCH; }
                    }
                    MOVE PROPERTY(logo(p)) {
                        size = (100, 100);
                        caption = '';
                    }
                }
                NEW headerPane {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    NEW headerColumn1 {
                        MOVE PROPERTY(address(p));
                        MOVE PROPERTY(city(p));
                        MOVE PROPERTY(state(p));
                        MOVE PROPERTY(zip(p));
                        MOVE PROPERTY(nameCountry(p));
                    }
                    NEW headerColumn2 {
                        MOVE PROPERTY(phone(p)) { regexp = phoneRegexp(); }
                        MOVE PROPERTY(email(p)) { regexp = emailRegexp(); }
                    }
                    NEW headerColumn3 {
                        MOVE PROPERTY(archived(p));                   
                    }
                }
            }
            NEW headerRight;
        }
        NEW tabs {
            tabbed = TRUE;
            fill = 1;
            NEW legalInfo {
                caption = 'Legal data';
                NEW legalInfoOptions;
            }
        }
    }
}

showPartner (LONG id) {
    FOR LONG(Partner p AS Partner) = id DO
        SHOW partner OBJECTS p = p DOCKED MANAGESESSION;
} @@api;


@defineObjectComments(partner, p, tabs);

FORM partners 'Partners'
    OBJECTS p = Partner
    PROPERTIES(p) READONLYIF isReadonly() name, id, nameType, addressCsv, phone, email 
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
    FILTER '{Active}' active(p) 'F10' DEFAULT
        FILTER 'Archived' archived(p) 'F9'
;

@extendFormEditable(partners);

DESIGN partners {
    OBJECTS {
        NEW header {
            fill = 1;
            MOVE BOX(p);
        }
        NEW operations {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

FORM dialogPartners 'Select partner'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS name(p)
     
    FILTERGROUP active
        FILTER '{Active}' active(p) 'F10' DEFAULT
        FILTER 'Archived' archived(p) 'F9'

    PROPERTIES() nameFilter
    FILTERS isISubstring(name(p), nameFilter()) OR NOT nameFilter()
    
    EVENTS ON INIT { nameFilter() <- NULL; }
    
    LIST Partner OBJECT p
;

DESIGN dialogPartners {
    OBJECTS {
        NEW filters FIRST {
            alignment = STRETCH;
            MOVE PROPERTY(nameFilter()) { placeholder = '{Name}'; }
        }
    }
}

NAVIGATOR {
    masterData {
        NEW partners;
    }
}