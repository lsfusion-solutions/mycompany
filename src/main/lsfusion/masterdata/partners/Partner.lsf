MODULE Partner;

REQUIRE Utils, MetaNumerator, Input;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Контрагент';

@defineID(partner, 'Контрагенты', 'КН', 4);

name 'Наименование' = ABSTRACT ISTRING[92] (Partner) IN id CHARWIDTH 15 NONULL;

address 'Адрес' = DATA ISTRING[50] (Partner);
city 'Город' = DATA ISTRING[30] (Partner);
state 'Область' = DATA ISTRING[30] (Partner);
zip 'Почтовый индекс' = DATA ISTRING[10] (Partner);

csvAddress 'Адрес' (Partner p) = CONCAT ', ', address(p), city(p), state(p), zip(p);
linesAddress 'Адрес' (Partner p) = CONCAT '\n ', address(p), city(p), state(p), zip(p);

phone 'Телефон' = DATA ISTRING[30] (Partner);
email 'Электронная почта' = DATA ISTRING[30] (Partner);

archived 'Неактивный' = DATA BOOLEAN (Partner);
active 'Активный' (Partner p) = NOT archived(p);

// type
CLASS PartnerType 'Тип контрагента';

name 'Имя' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Тип контрагента' (Partner p) = name(type(p));

FORM partner 'Контрагент'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) id, nameType, 
                  address, city, state, zip,
                  phone, email,
                  archived
    
    EDIT Partner OBJECT p
;

DESIGN partner {
    OBJECTS {
        NEW header {
            type = CONTAINERH;            
            NEW headerLeft {
                NEW headerTop {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(id(p)); 
                    MOVE PROPERTY(nameType(p));
                }
                NEW headerDetails { alignment = STRETCH; }
                NEW headerPane {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    NEW headerColumn1 {
                        MOVE PROPERTY(address(p));
                        MOVE PROPERTY(city(p));
                        MOVE PROPERTY(state(p));
                        MOVE PROPERTY(zip(p));
                    }
                    NEW headerColumn2 {
                        MOVE PROPERTY(phone(p));
                        MOVE PROPERTY(email(p));
                    }
                    NEW headerColumn3 {
                        MOVE PROPERTY(archived(p));                   
                    }
                }
            }
            NEW headerRight;    
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
            NEW legalInfo {
                caption = 'Юридические данные';
                NEW legalInfoOptions;
            }
        }
    }
}

FORM partners 'Контрагенты'
    OBJECTS p = Partner
    PROPERTIES(p) READONLYIF isReadonly() name, id, nameType, csvAddress, phone, email 
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT
        FILTER 'Неактивный' archived(p) 'F9'
;

@extendFormEditable(partners);

DESIGN partners {
    OBJECTS {
        NEW header {
            fill = 1;
            MOVE BOX(p);
        }
        NEW operations {
            type = TABBED;
            fill = 1;
        }
    }
}

FORM dialogPartners 'Выбор контрагента'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS name(p)
     
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT
        FILTER 'Неактивный' archived(p) 'F9'

    PROPERTIES() nameFilter, clearNameFilter
    FILTERS isISubstring(name(p), nameFilter()) OR NOT nameFilter()
    
    EVENTS ON INIT { nameFilter() <- NULL; }
    
    LIST Partner OBJECT p
;

NAVIGATOR {
    masterData {
        NEW partners;
    }
}