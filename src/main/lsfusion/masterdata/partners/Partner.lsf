MODULE Partner;

REQUIRE Utils, MetaNumerator, Input;

NAMESPACE MasterData;

CLASS ABSTRACT Partner 'Partner';

@defineID(partner, 'Partners', 'KN', 6);

name '{Name}' = ABSTRACT ISTRING[92] (Partner) IN id CHARWIDTH 15;
fullName 'Full name' = ABSTRACT ISTRING[200] (Partner) IN id CHARWIDTH 25;

address 'Address' = DATA ISTRING[150] (Partner) CHARWIDTH 30;
city 'City' = DATA ISTRING[50] (Partner) CHARWIDTH 12;
state 'State' = DATA ISTRING[50] (Partner) CHARWIDTH 12;
zip 'Postcode' = DATA ISTRING[10] (Partner);

addressCsv 'Address' (Partner p) = CONCAT ', ', address(p), city(p), state(p) IF NOT state(p) = city(p), zip(p);
addressLines 'Address' (Partner p) = CONCAT '\n ', address(p), city(p), state(p) IF NOT state(p) = city(p), zip(p);

phone 'Phone' = DATA ISTRING[100] (Partner) CHARWIDTH 10;
email 'Email' = DATA ISTRING[30] (Partner);

archived 'Archived' = DATA BOOLEAN (Partner);
active '{Active}' (Partner p) = NOT archived(p);

// type
CLASS PartnerType 'Partner type';

name '{First_name}' (PartnerType o) = staticCaption(o) IF o IS PartnerType CHARWIDTH 15;

type  = ABSTRACT CASE PartnerType (Partner);
nameType 'Partner type' (Partner p) = name(type(p));

FORM partner 'Partner'
    OBJECTS p = Partner PANEL 
    PROPERTIES(p) id, nameType, 
                  address, city, state, zip,
                  phone, email,
                  archived
    
    EDIT Partner OBJECT p
;

DESIGN partner {
    OBJECTS {
        NEW header {
            horizontal = TRUE;            
            NEW headerLeft {
                NEW headerTop {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE PROPERTY(id(p)); 
                    MOVE PROPERTY(nameType(p));
                }
                NEW headerDetails { alignment = STRETCH; }
                NEW headerPane {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    NEW headerColumn1 {
                        MOVE PROPERTY(address(p));
                        MOVE PROPERTY(city(p));
                        MOVE PROPERTY(state(p));
                        MOVE PROPERTY(zip(p));
                    }
                    NEW headerColumn2 {
                        MOVE PROPERTY(phone(p));
                        MOVE PROPERTY(email(p));
                    }
                    NEW headerColumn3 {
                        MOVE PROPERTY(archived(p));                   
                    }
                }
            }
            NEW headerRight;    
        }
        NEW tabs {
            tabbed = TRUE;
            fill = 1;
            NEW legalInfo {
                caption = 'Legal data';
                NEW legalInfoOptions;
            }
        }
    }
}

FORM partners 'Partners'
    OBJECTS p = Partner
    PROPERTIES(p) READONLYIF isReadonly() name, id, nameType, addressCsv, phone, email 
    PROPERTIES(p) NEWSESSION EDIT, DELETE 
    FILTERGROUP active
        FILTER '{Active}' active(p) 'F10' DEFAULT
        FILTER 'Archived' archived(p) 'F9'
;

@extendFormEditable(partners);

DESIGN partners {
    OBJECTS {
        NEW header {
            fill = 1;
            MOVE BOX(p);
        }
        NEW operations {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

FORM dialogPartners 'Select partner'
    OBJECTS p = Partner
    PROPERTIES(p) READONLY name, id, nameType
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS name(p)
     
    FILTERGROUP active
        FILTER '{Active}' active(p) 'F10' DEFAULT
        FILTER 'Archived' archived(p) 'F9'

    PROPERTIES() nameFilter
    FILTERS isISubstring(name(p), nameFilter()) OR NOT nameFilter()
    
    EVENTS ON INIT { nameFilter() <- NULL; }
    
    LIST Partner OBJECT p
;

DESIGN dialogPartners {
    OBJECTS {
        NEW filters FIRST {
            alignment = STRETCH;
            MOVE PROPERTY(nameFilter()) { placeholder = 'Name'; }
        }
    }
}

NAVIGATOR {
    masterData {
        NEW partners;
    }
}