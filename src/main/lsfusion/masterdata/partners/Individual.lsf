MODULE Individual;

REQUIRE Partner, MasterData, LegalEntity, Position, DocTemplate, Location;

NAMESPACE MasterData;

CLASS Individual 'Физическое лицо';

firstName 'Имя' = DATA ISTRING[30] (Individual) NONULL;
lastName 'Фамилия' = DATA ISTRING[30] (Individual) NONULL;
middleName 'Отчество' = DATA ISTRING[30] (Individual);

legalEntity = DATA LegalEntity (Individual) NONULL;
idLegalEntity 'Организация' (Individual i) = id(legalEntity(i));
nameLegalEntity 'Организация' (Individual i) = name(legalEntity(i));

position = DATA Position (Individual);
namePosition 'Должность' (Individual i) = name(position(i));

EXTEND CLASS Individual : Partner;

name (Individual i) += CONCAT ' ', lastName(i), firstName(i), middleName(i);
positionName (Individual i) = CONCAT ' ', namePosition(i), lastName(i), firstName(i), middleName(i);

EXTEND CLASS PartnerType {
    individual 'Физическое лицо'
}

type (Partner p) += WHEN p IS Individual THEN PartnerType.individual;

EXTEND FORM partner
    PROPERTIES(p) firstName, lastName, middleName, nameLegalEntity, namePosition
;

DESIGN partner {
    headerDetails {
        NEW individualName {
            fill = 1;
            type = CONTAINERH;
            MOVE PROPERTY(firstName(p));
            MOVE PROPERTY(lastName(p));
            MOVE PROPERTY(middleName(p));
        }
        NEW job {
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(nameLegalEntity(p));
            MOVE PROPERTY(namePosition(p));
        }
    }
}

newIndividual 'Физическое лицо' () {
    NEWSESSION {
        NEW i = Individual {
            
            SHOW partner OBJECTS p = i;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partners
    PROPERTIES newIndividual() DRAW p TOOLBAR
;
EXTEND FORM dialogPartners
    EXTEND FILTERGROUP type
        FILTER 'Физические лица' p IS Individual
    
    PROPERTIES newIndividual() DRAW p TOOLBAR
;

FORM individuals 'Физические лица'
    OBJECTS i = Individual
    PROPERTIES(i) READONLY firstName, lastName, middleName, nameLegalEntity, namePosition, id

    LIST Individual OBJECT i
;

EXTEND FORM partner
    OBJECTS em = Individual
    PROPERTIES(em) firstName, lastName, middleName, namePosition, phone, email
    PROPERTIES(em) NEW, DELETE 
    FILTERS legalEntity(em) = p  
;

DESIGN partner {
    tabs {
        NEW contacts {
            showIf = p IS LegalEntity;
            caption = 'Контакты';
            MOVE BOX(em);           
        }
    }
}

// location
DESIGN location {
    tabbedPane {
        NEW invoicePrint {
            caption = 'Печать накладных';
        }
    }
}

// employee
META defineEmployee (prop, caption, cls, obj, container)
    prop = DATA Individual (###cls);
    positionName###prop ##caption (###cls o) = positionName(prop(o)); 
    name###prop (###cls o) = name(prop(o)); 
    position###prop (###cls o) = position(prop(o));
     
    CONSTRAINT prop(###cls o) AND NOT type(legalEntity(prop(o))) = PartnerType.company 
        CHECKED BY prop[###cls] MESSAGE 'Должен быть наш работник';
    
    EXTEND FORM cls
        PROPERTIES(obj) positionName###prop 
    ;
    DESIGN cls {
        container {
            MOVE PROPERTY(positionName###prop(obj));
        }
    }
END