MODULE Employee;

REQUIRE Individual, Company, Authentication, Security, ObjectUtils;

NAMESPACE MasterData;

CLASS Employee 'Employee' : Individual, CustomUser;

name '{Name}' (Employee e) = name[Partner](e) IF e IS Employee CHARWIDTH 15;
archived 'Archived' (Employee e) = archived[Partner](e) IF e IS Employee;

@defineSychronizeProperty(Employee, firstName, Contact);
@defineSychronizeProperty(Employee, lastName, Contact);
@defineSychronizeProperty(Employee, email, Contact);
@defineSychronizeProperty(Employee, phone, Contact);
@defineSychronizeProperty(Employee, postAddress, Contact);

EXTEND CLASS PartnerType {
    employee 'Employee'
}

type (Partner p) += WHEN p IS Employee THEN PartnerType.employee;

WHEN CHANGED(address(Employee e)) AND NOT CHANGED(postAddress(e)) DO postAddress(e) <- address(e);
WHEN CHANGED(postAddress(Employee e)) AND NOT CHANGED(address(e)) DO address(e) <- postAddress(e);

CONSTRAINT Employee e IS Employee AND NOT legalEntity(e) IS Company
    CHECKED BY legalEntity
    MESSAGE 'An employee\'s legal entity can only be a company';

WHEN LOCAL SET(Employee e IS Employee) AND NOT CHANGED(legalEntity(e)) DO legalEntity(e) <- defaultCompany();

photo = DATA IMAGEFILE (Employee);

FORM employee 'Employee'
    OBJECTS e = Employee PANEL
    PROPERTIES(e) id, login, sha256Password ON CHANGE changeSHA256Password(e), photo, isLocked, archived, 
                 nameLegalEntity, nameDepartment, lastName, firstName, middleName, namePosition, email, phone, address, city, state,
                 zip, birthday
    
    OBJECTS ur = UserRole
    PROPERTIES(ur) READONLY name, sid
    PROPERTIES(e,ur) in

    EDIT Employee OBJECT e 
;

DESIGN employee {
    OBJECTS {
        NEW statusPane FIRST {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW statusActions {
                horizontal = TRUE;
                flex = 1;
                NEW primaryActions {
                    horizontal = TRUE;
                }
                NEW secondaryActions {
                    horizontal = TRUE;
                }
            }
            NEW status {
                horizontal = TRUE;
            }
        }
        NEW pane {
            horizontal = TRUE;
            fill = 1;
            NEW header {
                fill = 1;
                NEW headerRow1 {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    NEW login {
                        fill = 2;
                        caption = '{authentication.custom.user.login}';
                        MOVE PROPERTY(id(e));
                        MOVE PROPERTY(login(e));
                        MOVE PROPERTY(sha256Password(e));
                        MOVE PROPERTY(isLocked(e));
                        MOVE PROPERTY(archived(e));
                    }
                    MOVE PROPERTY(photo(e)) { 
                        fill = 1;
                        width = 100;
                        caption = '';
                    }
                }
                NEW info {
                    alignment = STRETCH;
                    caption = '{authentication.custom.user.info}';
                    MOVE PROPERTY(nameLegalEntity(e)) { notNull = TRUE; }
                    MOVE PROPERTY(nameDepartment(e));
                    MOVE PROPERTY(lastName(e));
                    MOVE PROPERTY(firstName(e));
                    MOVE PROPERTY(middleName(e));
                    MOVE PROPERTY(namePosition(e));
                    MOVE PROPERTY(email(e)) { regexp = emailRegexp(); }
                    MOVE PROPERTY(phone(e)) { regexp = phoneRegexp(); }
                    MOVE PROPERTY(address(e));
                    MOVE PROPERTY(city(e));
                    MOVE PROPERTY(state(e));
                    MOVE PROPERTY(zip(e));
                    MOVE PROPERTY(birthday(e));
                }
            }
            NEW details {
                tabbed = TRUE;
                fill = 3;
                NEW roles {
                    caption = '{security.user.roles}';
                    MOVE BOX(ur) { caption = ''; }
                }
            }
        }
    }
    NEW container BEFORE TOOLBARBOX {
        horizontal = TRUE;
        fill = 1;
        MOVE BOX(e) {
            flex = 0.3;
        };
        GROUP(,e) {
            lines = 1;
        }
    }
}

FORM employees 'Employees'
    OBJECTS e = Employee
    PROPERTIES(e) READONLY name, login, namePosition, nameLegalEntity
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE
    FILTERGROUP active
        FILTER '{Active}' active(e) 'F10' DEFAULT
        FILTER 'Archived' archived(e) 'F9'
;

FORM dialogEmployees 'Employees'
    OBJECTS e = Employee
    PROPERTIES(e) READONLY name, login, namePosition, nameLegalEntity
    ORDERS name(e)

    FILTERGROUP active
        FILTER '{Active}' active(e) 'F10' DEFAULT
        FILTER 'Archived' archived(e) 'F9'

    LIST Employee OBJECT e
;

NAVIGATOR {
    masterData {
        NEW employees;
    }
}
