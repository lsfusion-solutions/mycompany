MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Legal entity';

name '{Name}' = DATA ISTRING[92] (LegalEntity) CHARWIDTH 15 NONULL INDEXED MATCH;

dataFullName = DATA ISTRING[200] (LegalEntity) CHARWIDTH 25;
fullName 'Full name' (LegalEntity l) = OVERRIDE dataFullName(l), name(l);  

website 'Web site' = DATA ISTRING[30](LegalEntity);

EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);
fullName (LegalEntity l) += fullName(l);

EXTEND CLASS PartnerType {
    legalEntity 'Legal entity'
}

type (Partner p) += WHEN p IS LegalEntity THEN PartnerType.legalEntity;

EXTEND FORM partner
    PROPERTIES(p) name[LegalEntity], fullName, website
;

DESIGN partner {
    headerDetails {
        MOVE PROPERTY(name(p));
        MOVE PROPERTY(fullName(p));
    }
    headerColumn2 {
        MOVE PROPERTY(website(p));
    }
}

newLegalEntity 'Legal entity' () {
    NEWSESSION {
        NEW l = LegalEntity {
            DIALOG partner OBJECTS p = l DOCKED DO seek(l);
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM partners
    PROPERTIES newLegalEntity() DRAW p TOOLBAR
    
    LIST LegalEntity OBJECT p
;
EXTEND FORM dialogPartners
    FILTERGROUP type
        FILTER 'Legal entities' p IS LegalEntity

    PROPERTIES newLegalEntity() DRAW p TOOLBAR
;
