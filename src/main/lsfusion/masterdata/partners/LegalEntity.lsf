MODULE LegalEntity;

REQUIRE Partner, MasterData;

NAMESPACE MasterData;

CLASS LegalEntity 'Организация';

name 'Название' = DATA ISTRING[50] (LegalEntity) CHARWIDTH 12;
website 'Веб сайт' = DATA ISTRING[30](LegalEntity);

// Наследуем организацию от контрагента
EXTEND CLASS LegalEntity : Partner;

name (LegalEntity l) += name(l);

EXTEND CLASS PartnerType {
    legalEntity 'Организация'
}

type (Partner p) += WHEN p IS LegalEntity THEN PartnerType.legalEntity;

EXTEND FORM partner
    PROPERTIES(p) name[LegalEntity], website
;

DESIGN partner {
    headerDetails {
        MOVE PROPERTY(name(p));
    }
    headerColumn2 {
        MOVE PROPERTY(website(p));
    }
}

newLegalEntity 'Организация' () {
    NEWSESSION {
        NEW l = LegalEntity {
            
            SHOW partner OBJECTS p = l;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM partners
    PROPERTIES newLegalEntity() DRAW p TOOLBAR
    
    LIST LegalEntity OBJECT p
;
EXTEND FORM dialogPartners
    FILTERGROUP type
        FILTER 'Организации' p IS LegalEntity

    PROPERTIES newLegalEntity() DRAW p TOOLBAR
;
