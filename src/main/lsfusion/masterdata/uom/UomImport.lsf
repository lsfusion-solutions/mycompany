MODULE UomImport;

REQUIRE Uom;

NAMESPACE MasterData;

uomExport 'Export units' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Uom i AND i IS Uom) THEN {
            NEW i = Uom {
                id(i) <- '1';
                name(i) <- 'Piece';
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM '{ID}' = id(Uom i), '{Name}' = name(i) TO f;   
        open(f());
    }
}

uomImport 'Import units' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id = A, name = B;
            
            FOR imported(INTEGER i) DO NEW im = Uom {
                id(im) <- id(i);
                name(im) <- name(i);
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }    
}

EXTEND FORM migrationData 
    PROPERTIES uomExport(), uomImport()
;

DESIGN migrationData {
    commons {
        NEW uom {
            horizontal = TRUE;
            caption = 'Units of measure';
            MOVE PROPERTY(uomImport());
            MOVE PROPERTY(uomExport());
        }
    }
}