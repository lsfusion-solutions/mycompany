MODULE Doc;

REQUIRE Authentication, Time, BarCode;

NAMESPACE Utils;

META defineStatus (doc, title)
   CLASS ###doc##Status 'Статус '##title {
       draft 'Черновик'
   }
   
   name 'Имя' (###doc##Status s) = staticCaption(s) IF s IS ###doc##Status CHARWIDTH 15;
   
   status 'Статус' = ABSTRACT CASE ###doc##Status (###doc);
   nameStatus 'Статус' (###doc d) = name(status(d));
   colorStatus 'Цвет' = ABSTRACT CASE COLOR (###doc);
   
   status(###doc d) += WHEN d IS ###doc THEN ###doc##Status.draft;
   colorStatus(###doc d) += WHEN status(d) == ###doc##Status.draft THEN RGB(187,242,210);

   isDraft (###doc d) = status(d) = ###doc##Status.draft;
END

META defineStatusForm (doc, let)
   EXTEND FORM ##doc##s
       PROPERTIES(##let) READONLY nameStatus BACKGROUND colorStatus(##let) BEFORE number(##let)
       FILTERGROUP status
           FILTER 'Черновик' status(##let) = ###doc##Status.draft
   ;
   
   EXTEND FORM ##doc
       PROPERTIES(##let) nameStatus BACKGROUND colorStatus(##let)
   ;
   
   DESIGN ##doc {
       NEW statusPane FIRST {
           caption = 'Статусы';
           type = CONTAINERH;
           alignment = STRETCH;                          
           NEW statusActions {
               type = CONTAINERH;                    
               flex = 1; 
               NEW primaryActions {
                   type = CONTAINERH;
               }
               NEW secondaryActions {
                   type = CONTAINERH;
               }           
           }
           NEW status {
               type = CONTAINERH;
               MOVE PROPERTY(nameStatus(##let));
           }
       }    
   } 
END

META defineDocSearchAction (doc, it, fun, capt)
    fun capt (###doc i, ###it p) = 
        GROUP SUM fun(###doc##Line l) BY doc(l), it(l);
    
    set###fun (###doc i, ###it p, NUMERIC[14,3] q) {
        IF last###doc##Line(i, p) THEN {
            IF q THEN
                fun(###doc##Line l) <- q IF l = last###doc##Line(i, p)
                    WHERE doc(l) = i AND it(l) = p;
            ELSE
                DELETE ###doc##Line l WHERE doc(l) = i AND it(l) == p;
        } ELSE
            IF q THEN
                NEW l = ###doc##Line{
                    doc(l) <- i;
                    it(l) <- p;
                    fun(l) <- q;
                }
    
    }
    
    change###fun (###doc i, ###it p) {
        INPUT q = NUMERIC[14,3] DO
            set###fun(i, p, q);
    }
END

META defineDocSearchTab (doc, it)
    EXTEND FORM doc
        TREE categories c = Category PARENT parent(c)
        PROPERTIES READONLY name(c)
        ORDER name(c)
        
        OBJECTS itm = ###it
        PROPERTIES(itm) READONLY name, nameUom, idBarCode, id
        PROPERTIES(itm) EDIT
        FILTERS level(category(itm), c)
        FILTERGROUP active
            FILTER 'Активный' active(itm) 'F10' DEFAULT  
    ;
    
    DESIGN doc {
        details {
            NEW search {
                caption = 'Подбор';
                type = SPLITH;
                fill = 1;
                MOVE BOX(TREE categories);
                MOVE BOX(itm) { 
                    fill = 2;
                }
            }
        }
    }
END

META defineDocSearch (doc, it, fun, capt, let)
    last###doc##Line (###doc i, ###it p) = GROUP LAST ###doc##Line l ORDER l BY doc(l), it(l);

    @defineDocSearchAction(doc, it, fun, capt);
    @defineDocSearchTab(doc, it);
    
    EXTEND FORM doc
        PROPERTIES(let, itm) fun ON CHANGE change###fun(let, itm)
    ;        
END

META defineDocSearch (doc, it, fun, capt, let, check)
    @defineDocSearch(doc, it, fun, capt, let);
    
    EXTEND FORM doc
        FILTERS ##check(itm)
    ;
END

disableHistory 'Отключить запись истории' = DATA BOOLEAN () PREREAD;
EXTEND FORM MasterData.options 
    PROPERTIES() disableHistory
;
DESIGN MasterData.options {
    general { 
        MOVE PROPERTY(disableHistory()); 
    }
}

META defineDocHistory(doc, let, class, fun)

    @defineDocHistoryHead(##doc, ##let);
    
    WHEN SETCHANGED(###doc##Line l IS ###doc##Line) AND ##doc(l) = ###doc ##let 
                        AND NOT status(##let) = ###doc##Status.draft AND NOT disableHistory() DO {     
        NEW h = ###doc##History {
            ##doc(h) <- ##let;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            type(h) <- 'Добавлена строка';
            description (h) <- 'Добавлена строка с индексом ' + index(l);
        }
    }
    
    WHEN DROPPED(###doc##Line l IS ###doc##Line) AND PREV(##doc(l)) = ###doc ##let 
                        AND NOT status(##let) = ###doc##Status.draft AND NOT disableHistory() DO {     
        NEW h = ###doc##History {
            ##doc(h) <- ##let;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            type(h) <- 'Удалена строка';
            description (h) <- 'Удалена строка с индексом ' + PREV(index(l));
        }
    }
    
    WHEN CHANGED(##fun(###doc##Line l)) AND ##doc(l) = ###doc ##let 
                        AND NOT status(##let) = ###doc##Status.draft AND NOT disableHistory() DO {     
        NEW h = ###doc##History {
            ##doc(h) <- ##let;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            type(h) <- 'Изменено исходное кол-во';
            description (h) <- 'Изменено исходное кол-во продукта ' + name###class(l) + ' на ' + ##fun(l);
        }
    }
    
    WHEN CHANGED(class(###doc##Line l)) AND ##doc(l) = ###doc ##let 
                        AND NOT status(##let) = ###doc##Status.draft AND NOT disableHistory() DO {     
        NEW h = ###doc##History {
            ##doc(h) <- ##let;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            type(h) <- 'Изменен продукт';
            description (h) <- 'Изменен продукт в строке ' + index(l) + ' на ' + name###class(l);
        }
    }
END

META defineDocHistoryHead(doc, let)
    CLASS ###doc##History 'История изменений';
    
    ##doc = DATA ###doc (###doc##History);

    dateTime 'Дата' = DATA DATETIME (###doc##History);
    user 'Пользователь' = DATA User (###doc##History) CHARWIDTH 15;
    nameUser 'Пользователь' (###doc##History l) = name(user(l));
    type 'Тип' = DATA ISTRING[100] (###doc##History);
    description 'Описание' = DATA TEXT (###doc##History);
     
    WHEN SETCHANGED(status(###doc let)) AND NOT disableHistory() DO {     
        NEW l = ###doc##History {
            ##doc(l) <- let;
            dateTime(l) <- currentDateTime();
            user(l) <- currentUser();
            type(l) <- 'Изменен статус';
            description (l) <- 'Изменен статус на ' + staticCaption(status(let));
        }
    }

    EXTEND FORM doc
        OBJECTS doc##l = ###doc##History
        PROPERTIES(doc##l) READONLY dateTime, nameUser, type, description, pdescription = description PANEL
        FILTERS ##doc(doc##l) = let
    ;
    
    DESIGN doc {
        details {
            NEW history {
                caption = 'История';
                fill = 1;
                type = SPLITH;
                MOVE BOX(doc##l) { 
                    fill = 2;
                    PROPERTY(description(doc##l)) { valueHeight = 18; }
                }
                MOVE PROPERTY(pdescription) { panelCaptionAbove = TRUE; fill = 1; }
            }
        }
    }
END

META defineDocRelation(docTo, docFrom, captTo, captFrom, objTo, objFrom)
    docTo = DATA ###docTo (###docFrom) INDEXED;
    numberDate###docTo captTo (###docFrom s) = numberDate(docTo(s)); 
    
    count###docFrom (###docTo t) = GROUP SUM 1 IF docTo(###docFrom f) = t;
    
    // form
    open###docTo (###docFrom f) { 
        edit(docTo(f)); 
    }
    EXTEND FORM docFrom
        PROPERTIES(objFrom) numberDate###docTo ON CHANGE open###docTo(objFrom) SHOWIF docTo(objFrom)
    ;
    DESIGN docFrom {
        footer {
            MOVE PROPERTY(numberDate###docTo(objFrom));
        }
    }
    EXTEND FORM docFrom##s
        PROPERTIES(objFrom) READONLY numberDate###docTo
    ;
END

META defineDocLineRelation(docTo, docFrom, captTo, captFrom, objTo, objFrom)
    docTo##Line = DATA ###docTo##Line (###docFrom##Line) INDEXED;
    
    in (###docTo i, ###docFrom s) = GROUP SUM 1 IF docTo(docTo##Line(###docFrom##Line l)) = i AND docFrom(l) = s MATERIALIZED; 
    
    count###docTo (###docFrom s) = GROUP SUM 1 IF in(###docTo i, s) MATERIALIZED;
    numberDate###docTo captTo (###docFrom s) = GROUP CONCAT numberDate(###docTo i) IF in(i, s), ',' CHARWIDTH 30; 
    numberDate###docTo##Line captTo (###docFrom##Line s) = numberDate(docTo##Line(s)); 

    numberDate###docFrom##s captFrom (###docTo s) = GROUP CONCAT numberDate(###docFrom i) IF in(s, i), ',' CHARWIDTH 30; 
    
    count###docFrom##Line (###docTo b) = GROUP SUM 1 IF docTo(docTo##Line(###docFrom##Line l)) = b;
    
    // form
    open###docTo (###docFrom s) { 
        FOR in(###docTo i, s) DO 
            edit(i); 
    }
    EXTEND FORM docFrom
        PROPERTIES(objFrom) numberDate###docTo ON CHANGE open###docTo(objFrom) SHOWIF count###docTo(objFrom)
    ;
    DESIGN docFrom {
        footer {
            MOVE PROPERTY(numberDate###docTo(objFrom));
        }
    }
    EXTEND FORM docFrom##s
        PROPERTIES(objFrom) READONLY numberDate###docTo
    ;
    EXTEND FORM docTo##s
        PROPERTIES(objTo) READONLY numberDate###docFrom##s
    ;
END

META defineDocDiscount (doc)
    discount 'Скидка, %' = DATA NUMERIC[5,2] (###doc##Line);
    
    discountPrice 'Цена со скидкой' = DATA NUMERIC[10,2] (###doc##Line);
    resultPrice 'Цена итоговая' (###doc##Line l) = OVERRIDE discountPrice(l), price(l);
    
    WHEN LOCAL (CHANGED(price(###doc##Line l)) OR CHANGED(discount(l))) AND NOT CHANGED(discountPrice(l)) DO 
        discountPrice(l) <- NUMERIC[10,2] (price(l) * (100.0 - discount(l)) / 100.0);
    
    recalcDiscount (###doc##Line l) { 
        discount(l) <- NUMERIC[5,2](min(100.0 - (discountPrice(l) * 100.0 / price(l)), 100.0));
    }
    changeDiscountPrice (###doc##Line l) {
        INPUT p = discountPrice(l) CHANGE DO {
            recalcDiscount(l);
        } 
    }
    
END

META defineDocDiscountForm (doc)
    EXTEND FORM doc
        PROPERTIES(l) AFTER price(l) discount, discountPrice ON CHANGE changeDiscountPrice(l)
    ;
END

META defineDocLinesCount(doc, d) 
    count###doc###Lines 'Кол-во строк' (###doc d) = GROUP SUM 1 IF ##doc(###doc###Line l) = d MATERIALIZED;
    
    EXTEND FORM ##doc##s PROPERTIES(d) READONLY count###doc###Lines;
END