MODULE FileUtils;

NAMESPACE Utils;

uploadFile 'Upload' = DATA LOCAL NAMEDFILE ();

META defineObjectFilesForm(object, form, fobj)
    EXTEND FORM form
        OBJECTS of = ###object##File
        PROPERTIES(of) READONLYIF readonly(fobj) name, extension, description
        PROPERTIES(of) READONLY nameUser, dateTime
        PROPERTIES(of) GRID open, DELETE READONLYIF readonly(fobj)
        FILTERS object(of) = fobj

        PROPERTIES(fobj) newFile DRAW of TOOLBAR
        PROPERTIES inputFile = '' CUSTOM 'inputFile' ON CHANGE {
            INPUT f = JSON DO
                IMPORT JSON FROM f FIELDS() STRING name, STRING extension, STRING data DO 
                    IF NOT readonly(fobj) THEN NEW uof = ###object##File {
                        object(uof) <- fobj;
                        file(uof) <- decode(data, 'base64');
                        name(uof) <- name;
                        extension(uof) <- extension;
                    }
        }
    ;

    DESIGN form {
        details {
            MOVE BOX(of) {
                caption = badged('Files', countFiles(fobj));
                MOVE PROPERTY(inputFile);
            }
        }
    }
END

META defineObjectFiles(object, fobj, classCaption)

    CLASS ###object##File classCaption;

    object = DATA ###object (###object##File) INDEXED NONULL DELETE;

    file = DATA RAWFILE (###object##File);

    name '{Name}' = DATA ISTRING (###object##File) CHARWIDTH 15;
    extension 'Extension' = DATA ISTRING (###object##File) CHARWIDTH 5;
    nameExtension (###object##File f) = CONCAT '.', name(f), extension(f);

    description 'Description' = DATA ISTRING (###object##File) CHARWIDTH 30;

    dateTime 'Date' = DATA DATETIME (###object##File);
    dateTime(###object##File f) <- currentDateTime() WHEN SET(f IS ###object##File);

    user = DATA User (###object##File);
    nameUser 'User' (###object##File f) = name(user(f));
    user(###object##File f) <- currentUser() WHEN SET(f IS ###object##File);

    open 'Open' (###object##File f) { open(file(f), nameExtension(f)); } IMAGE 'open.png';

    countFiles 'Files' (###object i) = GROUP SUM 1 IF object(###object##File f) = i;

    firstFile (###object i) = GROUP MIN ###object##File f IF object(f) = i;
    openFirstFile 'Open' (###object i) { open(firstFile(i)); }
    openFiles 'Open' (###object i) {
        FOR object(###object##File f) = i DO
            open(f);
    }

    newFile 'File' (###object o) {
        INPUT uf = NAMEDFILE DO NEW uof = ###object##File {
            object(uof) <- o;
            file(uof) <- RAWFILE(uf);
            name(uof) <- name(uf);
            extension(uof) <- extension(uf);
        }
    } IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

    @defineObjectFilesForm(object, object, fobj);

END