MODULE Selection;

NAMESPACE Utils;

META defineSelection(cls)
    selected 'Отм' = DATA LOCAL NESTED BOOLEAN (cls);
    background(cls o) += WHEN selected(o) THEN RGB(224,224,255);

    select '+' (cls o) {
        selected(o) <- NOT selected(o);
    } CHANGEKEY 'ADD' HIDE;
END

META defineSelectionForm(form, cls, obj)
    selected###form (cls o) = selected(o) AND [FILTER form.obj](o);
    countSelected###form 'Отмечено' () = GROUP SUM 1 IF selected###form(cls o);
    
    invertSelection###form '*' () {
        selected(cls o) <- NOT selected(o) WHERE [FILTER form.obj](o);
    } CHANGEKEY 'MULTIPLY' HIDE;
    
    deselectAll###form '-' () {
        selected(cls o) <- NULL WHERE [FILTER form.obj](o);
    } CHANGEKEY 'SUBTRACT' HIDE;
    
    EXTEND FORM form
        PROPERTIES(obj) selected FIRST
    
        PROPERTIES DRAW obj TOOLBAR select(obj), invertSelection###form(), deselectAll###form()
        
        FILTERGROUP selected
            FILTER 'Отмеченные' selected(obj) 'ctrl O'
    ;
    
    DESIGN form {
        PROPERTY(selected(obj)) { changeOnSingleClick = TRUE; }        
        TOOLBARBOX(obj) {
            NEW selection AFTER TOOLBARLEFT(obj) {
                type = CONTAINERH;
                MOVE PROPERTY(select(obj));
                MOVE PROPERTY(invertSelection###form());
                MOVE PROPERTY(deselectAll###form());
                MOVE FILTERGROUP(selected);
            }
        } 
    }
END

META defineSelectionDelete(form, cls)
    deleteSelected###form 'Удалить' () {
        LOCAL NESTED toDelete = BOOLEAN (cls);
        toDelete(cls o) <- selected###form(o);
        NEWSESSION {
            DELETE cls o WHERE toDelete(o);
            APPLY;
        }
    } CONFIRM;
    
    EXTEND FORM form
        PROPERTIES() deleteSelected###form SHOWIF countSelected###form()
    ;
    
    DESIGN form {
        actions {
            MOVE PROPERTY(deleteSelected###form()) { alignment = STRETCH; }
        }
    }
END

META defineSelectionAction(form, cls, action, caption, container)
    action caption () {
        FOR selected###form(cls o) DO
            action(o);
    }
    
    EXTEND FORM form
        PROPERTIES() action SHOWIF countSelected###form()
    ;
    
    DESIGN form {
        container {
            MOVE PROPERTY(action()) { alignment = STRETCH; }
        }
    }
END