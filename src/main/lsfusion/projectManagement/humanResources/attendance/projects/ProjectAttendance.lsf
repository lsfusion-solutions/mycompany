MODULE ProjectAttendance;

REQUIRE Project, Attendance;

NAMESPACE ProjectManagement;

project = DATA Project (Attendance) INDEXED;
nameProject 'Project' (Attendance e) = name(project(e));
nameCustomer 'Partner' (Attendance e) = namePartner(project(e));

countAttendance 'Attendances' (Project p) = GROUP SUM 1 IF project(Attendance a) = p MATERIALIZED;
hoursAttendance 'Hours' (Project p) = GROUP SUM hours(Attendance a) IF project(a) = p MATERIALIZED;

countAttendance 'Attendances' (Project p, Employee e, DATE d) =
    GROUP SUM 1 IF project(Attendance a) = p AND employee(a) = e AND checkInDate(a) = d;
countEmployeesAttendance 'Employees' (Project p, DATE d) = GROUP SUM 1 IF countAttendance(p, Employee e, d);

hoursAttendance 'Hours' (Project p, Employee e, DATE d) = 
    GROUP SUM hours(Attendance a) IF project(a) = p AND employee(a) = e AND checkInDate(a) = d;

EXTEND FORM attendance
    PROPERTIES(a) nameProject
;

DESIGN attendance {
    header {
        MOVE PROPERTY(nameProject(a));
    }
}

EXTEND FORM attendances
    PROPERTIES(a) READONLY nameProject
;

EXTEND FORM project
    OBJECTS at = Attendance
    PROPERTIES(at) nameEmployee, checkIn, checkOut, hours,
                   latitude = checkInLatitude, longitude = checkInLongitude,
                   NEW, DELETE
    FILTERS project(at) = p
;

DESIGN project {
    details {
        NEW attendances {
            caption = badged('Attendances', '(' + countAttendance(p) + ')' + '(' + hoursAttendance(p) + ')');
            MOVE BOX(at) { caption = ''; }
        }
    }
}

EXTEND FORM employee
    PROPERTIES(at) READONLY nameProject
;

skipAttendance = ABSTRACT BOOLEAN (Project, CustomUser);