MODULE ProjectType;

REQUIRE ProjectManagementSettings, Numerator;

NAMESPACE ProjectManagement;

CLASS ProjectType 'Project type';
name '{Name}' = DATA ISTRING[50] (ProjectType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (ProjectType);
projectType = GROUP AGGR ProjectType t BY id(t);

numerator 'Numerator' = DATA Numerator (ProjectType);
nameNumerator 'Numerator' (ProjectType o) = name(numerator(o));

FORM projectType 'Project type'
    OBJECTS o = ProjectType PANEL 
    PROPERTIES(o) name, id, nameNumerator
     
    EDIT ProjectType OBJECT o;
;

DESIGN projectType {
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            MOVE PROPERTY(name(o)) { charWidth = 30; }
            MOVE PROPERTY(id(o));
            MOVE PROPERTY(nameNumerator(o));
        }
        NEW pane {
            lines = 3;
            NEW default {
                caption = 'Default';
            }
            NEW otherInformation {
                caption = 'Other information';
            }
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM projectTypes 'Project types'
    OBJECTS o = ProjectType
    PROPERTIES(o) READONLY name, id
    
    LIST ProjectType OBJECT o;
;

EXTEND FORM options
    OBJECTS pt = ProjectType
    PROPERTIES(pt) READONLY name, id
    PROPERTIES(pt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(pt);
    }
}

// default type
countProjectTypes 'Number of types' () = GROUP SUM 1 IF r IS ProjectType; 
firstProjectType = GROUP LAST ProjectType r ORDER DESC r WHERE r IS ProjectType;

defaultProjectType = firstProjectType() IF countProjectTypes() = 1;
