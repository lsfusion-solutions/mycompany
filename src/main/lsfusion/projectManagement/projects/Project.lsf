MODULE Project;

REQUIRE FileUtils, MetaNumerator, Authentication,
        ProjectManagementSettings, ProjectType, Company;

NAMESPACE ProjectManagement;

CLASS Project 'Project';

type 'Type' = DATA ProjectType (Project) NONULL;
nameType 'Type' (Project s) = name(type(s));
WHEN LOCAL SET(Project s IS Project) AND NOT CHANGED(type(s)) DO type(s) <- defaultProjectType(); 

@defineNumberType(project);

name 'Name' = DATA STRING (Project) CHARWIDTH 20 IN id;

company 'Company' = DATA Company (Project);
nameCompany 'Company' (Project p) = name(company(p));
WHEN LOCAL SET(Project p IS Project) AND NOT CHANGED(company(p)) DO company(p) <- defaultCompany();

partner 'Partner' = DATA Partner (Project);
namePartner 'Partner' (Project p) = name(partner(p)) IN id;

startDate 'Start date' = DATA DATE (Project) IN id;
startDate(Project p) <- currentDate() WHEN SET(p IS Project);
endDate 'End date' = DATA DATE (Project);

description 'Description' = DATA RICHTEXT (Project);

FORM project 'Project'
    OBJECTS p = Project PANEL
    
    PROPERTIES(p) nameType, number, name, nameCompany, namePartner, startDate, endDate,
                  description
    
    EDIT Project OBJECT p
;

DESIGN project {
    caption = CONCAT ' ', 'Project', name(p), number(p);
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Statuses';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1; 
                NEW primaryActions {
                    type = CONTAINERH;
                }
                NEW secondaryActions {
                    type = CONTAINERH;
                }           
            }
            NEW status {
                type = CONTAINERH;
            }
        }    
        NEW header {
            alignment = STRETCH;   
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(p));                   
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(name(p));                   
                MOVE PROPERTY(nameCompany(p));
                MOVE PROPERTY(namePartner(p));
            }
            NEW headerRight {
                MOVE PROPERTY(startDate(p));
                MOVE PROPERTY(endDate(p));
            }    
        }
        NEW details {
            type = TABBED;
            fill = 1;
            NEW description {
                caption = 'Description';
                MOVE PROPERTY(description(p)) { caption = ''; fill = 1; }
            }
        }
    }  
}

FORM projects 'Projects'
    OBJECTS p = Project

    PROPERTIES(p) READONLY number, startDate, endDate, namePartner, nameType, name, nameCompany
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
    
    LIST Project OBJECT p
;

@defineObjectFiles(project, p, 'Project file');

NAVIGATOR {
    operations {
        NEW projects;
    }
}