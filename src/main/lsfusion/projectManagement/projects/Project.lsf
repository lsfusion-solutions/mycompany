MODULE Project;

REQUIRE FileUtils, MetaNumerator, Authentication, Icon, Doc, Comments,
        ProjectManagementSettings, ProjectStatus, ProjectType, Company, Employee;

NAMESPACE ProjectManagement;

CLASS Project 'Project';
TABLE project(Project);

readonly = ABSTRACT CASE BOOLEAN (Project);

type 'Type' = DATA ProjectType (Project) NONULL;
nameType 'Type' (Project s) = name(type(s));
type(Project p) <- defaultProjectType() WHEN SET(p IS Project); 

@defineNumberType(project);

name '{Name}' = DATA STRING (Project) CHARWIDTH 15 IN id;

company 'Company' = DATA Company (Project);
nameCompany 'Company' (Project p) = name(company(p));
WHEN LOCAL SET(Project p IS Project) AND NOT CHANGED(company(p)) DO company(p) <- defaultCompany();

partner 'Partner' = DATA Partner (Project);
namePartner 'Partner' (Project p) = name(partner(p)) IN id;

startDate 'Start date' = DATA DATE (Project) IN id;
startDate(Project p) <- currentDate() WHEN SET(p IS Project);
endDate 'End date' = DATA DATE (Project);

status 'Status' = DATA ProjectStatus (Project);
nameStatus 'Status' (Project t) = name(status(t));

closed 'Closed' (Project p) = closed(status(p));
archived 'Archived' (Project p) = currentDate() > endDate(p);

active '{Active}' (Project p) = p IS Project AND NOT archived(p) AND NOT closed(p);

manager = DATA Employee (Project);
nameManager 'Manager' (Project o) = MasterData.name(manager(o));

manager(Project o) <- currentUser() IF currentUser() IS Employee WHEN SET(o IS Project);

description 'Description' = DATA RICHTEXT (Project);

FORM project 'Project'
    OBJECTS p = Project PANEL
    
    PROPERTIES(p) nameType, number, name, nameCompany, namePartner, startDate, endDate, 
                  nameStatus, nameManager, description
    
    EDIT Project OBJECT p
;

DESIGN project {
    caption = badged('Project', (CONCAT ' / ', name(p), number(p)));
    OBJECTS {        
        NEW statusPane FIRST {
            horizontal = TRUE;
            alignment = STRETCH;                          
            NEW statusActions {
                horizontal = TRUE;                    
                flex = 1; 
                NEW primaryActions {
                    horizontal = TRUE;
                }
                NEW secondaryActions {
                    horizontal = TRUE;
                }           
            }
            NEW status {
                horizontal = TRUE;
            }
        }    
        NEW header {
            alignment = STRETCH;   
            horizontal = TRUE;
            NEW headerLeft {
                MOVE PROPERTY(nameType(p));                   
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(name(p));                   
                MOVE PROPERTY(namePartner(p));
            }
            NEW headerRight {
                MOVE PROPERTY(startDate(p));
                MOVE PROPERTY(endDate(p));
                MOVE PROPERTY(nameStatus(p));
                MOVE PROPERTY(nameCompany(p));
                MOVE PROPERTY(nameManager(p));
            }    
        }
        NEW details {
            tabbed = TRUE;
            fill = 1;
            NEW description {
                caption = 'Description';
                MOVE PROPERTY(description(p)) { caption = ''; fill = 1; }
            }
        }
    }  
}

FORM dialogProjects 'Projects'
    OBJECTS p = Project
    PROPERTIES(p) READONLY number, startDate, endDate, namePartner, nameType, name, nameCompany, nameManager

    LIST Project OBJECT p
;

FORM projects 'Projects'
    OBJECTS p = Project

    PROPERTIES(p) READONLY number, nameStatus, startDate, endDate, namePartner, nameType, name, nameCompany, nameManager
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE

    FILTERGROUP active
        FILTER 'Opened' NOT closed(p) DEFAULT
        FILTER 'Closed' closed(p)

    FILTERGROUP my
        FILTER 'Assigned to me' manager(p) = currentUser() 'F5'
;

@defineDocObjectsForm(projects, p, '{Projects}');

@defineObjectComments(project, p, details);

@defineObjectFiles(project, p, 'Project file');

NAVIGATOR {
    operations {
        NEW projects;
    }
}