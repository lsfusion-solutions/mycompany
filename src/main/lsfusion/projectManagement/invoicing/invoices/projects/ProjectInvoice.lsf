MODULE ProjectInvoice;

REQUIRE Project, Invoice;

NAMESPACE ProjectManagement;

// type
invoiceType = DATA InvoiceType (ProjectType);
nameInvoiceType 'Invoice type' (ProjectType t) = name(invoiceType(t));
EXTEND FORM projectType PROPERTIES nameInvoiceType(o);

project = DATA Project (Invoice) INDEXED;
nameProject 'Project' (Invoice i) = name(project(i));

countInvoices 'Invoices' (Project p) = GROUP SUM 1 IF project(Invoice i) = p;

project (InvoiceLine l) = project(invoice(l));

EXTEND FORM invoice
    PROPERTIES(i) nameProject
;

DESIGN invoice {
    headerRight {
        MOVE PROPERTY(nameProject(i));
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY nameProject
;

EXTEND FORM project
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY number, dateTime, 
                           imagedNameStatus BACKGROUND colorStatus(i), nameType, 
                           dueDateTime, namePaymentTerms, amount
    FILTERS project(i) = p   
;

DESIGN project {
    details  {
        MOVE BOX(i) {
            caption = badged('Invoices', countInvoices(p));
        }
    }
}

fill ABSTRACT LIST (Invoice, Project);

create (Invoice i, Project p) {
    project(i) <- p;

    type(i) <- invoiceType(type(p));    

    company(i) <- company(p);
    customer(i) <- partner(p);
    
    fill(i, p);
}

createInvoice 'Create Invoice' (Project p) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW i = Invoice {
            create(i, p);
            
            SHOW invoice OBJECTS i = i DOCKED;
        }
    }
}

EXTEND FORM project
    PROPERTIES(p) 'Add' = createInvoice DRAW i TOOLBAR
    PROPERTIES(i) NEWSESSION EDIT, DELETE
;
