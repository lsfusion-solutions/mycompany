MODULE TaskWorkflow;

REQUIRE ProjectTask, ProjectAssignment;

NAMESPACE ProjectManagement;

TABLE taskWorkflowType(ProjectRole, TaskType, TaskStatus, TaskStatus);

allow 'Allow' = DATA BOOLEAN (ProjectRole, TaskType, TaskStatus, TaskStatus);
countAllow (ProjectRole r, TaskType t) = GROUP SUM 1 IF allow(r, t, TaskStatus from, TaskStatus to) MATERIALIZED;

allowAuthor 'Allow' = DATA BOOLEAN (ProjectRole, TaskType, TaskStatus, TaskStatus);
countAllowAuthor (ProjectRole r, TaskType t) = GROUP SUM 1 IF allowAuthor(r, t, TaskStatus from, TaskStatus to) MATERIALIZED;

allowAssignedTo 'Allow' = DATA BOOLEAN (ProjectRole, TaskType, TaskStatus, TaskStatus);
countAllowAssignedTo (ProjectRole r, TaskType t) = GROUP SUM 1 IF allowAssignedTo(r, t, TaskStatus from, TaskStatus to) MATERIALIZED;

countAllowAny (ProjectRole r, TaskType t) = countAllow(r, t) (+) countAllowAuthor(r, t) (+) countAllowAssignedTo(r, t) MATERIALIZED;

forbid (Task t, TaskType tt, Employee e, ProjectRole r, TaskStatus from, TaskStatus to) = countAllowAny(r, tt) AND //e IS Employee AND from IS TaskStatus AND to IS TaskStatus AND
    NOT allow(r, tt, from, to) AND NOT (allowAuthor(r, tt, from, to) AND author(t) = e) AND NOT (allowAssignedTo(r, tt, from, to) AND assignedTo(t, e));

allow (Task t, Employee e, TaskStatus from, TaskStatus to) =
    GROUP SUM 1 IF assignment(project(t), e, ProjectRole r) AND NOT forbid(t, type(t), e, r, from, to);

CONSTRAINT CHANGED(status(Task t)) AND countAssignments(currentUser()) AND 
    PREV(status(t)) AND NOT allow(t, currentUser(), PREV(status(t)), status(t))
    CHECKED BY status[Task]
        MESSAGE 'New task status is forbidden for the current user';

EXTEND FORM options
    OBJECTS r = ProjectRole PANEL
    PROPERTIES(r) name SELECTOR

    OBJECTS tst = TaskType PANEL
    PROPERTIES(tst) name SELECTOR

    OBJECTS tscc = TaskStatus

    OBJECTS tscr = TaskStatus
    PROPERTIES(tscr) READONLY name
    PROPERTIES allow(r, tst, tscr, tscc) COLUMNS (tscc) HEADER name(tscc)

    OBJECTS tscra = TaskStatus
    PROPERTIES(tscra) READONLY name
    PROPERTIES allowAuthor(r, tst, tscra, tscc) COLUMNS (tscc) HEADER name(tscc)

    OBJECTS tscras = TaskStatus
    PROPERTIES(tscras) READONLY name
    PROPERTIES allowAssignedTo(r, tst, tscras, tscc) COLUMNS (tscc) HEADER name(tscc)
;

DESIGN options {
    tabbedPane {
        NEW statusChange {
            caption = 'Workflow';
            NEW statusChangeHeader {
                horizontal = TRUE;
                MOVE PROPERTY(name(r)) { caption = 'Project role'; }
                MOVE PROPERTY(name(tst)) { caption = 'Task type'; };
            }
            MOVE BOX(tscr) {
                caption = '';
                PROPERTY(allow(r, tst, tscr, tscc)) { charWidth = 10; }
            }
            MOVE BOX(tscra) {
                caption = 'Author';
                PROPERTY(allowAuthor(r, tst, tscra, tscc)) { charWidth = 10; }
            }
            MOVE BOX(tscras) {
                caption = 'Assigned to';
                PROPERTY(allowAssignedTo(r, tst, tscras, tscc)) { charWidth = 10; }
            }
        }
    }
}
