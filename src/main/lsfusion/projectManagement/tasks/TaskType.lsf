MODULE TaskType;

REQUIRE ProjectManagementSettings, TaskStatus;

NAMESPACE ProjectManagement;

CLASS TaskType 'Task type';
name '{Name}' = DATA ISTRING[50] (TaskType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (TaskType);
taskType = GROUP AGGR TaskType t BY id(t);

in 'Incl.' = DATA BOOLEAN (TaskType, TaskStatus);
countStatus (TaskType t) = GROUP SUM 1 IF in(t, TaskStatus l) MATERIALIZED;

allow (TaskType t, TaskStatus s) = in(t, s) OR (t IS TaskType AND s IS TaskStatus AND NOT countStatus(t));

FORM taskType 'Task type'
    OBJECTS o = TaskType PANEL
    PROPERTIES(o) name, id

    OBJECTS s = TaskStatus
    PROPERTIES(s) READONLY name
    PROPERTIES             in(o, s)

    EDIT TaskType OBJECT o;
;

DESIGN taskType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM taskTypees 'Task types'
    OBJECTS o = TaskType
    PROPERTIES(o) READONLY name, id
    LIST TaskType OBJECT o
;

EXTEND FORM options
    OBJECTS tt = TaskType
    PROPERTIES(tt) READONLY name, id
    PROPERTIES(tt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(tt) { caption = 'Task types'; }
    }
}

// default type
countTaskTypes 'Number of types' () = GROUP SUM 1 IF t IS TaskType;
firstTaskType = GROUP LAST TaskType r ORDER DESC r WHERE r IS TaskType;

defaultTaskType = firstTaskType() IF countTaskTypes() = 1;

firstTaskStatus (TaskType t) = GROUP LAST TaskStatus l ORDER DESC sortingOrder(l), l WHERE allow(t, l) MATERIALIZED;