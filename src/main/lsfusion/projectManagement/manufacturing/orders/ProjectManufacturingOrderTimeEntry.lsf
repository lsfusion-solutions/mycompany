MODULE ProjectManufacturingOrderTimeEntry;

REQUIRE ProjectManufacturingOrder, ProjectTimeEntry;

NAMESPACE ProjectManufacturing;

manufacturingOrder = DATA ManufacturingOrder (TimeEntry) INDEXED;
numberDateManufacturingOrder 'Manufacturing order' (TimeEntry t) = numberDate(manufacturingOrder(t));
nameItemManufacturingOrder '{Item}' (TimeEntry t) = nameItem(manufacturingOrder(t));

countTimeEntry 'Time entries' (ManufacturingOrder o) = GROUP SUM 1 IF manufacturingOrder(TimeEntry t) = o MATERIALIZED;
hoursTimeEntry 'Hours' (ManufacturingOrder o) = GROUP SUM hours(TimeEntry t) IF manufacturingOrder(t) = o MATERIALIZED;

WHEN LOCAL SETCHANGED(project(manufacturingOrder(TimeEntry t))) AND NOT CHANGED(project(t)) DO
    project(t) <- project(manufacturingOrder(t));

CONSTRAINT manufacturingOrder(TimeEntry t) AND project(t) AND NOT project(t) = project(manufacturingOrder(t))
    CHECKED BY manufacturingOrder[TimeEntry]
    MESSAGE 'The time entry manufacturing order does not match the project';

clone (TimeEntry to, TimeEntry from) + {
    manufacturingOrder(to) <- manufacturingOrder(from);
}

EXTEND FORM timeEntry
    PROPERTIES(t) numberDateManufacturingOrder
;

DESIGN timeEntry {
    header {
        MOVE PROPERTY(numberDateManufacturingOrder(t));
    }
}

EXTEND FORM timeEntries
    PROPERTIES(t) READONLY numberDateManufacturingOrder, nameItemManufacturingOrder
;

EXTEND FORM project
    PROPERTIES(te) numberDateManufacturingOrder
;

EXTEND FORM manufacturingOrder
    OBJECTS te = TimeEntry
    PROPERTIES(te) nameEmployee, date, hours, nameType, description, NEW, DELETE
    FILTERS manufacturingOrder(te) = o
;

DESIGN manufacturingOrder {
    details {
        NEW timeEntries {
            caption = badged('Time entries', '(' + countTimeEntry(o) + ')' + '(' + hoursTimeEntry(o) + ')');
            MOVE BOX(te) { caption = ''; }
        }
    }
}

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLY countTimeEntry, hoursTimeEntry
;