MODULE TimeEntry;

REQUIRE Project, Employee, TimeEntryType;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

CLASS TimeEntry 'Time entry';
TABLE timeEntry (TimeEntry);

date 'Date' = DATA DATE (TimeEntry);
date(TimeEntry e) <- currentDate() WHEN SET(e IS TimeEntry);

hours 'Hours' = DATA NUMERIC[8,2] (TimeEntry);
hoursToChar (NUMERIC[8,2] h) = STRING(rtrim(toChar(h, 'FM99999.99'),'.'));

employee 'Employee' = DATA Employee (TimeEntry) NONULL;
nameEmployee 'Employee' (TimeEntry t) = name(employee(t));
WHEN LOCAL SET(TimeEntry t IS TimeEntry) AND NOT employee(t) AND currentUser() IS Employee DO 
    employee(t) <- currentUser();

type 'Type' = DATA TimeEntryType (TimeEntry);
nameType 'Type' (TimeEntry e) = name(type(e));
type (TimeEntry e) <- defaultTimeEntryType() WHEN SET(e IS TimeEntry);

description 'Description' = DATA ISTRING (TimeEntry) CHARWIDTH 20;

INDEX employee(TimeEntry e), date(e), e;
INDEX date(TimeEntry e), employee(e);

timeEntry (DATE d, Employee e, TimeEntryType et) = GROUP MIN TimeEntry t IF date(t) = d AND employee(t) = e AND type(t) = et;
hours 'Hours' (DATE d, Employee e, TimeEntryType et) =
    GROUP SUM hours(TimeEntry t) IF type(t) = et AND employee(t) = e AND date(t) = d;

countTimeEntry (DATE d, Employee e, TimeEntryType et) = GROUP SUM 1 IF date(TimeEntry t) = d AND employee(t) = e AND type(t) = et;
countTimeEntryType (DATE d, Employee e) = GROUP SUM 1 IF countTimeEntry(d, e, TimeEntryType et); 

timeEntry (DATE d, Employee e) = GROUP MIN TimeEntry t IF date(t) = d AND employee(t) = e;
hours 'Hours' (DATE d, Employee e) =
    GROUP SUM hours(TimeEntry t) IF date(t) = d AND employee(t) = e;
hours 'Hours' (Employee e, DATE from, DATE to) =
    GROUP SUM hours(TimeEntry t) IF employee(t) = e AND date(t) >= from AND date(t) <= to;
hours 'Hours' (Employee e, INTERVAL[DATE] i) = hours(e, from(i), to(i));

FORM timeEntry 'Time entry'
    OBJECTS t = TimeEntry PANEL
    PROPERTIES(t) nameEmployee, date, hours, nameType, description
    
    EDIT TimeEntry OBJECT t
;

DESIGN timeEntry {
    OBJECTS {
        NEW statusPane {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW statusActions {
                horizontal = TRUE;
                flex = 1;
                NEW primaryActions {
                    horizontal = TRUE;
                }
                NEW secondaryActions {
                    horizontal = TRUE;
                }
            }
            NEW status {
                horizontal = TRUE;
            }
        }
        NEW pane {
            NEW header {
                alignment = STRETCH;
                MOVE PROPERTY(nameEmployee(t));
                MOVE PROPERTY(nameType(t));
            }
            NEW time {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(date(t));
                MOVE PROPERTY(hours(t));
            }
            MOVE PROPERTY(description(t)) { alignment = STRETCH; charWidth = 80; }
        }
    }
}

FORM timeEntries 'Time entries'
    OBJECTS t = TimeEntry
    PROPERTIES(t) READONLY nameEmployee, date, hours, nameType, description
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    
    LIST TimeEntry OBJECT t
;

NAVIGATOR {
    operations {
        NEW timeEntries;
    }
}

// copy
clone ABSTRACT LIST (TimeEntry, TimeEntry);
clone (TimeEntry to, TimeEntry from) + {
    employee(to) <- employee(from);
    date(to) <- date(from);
    hours(to) <- hours(from);
    type(to) <- type(from);
    description(to) <- description(from);
}

copy 'Copy' (TimeEntry from) {
    NEWSESSION {
        NEW to = TimeEntry {
            clone(to, from);
            SHOW timeEntry OBJECTS t = to;
        }
    }
}

EXTEND FORM timeEntry
    PROPERTIES(t) copy
;

DESIGN timeEntry {
    secondaryActions {
        MOVE PROPERTY(copy(t)) { valueClass = 'btn-secondary'; }
    }
}