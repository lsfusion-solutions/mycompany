MODULE TimesheetSupervisorMobile;

REQUIRE Timesheet;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

FORM timesheetSupervisorMobile 'Timesheet'
    OBJECTS d = DATE PANEL
    PROPERTIES d '' = VALUE(d)
    PROPERTIES dow '' = extractDOWName (d) READONLY

    PROPERTIES prevWeek '<<' = { SEEK timesheetSupervisorMobile.d = subtract(d, 7); },
               prevDate '<' = { SEEK timesheetSupervisorMobile.d = subtract(d, 1); },
               nextDate '>' = { SEEK timesheetSupervisorMobile.d = sum(d, 1); },
               nextWeek '>>' = { SEEK timesheetSupervisorMobile.d = sum(d, 7); }

    PROPERTIES(d) copyApply, clearApply
    
    PROPERTIES() nameTimesheetProject, nameTimesheetTimeEntryType

    EVENTS ON INIT {
        timesheetProject() <- defaultProject(currentUser());
        timesheetTimeEntryType() <- OVERRIDE defaultTimeEntryType(), firstTimeEntryType();
    }

    OBJECTS e = Employee
    PROPERTIES(e) READONLY name
    PROPERTIES hoursTimesheet(d, e) BACKGROUND backgroundHoursTimesheet(d, e)
               READONLYIF NOT timesheetTimeEntryType()
               ON CHANGE { 
                   INPUT n = hoursTimesheet(d, e) DO
                       changeSaveTimesheetHours(d, e, timesheetTimeEntryType(), n, NULL, TRUE); 
               }
    FILTERS assigned(timesheetProject(), e) OR (NOT countAssignments(timesheetProject()) AND accessAllProjects(currentUser()))
    
    OBJECTS h = TimeEntryHours
    PROPERTIES NOWAIT changeHours = { changeSaveTimesheetHours(d, e, timesheetTimeEntryType(), hours(h), h, TRUE); }
               COLUMNS (h) HEADER nameHours(h)
    FILTERS type(h) = timesheetTimeEntryType()
;

DESIGN timesheetSupervisorMobile {
    OBJECTS {
        MOVE PROPERTY(nameTimesheetProject()) {
            alignment = STRETCH;
            caption = '';
            placeholder = 'Project';
        }
        NEW dates {
            alignment = STRETCH;
            horizontal = TRUE;
            MOVE PROPERTY(prevWeek);
            MOVE PROPERTY(prevDate);
            MOVE PROPERTY(d);
            MOVE PROPERTY(nextDate);
            MOVE PROPERTY(nextWeek);
            MOVE PROPERTY(dow) { width = 20; }
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
            NEW timesheet {
                caption = 'Timesheet';
                NEW hoursHeader {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE PROPERTY(copyApply(d)) { fill = 1; }
                    MOVE PROPERTY(clearApply(d)) { fill = 1; }
                }
                MOVE GRID(e) {
                    PROPERTY(name(e)) { caption = ''; }
                    PROPERTY(hoursTimesheet(d, e)) { caption = ''; charWidth = 4; };
                }
                REMOVE BOX(e);
                MOVE PROPERTY(nameTimesheetTimeEntryType()) {
                    alignment = STRETCH;
                    caption = '';
                    placeholder = 'Type';
                }
                MOVE PROPERTY(changeHours) {
                    alignment = STRETCH;
                }
            }
        }

        REMOVE TOOLBARBOX;
    }
} 

NAVIGATOR {
    dashboards {
        NEW 'Supervisor timesheet mobile' = timesheetSupervisorMobile;
    }
}