MODULE ProjectPurchaseOrder;

REQUIRE Project, PurchaseOrder;

NAMESPACE ProjectPurchase;

// type
orderType = DATA OrderType (ProjectType);
nameOrderType 'Purchase order type' (ProjectType t) = name(orderType(t));
EXTEND FORM projectType PROPERTIES purchaseOrderType = nameOrderType(o);

project = DATA Project (Order) INDEXED;
nameProject 'Project' (Order i) = name(project(i));
@definePropertyValue(order, project, 'Project', nameProject);

countOrders 'Purchase orders' (Project p) = GROUP SUM 1 IF project(Order i) = p;

EXTEND FORM order
    PROPERTIES(o) READONLYIF readonly(o) nameProject
;

DESIGN order {
    headerRight {
        MOVE PROPERTY(nameProject(o));
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLY nameProject
;

EXTEND FORM project
    OBJECTS po = Order
    PROPERTIES(po) READONLY number, dateTime,
                            imagedNameStatus BACKGROUND colorStatus(po), nameType,
                            scheduledDateTime, nameLocation, amount
    FILTERS project(po) = p
;

DESIGN project {
    details  {
        MOVE BOX(po) {
            caption = badged('Purchase orders', countOrders(p));
        }
    }
}

fill ABSTRACT LIST (Order, Project);

create (Order o, Project p) {
    project(o) <- p;

    type(o) <- orderType(type(p));

    company(o) <- company(p);

    fill(o, p);
}

createOrder 'Create Purchase Order' (Project p) {
    APPLY;
    IF canceled() THEN RETURN;

    NEWSESSION {
        NEW o = Order {
            create(o, p);

            SHOW order OBJECTS o = o DOCKED;
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM project
    PROPERTIES(p) createPurchaseOrder 'Add' = createOrder DRAW po TOOLBAR
    PROPERTIES(po) NEWSESSION EDIT, DELETE
;

@defineHistoryProperty(order, project, 'Project', nameProject);