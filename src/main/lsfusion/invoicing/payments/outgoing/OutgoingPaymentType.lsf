MODULE OutgoingPaymentType;

REQUIRE Numerator, InvoicingSettings, BankAccount, CashAccount;

NAMESPACE Invoicing;

CLASS OutgoingPaymentType 'Outgoing payment type';
name '{Name}' = DATA ISTRING[50] (OutgoingPaymentType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (OutgoingPaymentType);
outgoingPaymentType = GROUP AGGR OutgoingPaymentType t BY id(t);

interpay 'Internal payment' = DATA BOOLEAN (OutgoingPaymentType);

cash 'Cash account' = DATA BOOLEAN (OutgoingPaymentType);
bank 'Bank account' = DATA BOOLEAN (OutgoingPaymentType);

CONSTRAINT OutgoingPaymentType t IS OutgoingPaymentType AND NOT cash(t) AND NOT bank(t)
    MESSAGE 'At least one account type must be allowed';

numerator 'Numerator' = DATA Numerator (OutgoingPaymentType);
nameNumerator 'Numerator' (OutgoingPaymentType o) = name(numerator(o));

FORM outgoingPaymentType 'Outgoing payment type'
    OBJECTS o = OutgoingPaymentType PANEL 
    PROPERTIES(o) name, id, nameNumerator,
                  interpay, cash, bank
     
    EDIT OutgoingPaymentType OBJECT o;
;

DESIGN outgoingPaymentType {
    OBJECTS {
        NEW typeAcoount {
            caption = 'Allowed account types';
            horizontal = TRUE;
            MOVE PROPERTY(cash(o));
            MOVE PROPERTY(bank(o));
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }   
}

FORM outgoingPaymentTypes 'Types of outgoing payments'
    OBJECTS o = OutgoingPaymentType
    PROPERTIES(o) READONLY name, id
    
    LIST OutgoingPaymentType OBJECT o;
;

EXTEND FORM options
    OBJECTS opt = OutgoingPaymentType
    PROPERTIES(opt) READONLY name, id
    PROPERTIES(opt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(opt);
    }
}

defaultAccount(Partner p, OutgoingPaymentType t) = CASE OVERRIDE
    WHEN bank(t) AND cash(t) THEN defaultAccount(p)
    WHEN bank(t) THEN defaultBankAccount(p)
    WHEN cash(t) THEN defaultCashAccount(p)
;

firstPartnerOutgoingPaymentType () = GROUP LAST OutgoingPaymentType t ORDER DESC t WHERE t IS OutgoingPaymentType AND NOT interpay(t);
firstInterpayOutgoingPaymentType () = GROUP LAST OutgoingPaymentType t ORDER DESC t WHERE interpay(t);