MODULE OutgoingPayment;

REQUIRE Time, MetaNumerator,
        Partner, Company, Account, Doc,
        OutgoingPaymentType;

NAMESPACE Invoicing;

CLASS OutgoingPayment 'Исходящий платеж';

@defineStatus(outgoingPayment, 'исходящего платежа');

// type
type 'Тип' = DATA OutgoingPaymentType (OutgoingPayment) NONULL;
nameType 'Тип' (OutgoingPayment s) = name(type(s));

dateTime 'Дата' = DATA DATETIME (OutgoingPayment) NONULL;
dateTime(OutgoingPayment p) <- currentDateTime() WHEN SET(p IS OutgoingPayment);

@defineNumberType(outgoingPayment);

// partners
partner 'Контрагент' = DATA Partner (OutgoingPayment);
namePartner 'Контрагент' (OutgoingPayment p) = name(partner(p));

company 'Компания' = DATA Company (OutgoingPayment) NONULL;
nameCompany 'Компания' (OutgoingPayment p) = name(company(p));

// accounts
// partner
partnerAccount = DATA Account (OutgoingPayment);
namePartnerAccount 'Счет контрагента' (OutgoingPayment p) = name(partnerAccount(p));

WHEN LOCAL CHANGED(partner(OutgoingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO partnerAccount(p) <- defaultAccount(partner(p));

CONSTRAINT partnerAccount(OutgoingPayment p) AND NOT holder(partnerAccount(p)) = partner(p)
                CHECKED BY partnerAccount
                MESSAGE 'Владелец счета контрагента не совпадает с контрагентом в исходящем платеже';

// company
companyAccount = DATA Account (OutgoingPayment);
nameCompanyAccount 'Счет контрагента' (OutgoingPayment p) = name(companyAccount(p));

WHEN LOCAL CHANGED(company(OutgoingPayment p)) AND NOT CHANGED(companyAccount(p)) DO companyAccount(p) <- defaultAccount(company(p));

CONSTRAINT companyAccount(OutgoingPayment p) AND NOT holder(companyAccount(p)) = company(p)
                CHECKED BY companyAccount
                MESSAGE 'Владелец счета компании не совпадает с компанией в исходящем платеже';

amount 'Сумма' = DATA NUMERIC[14,2] (OutgoingPayment) NONULL;

note 'Примечание' = DATA ISTRING[50] (OutgoingPayment);
reference 'Ссылка' = DATA ISTRING[50] (OutgoingPayment);

// forms
FORM outgoingPayment 'Исходящий платеж'
    OBJECTS p = OutgoingPayment PANEL 
    PROPERTIES(p) nameType, dateTime, number, amount,
                  namePartner, namePartnerAccount, 
                  nameCompany, nameCompanyAccount,
                  note, reference
               
    
    EDIT OutgoingPayment OBJECT p
;

DESIGN outgoingPayment {
    OBJECTS {
        NEW header {
            NEW params {
                caption = 'Параметры';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameType(p)) { notNull = TRUE; } 
                MOVE PROPERTY(dateTime(p)); 
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(amount(p));
            }
            NEW partner {
                caption = 'Контрагент';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(namePartner(p));
                MOVE PROPERTY(namePartnerAccount(p));
            }
            NEW company { 
                caption = 'Компания';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameCompany(p)) { notNull = TRUE; }
                MOVE PROPERTY(nameCompanyAccount(p));
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                alignment = STRETCH;
                MOVE PROPERTY(note(p)); 
                MOVE PROPERTY(reference(p));    
            }
        }
        NEW details {
            type = TABBED;
            fill = 1;
        }
    }
}

FORM outgoingPayments 'Исходящие платежи'
    OBJECTS p = OutgoingPayment
    PROPERTIES(p) READONLY nameType, dateTime, number, amount,
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           note, reference
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST OutgoingPayment OBJECT p
;

@defineStatusForm(outgoingPayment, p);

NAVIGATOR {
    invoicing {
        operations {
            NEW outgoingPayments;
        }
    }
}
