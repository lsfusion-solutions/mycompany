MODULE OutgoingPayment;

REQUIRE Time, MetaNumerator, Doc, DateUtils,
        Partner, Company,
        Account, BankAccount, CashAccount,
        OutgoingPaymentType;

NAMESPACE Invoicing;

CLASS OutgoingPayment 'Outgoing payment';

@defineDocStatus(outgoingPayment, 'Outgoing payment status');

// type
type 'Type' = DATA OutgoingPaymentType (OutgoingPayment) NONULL;
nameType 'Type' (OutgoingPayment s) = name(type(s));

dateTime 'Date' = DATA DATETIME (OutgoingPayment) NONULL IN id;
dateTime(OutgoingPayment p) <- currentDateTime() WHEN SET(p IS OutgoingPayment);
@defineDateTimeAggregation(OutgoingPayment);

@defineNumberType(outgoingPayment);

numberDate 'Description' (OutgoingPayment p) = number(p) + ' (' + dateTime(p) + ')' IN id;

// partners
partner 'Partner' = DATA Partner (OutgoingPayment);
namePartner 'Partner' (OutgoingPayment p) = name(partner(p));

WHEN LOCAL SETCHANGED(type(OutgoingPayment p)) AND NOT CHANGED(partner(p)) AND interpay(type(p)) AND countCompany() = 1 DO
    partner(p) <- defaultCompany();

CONSTRAINT interpay(type(OutgoingPayment p)) AND NOT partner(p) IS Company
                CHECKED BY partner
                MESSAGE 'With an internal payment, only a company can be a partner';

company 'Company' = DATA Company (OutgoingPayment) NONULL;
nameCompany 'Company' (OutgoingPayment p) = name(company(p));

WHEN LOCAL SET(OutgoingPayment p IS OutgoingPayment) AND NOT CHANGED(company(p)) DO
    company(p) <- defaultCompany();

// accounts
// partner
partnerAccount = DATA Account (OutgoingPayment);
namePartnerAccount 'Partner account' (OutgoingPayment p) = name(partnerAccount(p));

WHEN LOCAL CHANGED(partner(OutgoingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p));
WHEN LOCAL SET(type(OutgoingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p)); 

CONSTRAINT partnerAccount(OutgoingPayment p) AND NOT holder(partnerAccount(p)) = partner(p)
                CHECKED BY partnerAccount
                MESSAGE 'The partner account owner does not match the partner in the outgoing payment';
CONSTRAINT partnerAccount(OutgoingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Partner bank accounts are not allowed for this type of payment';
CONSTRAINT partnerAccount(OutgoingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Partner cash desks are not allowed for this type of payment.';

// company
companyAccount = DATA Account (OutgoingPayment);
nameCompanyAccount 'Company account' (OutgoingPayment p) = name(companyAccount(p));

WHEN LOCAL CHANGED(company(OutgoingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p));
WHEN LOCAL SET(type(OutgoingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p)); 

CONSTRAINT companyAccount(OutgoingPayment p) AND NOT holder(companyAccount(p)) = company(p)
                CHECKED BY companyAccount
                MESSAGE 'The company account holder does not match the company in the outgoing payment';
CONSTRAINT companyAccount(OutgoingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Company bank accounts are not allowed for this type of payment';
CONSTRAINT companyAccount(OutgoingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY companyAccount
                MESSAGE 'For this type of payment, company cash registers are not allowed';

amount '{Amount}' = DATA NUMERIC[14,2] (OutgoingPayment) NONULL;

note 'Note' = DATA ISTRING[200] (OutgoingPayment) CHARWIDTH 25;
reference 'Reference' = DATA ISTRING[50] (OutgoingPayment);

// forms
FORM outgoingPayment 'Outgoing payment'
    OBJECTS p = OutgoingPayment PANEL 
    PROPERTIES(p) nameType, dateTime, number, amount,
                  namePartner, namePartnerAccount, 
                  nameCompany, nameCompanyAccount,
                  note, reference
    
    EDIT OutgoingPayment OBJECT p
;

DESIGN outgoingPayment {
    caption = badged('Outgoing payment', number(p) + '(' + dateTime(p) + ')');
    OBJECTS {
        NEW header {
            NEW params {
                caption = 'Parameters';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(nameType(p)) { notNull = TRUE; } 
                MOVE PROPERTY(dateTime(p)); 
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(amount(p));
            }
            NEW partner {
                caption = 'Partner';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(namePartner(p));
                MOVE PROPERTY(namePartnerAccount(p));
            }
            NEW company { 
                caption = 'Company';
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(nameCompany(p)) { notNull = TRUE; }
                MOVE PROPERTY(nameCompanyAccount(p));
            }
            NEW otherInformation {
                caption = 'Other information';
                alignment = STRETCH;
                MOVE PROPERTY(note(p)); 
                MOVE PROPERTY(reference(p));    
            }
        }
        NEW details {
            tabbed = TRUE;
            fill = 1;
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            horizontal = TRUE;
        }
    }
}

FORM outgoingPayments 'Outgoing payments'
    OBJECTS p = OutgoingPayment
    PROPERTIES(p) READONLY number, dateTime, namePartner, 
                           nameType, amount,
                           namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           note, reference
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE DISABLEIF readonly(p)
    
    LIST OutgoingPayment OBJECT p
;

@defineDocHistoryHeader(outgoingPayment, dateTime);
@defineHistoryProperty(outgoingPayment, amount, 'Amount');
@defineHistoryForm(outgoingPayment, outgoingPayment, p);

@defineDocObjectsForm(outgoingPayments, p, 'Outgoing payments');
@defineDocStatusForm(outgoingPayment, p);

@defineObjectComments(outgoingPayment, p, details);

NAVIGATOR {
    invoicing {
        operations {
            NEW outgoingPayments;
        }
    }
}
