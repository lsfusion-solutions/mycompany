MODULE OutgoingPayment;

REQUIRE Time, MetaNumerator, Doc, DateUtils,
        Partner, Company,
        Account, BankAccount, CashAccount,
        OutgoingPaymentType;

NAMESPACE Invoicing;

CLASS OutgoingPayment 'Исходящий платеж';

@defineDocStatus(outgoingPayment, 'исходящего платежа');

// type
type 'Тип' = DATA OutgoingPaymentType (OutgoingPayment) NONULL;
nameType 'Тип' (OutgoingPayment s) = name(type(s));

dateTime 'Дата' = DATA DATETIME (OutgoingPayment) NONULL;
dateTime(OutgoingPayment p) <- currentDateTime() WHEN SET(p IS OutgoingPayment);
@defineDateTimeAggregation(OutgoingPayment);

@defineNumberType(outgoingPayment);

numberDate 'Описание' (OutgoingPayment p) = number(p) + ' от ' + dateTime(p) IN id;

// partners
partner 'Контрагент' = DATA Partner (OutgoingPayment);
namePartner 'Контрагент' (OutgoingPayment p) = name(partner(p));

WHEN LOCAL SETCHANGED(type(OutgoingPayment p)) AND NOT CHANGED(partner(p)) AND interpay(type(p)) AND countCompany() = 1 DO
    partner(p) <- defaultCompany();

CONSTRAINT interpay(type(OutgoingPayment p)) AND NOT partner(p) IS Company
                CHECKED BY partner
                MESSAGE 'При внутреннем платеже контрагентом может быть только компания';

company 'Компания' = DATA Company (OutgoingPayment) NONULL;
nameCompany 'Компания' (OutgoingPayment p) = name(company(p));

WHEN LOCAL SET(OutgoingPayment p IS OutgoingPayment) AND NOT CHANGED(company(p)) DO
    company(p) <- defaultCompany();

// accounts
// partner
partnerAccount = DATA Account (OutgoingPayment);
namePartnerAccount 'Счет контрагента' (OutgoingPayment p) = name(partnerAccount(p));

WHEN LOCAL CHANGED(partner(OutgoingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p));
WHEN LOCAL SET(type(OutgoingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p)); 

CONSTRAINT partnerAccount(OutgoingPayment p) AND NOT holder(partnerAccount(p)) = partner(p)
                CHECKED BY partnerAccount
                MESSAGE 'Владелец счета контрагента не совпадает с контрагентом в исходящем платеже';
CONSTRAINT partnerAccount(OutgoingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Для этого типа платежа банковские счета контрагента не разрешены';
CONSTRAINT partnerAccount(OutgoingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Для этого типа платежа кассы контрагента не разрешены';

// company
companyAccount = DATA Account (OutgoingPayment);
nameCompanyAccount 'Счет компании' (OutgoingPayment p) = name(companyAccount(p));

WHEN LOCAL CHANGED(company(OutgoingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p));
WHEN LOCAL SET(type(OutgoingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p)); 

CONSTRAINT companyAccount(OutgoingPayment p) AND NOT holder(companyAccount(p)) = company(p)
                CHECKED BY companyAccount
                MESSAGE 'Владелец счета компании не совпадает с компанией в исходящем платеже';
CONSTRAINT companyAccount(OutgoingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Для этого типа платежа банковские счета компании не разрешены';
CONSTRAINT companyAccount(OutgoingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Для этого типа платежа кассы компании не разрешены';

amount 'Сумма' = DATA NUMERIC[14,2] (OutgoingPayment) NONULL;

note 'Примечание' = DATA ISTRING[50] (OutgoingPayment);
reference 'Ссылка' = DATA ISTRING[50] (OutgoingPayment);

// forms
FORM outgoingPayment 'Исходящий платеж'
    OBJECTS p = OutgoingPayment PANEL 
    PROPERTIES(p) nameType, dateTime, number, amount,
                  namePartner, namePartnerAccount, 
                  nameCompany, nameCompanyAccount,
                  note, reference
    
    EDIT OutgoingPayment OBJECT p
;

DESIGN outgoingPayment {
    caption = (CONCAT ' ', 'Исходящий платеж', '№' + number(p), 'от ' + dateTime(p));
    OBJECTS {
        NEW header {
            NEW params {
                caption = 'Параметры';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameType(p)) { notNull = TRUE; } 
                MOVE PROPERTY(dateTime(p)); 
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(amount(p));
            }
            NEW partner {
                caption = 'Контрагент';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(namePartner(p));
                MOVE PROPERTY(namePartnerAccount(p));
            }
            NEW company { 
                caption = 'Компания';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameCompany(p)) { notNull = TRUE; }
                MOVE PROPERTY(nameCompanyAccount(p));
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                alignment = STRETCH;
                MOVE PROPERTY(note(p)); 
                MOVE PROPERTY(reference(p));    
            }
        }
        NEW details {
            type = TABBED;
            fill = 1;
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistoryHead(outgoingPayment, p);

FORM outgoingPayments 'Исходящие платежи'
    OBJECTS p = OutgoingPayment
    PROPERTIES(p) READONLY nameType, dateTime, number, amount,
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           note, reference
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST OutgoingPayment OBJECT p
;

@defineDocStatusForm(outgoingPayment, p);
@defineDateFilterForm(outgoingPayments, p);

NAVIGATOR {
    invoicing {
        operations {
            NEW outgoingPayments;
        }
    }
}
