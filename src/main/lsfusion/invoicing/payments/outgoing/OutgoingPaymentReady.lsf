MODULE OutgoingPaymentReady;

REQUIRE OutgoingPayment;

NAMESPACE Invoicing;

EXTEND CLASS OutgoingPaymentStatus {
    ready 'To pay'
}
ready 'To pay' = DATA BOOLEAN (OutgoingPayment);

status (OutgoingPayment r) += WHEN ready(r) THEN OutgoingPaymentStatus.ready;
color (OutgoingPaymentStatus r) += WHEN r = OutgoingPaymentStatus.ready THEN RGB(252,247,149);

markAsToDo 'Mark as Todo' (OutgoingPayment r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM outgoingPayment
    PROPERTIES(p) markAsToDo SHOWIF status(p) = OutgoingPaymentStatus.draft, ready 
;

DESIGN outgoingPayment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(p)) { valueClass = 'btn-primary'; }
    }       
    statusGroup {
        MOVE PROPERTY(ready(p)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }
}

EXTEND FORM outgoingPayments    
    EXTEND FILTERGROUP status
        FILTER 'To pay' status(p) = OutgoingPaymentStatus.ready    
;