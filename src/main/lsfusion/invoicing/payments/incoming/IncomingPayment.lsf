MODULE IncomingPayment;

REQUIRE Time, MetaNumerator, Doc, DateUtils,
        Partner, Company, 
        Account, BankAccount, CashAccount,
        IncomingPaymentType;

NAMESPACE Invoicing;

CLASS IncomingPayment 'Входящий платеж';

@defineDocStatus(incomingPayment, 'входящего платежа');

// type
type 'Тип' = DATA IncomingPaymentType (IncomingPayment) NONULL;
nameType 'Тип' (IncomingPayment s) = name(type(s));

dateTime 'Дата' = DATA DATETIME (IncomingPayment) NONULL;
dateTime(IncomingPayment p) <- currentDateTime() WHEN SET(p IS IncomingPayment);
@defineDateTimeAggregation(IncomingPayment);

@defineNumberType(incomingPayment);

numberDate 'Описание' (IncomingPayment p) = number(p) + ' от ' + dateTime(p) IN id;

// partners
partner 'Контрагент' = DATA Partner (IncomingPayment);
namePartner 'Контрагент' (IncomingPayment p) = name(partner(p));

WHEN LOCAL SETCHANGED(type(IncomingPayment p)) AND NOT CHANGED(partner(p)) AND interpay(type(p)) AND countCompany() = 1 DO
    partner(p) <- defaultCompany();

CONSTRAINT interpay(type(IncomingPayment p)) AND NOT partner(p) IS Company
                CHECKED BY partner
                MESSAGE 'При внутреннем платеже контрагентом может быть только компания';

company 'Компания' = DATA Company (IncomingPayment) NONULL;
nameCompany 'Компания' (IncomingPayment p) = name(company(p));

WHEN LOCAL SET(IncomingPayment p IS IncomingPayment) AND NOT CHANGED(company(p)) DO
    company(p) <- defaultCompany();

// accounts
// partner
partnerAccount = DATA Account (IncomingPayment);
namePartnerAccount 'Счет контрагента' (IncomingPayment p) = name(partnerAccount(p));

WHEN LOCAL CHANGED(partner(IncomingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p));
WHEN LOCAL SET(type(IncomingPayment p)) AND NOT CHANGED(partnerAccount(p)) DO
    partnerAccount(p) <- defaultAccount(partner(p), type(p)); 

CONSTRAINT partnerAccount(IncomingPayment p) AND NOT holder(partnerAccount(p)) = partner(p)
                CHECKED BY partnerAccount
                MESSAGE 'Владелец счета контрагента не совпадает с контрагентом во входящем платеже';
CONSTRAINT partnerAccount(IncomingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Для этого типа платежа банковские счета контрагента не разрешены';
CONSTRAINT partnerAccount(IncomingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY partnerAccount
                MESSAGE 'Для этого типа платежа кассы контрагента не разрешены';
                
// company
companyAccount = DATA Account (IncomingPayment);
nameCompanyAccount 'Счет компании' (IncomingPayment p) = name(companyAccount(p));

WHEN LOCAL CHANGED(company(IncomingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p));
WHEN LOCAL SET(type(IncomingPayment p)) AND NOT CHANGED(companyAccount(p)) DO
    companyAccount(p) <- defaultAccount(company(p), type(p)); 

CONSTRAINT companyAccount(IncomingPayment p) AND NOT holder(companyAccount(p)) = company(p)
                CHECKED BY companyAccount
                MESSAGE 'Владелец счета компании не совпадает с компанией во входящем платеже';
CONSTRAINT companyAccount(IncomingPayment p) IS BankAccount AND NOT bank(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Для этого типа платежа банковские счета компании не разрешены';
CONSTRAINT companyAccount(IncomingPayment p) IS CashAccount AND NOT cash(type(p))
                CHECKED BY companyAccount
                MESSAGE 'Для этого типа платежа кассы компании не разрешены';

amount 'Сумма' = DATA NUMERIC[14,2] (IncomingPayment) NONULL;

note 'Примечание' = DATA ISTRING[50] (IncomingPayment);
reference 'Ссылка' = DATA ISTRING[50] (IncomingPayment);

// forms
FORM incomingPayment 'Входящий платеж'
    OBJECTS p = IncomingPayment PANEL 
    PROPERTIES(p) nameType, dateTime, number, amount,
                  namePartner, namePartnerAccount, 
                  nameCompany, nameCompanyAccount,
                  note, reference
               
    
    EDIT IncomingPayment OBJECT p
;

DESIGN incomingPayment {
    caption = (CONCAT ' ', 'Входящий платеж', '№' + number(p), 'от ' + dateTime(p));
    OBJECTS {
        NEW header {
            NEW params {
                caption = 'Параметры';
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(nameType(p)) { notNull = TRUE; } 
                MOVE PROPERTY(dateTime(p)); 
                MOVE PROPERTY(number(p));
                MOVE PROPERTY(amount(p));
            }
            NEW partner {
                type = CONTAINERH;
                alignment = STRETCH;
                caption = 'Контрагент';
                MOVE PROPERTY(namePartner(p));
                MOVE PROPERTY(namePartnerAccount(p));
            }
            NEW company { 
                type = CONTAINERH;
                alignment = STRETCH;
                caption = 'Компания';
                MOVE PROPERTY(nameCompany(p)) { notNull = TRUE; }
                MOVE PROPERTY(nameCompanyAccount(p));
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                alignment = STRETCH;
                MOVE PROPERTY(note(p)); 
                MOVE PROPERTY(reference(p));    
            }
        }
        NEW details {
            type = TABBED;
            fill = 1;
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistoryHead(incomingPayment, p);

FORM incomingPayments 'Входящие платежи'
    OBJECTS p = IncomingPayment
    PROPERTIES(p) READONLY nameType, dateTime, number, amount,
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount,
                           note, reference
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 
    
    LIST IncomingPayment OBJECT p
;

@defineDocStatusForm(incomingPayment, p);
@defineDateFilterForm(incomingPayments, p);

NAVIGATOR {
    invoicing {
        operations {
            NEW incomingPayments;
        }
    }
}
