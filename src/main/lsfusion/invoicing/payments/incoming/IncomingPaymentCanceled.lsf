MODULE IncomingPaymentCanceled;

REQUIRE IncomingPaymentDone;

NAMESPACE Invoicing;
    
EXTEND CLASS IncomingPaymentStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (IncomingPayment);
active '{Active}' (IncomingPayment s) = done(s) AND NOT canceled(s);

status(IncomingPayment s) += WHEN canceled(s) THEN IncomingPaymentStatus.canceled;
color(IncomingPaymentStatus s) += WHEN s = IncomingPaymentStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (IncomingPayment s) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM incomingPayment
    PROPERTIES(p) cancel SHOWIF NOT (status(p) = IncomingPaymentStatus.draft OR status(p) = IncomingPaymentStatus.canceled), canceled 
;

DESIGN incomingPayment {
    secondaryActions {
        MOVE PROPERTY(cancel(p)) { valueClass = 'btn-danger'; }
    }             
    statusGroup {
        MOVE PROPERTY(canceled(p)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM incomingPayments    
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(p) = IncomingPaymentStatus.canceled    
;
