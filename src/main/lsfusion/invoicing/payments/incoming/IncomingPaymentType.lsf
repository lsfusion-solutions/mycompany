MODULE IncomingPaymentType;

REQUIRE Numerator, InvoicingSettings, BankAccount, CashAccount;

NAMESPACE Invoicing;

CLASS IncomingPaymentType 'Тип входящего платежа';
name 'Название' = DATA ISTRING[50] (IncomingPaymentType) NONULL IN id;

id 'Код' = DATA STRING[20] (IncomingPaymentType);
incomingPaymentType = GROUP AGGR IncomingPaymentType t BY id(t);

interpay 'Внутренний платеж' = DATA BOOLEAN (IncomingPaymentType);

cash 'Касса' = DATA BOOLEAN (IncomingPaymentType);
bank 'Банковский счет' = DATA BOOLEAN (IncomingPaymentType);

CONSTRAINT IncomingPaymentType t IS IncomingPaymentType AND NOT cash(t) AND NOT bank(t)
    MESSAGE 'Должен быть разрешен хотя бы один вид счета';

numerator 'Нумератор' = DATA Numerator (IncomingPaymentType);
nameNumerator 'Нумератор' (IncomingPaymentType o) = name(numerator(o));

FORM incomingPaymentType 'Тип входящего платежа'
    OBJECTS o = IncomingPaymentType PANEL 
    PROPERTIES(o) name, id, nameNumerator,
                  interpay, cash, bank  
     
    EDIT IncomingPaymentType OBJECT o;
;

DESIGN incomingPaymentType {
    OBJECTS {
        NEW typeAccount {
            caption = 'Разрешенные типы счетов';
            type = CONTAINERH;
            MOVE PROPERTY(cash(o));
            MOVE PROPERTY(bank(o));
        }
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }   
}

FORM incomingPaymentTypes 'Типы входящих платежей'
    OBJECTS o = IncomingPaymentType
    PROPERTIES(o) READONLY name, id
    
    LIST IncomingPaymentType OBJECT o;
;

EXTEND FORM options
    OBJECTS ipt = IncomingPaymentType
    PROPERTIES(ipt) READONLY name, id
    PROPERTIES(ipt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(ipt);
    }
}

defaultAccount(Partner p, IncomingPaymentType t) = CASE OVERRIDE
    WHEN bank(t) AND cash(t) THEN defaultAccount(p)
    WHEN bank(t) THEN defaultBankAccount(p)
    WHEN cash(t) THEN defaultCashAccount(p)
;