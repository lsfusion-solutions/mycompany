MODULE IncomingPaymentDone;

REQUIRE IncomingPayment;

NAMESPACE Invoicing;

EXTEND CLASS IncomingPaymentStatus {
    done 'Done'
}
done 'Done' = DATA BOOLEAN (IncomingPayment);

status(IncomingPayment s) += WHEN done(s) THEN IncomingPaymentStatus.done;

markAsDone 'Mark as Done' (IncomingPayment s) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        done(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM incomingPayment
    PROPERTIES(p) markAsDone SHOWIF status(p) = IncomingPaymentStatus.draft, done
;
    

DESIGN incomingPayment {
    primaryActions {
        MOVE PROPERTY(markAsDone(p)) { valueClass = 'btn-primary'; };
    }
    statusGroup {
        MOVE PROPERTY(done(p))  {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }
    }
}

EXTEND FORM incomingPayments
    EXTEND FILTERGROUP status
        FILTER 'Done' status(p) = IncomingPaymentStatus.done
;
