MODULE InvoiceReady;

REQUIRE Invoice, InvoiceSelection;

NAMESPACE Invoicing;

EXTEND CLASS InvoiceStatus {
    ready 'To pay'
}
ready 'To pay' = DATA BOOLEAN (Invoice);

status(Invoice i) += WHEN ready(i) THEN InvoiceStatus.ready;
color(InvoiceStatus i) += WHEN i = InvoiceStatus.ready THEN RGB(252,247,149);

markAsToDo 'Mark as Todo' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        ready(i) <- TRUE;
        APPLY;
    }
}

EXTEND FORM invoice
    PROPERTIES(i) markAsToDo SHOWIF status(i) = InvoiceStatus.draft, ready
;

DESIGN invoice {
    primaryActions {
        MOVE PROPERTY(markAsToDo(i)) { valueClass = 'btn-primary'; }
    }                
    statusGroup {
        MOVE PROPERTY(ready(i)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM invoices    
    EXTEND FILTERGROUP status
        FILTER 'To pay' status(i) = InvoiceStatus.ready 'F8'    
;

@defineSelectionAction(invoices, Invoice, markAsToDo, 'Mark as Todo', statuses);