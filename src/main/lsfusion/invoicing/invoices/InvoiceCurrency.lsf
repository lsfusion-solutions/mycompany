MODULE InvoiceCurrency;

REQUIRE Invoice, Currency, ExchangeRate;

NAMESPACE Invoicing;

currency = DATA Currency (InvoiceType);
nameCurrency 'Currency' (InvoiceType o) = name(currency(o));

EXTEND FORM invoiceType
    PROPERTIES(o) nameCurrency
;

currency = DATA Currency (Invoice);
nameCurrency 'Currency' (Invoice b) = name(currency(b));

WHEN LOCAL SETCHANGED(type(Invoice b)) AND currency(type(b)) AND NOT CHANGED(currency(b)) DO currency(b) <- currency(type(b));  

currency (InvoiceLine l) = currency(invoice(l));
nameCurrency 'Currency' (InvoiceLine l) = nameCurrency(invoice(l));

currencyDate = ABSTRACT VALUE DATE (Invoice);
currencyDate(Invoice i) += date(i);

EXTEND FORM invoice
    PROPERTIES(i) nameCurrency
;

DESIGN invoice {
    headerRight {
        MOVE PROPERTY(nameCurrency(i));
    }           
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY nameCurrency AFTER amount(i)
;

clone (Invoice to, Invoice from) + {
    currency(to) <- currency(from);
}

// exchange rate

rate1 (Invoice i) = rate1(currency(i), currencyDate(i));
rate1 (InvoiceLine l) = rate1(invoice(l));

currencyAmount 'Currency base amount' (InvoiceLine l) = NUMERIC[14,2](amount(l) * rate1(l));