MODULE InvoiceDebt;

REQUIRE InvoiceDone, InvoiceCanceled, Debt;

NAMESPACE Invoicing;

// extend
EXTEND CLASS Invoice : OutgoingDebt;
type(Invoice i) += nameType(i);

active (Invoice i) += active(i);
number (Invoice i) += number(i);
dateTime (Invoice i) += dateTime(i);
dueDateTime (Invoice i) += dueDateTime(i);

partner (Invoice i) += customer(i);
company (Invoice i) += company(i);

amount (Invoice i) += amount(i);

// pay
pay 'Разнести' (IncomingDebt d, Invoice i) {
    paid(d, i) <- left(d, i) (+) paid(d, i);
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM invoice
    PROPERTIES(i) READONLY paid  

    OBJECTS d = IncomingDebt
    PROPERTIES(d) READONLY dateTime, number, class, type
    PROPERTIES paid(d, i)
    FILTERS paid(d, i), active(d)
    
    OBJECTS dd = IncomingDebt
    PROPERTIES(dd) READONLY dateTime, number, class, type, left
    PROPERTIES pay(dd, i) TOOLBAR
    FILTERS canBePaid(dd, i), active(dd)
;

DESIGN invoice {
    total {
        MOVE PROPERTY(paid(i));
    }
    details {
        NEW debts {
            caption = 'Разнесение оплат';
            MOVE BOX(d) { caption = 'Разнесенные'; }            
            MOVE BOX(dd) { caption = 'Доступные'; }          
        }
    }
}

EXTEND FORM invoices   
    PROPERTIES(i) READONLY paid  
;

// auto set done
WHEN SET(amount(Invoice i) = paid(i)) DO done(i) <- TRUE;
WHEN DROPPED(amount(Invoice i) = paid(i)) DO done(i) <- NULL;