MODULE InvoiceType;

REQUIRE Partner, Numerator, InvoicingSettings, Tax;

NAMESPACE Invoicing;

CLASS InvoiceType 'Invoice type';
name '{Name}' = DATA ISTRING[50] (InvoiceType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (InvoiceType);
invoiceType = GROUP AGGR InvoiceType t BY id(t);

numerator 'Numerator' = DATA Numerator (InvoiceType);
nameNumerator 'Numerator' (InvoiceType o) = name(numerator(o));

customer = DATA Partner (InvoiceType);
nameCustomer 'Customer' (InvoiceType o) = name(customer(o));

taxIncluded 'Price includes taxes' = DATA BOOLEAN (InvoiceType);

FORM invoiceType 'Invoice type'
    OBJECTS o = InvoiceType PANEL 
    PROPERTIES(o) name, id, nameNumerator, nameCustomer, taxIncluded
     
    EDIT InvoiceType OBJECT o;
;

DESIGN invoiceType {
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            MOVE PROPERTY(name(o)) { charWidth = 30; }
            MOVE PROPERTY(id(o));
            MOVE PROPERTY(nameNumerator(o));
        }
        NEW pane {
            lines = 3;
            NEW price {
                caption = 'Price';
                MOVE PROPERTY(taxIncluded(o));
            }
            NEW return {
                caption = 'Return';
            }
            NEW default {
                caption = 'Default';
                MOVE PROPERTY(nameCustomer(o));
            }
            NEW otherInformation {
                caption = 'Other information';
            }
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM invoiceTypes 'Invoice types'
    OBJECTS o = InvoiceType
    PROPERTIES(o) READONLY name, id
    
    LIST InvoiceType OBJECT o;
;

EXTEND FORM options
    OBJECTS it = InvoiceType
    PROPERTIES(it) READONLY name, id
    PROPERTIES(it) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(it);
    }
}

// default type
countInvoiceTypes 'Number of types' () = GROUP SUM 1 IF r IS InvoiceType; 
firstInvoiceType = GROUP LAST InvoiceType r ORDER DESC r WHERE r IS InvoiceType;

defaultInvoiceType = firstInvoiceType() IF countInvoiceTypes() = 1;