MODULE InvoicePrint;

REQUIRE Invoice, Product, DocTemplate;

NAMESPACE Invoicing;

addressCompany (Invoice i) = linesAddress(company(i));
phoneCompany (Invoice i) = phone(company(i));
emailCompany (Invoice i) = email(company(i));
websiteCompany (Invoice i) = website(company(i));

addressCustomer (Invoice i) = linesAddress(customer(i)); 
phoneCustomer (Invoice i) = phone(customer(i));

nameCountryOrigin (InvoiceLine l) = nameCountryOrigin(item(l));
idCountryOrigin (InvoiceLine l) = id(countryOrigin(item(l)));

printUom = ABSTRACT VALUE Uom (InvoiceLine);
namePrintUom (InvoiceLine l) = name(printUom(l));
printUom(InvoiceLine l) += uom(l);

printQuantity = ABSTRACT VALUE NUMERIC[16,3] (InvoiceLine);
printQuantity(InvoiceLine l) += quantity(l);  

printPrice = ABSTRACT VALUE NUMERIC[16,3] (InvoiceLine);
printPrice(InvoiceLine l) += price(l);  

FORM printInvoice 'Реализация'
    OBJECTS o = Invoice PANEL 
    PROPERTIES(o) dateTime, dueDateTime, number,
                  nameCompany, addressCompany, phoneCompany, emailCompany, websiteCompany,
                  nameCustomer, addressCustomer, phoneCustomer
    
    OBJECTS l = InvoiceLine
    PROPERTIES(l) nameItem, idItem, nameUom = namePrintUom,
                  quantity = printQuantity, price = printPrice, untaxedAmount,
                  taxPercent, taxAmount, taxes, 
                  amount, nameCountryOrigin, idCountryOrigin,
                  weight
    FILTERS invoice(l) = o
;

@defineDocTemplateType(invoice, 'реализации');
@defineDocTemplateTypeForm(invoice, i);