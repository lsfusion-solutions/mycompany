MODULE CreditNote;

REQUIRE InvoiceDone, BillCanceled;

NAMESPACE Invoicing;

// types
isCreditNote 'Возврат' = DATA BOOLEAN (BillType);
EXTEND FORM billType PROPERTIES(o) isCreditNote;

isCreditNote (Bill r) = isCreditNote(type(r));

billType = DATA BillType (InvoiceType);
nameBillType 'Тип возврата' (InvoiceType t) = name(billType(t));
EXTEND FORM invoiceType PROPERTIES(o) nameBillType;

// lines
@defineDocLineRelation(invoice, bill, 'Реализации', 'Возвраты', i, b);

numberInvoiceLine 'Реализация' (BillLine l) = number(invoiceLine(l)); 

CONSTRAINT invoiceLine(BillLine l) AND NOT 
                (customer(invoiceLine(l)) = vendor(l) OR (NOT customer(invoiceLine(l)) AND NOT vendor(l)))
    CHECKED BY invoiceLine
    MESSAGE 'Контрагент реализации должен совпадать с контрагентом возврата';

CONSTRAINT invoiceLine(BillLine l) AND NOT item(invoiceLine(l)) = item(l)
    CHECKED BY invoiceLine
    MESSAGE 'Номенклатура реализации должны совпадать с номенклатурой возврата';

// events
WHEN LOCAL SETCHANGED(invoiceLine(BillLine l)) DO {
    price(l) <- resultPrice(invoiceLine(l));
    in(l, Tax t) <- in(invoiceLine(l), t);
}

EXTEND FORM bill
    PROPERTIES(l) numberInvoiceLine SHOWIF isCreditNote(b)
;

// add
fill ABSTRACT (BillLine, InvoiceLine);
fill ABSTRACT (Bill, Invoice);
 
create (Bill b, Invoice i) {
    type(b) <- billType(type(i));
        
    vendor(b) <- customer(i);
    vendorAccount(b) <- customerAccount(i);
    
    company(b) <- company(i);
    companyAccount(b) <- companyAccount(i);
    
    note(b) <- note(i);
    
    FOR invoice(InvoiceLine l) = i INLINE NEW bl = BillLine DO {
        invoiceLine(bl) <- l; 
        bill(bl) <- b;

        item(bl) <- item(l);

        quantity(bl) <- quantity(l);
        
        fill(bl, l);
    }
    
    fill(b, i);
}

addCreditNote 'Вернуть' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW b = Bill {
            create(b, i);
            SHOW bill OBJECTS b = b DOCKED;
        }
        
    }
}

EXTEND FORM invoice
    PROPERTIES(i) addCreditNote SHOWIF ready(i)
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(addCreditNote(i));
    }               
}

// returned quantity
returned 'Возвращено' (InvoiceLine il) = 
    GROUP SUM quantity(BillLine bl) IF invoiceLine(bl) = il AND active(bill(bl));

FORM invoiceBillShow 'Возвраты'
    OBJECTS il = InvoiceLine
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY nameStatus, number, dateTime, quantity
    FILTERS invoiceLine(bl) = il,
            active(bill(bl))
;
    
EXTEND FORM invoice
    PROPERTIES(l) returned ON CHANGE { SHOW invoiceBillShow OBJECTS il = l; } SHOWIF countBillLine(i)
;