MODULE InvoicePayment;

REQUIRE InvoiceDone, InvoiceDebt, IncomingPaymentDebt;

NAMESPACE Invoicing;

paymentType = DATA IncomingPaymentType (InvoiceType);
namePaymentType 'Payment type' (InvoiceType t) = name(paymentType(t));

EXTEND FORM invoiceType PROPERTIES namePaymentType(o);

fill ABSTRACT LIST (IncomingPayment, Invoice);

registerPayment 'Register Payment' (Invoice i) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW p = IncomingPayment {
            type(p) <- paymentType(type(i));

            partner(p) <- customer(i);
            partnerAccount(p) <- customerAccount(i);
            
            company(p) <- company(i);
            companyAccount(p) <- companyAccount(i);
            
            done(p) <- TRUE;

            fill (p, i);

            executeLocalEvents();
                        
            amount(p) <- sourceLeft(i);
            paid(p, i) <- left(p, i);
            
            DIALOG incomingPayment OBJECTS p = p DOCKED NOMANAGESESSION DO {
                IF amount[Debt](p) < paid(p, i) THEN paid(p, i) <- amount[Debt](p);
                APPLY; 
            }
       } 
   }
}

EXTEND FORM invoice
    PROPERTIES(i) registerPayment SHOWIF status(i) = InvoiceStatus.ready 
;

DESIGN invoice {
    primaryActions {
        MOVE PROPERTY(registerPayment(i)) { valueClass = 'btn-primary'; }
    }
}