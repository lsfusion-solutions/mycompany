MODULE InvoiceCanceled;

REQUIRE InvoiceReady;

NAMESPACE Invoicing;
    
EXTEND CLASS InvoiceStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (Invoice);
active '{Active}' (Invoice i) = ready(i) AND NOT canceled(i);

status(Invoice i) += WHEN canceled(i) THEN InvoiceStatus.canceled;
color(InvoiceStatus i) += WHEN i = InvoiceStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Invoice i) {
    APPLY; 
       IF canceled() THEN RETURN;
           
       NEWSESSION {
           canceled(i) <- TRUE;
           APPLY;
       }
}

EXTEND FORM invoice
    PROPERTIES(i) cancel SHOWIF NOT (status(i) = InvoiceStatus.draft OR status(i) = InvoiceStatus.canceled), canceled 
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(cancel(i)) { valueClass = 'btn-danger'; }
    }             
    statusGroup {
        MOVE PROPERTY(canceled(i)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';            
        }                               
    }    
}

EXTEND FORM invoices    
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(i) = InvoiceStatus.canceled
;

@defineSelectionAction(invoices, Invoice, cancel, 'Cancel', statuses);