MODULE InvoiceCanceled;

REQUIRE InvoiceReady;

NAMESPACE Invoicing;
    
EXTEND CLASS InvoiceStatus {
    canceled 'Cancelled'
}
canceled 'Cancelled' = DATA BOOLEAN (Invoice);
active '{Active}' (Invoice i) = ready(i) AND NOT canceled(i);

status(Invoice i) += WHEN canceled(i) THEN InvoiceStatus.canceled;
color(InvoiceStatus i) += WHEN i = InvoiceStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Invoice i) {
    APPLY; 
       IF canceled() THEN RETURN;
           
       NEWSESSION {
           canceled(i) <- TRUE;
           APPLY;
       }
}

EXTEND FORM invoice
    PROPERTIES(i) cancel SHOWIF NOT (status(i) = InvoiceStatus.draft OR status(i) = InvoiceStatus.canceled), canceled 
;

DESIGN invoice {
    secondaryActions {
        MOVE PROPERTY(cancel(i)) { valueClass = 'btn-danger'; }
    }             
    status {
        MOVE PROPERTY(canceled(i));                               
    }    
}

EXTEND FORM invoices    
    EXTEND FILTERGROUP status
        FILTER 'Cancelled' status(i) = InvoiceStatus.canceled
;

@defineSelectionAction(invoices, Invoice, cancel, 'Cancel', statuses);