MODULE DebtPartner;

REQUIRE Debt, Partner;

NAMESPACE Invoicing;

currency = DATA Currency (Partner);
nameCurrency 'Currency' (Partner p) = name(currency(p));
currency (Debt d) += currency(partner(d));

debt 'Debt' (Debt d) = PARTITION SUM signedAmount(d) IF active(d) ORDER dateTime(d), d BY partner(d); 

debt 'Debt' (Partner p) = GROUP SUM signedAmount(Debt d) IF active(d) AND partner(d) = p;

pastDueDebt 'Overdue debt' (Partner p) = GROUP SUM signedLeft(Debt d) IF active(d) AND partner(d) = p AND dueDateTime(d) < currentDateTime();

FORM partnerDebts 'Partner debts'
    OBJECTS p = Partner PANEL
    PROPERTIES(p) READONLY name

    OBJECTS d = Debt
    PROPERTIES(d) READONLY class, type, number, dateTime, dueDateTime, nameCompany, 
                           sourceAmount, nameSourceCurrency, rate1, amount, left, debt
    PROPERTIES(d) NEWSESSION EDIT
    ORDERS dateTime(d)
    
    FILTERGROUP pastDue
        FILTER 'Overdue' dueDateTime(d) < currentDateTime() AND signedLeft(d)
    FILTERS active(d),
            partner(d) = p
;

EXTEND FORM partner
    PROPERTIES(p) nameCurrency
    PROPERTIES(p) ON CHANGE { SHOW partnerDebts OBJECTS p = p DOCKED; } debt, pastDueDebt
;
DESIGN partner {
    headerRight {
        MOVE PROPERTY(nameCurrency(p));
        MOVE PROPERTY(debt(p));
        MOVE PROPERTY(pastDueDebt(p));
    }
}

EXTEND FORM partners
    PROPERTIES(p) READONLY nameCurrency, debt, pastDueDebt
;