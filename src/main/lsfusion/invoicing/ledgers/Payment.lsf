MODULE Payment;

REQUIRE Company, Account, InvoicingSettings;

NAMESPACE Invoicing;

CLASS ABSTRACT Payment 'Payment';

active '{Active}' = ABSTRACT BOOLEAN (Payment) MATERIALIZED;
nameType 'Type' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

dateTime 'Date' = ABSTRACT DATETIME (Payment) MATERIALIZED;

number 'Number' = ABSTRACT STRING[31] (Payment) MATERIALIZED;

partner = ABSTRACT Partner (Payment) MATERIALIZED;
namePartner 'Partner' (Payment p) = name(partner(p));

company = ABSTRACT Company (Payment) MATERIALIZED;
nameCompany 'Company' (Payment p) = name(company(p));

// acounts
partnerAccount = ABSTRACT Account (Payment) MATERIALIZED;
namePartnerAccount 'Partner account' (Payment p) = name(partnerAccount(p));

companyAccount = ABSTRACT Account (Payment) MATERIALIZED;
nameCompanyAccount 'Company account' (Payment p) = name(companyAccount(p));

signedAmount '{Amount}' = ABSTRACT NUMERIC[14,2] (Payment) MATERIALIZED;

note 'Note' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;
reference 'Reference' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

// balance

balance 'On hand' (Account a) = GROUP SUM signedAmount(Payment p) IF companyAccount(p) = a AND active(p) MATERIALIZED;

CONSTRAINT balance(Account a) < 0 AND NOT allowNegative(a)
    MESSAGE 'Account / cash balance cannot be negative';

FORM payments 'Payments'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY nameType, dateTime, number, 
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           signedAmount, note, reference   
    PROPERTIES(p) NEWSESSION EDIT
    FILTERS active(p)
    
    OBJECTS a = Account
    PROPERTIES(a) READONLY nameType, name, nameAccountHolder, note, balance 
    FILTERS holder(a) IS Company
    
    OBJECTS pa = Payment
    PROPERTIES(pa) READONLY nameType, dateTime, number, 
                            namePartner, namePartnerAccount, 
                            nameCompany, nameCompanyAccount, 
                            signedAmount, note, reference   
    PROPERTIES(pa) NEWSESSION EDIT
    FILTERS active(pa),
            companyAccount(pa) = a
;

DESIGN payments {
    OBJECTS {
        NEW pane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(p) {
                caption = 'Payments';
            }
            NEW accounts {
                caption = 'Accounts';
                MOVE BOX(a);
                MOVE BOX(pa);
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW payments;
    }   
}