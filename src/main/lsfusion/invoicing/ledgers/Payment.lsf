MODULE Payment;

REQUIRE Company, Account, InvoicingSettings;

NAMESPACE Invoicing;

CLASS ABSTRACT Payment 'Payment';

active '{Active}' = ABSTRACT BOOLEAN (Payment) MATERIALIZED;
nameType 'Type' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

dateTime 'Date' = ABSTRACT DATETIME (Payment) MATERIALIZED;

number 'Number' = ABSTRACT STRING[50] (Payment) CHARWIDTH 10 MATERIALIZED;

partner = ABSTRACT Partner (Payment) MATERIALIZED;
namePartner 'Partner' (Payment p) = name(partner(p));

company = ABSTRACT Company (Payment) MATERIALIZED;
nameCompany 'Company' (Payment p) = name(company(p));

// acounts
partnerAccount = ABSTRACT Account (Payment) MATERIALIZED;
namePartnerAccount 'Partner account' (Payment p) = name(partnerAccount(p));

companyAccount = ABSTRACT Account (Payment) MATERIALIZED;
nameCompanyAccount 'Company account' (Payment p) = name(companyAccount(p));

INDEX companyAccount(Payment p), dateTime(p);

signedAmount '{Amount}' = ABSTRACT NUMERIC[14,2] (Payment) MATERIALIZED;

note 'Note' = ABSTRACT ISTRING[200] (Payment) MATERIALIZED CHARWIDTH 25;
reference 'Reference' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

// balance

balance 'Balance' (Account a) = GROUP SUM signedAmount(Payment p) IF companyAccount(p) = a AND active(p) MATERIALIZED;
cashBalance 'Cash balance' (Company c) = GROUP SUM balance(Account a) IF holder(a) = c; 

balanceA 'Balance' (Account a, DATETIME d) = balance(a) (-)
    GROUP SUM signedAmount(Payment p) IF companyAccount(p) = a AND active(p) AND dateTime(p) > d;

CONSTRAINT balance(Account a) < 0 AND NOT allowNegative(a)
    MESSAGE 'Account / cash balance cannot be negative';

FORM payments 'Payments'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY nameType, dateTime, number, 
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           signedAmount, note, reference   
    PROPERTIES(p) NEWSESSION EDIT
    FILTERS active(p)

    OBJECTS dt = DATETIME PANEL NULL
    PROPERTIES(dt) dateTime 'Select date' = VALUE

    OBJECTS a = Account
    PROPERTIES(a) READONLY nameType, name, nameAccountHolder, note
    PROPERTIES(a) SHOWIF NOT dt IS DATETIME READONLY balance
    PROPERTIES(a, dt) SHOWIF dt IS DATETIME READONLY balanceA
    FILTERS holder(a) IS Company
    
    OBJECTS pa = Payment
    PROPERTIES(pa) BACKGROUND RGB(212,255,212) IF dateTime(pa) > dt 
                            READONLY nameType, dateTime, number, 
                            namePartner, namePartnerAccount, 
                            nameCompany, nameCompanyAccount, 
                            signedAmount, note, reference   
    PROPERTIES(pa) NEWSESSION EDIT
    FILTERS active(pa),
            companyAccount(pa) = a
;

DESIGN payments {
    OBJECTS {
        NEW pane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(p) {
                caption = 'Payments';
            }
            NEW accounts {
                caption = 'Accounts';
                NEW accountHeader {
                    horizontal = TRUE;
                    MOVE PROPERTY (dateTime);
                }
                MOVE BOX(a);
                MOVE BOX(pa);
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW payments;
    }   
}