MODULE Payment;

REQUIRE Company, Account, InvoicingSettings;

NAMESPACE Invoicing;

CLASS ABSTRACT Payment 'Платеж';

active 'Активен' = ABSTRACT BOOLEAN (Payment) MATERIALIZED;
nameType 'Тип' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

dateTime 'Дата' = ABSTRACT DATETIME (Payment) MATERIALIZED;

number 'Номер' = ABSTRACT STRING[31] (Payment) MATERIALIZED;

partner = ABSTRACT Partner (Payment) MATERIALIZED;
namePartner 'Контрагент' (Payment p) = name(partner(p));

company = ABSTRACT Company (Payment) MATERIALIZED;
nameCompany 'Компания' (Payment p) = name(company(p));

// acounts
partnerAccount = ABSTRACT Account (Payment) MATERIALIZED;
namePartnerAccount 'Счет контрагента' (Payment p) = name(partnerAccount(p));

companyAccount = ABSTRACT Account (Payment) MATERIALIZED;
nameCompanyAccount 'Счет компании' (Payment p) = name(companyAccount(p));

signedAmount 'Сумма' = ABSTRACT NUMERIC[14,2] (Payment) MATERIALIZED;

note 'Примечание' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;
reference 'Ссылка' = ABSTRACT ISTRING[50] (Payment) MATERIALIZED;

// balance

balance 'Остаток' (Account a) = GROUP SUM signedAmount(Payment p) IF companyAccount(p) = a AND active(p) MATERIALIZED;

CONSTRAINT balance(Account a) < 0 AND NOT allowNegative(a)
    MESSAGE 'Остаток по счету / кассе не может быть отрицательным';

FORM payments 'Движение денежных средств'
    OBJECTS p = Payment
    PROPERTIES(p) READONLY nameType, dateTime, number, 
                           namePartner, namePartnerAccount, 
                           nameCompany, nameCompanyAccount, 
                           signedAmount, note, reference   
    PROPERTIES(p) NEWSESSION EDIT
    FILTERS active(p)
    
    OBJECTS a = Account
    PROPERTIES(a) READONLY nameType, name, nameAccountHolder, note, balance 
    FILTERS holder(a) IS Company
    
    OBJECTS pa = Payment
    PROPERTIES(pa) READONLY nameType, dateTime, number, 
                            namePartner, namePartnerAccount, 
                            nameCompany, nameCompanyAccount, 
                            signedAmount, note, reference   
    PROPERTIES(pa) NEWSESSION EDIT
    FILTERS active(pa),
            companyAccount(pa) = a
;

DESIGN payments {
    OBJECTS {
        NEW pane {
            type = TABBED;
            fill = 1;
            MOVE BOX(p) {
                caption = 'Платежи';
            }
            NEW accounts {
                caption = 'Счета';
                type = SPLITV;
                MOVE BOX(a);
                MOVE BOX(pa);
            }
        }
    }
}

NAVIGATOR {
    reporting {
        NEW payments;
    }   
}