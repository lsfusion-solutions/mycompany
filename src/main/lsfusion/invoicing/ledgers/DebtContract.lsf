MODULE DebtContract;

REQUIRE DebtPartner, Contract;

NAMESPACE Invoicing;

currency = DATA Currency (Contract);
nameCurrency 'Currency' (Contract c) = name(currency(c));
currency (Debt d) += currency(contract(d));

debtContract 'Debt' (Debt d) = PARTITION SUM signedAmount(d) IF active(d) ORDER dateTime(d), d BY contract(d); 

debt 'Debt' (Contract c) = GROUP SUM signedAmount(Debt d) IF active(d) AND contract(d) = c;

pastDueDebt 'Overdue debt' (Contract c) = GROUP SUM signedLeft(Debt d) IF active(d) AND contract(d) = c AND dueDateTime(d) < currentDateTime();

FORM debtsContract 'Contract debts'
    OBJECTS c = Contract PANEL
    PROPERTIES(c) READONLY name

    OBJECTS d = Debt
    PROPERTIES(d) READONLY class, type, number, dateTime, dueDateTime, nameCompany, 
                           sourceAmount, nameSourceCurrency, rate1, amount, left, debtContract
    PROPERTIES(d) NEWSESSION EDIT
    ORDERS dateTime(d)
    
    FILTERGROUP pastDue
        FILTER 'Overdue' dueDateTime(d) < currentDateTime() AND signedLeft(d)
    FILTERS active(d),
            contract(d) = c
;

EXTEND FORM contract
    PROPERTIES(c) nameCurrency
    PROPERTIES(c) ON CHANGE { SHOW debtsContract OBJECTS c = c DOCKED; } debt, pastDueDebt
;
DESIGN contract {
    headerRight {
        NEW debt {
            caption = 'Debt';
            alignment = STRETCH;
            MOVE PROPERTY(nameCurrency(c));
            MOVE PROPERTY(debt(c));
            MOVE PROPERTY(pastDueDebt(c));
        }
    }
}

EXTEND FORM contracts
    PROPERTIES(c) READONLY nameCurrency, debt, pastDueDebt
;