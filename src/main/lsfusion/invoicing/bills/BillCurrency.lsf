MODULE BillCurrency;

REQUIRE Bill, Currency, ExchangeRate;

NAMESPACE Invoicing;

currency = DATA Currency (BillType);
nameCurrency 'Currency' (BillType o) = name(currency(o));

EXTEND FORM billType
    PROPERTIES(o) nameCurrency
;

currency = DATA Currency (Bill);
nameCurrency 'Currency' (Bill b) = name(currency(b));

WHEN LOCAL SETCHANGED(type(Bill b)) AND currency(type(b)) AND NOT CHANGED(currency(b)) DO currency(b) <- currency(type(b));  

currency (BillLine l) = currency(bill(l));
nameCurrency 'Currency' (BillLine l) = nameCurrency(bill(l));

currencyDate = ABSTRACT VALUE DATE (Bill);
currencyDate(Bill b) += date(b);

EXTEND FORM bill
    PROPERTIES(b) nameCurrency READONLYIF readonly(b)
;

DESIGN bill {
    headerRight {
        MOVE PROPERTY(nameCurrency(b));
    }           
}

EXTEND FORM bills
    PROPERTIES(b) READONLY nameCurrency AFTER amount(b)
;

clone (Bill to, Bill from) + {
    currency(to) <- currency(from);
}

// exchange rate

rate1 (Bill b) = rate1(currency(b), currencyDate(b));
rate1 (BillLine l) = rate1(bill(l));

currencyAmount 'Currency base amount' (BillLine l) = NUMERIC[14,2](amount(l) * rate1(l));