MODULE BillReady;

REQUIRE Bill, BillSelection;

NAMESPACE Invoicing;

EXTEND CLASS BillStatus {
    ready 'To pay'
}
ready 'To pay' = DATA BOOLEAN (Bill);

status(Bill b) += WHEN ready(b) THEN BillStatus.ready;
color(BillStatus b) += WHEN b = BillStatus.ready THEN RGB(252,247,149);

markAsToDo 'Mark as Todo' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        ready(b) <- TRUE;
        APPLY;
    }
}

EXTEND FORM bill
    PROPERTIES(b) markAsToDo SHOWIF status(b) = BillStatus.draft, ready
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(markAsToDo(b)) { valueClass = 'btn-primary'; }
    }                
    statusGroup {
        MOVE PROPERTY(ready(b)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM bills    
    EXTEND FILTERGROUP status
        FILTER 'Ready' status(b) = BillStatus.ready    
;

@defineSelectionAction(bills, Bill, markAsToDo, 'Mark as Todo', statuses);