MODULE BillReady;

REQUIRE Bill, BillSelection;

NAMESPACE Invoicing;

EXTEND CLASS BillStatus {
    ready 'К оплате'
}
ready 'К оплате' = DATA BOOLEAN (Bill);

status(Bill b) += WHEN ready(b) THEN BillStatus.ready;
colorStatus(Bill b) += WHEN status(b) = BillStatus.ready THEN RGB(252,247,149);

markAsToDo 'В работу' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        ready(b) <- TRUE;
        APPLY;
    }
}

EXTEND FORM bill
    PROPERTIES(b) markAsToDo SHOWIF status(b) = BillStatus.draft, ready
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(markAsToDo(b)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(ready(b));                               
    }    
}

EXTEND FORM bills    
    EXTEND FILTERGROUP status
        FILTER 'К отгрузке' status(b) = BillStatus.ready    
;

@defineSelectionAction(bills, Bill, markAsToDo, 'В работу', statuses);