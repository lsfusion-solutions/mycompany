MODULE Bill;

REQUIRE InvoicingSettings, TaxItem, BankAccount, 
        MetaTax, PaymentTermsPurchase, DateUtils, 
        ItemPurchase, ItemCost, PartnerPurchase, Company,
        BarCode, MetaNumerator, Doc, LocationEmployee,
        BillType, Product, ServiceP;

NAMESPACE Invoicing;

CLASS Bill 'Поступление';

@defineDocStatus(bill, 'счета');

// type
type 'Тип' = DATA BillType (Bill) NONULL;
nameType 'Тип' (Bill s) = name(type(s));

WHEN LOCAL SET(Bill s IS Bill) AND NOT CHANGED(type(s)) DO type(s) <- defaultBillType(); 

// dateTime
dateTime 'Дата' = DATA DATETIME (Bill) NONULL IN id;
WHEN LOCAL SET (Bill b IS Bill) DO dateTime(b) <- currentDateTime();
@defineDateTimeAggregation(Bill);

@defineNumberType(bill);

numberDate 'Описание' (Bill b) = number(b) + ' от ' + dateTime(b) IN id;

// vendor
vendor 'Поставщик' = DATA Partner (Bill);
nameVendor 'Поставщик' (Bill b) = name(vendor(b));
CONSTRAINT vendor(Bill b) AND NOT isVendor(vendor(b)) 
                CHECKED BY vendor
                MESSAGE 'Контрагент должен быть поставщиком';

// company
company 'Компания' = DATA Company (Bill) NONULL;
nameCompany 'Компания' (Bill b) = name(company(b));
WHEN LOCAL SET(Bill b IS Bill) AND NOT CHANGED(company(b)) DO company(b) <- defaultCompany(); 

// payment
paymentTerms = DATA PaymentTerm (Bill); 
namePaymentTerms 'Условия оплаты' (Bill b) = name(paymentTerms(b));

dueDateTime 'Оплатить до' = DATA DATETIME (Bill);

WHEN LOCAL CHANGED(vendor(Bill b)) AND NOT CHANGED(paymentTerms(b)) DO paymentTerms(b) <- vendorPaymentTerms(vendor(b)); 

WHEN LOCAL (SETCHANGED(paymentTerms(Bill b)) OR SETCHANGED(dateTime(b))) AND paymentTerms(b) AND NOT CHANGED(dueDateTime(b)) DO {
    dueDateTime(b) <- sumDay(dateTime(b), days(paymentTerms(b)));
}

// partner account
vendorAccount 'Счет поставщика' = DATA Account (Bill);
nameVendorAccount 'Счет поставщика' (Bill b) = name(vendorAccount(b));
WHEN LOCAL CHANGED (vendor(Bill b)) DO vendorAccount(b) <- defaultAccount(vendor(b));

CONSTRAINT vendorAccount(Bill b) AND NOT vendor(b) = holder(vendorAccount(b))
    CHECKED BY vendorAccount
    MESSAGE 'Счет должен принадлежать выбранному контрагенту';

// company account
companyAccount 'Счет компании' = DATA Account (Bill);
nameCompanyAccount 'Счет компании' (Bill b) = name(companyAccount(b));
WHEN LOCAL CHANGED(company(Bill b)) DO companyAccount(b) <- defaultAccount(company(b));

CONSTRAINT companyAccount(Bill b) AND NOT company(b) = holder(companyAccount(b))
    CHECKED BY companyAccount
    MESSAGE 'Счет должен принадлежать выбранной компании';

vendorReference 'Код поставщика' = DATA STRING[30] (Bill);
note 'Примечание' = DATA ISTRING[50] (Bill);

// lines
CLASS BillLine 'Строка поступления';

bill 'Поступление' = DATA Bill (BillLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER BillLine l BY bill(l) IN id MATERIALIZED CHARWIDTH 3;

item 'Номенклатура' = DATA Item (BillLine) NONULL;
nameItem 'Номенклатура' (BillLine l) = name(item(l)) IN id;
idBarCodeItem 'Штрихкод' (BillLine l) = idBarCode(item(l));
idItem 'Код' (BillLine l) = id(item(l));

countProductLines 'Кол-во строк с товарами' (Bill b) = GROUP SUM 1 IF bill(BillLine l) = b AND item(l) IS Product;
countServiceLine 'Кол-во строк с услугами' (Bill b) = GROUP SUM 1 IF b = bill(BillLine bl) AND item(bl) IS Service;

CONSTRAINT item(BillLine l) AND NOT canBePurchased(item(l))
            CHECKED BY item 
            MESSAGE 'Номенклатура не предназначена для закупки';

uom (BillLine l) = uom(item(l));
nameUom 'Ед. изм.' (BillLine l) = name(uom(l));

description 'Описание' = DATA STRING (BillLine); 

quantity 'Кол-во' = DATA NUMERIC[16,3] (BillLine) NONULL;
price 'Цена' = DATA NUMERIC[10,2] (BillLine) NONULL;

defaultPrice = ABSTRACT CASE NUMERIC[10,2] (BillLine);
WHEN LOCAL SETCHANGED(item(BillLine l)) AND NOT CHANGED(price(l)) DO price(l) <- defaultPrice(l);

defaultPrice (BillLine l) += WHEN cost(item(l), DATE(dateTime(bill(l)))) THEN cost(item(l)); 

// taxes
@defineTaxCalc(bill, b, purchase);
taxIncluded (BillLine l) += taxIncluded(type(bill(l)));

// Line properties
nameStatus 'Статус' (BillLine l) = nameStatus(bill(l));
dateTime 'Дата' (BillLine l) = dateTime(bill(l));
number 'Номер' (BillLine l) = number(bill(l));
numberDate 'Описание' (BillLine l) = numberDate(bill(l));
vendor (BillLine l) = vendor(bill(l));
nameVendor 'Поставщик' (BillLine l) = nameVendor(bill(l));
nameCompany 'Компания' (BillLine l) = nameCompany(bill(l));

FORM billLines 'Строки поступления'
    OBJECTS l = BillLine
    PROPERTIES(l) READONLY dateTime, number, index, nameItem, quantity, price
    PROPERTIES(l) NEWSESSION EDIT
    
    LIST BillLine OBJECT l
;

edit (BillLine l) + { edit(bill(l)); } 

FORM bill 'Поступление'
    OBJECTS  b = Bill PANEL 
    PROPERTIES(b) nameType, dateTime, number, 
                  nameVendor, nameCompany,
                  namePaymentTerms, dueDateTime, nameVendorAccount, nameCompanyAccount,
                  vendorReference, note
        
    OBJECTS l = BillLine
    PROPERTIES(l) index, nameItem, description, nameUom, idBarCodeItem, idItem, 
                  quantity, price, taxes ON CHANGE changeTax(l), NEW, DELETE 
    FILTERS bill(l) = b

    EDIT Bill OBJECT b
;

DESIGN bill {
    caption = (CONCAT ' ', 'Поступление', '№' + number(b), 'от ' + dateTime(b));
    OBJECTS {
        NEW header {
            alignment = STRETCH;    
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(b)) { notNull = TRUE; }
                MOVE PROPERTY(dateTime(b));
                MOVE PROPERTY(number(b));
            }
            NEW headerRight {
                MOVE PROPERTY(nameVendor(b));
                MOVE PROPERTY(nameCompany(b)) { notNull = TRUE; }           
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l) { 
                    fill = 3;
                    PROPERTY(nameItem(l)) { notNull = TRUE; } 
                }
                NEW linesFooter {                     
                    type = CONTAINERH;
                    alignment = STRETCH;
                }
            }
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW payment {
                    caption = 'Оплата';
                    alignment = STRETCH;
                    MOVE PROPERTY(namePaymentTerms(b)); 
                    MOVE PROPERTY(dueDateTime(b)); 
                    MOVE PROPERTY(nameCompanyAccount(b));
                    MOVE PROPERTY(nameVendorAccount(b));              
                }           
                NEW billInformat {
                    caption = 'Прочие';
                    alignment = STRETCH;
                    MOVE PROPERTY(vendorReference(b)); 
                    MOVE PROPERTY(note(b));
                }
            }
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistory(bill, b, item, quantity);
@defineDocSearch(bill, item, quantity, 'Кол-во', b, canBePurchased);
@defineBarCodeSearch(bill, b, l, item, quantity);

background = ABSTRACT CASE COLOR (Bill);
FORM bills 'Поступления'
    OBJECTS b = Bill BACKGROUND background(b)
    PROPERTIES(b) READONLYIF isReadonly()
                           nameType, dateTime, number, 
                           nameVendor, nameCompany,
                           namePaymentTerms, dueDateTime, nameVendorAccount, nameCompanyAccount,
                           vendorReference, note
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE
           
    LIST Bill OBJECT b
;

@extendFormEditable(bills);

@defineDocObjectsForm(bills, b, 'Поступления');
@defineTaxForm(bill, b);
@defineDocStatusForm(bill, b);
@defineDocLinesCount(bill, b);

NAVIGATOR {
    operations {
        NEW bills FIRST;
    }
}

// copy
clone ABSTRACT LIST (Bill, Bill);
clone (Bill to, Bill from) + {
    type(to) <- type(from);
    vendor(to) <- vendor(from);
    note(to) <- note(from);
    FOR bill(BillLine l) = from INLINE NEW nl = BillLine DO {
        bill(nl) <- to;
        item(nl) <- item(l);
        quantity(nl) <- quantity(l);
        price(nl) <- price(l);
    }
}

copy 'Копировать' (Bill from)  { 
    NEWSESSION {
        NEW to = Bill {
            clone(to, from);
            SHOW bill OBJECTS b = to DOCKED;
        }
    }
}
EXTEND FORM bill PROPERTIES(b) copy;

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(copy(b)); 
    }
}