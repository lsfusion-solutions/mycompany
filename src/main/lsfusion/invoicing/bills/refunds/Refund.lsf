MODULE Refund;

REQUIRE BillDone, InvoiceCanceled;

NAMESPACE Invoicing;

// types
isRefund 'Return' = DATA BOOLEAN (InvoiceType);
EXTEND FORM invoiceType PROPERTIES(o) isRefund;

isRefund (Invoice i) = isRefund(type(i));
isCustomer(Invoice i) += isRefund(i) AND isVendor(customer(i));

invoiceType = DATA InvoiceType (BillType);
nameInvoiceType 'Return type' (BillType t) = name(invoiceType(t));
EXTEND FORM billType PROPERTIES(o) nameInvoiceType;

// lines
@defineDocLineRelation(bill, invoice, 'Bill', 'Refund', b, i);

numberBillLine 'Invoice' (InvoiceLine l) = number(billLine(l)); 

CONSTRAINT billLine(InvoiceLine l) AND NOT 
                (vendor(billLine(l)) = customer(l) AND 
                 item(billLine(l)) = item(l))
    CHECKED BY billLine
    MESSAGE 'Partner and sales item must match the partner and return item';

// events
WHEN LOCAL SETCHANGED(billLine(InvoiceLine l)) DO {
    price(l) <- price(billLine(l));
    in(l, Tax t) <- in(billLine(l), t);
}

EXTEND FORM invoice
    PROPERTIES(l) numberBillLine SHOWIF isRefund(i)
;

// add
fill ABSTRACT LIST (InvoiceLine, BillLine);
fill ABSTRACT LIST (Invoice, Bill);

addRefund 'Return' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION { 
        NEW i = Invoice {
            type(i) <- invoiceType(type(b));
            
            dateTime(i) <- currentDateTime();
            
            customer(i) <- vendor(b);
            customerAccount(i) <- vendorAccount(b);
            
            company(i) <- company(b);
            companyAccount(i) <- companyAccount(b);
            
            note(i) <- note(b);
            
            FOR bill(BillLine l) = b INLINE NEW bl = InvoiceLine DO {
                billLine(bl) <- l; 
                invoice(bl) <- i;

                item(bl) <- item(l);

                quantity(bl) <- quantity(l);
                
                fill(bl, l);
            }
            
            fill(i, b);
            
            SHOW invoice OBJECTS i = i DOCKED;
        }
        
    }
}

EXTEND FORM bill
    PROPERTIES(b) addRefund SHOWIF ready(b)
;

DESIGN bill {
    secondaryActions {
        MOVE PROPERTY(addRefund(b)) { valueClass = 'btn-warning'; }
    }               
}

// returned quantity
checkReturnedQuantity 'Check returned quantity' = DATA BOOLEAN (BillType);
EXTEND FORM billType PROPERTIES(o) checkReturnedQuantity;

returned 'Returned' (BillLine bl) = 
    GROUP SUM quantity(InvoiceLine il) IF billLine(il) = bl AND active(invoice(il));

CONSTRAINT SET(returned(BillLine bl) > OVERRIDE quantity(bl), 0.0) AND checkReturnedQuantity(type(bl))
    MESSAGE 'The returned quantity on the bill line exceeds the original quantity';

FORM billInvoiceShow 'Refunds'
    OBJECTS bl = BillLine
    
    OBJECTS il = InvoiceLine
    PROPERTIES(il) READONLY imagedNameStatus, number, dateTime, quantity
    FILTERS billLine(il) = bl,
            active(invoice(il))
;
    
EXTEND FORM bill
    PROPERTIES(l) returned ON CHANGE { SHOW billInvoiceShow OBJECTS bl = l; } SHOWIF countInvoiceLine(b)
;