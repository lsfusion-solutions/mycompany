MODULE BillDebt;

REQUIRE BillDone, BillCanceled, Debt;

NAMESPACE Invoicing;

// extend
EXTEND CLASS Bill : IncomingDebt;
type(Bill b) += nameType(b);

active (Bill b) += active(b);
number (Bill b) += number(b);
dateTime (Bill b) += dateTime(b);
dueDateTime (Bill b) += dueDateTime(b);

partner (Bill b) += vendor(b);
company (Bill b) += company(b);

amount (Bill b) += amount(b);

// pay
pay 'Разнести' (Bill b, OutgoingDebt d) {
    paid(b, d) <- left(b, d) (+) paid(b, d);
} CHANGEMOUSE 'DBLCLK';

EXTEND FORM bill
    PROPERTIES(b) paid
    
    OBJECTS d = OutgoingDebt
    PROPERTIES(d) READONLY dateTime, number, class, type
    FILTERS paid(b, d), active(d)
    
    OBJECTS dd = OutgoingDebt
    PROPERTIES(dd) READONLY dateTime, number, class, type, left
    PROPERTIES pay(b, dd) TOOLBAR
    FILTERS canBePaid(b, dd), active(dd)   
;

DESIGN bill {
    total {
        MOVE PROPERTY(paid(b));
    }
    details {
        NEW debts {
            caption = 'Разнесение оплат';
            MOVE BOX(d) { caption = 'Разнесенные'; }            
            MOVE BOX(dd) { caption = 'Доступные'; }          
        }
    }
}

EXTEND FORM bills   
    PROPERTIES(b) READONLY paid  
;

// auto set done
WHEN SET(amount(Bill b) = paid(b)) DO done(b) <- TRUE;
WHEN DROPPED(amount(Bill b) = paid(b)) DO done(b) <- NULL;