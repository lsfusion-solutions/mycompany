MODULE BillServiceCost;

REQUIRE BillCost;

NAMESPACE Invoicing;

EXTEND CLASS BillLine : CostAllocationTarget;

allocationBase (BillLine bl, CostAllocationBase b) += 
    WHEN item(bl) IS Product AND b = CostAllocationBase.amount THEN NUMERIC[14,3](untaxedAmount(bl));  
allocationBase (BillLine bl, CostAllocationBase b) += 
    WHEN item(bl) IS Product AND b = CostAllocationBase.cost THEN NUMERIC[14,3](cost(item(bl), DATE(dateTime(bl))) * quantity(bl)); 
allocationBase (BillLine bl, CostAllocationBase b) += 
    WHEN item(bl) IS Product AND b = CostAllocationBase.salesPrice THEN NUMERIC[14,3](salesPrice(item(bl)) * quantity(bl));
allocationBase (BillLine bl, CostAllocationBase b) += 
    WHEN item(bl) IS Product AND b = CostAllocationBase.weight THEN NUMERIC[14,3](weight(item(bl)) * quantity(bl));
allocationBase (BillLine bl, CostAllocationBase b) += 
    WHEN item(bl) IS Product AND b = CostAllocationBase.volume THEN NUMERIC[14,3](volume(item(bl)) * quantity(bl));
allocationBase (BillLine bl, CostAllocationBase b) +=
    WHEN item(bl) IS Product AND b = CostAllocationBase.quantity THEN NUMERIC[14,3](quantity(bl));

serviceLine 'Distribute' = DATA BOOLEAN (BillLine, BillLine) CHARWIDTH 12;
serviceLine(BillLine t, BillLine b) += serviceLine(t, b);

countServiceLineBill (BillLine l) = GROUP SUM 1 IF serviceLine(BillLine bl, l);  

costAllocated 'Distributed' = DATA NUMERIC[14,2] (BillLine, BillLine);
costAllocated(BillLine t, BillLine b) += costAllocated(t, b);

service 'Distribute' (Bill b, BillLine bsl) = GROUP MAX serviceLine(CostAllocationTarget t, bsl) IF bill(t) = b CHARWIDTH 12;
costAllocated 'Distribute' (Bill b, BillLine bsl) = GROUP SUM costAllocated(CostAllocationTarget t, bsl) IF bill(t) = b;

checkBill (Bill b, BillLine bsl) {
    LOCAL checked = BOOLEAN ();
    checked() <- NOT service(b,bsl);
    FOR bill(BillLine bl) = b DO
        serviceLine(bl, bsl) <- checked();
}

EXTEND FORM bill
    OBJECTS bc = Bill
    PROPERTIES service(bc, bsl) ON CHANGE checkBill(bc, bsl)
    PROPERTIES(bc) READONLY number, dateTime, nameVendor, imagedNameStatus BACKGROUND colorStatus(bc), nameType, nameCompany, 
                            countBillLine, untaxedAmount, amount
    FILTERGROUP distributedBill
        FILTER 'Distributed' costAllocated(bc, bsl)

    OBJECTS bl = BillLine
    PROPERTIES(bl, bsl) serviceLine, costAllocated
    PROPERTIES(bl, bsl) READONLY allocationBase
    PROPERTIES(bl) READONLY numberDate SHOWIF (GROUP SUM 1 IF service(Bill bb, bsl)) > 1
    PROPERTIES(bl) READONLY index, nameItem, nameUom, idBarCodeItem, idItem, referenceItem, quantity, price, taxes, untaxedAmount
    FILTERS service(bill(bl), bsl),
            item(bl) IS Product

    FILTERGROUP distributedBillLine
        FILTER 'Distributed' costAllocated(bl, bsl)
;

DESIGN bill {
    costAllocationPane {
        NEW billCostAllocationPane {
            caption = badged('Bills', countServiceLineBill(bsl));
            MOVE BOX(bc) {
                GRID(bc) {
                    height = 100;
                }
            }
            MOVE BOX(bl) {
                GRID(bl) {
                    height = 100;
                }
            }
        }
    }
}