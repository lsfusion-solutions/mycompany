MODULE BillType;

REQUIRE Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS BillType 'Тип поступления';
name 'Название' = DATA ISTRING[50] (BillType) NONULL;

id 'Код' = DATA STRING[20] (BillType);
billType = GROUP AGGR BillType t BY id(t);

numerator 'Нумератор' = DATA Numerator (BillType);
nameNumerator 'Нумератор' (BillType o) = name(numerator(o));

taxIncluded 'Цена включает налоги' = DATA BOOLEAN (BillType);

FORM billType 'Тип поступления'
    OBJECTS o = BillType PANEL 
    PROPERTIES(o) name, id, nameNumerator, taxIncluded
     
    EDIT BillType OBJECT o;
;

DESIGN billType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM billTypes 'Типы поступления'
    OBJECTS o = BillType
    PROPERTIES(o) READONLY name, id
    
    LIST BillType OBJECT o;
;

EXTEND FORM options
    OBJECTS bt = BillType
    PROPERTIES(bt) READONLY name, id
    PROPERTIES(bt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(bt);
    }
}

// default type
countBillType 'Кол-во типов' () = GROUP SUM 1 IF r IS BillType; 
firstBillType = GROUP LAST BillType r ORDER DESC r WHERE r IS BillType;

defaultBillType = firstBillType() IF countBillType() = 1;