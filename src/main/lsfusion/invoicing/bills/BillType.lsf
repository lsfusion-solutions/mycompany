MODULE BillType;

REQUIRE Partner, Numerator, InvoicingSettings;

NAMESPACE Invoicing;

CLASS BillType 'Bill type';
name '{Name}' = DATA ISTRING[50] (BillType) NONULL CHARWIDTH 15;

id '{ID}' = DATA STRING[20] (BillType);
billType = GROUP AGGR BillType t BY id(t);

numerator 'Numerator' = DATA Numerator (BillType);
nameNumerator 'Numerator' (BillType o) = name(numerator(o));

vendor = DATA Partner (BillType);
nameVendor 'Vendor' (BillType o) = name(vendor(o));

taxIncluded 'Price includes taxes' = DATA BOOLEAN (BillType);

FORM billType 'Bill type'
    OBJECTS o = BillType PANEL 
    PROPERTIES(o) name, id, nameNumerator, nameVendor, taxIncluded
     
    EDIT BillType OBJECT o;
;

DESIGN billType {
    OBJECTS {
        NEW pane {
            lines = 2;
            NEW header {
                horizontal = TRUE;
                MOVE PROPERTY(name(o));
                MOVE PROPERTY(id(o));
                MOVE PROPERTY(nameNumerator(o));
            }
            NEW price {
                lines = 3;
                caption = 'Price';
                MOVE PROPERTY(taxIncluded(o));
            }
            NEW return {
                lines = 3;
                caption = 'Return';
            }
            NEW default {
                horizontal = TRUE;
                caption = 'Default';
                MOVE PROPERTY(nameVendor(o));
            }
            NEW otherInformation {
                horizontal = TRUE;
                caption = 'Other information';
            }
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM billTypes 'Bill types'
    OBJECTS o = BillType
    PROPERTIES(o) READONLY name, id
    
    LIST BillType OBJECT o;
;

EXTEND FORM options
    OBJECTS bt = BillType
    PROPERTIES(bt) READONLY name, id
    PROPERTIES(bt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(bt);
    }
}

// default type
countBillType 'Number of types' () = GROUP SUM 1 IF r IS BillType; 
firstBillType = GROUP LAST BillType r ORDER DESC r WHERE r IS BillType;

defaultBillType = firstBillType() IF countBillType() = 1;