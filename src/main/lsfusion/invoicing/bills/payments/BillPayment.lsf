MODULE BillPayment;

REQUIRE BillDone, BillDebt, OutgoingPaymentDebt, OutgoingPaymentCurrency;

NAMESPACE Invoicing;

paymentType = DATA OutgoingPaymentType (BillType);
namePaymentType 'Payment type' (BillType t) = name(paymentType(t));

EXTEND FORM billType PROPERTIES namePaymentType(o);

fill ABSTRACT LIST (OutgoingPayment, Bill);
fill (OutgoingPayment p, Bill b) + {
    type(p) <- paymentType(type(b));

    partner(p) <- vendor(b);
    partnerAccount(p) <- vendorAccount(b);

    company(p) <- company(b);
    companyAccount(p) <- companyAccount(b);
    
    currency(p) <- currency(b);
}

registerPayment 'Register Payment' (Bill b) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW p = OutgoingPayment {
            ready(p) <- TRUE;
            
            fill(p, b);

            executeLocalEvents();
            
            amount(p) <- sourceLeft(b);
            paid(b, p) <- left(b, p);
            
            DIALOG outgoingPayment OBJECTS p = p DOCKED NOMANAGESESSION DO {
                IF amount[Debt](p) < paid(b, p) THEN paid(b, p) <- amount[Debt](p);
                APPLY; 
            }
       } 
   }
}

EXTEND FORM bill
    PROPERTIES(b) registerPayment SHOWIF status(b) = BillStatus.ready 
;

DESIGN bill {
    primaryActions {
        MOVE PROPERTY(registerPayment(b)) { valueClass = 'btn-primary'; }
    }
}