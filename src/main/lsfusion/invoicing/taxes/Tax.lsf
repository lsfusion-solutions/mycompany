MODULE Tax;

REQUIRE TaxGroup;

NAMESPACE Invoicing;

CLASS Tax 'Tax';

name '{Name}' = DATA ISTRING[50] (Tax) IN id CHARWIDTH 8;
tax (STRING name) = GROUP MAX Tax t BY name(t);

value 'Value, %' = DATA NUMERIC[5,2] (Tax);

taxGroup 'Tax group' = DATA TaxGroup (Tax);
nameTaxGroup 'Tax group' (Tax t) = name(taxGroup(t));

tax (TaxGroup g, NUMERIC[5,2] v) = GROUP LAST Tax t ORDER t WHERE taxGroup(t) = g AND value(t) = v; 

FORM tax 'Tax'
    OBJECTS t = Tax PANEL 
    PROPERTIES(t) name, value, nameTaxGroup
    
    EDIT Tax OBJECT t
;

FORM taxes 'Taxes'
    OBJECTS t = Tax
    PROPERTIES(t) READONLY name, value, nameTaxGroup
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    
    LIST Tax OBJECT t
;

NAVIGATOR {
    settings {
        NEW taxes;
    }
}

META defineTax (prefix, cls, caption, form, obj, container)
    prefix###in 'Incl.' = DATA BOOLEAN (cls, Tax) CHARWIDTH 5;
    prefix###tax caption##'Tax'##', %' (cls i) = GROUP SUM value(Tax tax) IF prefix###in(i, tax);
    name###prefix##Tax caption##'Tax' (cls i) = GROUP CONCAT name(Tax t) IF prefix###in(i, t), ', ' ORDER t CHARWIDTH 8;
    
    prefix###countIn (cls i, TaxGroup g) = GROUP SUM 1 IF prefix###in(i, Tax t) AND taxGroup(t) = g;
    
    WHEN LOCAL SET(prefix###in(cls i, Tax t)) DO {
        FOR taxGroup(t) = taxGroup(Tax tx) AND NOT t = tx DO {
            prefix###in(i, tx) <- NULL;
        }
    }
    
    EXTEND FORM form
        PROPERTIES(obj) name###prefix##Tax
    ;
    DESIGN form {
        container {
            MOVE PROPERTY(name###prefix##Tax(obj));
        }
    } 
END