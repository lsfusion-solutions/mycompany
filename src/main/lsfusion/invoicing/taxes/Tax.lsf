MODULE Tax;

REQUIRE TaxGroup;

NAMESPACE Invoicing;

CLASS Tax 'Налог';

name 'Название' = DATA ISTRING[50] (Tax) IN id;

value 'Значение, %' = DATA NUMERIC[5,2] (Tax);

taxGroup 'Группа налога' = DATA TaxGroup (Tax);
nameTaxGroup 'Группа налога' (Tax t) = name(taxGroup(t));

FORM tax 'Налог'
    OBJECTS t = Tax PANEL 
    PROPERTIES(t) name, value, nameTaxGroup
    
    EDIT Tax OBJECT t
;

FORM taxes 'Налоги'
    OBJECTS t = Tax
    PROPERTIES(t) READONLY name, value, nameTaxGroup
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    
    LIST Tax OBJECT t
;

NAVIGATOR {
    settings {
        NEW taxes;
    }
}

// change tax
setTax 'Вкл' = DATA LOCAL BOOLEAN (Tax);

WHEN LOCAL CHANGED(setTax(Tax t)) DO {
    FOR taxGroup(t) = taxGroup(Tax tx) AND NOT t = tx DO {
        setTax(tx) <- NULL;
    }
}

FORM changeTax 'Установить налог'
    OBJECTS t = Tax
    PROPERTIES(t) setTax, name READONLY
;

META defineTax (prefix, cls, caption, form, obj, container)
    prefix##In 'Вкл' = DATA BOOLEAN (cls, Tax) CHARWIDTH 5;
    prefix##Tax 'Налоги '##caption##', %' (cls i) = GROUP SUM value(Tax tax) IF prefix##In(i, tax);
    name###prefix##Tax 'Налоги '##caption (cls i) = GROUP CONCAT name(Tax t) IF prefix##In(i, t), ', ' ORDER t CHARWIDTH 8;
    
    WHEN LOCAL CHANGED(prefix##In(cls i, Tax t)) DO {
        FOR taxGroup(t) = taxGroup(Tax tx) AND NOT t = tx DO {
            prefix##In(i, tx) <- NULL;
        }
    }
    
    change###prefix##Tax (cls i) {
        setTax (Tax t) <- prefix##In(i, t);  
        DIALOG changeTax DO {
            prefix##In(i, Tax t) <- setTax(t);
        } 
    }
    
    EXTEND FORM form
        PROPERTIES(obj) name###prefix##Tax ON CHANGE change###prefix##Tax(obj)
    ;
    DESIGN form {
        container {
            MOVE PROPERTY(name###prefix##Tax(obj));
        }
    } 
END