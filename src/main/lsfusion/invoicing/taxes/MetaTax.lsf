MODULE MetaTax;

META defineTaxCalc(doc, let, prefix, price)
    untaxedAmount '{Amount}'  = DATA NUMERIC[14,2] (###doc##Line);
    
    WHEN LOCAL (CHANGED(quantity(###doc##Line l)) OR CHANGED(price(l))) AND NOT CHANGED(untaxedAmount(l)) DO {
        untaxedAmount(l) <- NUMERIC[14,2](quantity(l) * price(l));
    }
    
    // taxes for line
    in 'Incl.' = DATA BOOLEAN (###doc##Line, Tax);
    WHEN LOCAL CHANGED(item(###doc##Line l)) AND NOT GROUP SUM 1 IF SET(in(l, Tax t)) DO
        in(l, Tax t) <- prefix##In(item(l), t) WHERE prefix##CountIn(item(l), taxGroup(t));

    WHEN LOCAL SET(in(###doc##Line l, Tax t)) DO {
        in(l, Tax tx) <- NULL WHERE taxGroup(t) = taxGroup(tx) AND NOT t = tx;
    }
    tax (###doc##Line l, TaxGroup tg) = GROUP AGGR Tax t WHERE in(l, t) BY taxGroup(t);
    nameTax (###doc##Line l, TaxGroup tg) = name(tax(l, tg));
    
    changeTax (###doc##Line l, TaxGroup tg) {
        DIALOG taxes OBJECTS t = tax(l, tg) INPUT LIST name(t) FILTERS taxGroup(t) = tg DO {
            in(l, Tax tx) <- t = tx WHERE taxGroup(tx) = tg;
        }
    }

    // amount    
    taxAmount 'Taxes' = DATA NUMERIC[14,2] (Tax, ###doc##Line);
    
    taxIncluded = ABSTRACT VALUE BOOLEAN (###doc##Line);
    WHEN LOCAL (CHANGED(untaxedAmount(###doc##Line l) IF in(l, Tax t)) OR CHANGED(taxIncluded(l))) AND value(t) AND NOT CHANGED(taxAmount(t, l)) DO
        taxAmount (t, l) <- NUMERIC[14,2] (value(t) / (100 (+) (value(t) IF taxIncluded(l))) * untaxedAmount(l));
    
    CONSTRAINT (DROPPED(Tax t IS Tax) AND PREV(taxAmount(t, ###doc##Line l))) 
                                                            MESSAGE 'You cannot remove tax involved in calculations';
    
    taxAmount 'Taxes' (###doc##Line l) = GROUP SUM taxAmount(Tax t, l) IF in(l, t);
    taxPercent 'Taxes,%' (###doc##Line l) = GROUP SUM value(Tax t) IF in(l, t);
    countTaxes (###doc##Line l) = GROUP SUM 1 IF in(l, Tax t);
    taxes 'Taxes' (###doc##Line l) = GROUP CONCAT name(Tax t) IF in(l, t), ', ' ORDER t CHARWIDTH 8;

    amount 'Total amount' (###doc##Line l) = untaxedAmount(l) (+) (taxAmount(l) IF NOT taxIncluded(l));
    
    // taxes for document
    taxCount (###doc let, Tax t) = GROUP SUM 1 IF in(###doc##Line l, t) AND doc(l) = let;
    untaxedAmount '{Amount}' (Tax t, ###doc let) =
        GROUP SUM untaxedAmount(###doc##Line l) IF doc(l) = let AND in(l, t);
    taxAmount 'Tax' (Tax t, ###doc let) = 
        GROUP SUM taxAmount(t, ###doc##Line l) IF doc(l) = let AND in(l, t);
    amount '{Amount}' (Tax t, ###doc let) =
        GROUP SUM amount(###doc##Line l) IF doc(l) = let AND in(l, t);
    
    untaxedAmount '{Amount}' (###doc let) = GROUP SUM untaxedAmount(###doc##Line l) IF doc(l) = let;

    taxAmount 'Tax' (###doc let) = GROUP SUM taxAmount(Tax t, let);

    amount 'Total amount' (###doc let) = GROUP SUM amount(###doc##Line l) IF doc(l) = let;

    index (###doc let, Tax t) = PARTITION SUM 1 IF taxCount(let, t) ORDER value(t), t BY let;
    tax (###doc let, INTEGER it) = GROUP MAX Tax t IF index(let, t) = it;
END

META defineTaxCalc(doc, let, prefix)
    @defineTaxCalc(doc, let, prefix, price);
END

META defineTaxData(doc, let, container)
    taxIncluded 'Price includes taxes' = DATA BOOLEAN (###doc);
    EXTEND FORM doc PROPERTIES(let) taxIncluded SHOWIF NOT taxIncluded(type(let));
    DESIGN doc {
        container {
            MOVE PROPERTY(taxIncluded(let));
        }
    }
    taxIncluded(###doc###Line l) += taxIncluded(doc(l));
END

META defineDocTaxForm(doc, let)
    EXTEND FORM doc  
        PROPERTIES(l) untaxedAmount ON CHANGE changeUntaxedAmount(l)
            
        OBJECTS t = Tax
        PROPERTIES name(t) READONLY, taxAmount(t, let) READONLY, taxAmount(t, l) READONLYIF readonly(let)
        FILTERS taxCount(let, t)
                
        PROPERTIES(let) READONLY untaxedAmount, taxAmount, amount
    ;
        
    DESIGN doc {
        linesFooter {
            NEW taxes {           
                fill = 1;
                MOVE BOX(t) {
                    caption = '';
                    GRID(t) { size = (100, -1); }
                }
                REMOVE TOOLBARBOX(t);
            }
            NEW total {
                MOVE PROPERTY(untaxedAmount(let));                
                MOVE PROPERTY(taxAmount(let));                
                MOVE PROPERTY(amount(let));                
            }
        }
    }
        
    EXTEND FORM doc##s 
        PROPERTIES(let) READONLY untaxedAmount, taxAmount, amount     
    ;
END

META defineTaxFormIndex (doc, form, let, ind)
    untaxedAmount##ind (###doc o) = untaxedAmount(tax(o, ind), o);
    nameTax##ind (###doc o) = name(tax(o, ind));
    taxAmount##ind (###doc o) = taxAmount(tax(o, ind), o);
    amount##ind (###doc o) = amount(tax(o, ind), o);

    EXTEND FORM form
        PROPERTIES(let) untaxedAmount##ind, nameTax##ind, taxAmount##ind, amount##ind
    ;
END
