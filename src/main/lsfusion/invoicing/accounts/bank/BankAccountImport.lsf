MODULE BankAccountImport;

REQUIRE BankAccount, Partner;

NAMESPACE Invoicing;

bankAccountExport 'Export accounts' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'HolderID' = id(holder(BankAccount b)), 'Note' = note(b),
                                'Number' = number(b), 'BankID' = id(bank(b)) WHERE b IS BankAccount TO f;
        open(f());
    }
}

bankAccountImport 'Import accounts' () {
    LOCAL idPartner = STRING[50] (INTEGER);
    LOCAL note = ISTRING[100] (INTEGER);
    LOCAL number = ISTRING[100] (INTEGER);
    LOCAL idBank = STRING[11] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idPartner = A, note = B, number = C, idBank = D;
    
            FOR [GROUP SUM 1 BY idPartner(INTEGER i)](num) AND NOT partner[STRING](num) DO {
                MESSAGE 'There is no partner in the database with the code' + ' ' + num + '... Import canceled';
                RETURN;
            } 

            FOR [GROUP SUM 1 BY idBank(INTEGER i)](num) AND NOT bank[STRING](num) DO {
                MESSAGE 'There is no bank in the database with the code' + ' ' + num + '... Import canceled';
                RETURN;
            } 
        
            FOR imported(INTEGER i)  AND
                partner(idPartner(i)) AND
                bank(idBank(i))
                DO NEW b = BankAccount {
                    holder(b) <- partner(idPartner(i));
                    note(b) <- note(i);
                    number(b) <- number(i);
                    bank(b) <- bank(idBank(i)); 
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}    

EXTEND FORM migrationData 
    PROPERTIES bankAccountExport(), bankAccountImport()
;

DESIGN migrationData {
    commons {
        NEW bankAccount {
            horizontal = TRUE;
            caption = 'Bank accounts';
            MOVE PROPERTY(bankAccountImport());
            MOVE PROPERTY(bankAccountExport());
        }
    }
}