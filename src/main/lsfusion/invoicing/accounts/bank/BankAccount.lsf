MODULE BankAccount;

REQUIRE Account, Bank, Utils;

NAMESPACE Invoicing;

CLASS BankAccount 'Банковский счет' : Account;

number 'Номер счета' = DATA STRING[30] (BankAccount) IN id NONULL;

bank 'Банк' = DATA Bank (BankAccount) NONULL;
nameBank 'Банк' (BankAccount b) = name(bank(b));
id 'Код банка' (BankAccount b) = id(bank(b));

name (BankAccount a) += (CONCAT ' ', number(a), 'в ' + nameBank(a)) IF a IS BankAccount; 

EXTEND CLASS AccountType {
     bank 'Банковский счет'
}

type(Account a) += WHEN a IS BankAccount THEN AccountType.bank;

EXTEND FORM account
    PROPERTIES(a) number, nameBank, id READONLY
;

DESIGN account {
    OBJECTS {
        NEW bank AFTER params {
            type = CONTAINERH;
            MOVE PROPERTY(number(a));
            MOVE PROPERTY(nameBank(a)) { notNull = TRUE; }
            MOVE PROPERTY(id(a));
        }
    }
}

newBankAccount 'Банковский счет' () {
    NEWSESSION {
        NEW b = BankAccount {
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

newBankAccount 'Банковский счет' (Partner p) {
    NESTEDSESSION {
        NEW b = BankAccount {
            holder(b) <- p;
            SHOW account OBJECTS a = b;  
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM accounts
    PROPERTIES newBankAccount() DRAW a TOOLBAR
    
    LIST BankAccount OBJECT a
;

EXTEND FORM partner
    PROPERTIES newBankAccount(p) DRAW a TOOLBAR
;

countBankAccount (Partner p) = GROUP SUM 1 IF BankAccount a IS BankAccount AND holder(a) = p; 
calcDefaultBankAccount (Partner p) = GROUP LAST BankAccount a WHERE holder(a) = p IF countBankAccount(p) = 1;
defaultBankAccount (Partner p) = OVERRIDE defaultAccount(p) AS BankAccount, calcDefaultBankAccount(p);
