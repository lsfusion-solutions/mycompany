MODULE AccountImport;

REQUIRE Account, BankAccount, CashAccount, Partner;

NAMESPACE Invoicing;

accountFeature (Account a) = CASE EXCLUSIVE 
    WHEN type(a) == AccountType.bank THEN number(a)
    WHEN type(a) == AccountType.cash THEN name(a)
;

partner (STRING[50] s) = GROUP MAX Partner p BY id(p);

accountExport 'Экспорт счетов' () {
    NEWSESSION {
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код контрагента' = id(holder(Account a)), 'Примечание' = note(a),
                                'Тип счета' = nameType(a), 'Номер счета / Наименование' = accountFeature(a),
                                'Код банка' = id(bank(a)) TO f;
        open(f());
    }
}

accountImport 'Импорт счетов' () {
    LOCAL idPartner = STRING[50] (INTEGER);
    LOCAL note = ISTRING[100] (INTEGER);
    LOCAL nameType = STRING[100] (INTEGER);
    LOCAL accountFeature = ISTRING[100] (INTEGER);
    LOCAL idBank = STRING[11] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO idPartner = A, note = B, nameType = C, accountFeature = D, idBank = E;
            
            FOR imported(INTEGER i)  AND
                partner(idPartner(i)) AND
                bank(idBank(i)) AND
                nameType(i) == staticCaption(AccountType.bank)
                DO NEW a = BankAccount {
                    holder(a) <- partner(idPartner(i));
                    note(a) <- note(i);
                    number(a) <- accountFeature(i);
                    bank(a) <- bank(idBank(i)); 
            }
            
            FOR imported(INTEGER i) AND
                partner(idPartner(i)) AND
                nameType(i) == staticCaption(AccountType.cash) DO NEW a = CashAccount {
                holder(a) <- partner(idPartner(i));
                note(a) <- note(i);
                name(a) <- accountFeature(i);
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }
}    

EXTEND FORM MasterData.options 
    PROPERTIES accountExport(), accountImport()
;

DESIGN MasterData.options {
    import {
        NEW account {
            type = CONTAINERH;
            caption = 'Счета контрагентов';
            MOVE PROPERTY(accountImport());
            MOVE PROPERTY(accountExport());
        }
    }
}