MODULE CashAccount;

REQUIRE Account;

NAMESPACE Invoicing;

CLASS CashAccount 'Касса' : Account;

name 'Наименование' = DATA ISTRING[100] (CashAccount);
name (CashAccount a) += name(a); 

EXTEND CLASS AccountType {
    cash 'Касса'
}

type(Account a) += WHEN a IS CashAccount THEN AccountType.cash;

EXTEND FORM account
    PROPERTIES(a) name[CashAccount]
;

DESIGN account {
    OBJECTS {
        MOVE PROPERTY(name(a)) AFTER params;
    }
}

newCashAccount 'Касса' () {
    NEWSESSION {
        NEW c = CashAccount { 
            SHOW account OBJECTS a = c;  
        }
    }
} IMAGE 'add.png';

newCashAccount 'Касса' (Partner p) {
    NESTEDSESSION {
        NEW c = CashAccount {
            holder(c) <- p;
            SHOW account OBJECTS a = c;  
        }
    }
} IMAGE 'add.png';

EXTEND FORM accounts
    PROPERTIES newCashAccount() DRAW a TOOLBAR
    
    LIST CashAccount OBJECT a
;

EXTEND FORM partner
    PROPERTIES newCashAccount(p) DRAW a TOOLBAR
;

FORM cashAccounts 'Кассы'
    OBJECTS a = CashAccount
    PROPERTIES(a) READONLY name, nameAccountHolder, note
    
    LIST CashAccount OBJECT a
;

countCashAccount (Partner p) = GROUP SUM 1 IF CashAccount a IS CashAccount AND holder(a) = p; 
calcDefaultCashAccount (Partner p) = GROUP LAST CashAccount a WHERE holder(a) = p IF countCashAccount(p) = 1;
defaultCashAccount (Partner p) = OVERRIDE defaultAccount(p) AS CashAccount, calcDefaultCashAccount(p);
