MODULE Account;

REQUIRE InvoicingSettings, Partner, Currency, Icon;

NAMESPACE Invoicing;

CLASS ABSTRACT Account 'Account';

holder 'Account holder' = DATA Partner (Account) NONULL;
nameAccountHolder 'Account holder' (Account a) = name(holder(a)) IN id;

countAccount (Partner p) = GROUP SUM 1 IF holder(Account a) = p;  

dataDefaultAccount = DATA Account (Partner);
calcDefaultAccount (Partner p) = GROUP LAST Account a ORDER DESC a WHERE holder(a) = p;

defaultAccount (Partner p) = OVERRIDE dataDefaultAccount(p), calcDefaultAccount(p);
default 'Default' (Account a) = defaultAccount(holder(a)) = a CHARWIDTH 10;

// currency
currency = DATA Currency (Account);
nameCurrency 'Currency' (Account a) = name(currency(a));

note 'Note' = DATA ISTRING (Account);

name '{Name}' = ABSTRACT ISTRING (Account) CHARWIDTH 20 IN id;

allowNegative 'Allow negative balance' = DATA BOOLEAN (Account);

CLASS AccountType 'Account type';

name '{First_name}' (AccountType o) = staticCaption(o) IF o IS AccountType CHARWIDTH 15;

type  = ABSTRACT CASE AccountType (Account);
nameType 'Account type' (Account a) = name(type(a));

changeDefault (Account a) {
    INPUT b = BOOLEAN DO {
        IF b THEN
            defaultAccount(Partner p) <- a WHERE p = holder(a);
    }
}

FORM account 'Account'
    OBJECTS a = Account PANEL 
    PROPERTIES(a) nameType, default ON CHANGE changeDefault(a), 
                  allowNegative, nameAccountHolder, nameCurrency, note
    
    EDIT Account OBJECT a
;

DESIGN account {
    OBJECTS {
        NEW params {
            horizontal = TRUE;
            MOVE PROPERTY(nameType(a));
            MOVE PROPERTY(default(a));
            MOVE PROPERTY(allowNegative(a));
        }
        NEW extraParams {
            horizontal = TRUE;
            alignment = STRETCH;
            MOVE PROPERTY(nameAccountHolder(a)) { notNull = TRUE; }
            MOVE PROPERTY(nameCurrency(a));
        }
        MOVE PROPERTY(note(a));
    }
}

FORM accounts 'Accounts'
    OBJECTS a = Account
    PROPERTIES(a) READONLY nameType, name, nameAccountHolder, nameCurrency, note, default
    PROPERTIES(a) NEWSESSION EDIT, DELETE
    
    LIST Account OBJECT a

;

NAVIGATOR {
    invoicing {
        Invoicing.settings {
            NEW accounts;        
        }
    }
}

newAccount 'Account' (Partner p) {
    NESTEDSESSION {
        NEW a = Account {
            holder(a) <- p;
            DIALOG account OBJECTS a = a DOCKED DO seek(a);  
        }
    }
} IMAGE 'add.png';

EXTEND FORM partner
    OBJECTS a = Account
    PROPERTIES(a) READONLY nameType, name, nameCurrency, note, default
    PROPERTIES(a) NESTEDSESSION EDIT, DELETE 
//    PROPERTIES newAccount(p) DRAW a TOOLBAR
        
    FILTERS holder(a) = p
;

DESIGN partner {
    tabs {
        MOVE BOX(a) { caption = badged('Accounts', countAccount(p)); }
    }
}
