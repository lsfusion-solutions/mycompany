MODULE SalesOrderInvoiceShipment;

REQUIRE SalesOrderInvoice, SalesOrderShipment, InvoiceShipment;

NAMESPACE Sales;

EXTEND CLASS InvoicingPolicy {
    delivered 'Shipped quantity'
}

toInvoiceShipped (Order o) = 
    GROUP SUM done(ShipmentLine sl) IF order(orderLine(sl)) = o AND active(shipment(sl)) AND NOT invoiceLine(sl);
toInvoice(o) += toInvoiceShipped(o) > 0;

fill (Invoice i, Order o) + {
    location(i) <- location(o);

    IF invoicingPolicy(type(o)) = InvoicingPolicy.delivered THEN {
       
        FOR order(orderLine(ShipmentLine sl)) = o AND active(shipment(sl)) AND NOT invoiceLine(sl) AND done(sl) > 0 INLINE 
            NEW il = InvoiceLine DO {
            invoice(il) <- i;
            orderLine(il) <- orderLine(sl);
    
            item(il) <- product(sl);
            description(il) <- description(orderLine(sl));
            quantity(il) <- done(sl);
            
            price(il) <- price(orderLine(sl));
            discount(il) <- discount(orderLine(sl));
            discountPrice(il) <- discountPrice(orderLine(sl));
    
            invoiceLine(sl) <- il; 
        }
    }                            
}

fill (ShipmentLine sl, InvoiceLine il) + {
    orderLine(sl) <- orderLine(il);
}