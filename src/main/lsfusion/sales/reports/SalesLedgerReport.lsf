MODULE SalesLedgerReport;

REQUIRE SalesLedger, ItemAttribute, SalesSettings;

NAMESPACE Sales;

attribute(SalesLedger l, ItemAttribute a) = value(attribute(item(l), a));

FORM salesLedgerReport 'Sales report'
    OBJECTS a = ItemAttribute

    OBJECTS l = SalesLedger PIVOT
    PROPERTIES(l) READONLY class, type, dateTime, number, 
                           nameCustomer, nameLocation, nameItem,
                           price, markup 
    PROPERTIES READONLY 'Category 1' = level1(item(l)), 'Category 2' = level2(item(l)), 'Category 3' = level3(item(l)), 'Category 4' = level4(item(l)), 
                        'Canonical group' = canonicalNameCategory(item(l))
    PROPERTIES READONLY attribute(l, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(l) READONLY MEASURE
                           quantity, costAmount, markupAmount, untaxedAmount, taxAmount, amount
    
    FILTERS NOT skip(l)
;

DESIGN salesLedgerReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
        NEW pane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(l) {
                caption = 'Sales';
            }
        }
    }
}

@defineDateTimeAggregationForm(salesLedgerReport, l);
@defineDateFilterForm(salesLedgerReport, l);

NAVIGATOR {
    sales {
        reporting {
            NEW salesLedgerReport;        
        }
    }
}