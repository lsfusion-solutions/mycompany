MODULE SalesSummary;

REQUIRE SalesSettings, SalesLedger;

NAMESPACE Sales;

FORM salesSummary 'Sales summary'
    OBJECTS sw 'Sales by week' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(sw) READONLY year, week, markupAmount MEASURE, amount MEASURE
    PIVOT ROWS (year(sw), week(sw))

    OBJECTS sc 'Sales by customer' = SalesLedger PIVOT 'Bar Chart' NOSETTINGS
    PROPERTIES(sc) READONLY nameCustomer ROW, markupAmount MEASURE, amount MEASURE
    FILTERS NOT date(sc) < filterDateFrom(), NOT date(sc) > filterDateTo() 

    OBJECTS sls 'Turnover' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(sls) READONLY year, week, nameLocation COLUMN, amount MEASURE
    PIVOT ROWS (year(sls), week(sls))
    FILTERS NOT date(sls) < filterDateFrom(), NOT date(sls) > filterDateTo() 

    OBJECTS slm 'Income' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(slm) READONLY year, week, nameLocation COLUMN, markupAmount MEASURE
    PIVOT ROWS (year(slm), week(slm))
    FILTERS NOT date(slm) < filterDateFrom(), NOT date(slm) > filterDateTo() 

    OBJECTS sis 'Turnover' = SalesLedger PIVOT 'Multiple Pie Chart' NOSETTINGS
    PROPERTIES(sis) READONLY level2 ROW, amount MEASURE
    FILTERS NOT date(sis) < filterDateFrom(), NOT date(sis) > filterDateTo() 

    OBJECTS sim 'Income' = SalesLedger PIVOT 'Multiple Pie Chart' NOSETTINGS
    PROPERTIES(sim) READONLY level2 ROW, markupAmount MEASURE
    FILTERS NOT date(sim) < filterDateFrom(), NOT date(sim) > filterDateTo() 
;

DESIGN salesSummary {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
            MOVE BOX(sw);
            MOVE BOX(sc);
            NEW salesLocation {
                caption = 'Sales by location';
                type = SPLITV;
                MOVE BOX(sls);
                MOVE BOX(slm);
            }
            NEW salesCategory {
                caption = 'Sales by category';
                type = SPLITH;
                MOVE BOX(sis);
                MOVE BOX(sim);
            }
        }
    }
}

@defineDateFilterForm(salesSummary, sw);

NAVIGATOR {
    reporting {
        NEW salesSummary;        
    }
}