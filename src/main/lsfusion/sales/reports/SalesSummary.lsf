MODULE SalesSummary;

REQUIRE SalesSettings, SalesLedger;

NAMESPACE Sales;

FORM salesSummary 'Sales summary'
    OBJECTS sw 'Sales by week' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(sw) READONLY year, week, markupAmount MEASURE, amount MEASURE
    PIVOT ROWS (year(sw), week(sw))
    FILTERS NOT skip(sw)

    OBJECTS sc 'Sales by customer' = SalesLedger PIVOT 'Bar Chart' NOSETTINGS
    PROPERTIES(sc) READONLY nameCustomer ROW, markupAmount MEASURE, amount MEASURE
    FILTERS NOT skip(sc)

    OBJECTS sls 'Turnover' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(sls) READONLY year, week, nameLocation COLUMN, amount MEASURE
    PIVOT ROWS (year(sls), week(sls))
    FILTERS NOT skip(sls)

    OBJECTS slm 'Income' = SalesLedger PIVOT 'Line Chart' NOSETTINGS
    PROPERTIES(slm) READONLY year, week, nameLocation COLUMN, markupAmount MEASURE
    PIVOT ROWS (year(slm), week(slm))
    FILTERS NOT skip(slm)
 
    OBJECTS sis 'Turnover' = SalesLedger PIVOT 'Multiple Pie Chart' NOSETTINGS
    PROPERTIES(sis) READONLY level2 ROW, amount MEASURE
    FILTERS NOT skip(sis)
 
    OBJECTS sim 'Income' = SalesLedger PIVOT 'Multiple Pie Chart' NOSETTINGS
    PROPERTIES(sim) READONLY level2 ROW, markupAmount MEASURE
    FILTERS NOT skip(sim)
;

DESIGN salesSummary {
    OBJECTS {
        NEW filters { caption = 'Filters'; }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
            MOVE BOX(sw);
            MOVE BOX(sc);
            NEW salesLocation {
                caption = 'Sales by location';
                MOVE BOX(sls);
                MOVE BOX(slm);
            }
            NEW salesCategory {
                caption = 'Sales by category';
                horizontal = TRUE;
                MOVE BOX(sis);
                MOVE BOX(sim);
            }
        }
    }
}

@defineDateFilterForm(salesSummary, sw);

EXTEND FORM salesSummary
    FILTERS NOT date(sc) < from(dates), NOT date(sc) > to(dates)
    FILTERS NOT date(sls) < from(dates), NOT date(sls) > to(dates)
    FILTERS NOT date(slm) < from(dates), NOT date(slm) > to(dates)
    FILTERS NOT date(sis) < from(dates), NOT date(sis) > to(dates)
    FILTERS NOT date(sim) < from(dates), NOT date(sim) > to(dates)
;

NAVIGATOR {
    reporting {
        NEW salesSummary;        
    }
}