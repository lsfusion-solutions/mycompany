MODULE SalesOrderCanceled;

REQUIRE SalesOrder, SalesOrderSelection;

NAMESPACE Sales;

EXTEND CLASS OrderStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (Order);

status(Order o) += WHEN canceled(o) THEN OrderStatus.canceled;
color(OrderStatus o) += WHEN o = OrderStatus.canceled THEN RGB(252,247,149);

cancel 'Cancel' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;

    NEWSESSION {
        canceled(o) <- TRUE;
        APPLY;
    }    
}

EXTEND FORM order
     PROPERTIES(o) cancel SHOWIF NOT (status(o) = OrderStatus.draft OR status(o) = OrderStatus.canceled), canceled
;

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(cancel(o)) { valueClass = 'btn-danger'; }
    }
    statusGroup {
        MOVE PROPERTY(canceled(o)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }    
    }
}

EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(o) = OrderStatus.canceled    
;

@defineSelectionAction(orders, Order, cancel, 'Cancel', statuses);