MODULE SalesOrderConfirmed;

REQUIRE SalesOrder, SalesOrderSent;

NAMESPACE Sales;

EXTEND CLASS OrderStatus {
    confirmed 'Confirmed'
}
confirmed 'Confirmed' = DATA BOOLEAN (Order);

status(Order o) += WHEN confirmed(o) THEN OrderStatus.confirmed;
color(OrderStatus o) += WHEN o = OrderStatus.confirmed THEN RGB(150,255,150);

confirm 'Confirm' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;

    NEWSESSION {
        confirmed(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order 
     PROPERTIES(o) confirm SHOWIF (status(o) = OrderStatus.sent OR status(o) = OrderStatus.draft), confirmed
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(confirm(o)) { valueClass = 'btn-primary'; }
    }
    statusGroup {
        MOVE PROPERTY(confirmed(o)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }    
    }
}

EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Confirmed' status(o) = OrderStatus.confirmed    
;

@defineSelectionAction(orders, Order, confirm, 'Confirm', statuses);