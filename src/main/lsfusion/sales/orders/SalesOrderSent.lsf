MODULE SalesOrderSent;

REQUIRE SalesOrder, SalesOrderPrint, SalesOrderSelection;

NAMESPACE Sales;

// order type
defaultMailTemplate 'Default template' = DATA OrderTemplate (OrderType);
nameDefaultMailTemplate 'Default template' (OrderType o) = name(defaultMailTemplate(o)) IN id;

mailSendCopyTo 'Copy to' = DATA STRING[50] (OrderType);
mailSubject 'Topic' = DATA STRING (OrderType);
mailBody '' = DATA TEXT (OrderType);

CONSTRAINT defaultMailTemplate(OrderType o) AND NOT in(defaultMailTemplate(o),  o) CHECKED 
    MESSAGE 'Template must be included for this type of order';  

EXTEND FORM orderType
    PROPERTIES(o) mailSendCopyTo, mailSubject, mailBody, nameDefaultMailTemplate 
;

DESIGN orderType {
    tabbedPane {
        NEW mail {
            caption = 'Mail';
            MOVE PROPERTY(mailSendCopyTo(o));
            MOVE PROPERTY(mailSubject(o));
            MOVE PROPERTY(mailBody(o));
            MOVE PROPERTY(nameDefaultMailTemplate(o));
        }
    }
}

// order
EXTEND CLASS OrderStatus {
    sent 'Sent'
}
sent 'Sent'  = DATA BOOLEAN (Order);

status(Order o) += WHEN sent(o) THEN OrderStatus.sent;
color(OrderStatus i) += WHEN i = OrderStatus.sent THEN RGB(252,247,149);

send 'Send' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
    
    LOCAL attachFile = FILE();
    LOCAL nameFile = STRING();
    nameFile() <- 'Order no.' + numberDate(o) + ' (' + OVERRIDE nameCompany(o), name(defaultCompany()) + ')';
    orderTemplate() <- defaultMailTemplate(type(o));
    IF defaultMailTemplate(type(o)) THEN {
        CASE EXCLUSIVE
            WHEN format(orderTemplate()) = TemplateFormat.docx THEN
                PRINT printOrder OBJECTS o = o DOCX TO attachFile;
            WHEN format(orderTemplate()) = TemplateFormat.xlsx THEN
                PRINT printOrder OBJECTS o = o XLSX TO attachFile;
            WHEN format(orderTemplate()) = TemplateFormat.rtf THEN
                PRINT printOrder OBJECTS o = o RTF TO attachFile;
            WHEN format(orderTemplate()) = TemplateFormat.html THEN
                PRINT printOrder OBJECTS o = o HTML TO attachFile;
            ELSE
                PRINT printOrder OBJECTS o = o PDF TO attachFile;

        EMAIL
            SUBJECT mailSubject(type(o))
            TO email(customer(o))
            BCC mailSendCopyTo(type(o))
            BODY mailBody(type(o))
            ATTACH attachFile() NAME nameFile(); 
        ;
    }
    NEWSESSION {
        sent(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order
     PROPERTIES(o) send SHOWIF status(o) = OrderStatus.draft, sent
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(send(o)) { valueClass = 'btn-primary'; };
    }
    statusGroup {
        MOVE PROPERTY(sent(o)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }    
    }
}

EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Sent' status(o) = OrderStatus.sent    
;

@defineSelectionAction(orders, Order, send, 'Send', statuses);