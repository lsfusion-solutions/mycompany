MODULE SalesOrderPrint;

REQUIRE SalesOrder, DocTemplate;

NAMESPACE Sales;

addressLinesCustomer (Order o) = addressLines(customer(o));
phoneCustomer (Order o) = phone(customer(o));

addressLinesLocation (Order o) = addressLines(location(o));
addressCsvLocation (Order o) = addressCsv(location(o));

addressLinesCompany (Order o) = addressLines(company(o));
addressCsvCompany (Order o) = addressCsv(company(o));
phoneCompany (Order o) = phone(company(o));
fullNameCompany (Order o) = fullName(company(o));
logoCompany (Order o) = logo(company(o));

printUom = ABSTRACT VALUE Uom (OrderLine);
namePrintUom (OrderLine l) = name(printUom(l));
printUom(OrderLine l) += uom(l);

printQuantity = ABSTRACT VALUE NUMERIC[16,3] (OrderLine);
printQuantity(OrderLine l) += quantity(l);

printPrice = ABSTRACT VALUE NUMERIC[16,3] (OrderLine);
printPrice(OrderLine l) += price(l);

FORM printOrder 'Sales order'   
    OBJECTS o = Order PANEL
    PROPERTIES document = r'Order'
    PROPERTIES(o) number, dateTime, scheduledDateTime, dueDateTime, nameRepresentative,
                  nameCustomer, addressLinesCustomer, addressCsvCustomer, 
                  phoneCustomer, deliveryAddress,   
                  nameLocation, addressLinesLocation, addressCsvLocation, 
                  nameCompany, fullNameCompany, logoCompany,
                  addressLinesCompany, addressCsvCompany, phoneCompany,
                  nameCurrency
        
    OBJECTS l = OrderLine
    PROPERTIES(l) index, idItem, nameItem, description, nameUom = namePrintUom, idBarCodeItem, referenceItem, isService,
                  nameCategoryItem, level1Item, level2Item, level3Item, level4Item,
                  quantity = printQuantity, price = printPrice,
                  taxPercent, taxAmount, taxes, 
                  untaxedAmount, amount
    FILTERS order(l) = o
; 

@defineDocTemplateType(order, 'Sales order template');
@defineDocTemplateTypeForm(order, o, Sales);