MODULE SalesOrderMobile;

REQUIRE Input, Reflection, 
        Mobile, CategoryMobile, 
        SalesOrderLocked, SalesOrderDiscount, SalesOrderPack;

NAMESPACE Sales;

// order
FORM orderMobile 'Mobile order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) nameType, dateTime, number, 
                  nameCustomer, 
                  nameLocation,
                  untaxedAmount, amount

    PROPERTIES(o) confirm SHOWIF (status(o) = OrderStatus.sent OR status(o) = OrderStatus.draft), confirmed 
    
    OBJECTS l = OrderLine
    PROPERTIES(l) nameItem 
    PROPERTIES(l) 'Qty' = quantity, price, NEW, DELETE
    PROPERTIES(l) PANEL SHOWIF pack(type(o))
                  'Packs' = packQuantity ON CHANGE changePackQuantity(l),
                  'Packing' = packRatio READONLY
    PROPERTIES(l) PANEL nameSalesDiscount, '' = discount, '' = discountPrice
    PROPERTIES(l) PANEL READONLY idItem, idBarCodeItem, referenceItem,
                                 nameUom, taxes, '{Amount}' = amount
    FILTERS order(l) = o
;

DESIGN orderMobile {
    caption = badged('{Order}', 'â„–' + number(o));
    
    OBJECTS {
        class = '';
        border = FALSE;
        NEW pane {
            type = TABBED;
            fill = 1;
            NEW params {
                caption = 'Parameters';
                MOVE PROPERTY(nameType(o)) { alignment = STRETCH; }
                NEW id {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(dateTime(o));
                    MOVE PROPERTY(number(o));
                }
                MOVE PROPERTY(nameCustomer(o)) { alignment = STRETCH; }
                MOVE PROPERTY(nameLocation(o)) { alignment = STRETCH; }
                
                NEW amount {
                    flex = 1;
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(untaxedAmount(o));
                    MOVE PROPERTY(amount(o));
                }
                
                NEW actions {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    
                    MOVE PROPERTY(confirm(o));
                    MOVE PROPERTY(confirmed(o));
                }
                
                MOVE TOOLBARBOX;
            }
            NEW lines {
                caption = badged('Lines', countOrderLine(o));
                MOVE USERFILTER(l);
                MOVE GRID(l) {
                    PROPERTY(quantity(l)) { charWidth = 5; }
                }
                MOVE TOOLBARBOX(l);
                NEW pack {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(packQuantity(l));
                    MOVE PROPERTY(packRatio(l));
                }
                NEW discount {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(nameSalesDiscount(l));
                    MOVE PROPERTY(discount(l));
                    MOVE PROPERTY(discountPrice(l));
                }
                NEW item {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(idItem(l));
                    MOVE PROPERTY(idBarCodeItem(l));
                    MOVE PROPERTY(referenceItem(l));
                }
                NEW misc {
                    type = CONTAINERH;
                    alignment = STRETCH;
                    MOVE PROPERTY(nameUom(l));
                    MOVE PROPERTY(taxes(l));
                    MOVE PROPERTY(amount(l));
                }
                REMOVE TOOLBARSYSTEM(l);
            }
        }
    }
    PROPERTY(formApply()) { caption = ''; }
    PROPERTY(formCancel()) { caption = ''; }
}

// orders

newMobile 'Add to' () {
    NEWSESSION {
        NEW o = Order {
            DIALOG orderMobile OBJECTS o = o DOCKED DO seek(o);
        }
    }
} IMAGE 'add.png';

editMobile 'Edit' (Order o) {
    NEWSESSION {
        SHOW orderMobile OBJECTS o = o DOCKED;
    }
} IMAGE 'edit.png' CHANGEMOUSE 'DBLCLK';

customerFilter = DATA LOCAL Partner ();
nameCustomerFilter 'Customer' = name(customerFilter());

FORM ordersMobile 'Mobile order'
    OBJECTS o = Order
    PROPERTIES(o) READONLY number, date, nameCustomer, imagedNameStatus BACKGROUND colorStatus(o)
    PROPERTIES '' = newMobile() DRAW o TOOLBAR, '' = editMobile(o) TOOLBAR, '' = DELETE(o) NEWSESSION

    FILTERGROUP unlocked
        FILTER 'Opened' NOT locked(o) DEFAULT    

    PROPERTIES() nameCustomerFilter
    FILTERS customer(o) = customerFilter() OR NOT customerFilter()
;

DESIGN ordersMobile {
    caption = 'Orders';
    OBJECTS {
        class = '';
        border = FALSE;
        NEW filters {
            type = CONTAINERH;
            MOVE PROPERTY(nameCustomerFilter());
            MOVE FILTERGROUPS(o);
            MOVE PROPERTY(formRefresh());
        }
        MOVE USERFILTER(o);
        MOVE GRID(o);
        MOVE TOOLBARSYSTEM(o) {
            showSettings = FALSE;
        }
        MOVE TOOLBAR(o);
        REMOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    dashboards {
        NEW ordersMobile;
    }
}

showMobileForms() + { 
    IF permit(currentUser(), navigatorElementCanonicalName('Sales.ordersMobile')) THEN
        SHOW ordersMobile NOWAIT; 
}