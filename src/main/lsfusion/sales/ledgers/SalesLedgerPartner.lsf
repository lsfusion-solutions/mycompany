MODULE SalesLedgerPartner;

REQUIRE SalesLedger;

NAMESPACE Sales;

yearMonth (DATETIME d) = extractYear(d) * 12 + extractMonthNumber(d);

initialSoldAmount 'Sold (initial)' = DATA NUMERIC[14,2] (Partner);

// partner
soldAmount 'Sold' (Partner p) = GROUP SUM amount(SalesLedger l) IF customer(l) = p AND NOT skip(l) MATERIALIZED;

totalSoldAmount 'Sold' (Partner p) = OVERRIDE soldAmount(p) (+) initialSoldAmount(p), (0.0 IF p IS Partner) MATERIALIZED;
prevTotalSoldAmount (Partner p) = PREV(totalSoldAmount(p));

soldAmount (Partner p, INTEGER ym) = 
    GROUP SUM amount(SalesLedger l) IF customer(l) = p AND yearMonth(dateTime(l)) = ym AND NOT skip(l) MATERIALIZED; 

soldAmountPreviousMonth (Partner p, DATETIME d) = soldAmount(p, yearMonth(d) - 1);

soldAmountPreviousMonth 'Sold in the previous month' (Partner p) = soldAmountPreviousMonth(p, currentDateTime());
prevSoldAmountPreviousMonth (Partner p) = PREV(soldAmountPreviousMonth(p));

FORM salesLedgers 'Sales by partner'
    OBJECTS p = Partner PANEL
    PROPERTIES(p) READONLY name

    OBJECTS s = SalesLedger
    PROPERTIES(s) READONLY class, type, number, dateTime, nameCustomer, nameItem, quantity, price, 
                           costAmount, markupAmount, markup, untaxedAmount, taxAmount, amount
    PROPERTIES(s) NEWSESSION EDIT
    ORDERS dateTime(s)
    
    FILTERS customer(s) = p,
            NOT skip(s)
;

EXTEND FORM partner
    PROPERTIES(p) initialSoldAmount
    PROPERTIES(p) ON CHANGE { SHOW salesLedgers OBJECTS p = p DOCKED; } totalSoldAmount, soldAmountPreviousMonth
;
DESIGN partner {
    headerRight {
        MOVE PROPERTY(initialSoldAmount(p));
        MOVE PROPERTY(totalSoldAmount(p));
        MOVE PROPERTY(soldAmountPreviousMonth(p));
    }
}

EXTEND FORM partners
    PROPERTIES(p) READONLY initialSoldAmount, totalSoldAmount, soldAmountPreviousMonth
;