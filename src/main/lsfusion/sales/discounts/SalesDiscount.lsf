MODULE SalesDiscount;

REQUIRE SalesPriceType, BarCode;

NAMESPACE Sales;

CLASS Discount 'Скидка';

name 'Название' = DATA STRING[50] (Discount);
manual 'Применяется вручную' = DATA BOOLEAN (Discount);

startDateTime 'Начало действия' = DATA DATETIME (Discount);
startDateTime(Discount d) <- currentDateTime() WHEN SET(d IS Discount);

endDateTime 'Окончание действия' = DATA DATETIME (Discount);

active (Discount d, DATETIME time) = d IS Discount AND time IS DATETIME AND 
                                                    NOT startDateTime(d) > time AND
                                                    NOT endDateTime(d) < time;
active (Discount d) = active(d, currentDateTime()); 

// conditions
fromAmount 'Сумма покупки от' = DATA NUMERIC[14,2] (Discount);
fromQuantity 'Кол-во в строке от' = DATA NUMERIC[16,3] (Discount);

fromAccumulated 'Продано клиенту всего от' = DATA NUMERIC[14,2] (Discount);
fromAccumulatedPreviousMonth 'Продано клиенту за прошедший месяц от' = DATA NUMERIC[14,2] (Discount);

// item
in 'Вкл' = DATA BOOLEAN (Discount, Category);
set (Discount d, Category c) = GROUP LAST in(d, Category cp) ORDER level(c, cp) MATERIALIZED;
categories 'Категории' (Discount d) = GROUP CONCAT name(Category c) IF in(d, c), ',' ORDER name(c), c CHARWIDTH 20;

in 'Вкл' = DATA BOOLEAN (Discount, Item);
set (Discount d, Item i) = in(d, i) OR set(d, category(i));
items 'Номенклатура' (Discount d) = GROUP CONCAT name(Item i) IF in(d, i), ',' ORDER name(i), i CHARWIDTH 30;

// pricetype
in 'Вкл' = DATA BOOLEAN (Discount, PriceType);
countPriceType (Discount d) = GROUP SUM 1 IF in(d, PriceType t) MATERIALIZED;

// discount
discount 'Скидка, %' = DATA NUMERIC[5,2] (Discount);

priceType = DATA PriceType (Discount);
namePriceType 'Вид цены' (Discount d) = name(priceType(d));

FORM discount 'Скидка'
    OBJECTS d = Discount PANEL
    PROPERTIES(d) name, manual,
                  startDateTime, endDateTime,
                  fromAmount, fromQuantity,
                  fromAccumulatedPreviousMonth, fromAccumulated,
                  discount SHOWIF NOT priceType(d), namePriceType

    TREE categories c = Category PARENT parent(c) 
    PROPERTIES name(c) READONLY, in(d, c) BACKGROUND (GROUP SUM 1 IF in(d, Category cc) AND level(cc, c))
    ORDERS name(c)
    
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id
    PROPERTIES in(d, i)
    ORDERS name(i)
    FILTERGROUP inItem
        FILTER 'Включенные' set(d, i)
    FILTERS level(category(i), c)
    
    OBJECTS t = PriceType
    PROPERTIES(t) READONLY name
    PROPERTIES in(d, t)  
    
    EDIT Discount OBJECT d
;

DESIGN discount {
    OBJECTS {
        NEW general {
            caption = 'Основные';
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(name(d));
            MOVE PROPERTY(manual(d));
        }
        NEW period {
            caption = 'Период действия';
            type = CONTAINERH;
            alignment = STRETCH;
            MOVE PROPERTY(startDateTime(d));
            MOVE PROPERTY(endDateTime(d));
        }
        NEW conditions {
            caption = 'Условия';
            alignment = STRETCH;
            NEW generalConditions {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(fromAmount(d));
                MOVE PROPERTY(fromQuantity(d));
            }
            NEW accumulated {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(fromAccumulated(d));
                MOVE PROPERTY(fromAccumulatedPreviousMonth(d));
            }
        }
        NEW discounts {
            caption = 'Скидка';
            type = CONTAINERH;
            MOVE PROPERTY(discount(d));
            MOVE PROPERTY(namePriceType(d));
        }
        NEW details {
            type = TABBED;
            fill = 1;
            NEW items {
                caption = 'Номенклатура';
                type = SPLITH;
                MOVE BOX(TREE categories);
                MOVE BOX(i) { fill = 2; }
            }
            MOVE BOX(t) {
                caption = 'Виды цен';
            }
        }
    }
}

FORM discounts 'Скидки'
    OBJECTS d = Discount
    PROPERTIES(d) READONLY name,
                           startDateTime, endDateTime,
                           fromAmount, fromQuantity,
                           fromAccumulatedPreviousMonth, fromAccumulated,
                           discount, namePriceType,
                           categories, items
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE
    
    FILTERGROUP active
        FILTER 'Активные' active(d) DEFAULT
    
    LIST Discount OBJECT d
;

NAVIGATOR {
    operations {
        NEW discounts;
    }
}

// meta
META defineDocSalesDiscount(doc)
    salesDiscount = DATA Discount (###doc##Line);
    nameSalesDiscount 'Скидка' (###doc##Line l) = name(salesDiscount(l)) CHARWIDTH 10;
    
    changeSalesDiscount(###doc##Line l) {
        DIALOG selectDiscount OBJECTS l = l, d = salesDiscount(l) CHANGE;
    } 
    
    EXTEND FORM doc
        PROPERTIES(l) nameSalesDiscount BEFORE discount(l) ON CHANGE changeSalesDiscount(l)
    ;
    
    WHEN LOCAL CHANGED(salesDiscount(###doc##Line l)) AND NOT priceType(salesDiscount(l)) AND NOT CHANGED(discount(l)) DO
        discount(l) <- discount(salesDiscount(l));
    
    WHEN LOCAL CHANGED(salesDiscount(###doc##Line l)) AND priceType(salesDiscount(l)) AND NOT CHANGED(discountPrice(l)) DO {
        discountPrice(l) <- priceA(priceType(salesDiscount(l)), item(l), dateTime(l));
        recalcDiscount(l);
    }
END