MODULE SalesPricelistPrint;

REQUIRE Utils, SalesPricelistPrintType, ItemImage, ItemAttribute;

NAMESPACE Sales;

// print
addressLinesCompany (PrintPricelistType t) = addressLines(company(t));
addressCsvCompany (PrintPricelistType t) = addressCsv(company(t));
phoneCompany (PrintPricelistType t) = phone(company(t));
emailCompany (PrintPricelistType t) = email(company(t));
websiteCompany (PrintPricelistType t) = website(company(t));

price = DATA LOCAL NUMERIC[10,2] (PriceType, Item); 

price (PrintPricelistType pt, Item it, INTEGER i) = price(priceType(pt, i), it);

price1 (PrintPricelistType pt, Item it) = price(pt, it, 1);
price2 (PrintPricelistType pt, Item it) = price(pt, it, 2);
price3 (PrintPricelistType pt, Item it) = price(pt, it, 3);

FORM printPricelist 'Pricelist'
    OBJECTS pt = PrintPricelistType PANEL
    PROPERTIES(pt) name, nameCompany, addressLinesCompany, addressCsvCompany, phoneCompany, emailCompany, websiteCompany
    REPORT OVERRIDE file(pt), file(RAWFILE(template(pt)), 'path')

    OBJECTS t = PriceType
    FILTERS set(pt, t)

    OBJECTS c = Category
    PROPERTIES(c) name
    FILTERS c IS Category AND pt IS PrintPricelistType

    OBJECTS i = Item
    PROPERTIES(i) id, name, nameUom, idBarCode, reference, nameSalesTax,
                  nameCategory, level1, level2, level3, level4

    PROPERTIES price = price(t, i) COLUMNS (t) HEADER name(t), image(i),
               price1 = price1(pt, i), price2 = price2(pt, i), price3 = price3(pt, i)
    ORDERS name(i)
    FILTERS category(i) = c, GROUP SUM 1 IF price(PriceType ppt, i)
;

@defineAttributeForm(printPricelist, i);

print (PrintPricelistType pt) {
    CASE EXCLUSIVE
        WHEN format(pt) = TemplateFormat.pdf THEN
            PRINT printPricelist OBJECTS pt = pt PDF;
        WHEN format(pt) = TemplateFormat.docx THEN
            PRINT printPricelist OBJECTS pt = pt DOCX;
        WHEN format(pt) = TemplateFormat.xlsx THEN
            PRINT printPricelist OBJECTS pt = pt XLSX;
        WHEN format(pt) = TemplateFormat.rtf THEN
            PRINT printPricelist OBJECTS pt = pt RTF;
        WHEN format(pt) = TemplateFormat.html THEN
            PRINT printPricelist OBJECTS pt = pt HTML;
        WHEN format(pt) = TemplateFormat.noPreview THEN
            PRINT printPricelist OBJECTS pt = pt NOPREVIEW;
        ELSE
            PRINT printPricelist OBJECTS pt = pt;
}