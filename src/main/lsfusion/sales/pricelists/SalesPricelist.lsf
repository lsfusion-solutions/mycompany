MODULE SalesPricelist;

REQUIRE SalesPricelistType, Item, Partner, ItemSales, PartnerSales, TaxItem, SalesSettings, SalesPriceType, CostLedger, Doc;

NAMESPACE Sales; 

CLASS Pricelist 'Прайс-лист';

type 'Тип' = DATA PricelistType (Pricelist);
nameType 'Тип' (Pricelist p) = name(type(p));

WHEN LOCAL SET(Pricelist p IS Pricelist) AND NOT CHANGED(type(p)) DO type(p) <- defaultPricelistType();

@defineDocStatus(pricelist, 'прайс-листа');

startDateTime 'Начало действия' = DATA DATETIME (Pricelist) NONULL;
startDateTime(Pricelist l) <- currentDateTime() WHEN SET(l IS Pricelist);

endDateTime 'Окончание действия' = DATA DATETIME (Pricelist);

@defineNumberType(pricelist);

numberDate 'Описание' (Pricelist p) = number(p) + ' от ' + startDateTime(p) IN id;

note 'Примечание' = DATA STRING[50] (Pricelist);

// price types
editIn 'Изменять цены' = DATA BOOLEAN (Pricelist, PriceType);

priceListTypes 'Виды цен' (Pricelist pricelist) = 
    GROUP CONCAT name(PriceType pricelistType) IF editIn(pricelist, pricelistType) , ', '
          ORDER pricelistType CHARWIDTH 10 MATERIALIZED;

WHEN LOCAL CHANGED (type(Pricelist pl)) DO editIn(pl, PriceType pt) <- in(type(pl), pt);

FORM chooseTypes 'Выбор видов цен'
    OBJECTS pr = Pricelist PANEL 
    
    OBJECTS t = PriceType
    PROPERTIES(t) READONLY name 
    PROPERTIES(pr, t) editIn[Pricelist,PriceType]
;

// lines
CLASS PricelistLine 'Номенклатура';

pricelist 'Прайс-лист' = DATA Pricelist (PricelistLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER PricelistLine l BY pricelist(l) IN id MATERIALIZED CHARWIDTH 3;

item 'Номенклатура' = DATA Item (PricelistLine);
nameItem 'Номенклатура' (PricelistLine prl) = name(item(prl));
idBarCodeItem 'Штрихкод' (PricelistLine l) = idBarCode(item(l));
idItem 'Код' (PricelistLine l) = id(item(l));
nameSalesTax 'Налоги' (PricelistLine l) = nameSalesTax(item(l));

uom (PricelistLine l) = uom(item(l));
nameUom 'Ед. изм.' (PricelistLine l) = name(uom(l)); 

cost 'Себестоимость' = DATA NUMERIC[14,2] (PricelistLine);
WHEN LOCAL SETCHANGED(item(PricelistLine l)) AND NOT CHANGED(cost(l)) DO cost(l) <- overCost(item(l), startDateTime(pricelist(l)));

markup 'Надбавка' = DATA NUMERIC[10,2] (PricelistLine, PriceType);
price 'Цена' = DATA NUMERIC[10,2] (PricelistLine, PriceType);

CONSTRAINT DROPPED(PriceType t IS PriceType) AND PREV(price(PricelistLine l, t))
    MESSAGE 'Запрещено удалять виды цен, по которым есть прайсы';

editIn (PricelistLine pl, PriceType priceType) = editIn(pricelist(pl), priceType);

// events
basePriceType (PricelistLine l, PriceType t) = basePriceType(type(pricelist(l)), t); 
basePrice (PricelistLine l, PriceType t) = 
    IF basePriceType(l, t) THEN price(l, basePriceType(l, t)) ELSE (cost(l) IF editIn(l, t));

taxCoeff (PricelistLine l, PriceType t) = 
    (100.0 + salesTax(item(l))) / 100.0 IF taxIncluded(t) AND NOT taxIncluded(basePriceType(l, t));
    
noTaxPrice (PricelistLine l, PriceType t) = 
    price(l, t) / (OVERRIDE taxCoeff(l, t), 1.0);

WHEN LOCAL (SETCHANGED(basePrice(PricelistLine l, PriceType t)) OR SETCHANGED(price(l, t))) 
            AND editIn(l, t) AND NOT CHANGED(markup(l, t)) DO
    markup(l, t) <- round2((noTaxPrice(l, t) * 100.0 / basePrice(l, t)) - 100.0);

calcPrice(PricelistLine l, PriceType t) = 
    NUMERIC[10,2](basePrice(l, t) * 
                  (markup(l, t) + 100.0) / 100.0 * 
                  (OVERRIDE taxCoeff(l, t), 1.0));

changeMarkup (PricelistLine l, PriceType t) {
    INPUT val = markup(l, t) CHANGE DO {
        price(l, t) <- calcPrice(l, t);
    }
}

// Line properties
nameType 'Тип' (PricelistLine l) = nameType(pricelist(l));
nameStatus 'Статус' (PricelistLine l) = nameStatus(pricelist(l));
number 'Номер' (PricelistLine prl) = number(pricelist(prl));
numberDate 'Описание' (PricelistLine l) = numberDate(pricelist(l));
startDateTime 'Начало действия' (PricelistLine prl) = startDateTime(pricelist(prl));
endDateTime 'Окончание действия' (PricelistLine prl) = endDateTime(pricelist(prl));
note 'Примечание' (PricelistLine prl) = note(pricelist(prl));

FORM pricelist 'Прайс-лист'
    OBJECTS p = Pricelist PANEL
    PROPERTIES(p) nameType, number, startDateTime, endDateTime, note,
                  priceListTypes ON CHANGE { SHOW chooseTypes OBJECTS pr = p; }

    OBJECTS t = PriceType
    FILTERS editIn(p, t) 
   
    OBJECTS l = PricelistLine
    PROPERTIES(l) index READONLY, nameItem, nameUom, idBarCodeItem, idItem, nameSalesTax, cost SHOWIF showCost(type(p)) READONLY
    PROPERTIES    markup(l, t) COLUMNS 'p' (t) HEADER name(t) + ', %' SHOWIF showMarkup(type(p), t) ON CHANGE changeMarkup(l, t)
    PROPERTIES    price(l, t) COLUMNS 'p' (t) HEADER name(t) 
    PROPERTIES(l) NEW, DELETE 
    FILTERS pricelist(l) = p 

    EDIT Pricelist OBJECT p
;

DESIGN pricelist {
    caption = (CONCAT ' ', 'Прайс-лист', '№' + number(p), 'от ' + startDateTime(p));
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            NEW params {
                NEW title {
                    type = CONTAINERH;
                    MOVE PROPERTY(nameType(p));
                    MOVE PROPERTY(number(p));
                }
                NEW period {
                    alignment = STRETCH;
                    type = CONTAINERH;
                    MOVE PROPERTY(startDateTime(p)) { caption = 'Действует с '; }
                    MOVE PROPERTY(endDateTime(p)) { caption = 'по '; }
                }
                MOVE PROPERTY(priceListTypes(p)); 
                MOVE PROPERTY(note(p)); 
            }
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l) {
                    PROPERTY(markup(l, t)) { background = RGB(212,242,252); }
                    PROPERTY(price(l, t)) { background = RGB(198,230,247); }
                }
            }                
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

FORM pricelists 'Прайс-листы'
    OBJECTS p = Pricelist
    PROPERTIES(p) READONLY nameType, startDateTime, endDateTime, number, note, priceListTypes
    PROPERTIES(p) NEWSESSION NEW, EDIT ,DELETE
    
    LIST Pricelist OBJECT p
;

@defineDocStatusForm(pricelist, p);
@defineDocLinesCount(pricelist, p);

NAVIGATOR {
    operations {
        NEW pricelists;    
    }
}

// copy
copy 'Копировать' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            type(p) <- type(pl);
            startDateTime(p) <- startDateTime(pl);
            endDateTime(p) <- endDateTime(pl);
            note(p) <- note(pl);
            editIn(p, PriceType t) <- editIn(pl, t);
            FOR pricelist(PricelistLine l)  = pl DO NEW nl = PricelistLine {
                pricelist(nl) <- p;
                item(nl) <- item(l);
                price(nl, PriceType t) <- price(l, t);
            }
            SHOW pricelist OBJECTS p = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES(p) copy 
;

DESIGN pricelist {
    secondaryActions {
        MOVE PROPERTY(copy(p)); 
    }
}