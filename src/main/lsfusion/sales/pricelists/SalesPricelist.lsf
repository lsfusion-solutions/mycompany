MODULE SalesPricelist;

REQUIRE SalesPricelistType, Item, Partner, ItemSales, PartnerSales, TaxItem, 
        SalesSettings, SalesPriceType, CostLedger, Doc, ExchangeRate, DateUtils;

NAMESPACE Sales; 

CLASS Pricelist 'Pricelist';

type 'Type' = DATA PricelistType (Pricelist);
nameType 'Type' (Pricelist p) = name(type(p));

WHEN LOCAL SET(Pricelist p IS Pricelist) AND NOT CHANGED(type(p)) DO type(p) <- defaultPricelistType();

@defineDocStatus(pricelist, 'Pricelist status');

startDateTime 'Start date' = DATA DATETIME (Pricelist) NONULL;
startDateTime(Pricelist l) <- currentDateTime() WHEN SET(l IS Pricelist);

@defineDateTimeAggregation(Pricelist, start, '(start)');

endDateTime 'End date' = DATA DATETIME (Pricelist);

@defineNumberType(pricelist);

numberDate 'Description' (Pricelist p) = number(p) + ' (' + startDateTime(p) + ')' IN id;

note 'Note' = DATA STRING[50] (Pricelist);

// price types
editIn 'Change prices' = DATA BOOLEAN (Pricelist, PriceType);

priceListTypes 'Price types' (Pricelist pricelist) = 
    GROUP CONCAT name(PriceType pricelistType) IF editIn(pricelist, pricelistType) , ', '
          ORDER pricelistType CHARWIDTH 10 MATERIALIZED;

WHEN LOCAL CHANGED (type(Pricelist pl)) DO editIn(pl, PriceType pt) <- in(type(pl), pt);

// lines
CLASS PricelistLine 'Pricelist line';

pricelist 'Pricelist' = DATA Pricelist (PricelistLine) NONULL DELETE;

index 'â„–' = PARTITION SUM 1 ORDER PricelistLine l BY pricelist(l) IN id MATERIALIZED CHARWIDTH 3;

startDate (PricelistLine l) = startDate(pricelist(l));

item '{Item}' = DATA Item (PricelistLine);
nameItem '{Item}' (PricelistLine prl) = name(item(prl));
idBarCodeItem 'Barcode' (PricelistLine l) = idBarCode(item(l));
idItem '{ID}' (PricelistLine l) = id(item(l));
referenceItem 'Reference' (PricelistLine l) = reference(item(l));
nameSalesTax 'Taxes' (PricelistLine l) = nameSalesTax(item(l));

uom (PricelistLine l) = uom(item(l));
nameUom 'UoM' (PricelistLine l) = name(uom(l)); 

cost 'Cost' = DATA NUMERIC[14,2] (PricelistLine);
WHEN LOCAL SETCHANGED(item(PricelistLine l)) AND NOT CHANGED(cost(l)) DO cost(l) <- overCost(item(l), startDateTime(pricelist(l)));

markup 'Markup' = DATA NUMERIC[10,2] (PricelistLine, PriceType);
price 'Price' = DATA NUMERIC[10,2] (PricelistLine, PriceType);

WHEN SETCHANGED(price(PricelistLine l, PriceType t)) AND NOT editIn(pricelist(l), t) DO
    price(l, t) <- NULL;

WHEN DROPPED(editIn(Pricelist p, PriceType t)) DO
    price(PricelistLine l, t) <- NULL WHERE pricelist(l) = p;

CONSTRAINT DROPPED(PriceType t IS PriceType) AND PREV(price(PricelistLine l, t))
    MESSAGE 'It is forbidden to delete the types of prices for which there are prices';

editIn (PricelistLine pl, PriceType priceType) = editIn(pricelist(pl), priceType);

// events
basePriceType (PricelistLine l, PriceType t) = basePriceType(type(pricelist(l)), t); 
basePrice (PricelistLine l, PriceType t) = 
    IF basePriceType(l, t) 
        THEN (price(l, basePriceType(l, t)) / rate1(currency(basePriceType(l, t)), currency(t), startDate(l))) 
        ELSE ((cost(l) / rate1(currency(t), startDate(l))) IF editIn(l, t));

taxCoeff (PricelistLine l, PriceType t) = 
    (100.0 + salesTax(item(l))) / 100.0 IF taxIncluded(t) AND NOT taxIncluded(basePriceType(l, t));
    
noTaxPrice (PricelistLine l, PriceType t) = 
    price(l, t) / (OVERRIDE taxCoeff(l, t), 1.0);

WHEN LOCAL (SETCHANGED(basePrice(PricelistLine l, PriceType t)) OR SETCHANGED(price(l, t))) 
            AND editIn(l, t) AND NOT CHANGED(markup(l, t)) DO
    markup(l, t) <- round2((noTaxPrice(l, t) * 100.0 / basePrice(l, t)) - 100.0);

calcPrice(PricelistLine l, PriceType t) = 
    NUMERIC[10,2](basePrice(l, t) * 
                  (markup(l, t) + 100.0) / 100.0 * 
                  (OVERRIDE taxCoeff(l, t), 1.0));

changeMarkup (PricelistLine l, PriceType t) {
    INPUT val = markup(l, t) CHANGE DO {
        price(l, t) <- calcPrice(l, t);
    }
}

// Line properties
nameType 'Type' (PricelistLine l) = nameType(pricelist(l));
imagedNameStatus 'Status' (PricelistLine l) = imagedNameStatus(pricelist(l));
number 'Number' (PricelistLine prl) = number(pricelist(prl)) IN id;
numberDate 'Description' (PricelistLine l) = numberDate(pricelist(l));
startDateTime 'Start date' (PricelistLine prl) = startDateTime(pricelist(prl));
endDateTime 'End date' (PricelistLine prl) = endDateTime(pricelist(prl));
note 'Note' (PricelistLine prl) = note(pricelist(prl));

FORM pricelist 'Pricelist'
    OBJECTS p = Pricelist PANEL
    PROPERTIES(p) READONLYIF readonly(p)
                  nameType, number, startDateTime, endDateTime, note,
                  priceListTypes// ON CHANGE { SHOW chooseTypes OBJECTS pr = p; }

    OBJECTS t = PriceType
    FILTERS editIn(p, t) 
   
    OBJECTS l = PricelistLine
    PROPERTIES(l) READONLYIF readonly(p) index READONLY, nameItem, nameUom, idBarCodeItem, idItem, referenceItem, nameSalesTax, cost SHOWIF showCost(type(p)) READONLY
    PROPERTIES    READONLYIF readonly(p) markup(l, t) COLUMNS 'p' (t) HEADER name(t) + ', %' SHOWIF showMarkup(type(p), t) ON CHANGE changeMarkup(l, t)
    PROPERTIES    READONLYIF readonly(p) price(l, t) COLUMNS 'p' (t) HEADER name(t) 
    PROPERTIES(l) READONLYIF readonly(p) NEW, DELETE 
    FILTERS pricelist(l) = p 

    EDIT Pricelist OBJECT p
;

DESIGN pricelist {
    caption = badged('Pricelist', number(p) + '(' + startDateTime(p) + ')');
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            NEW params {
                NEW title {
                    horizontal = TRUE;
                    MOVE PROPERTY(nameType(p));
                    MOVE PROPERTY(number(p));
                }
                NEW period {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    MOVE PROPERTY(startDateTime(p)) { caption = 'Valid from'; }
                    MOVE PROPERTY(endDateTime(p)) { caption = '{to}'; }
                }
                MOVE PROPERTY(priceListTypes(p)); 
                MOVE PROPERTY(note(p)); 
            }
        }
        NEW details {
            fill = 1;
            tabbed = TRUE;
            NEW lines {
                caption = 'Lines';
                MOVE BOX(l) {
                    caption = '';
                    PROPERTY(markup(l, t)) { background = RGB(212,242,252); }
                    PROPERTY(price(l, t)) { background = RGB(198,230,247); }
                }
            }                
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            horizontal = TRUE;
        }
    }
}

FORM pricelists 'Pricelists'
    OBJECTS p = Pricelist
    PROPERTIES(p) READONLY number, startDateTime, endDateTime, nameType, note, priceListTypes
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE DISABLEIF readonly(p)
    
    LIST Pricelist OBJECT p
;

DESIGN pricelists {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDocStatusForm(pricelist, p);
@defineDocLinesCount(pricelist, p);

@defineObjectComments(pricelist, p, details);

NAVIGATOR {
    operations {
        NEW pricelists;    
    }
}

// copy
copy 'Copy' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            type(p) <- type(pl);
            startDateTime(p) <- startDateTime(pl);
            endDateTime(p) <- endDateTime(pl);
            note(p) <- note(pl);
            editIn(p, PriceType t) <- editIn(pl, t);
            FOR pricelist(PricelistLine l)  = pl DO NEW nl = PricelistLine {
                pricelist(nl) <- p;
                item(nl) <- item(l);
                price(nl, PriceType t) <- price(l, t);
            }
            SHOW pricelist OBJECTS p = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES(p) copy 
;

DESIGN pricelist {
    secondaryActions {
        MOVE PROPERTY(copy(p)) { valueClass = 'btn-secondary'; }
    }
}