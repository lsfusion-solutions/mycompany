MODULE SalesPricelistSearch;

REQUIRE SalesOrder, SalesPricelist, SalesPricelistValue;

NAMESPACE Sales;

detail= GROUP SUM 1 BY item(PricelistLine pl), pricelist(pl);

in 'Incl.' (Item item, Pricelist pr) = TRUE IF detail(item, pr);

changeIn(Item item, Pricelist pricelist)  { 
    INPUT b = BOOLEAN DO
        IF detail(item, pricelist) THEN {
            IF NOT b THEN
                DELETE PricelistLine pl WHERE item = item(pl) AND pricelist = pricelist(pl);
        } ELSE
            IF b THEN 
                NEW d = PricelistLine {
                   pricelist(d) <- pricelist;
                   item(d) <- item;
                }
}

markup 'Current markup' (PriceType t, Item i, DATETIME dt) = 
    round2(priceB(t, i, dt) / (OVERRIDE 100.0 / (100.0 + salesTax(i)) IF taxIncluded(t), 1.0) * 100.0 / overCost(i, dt)) - 100.0;

EXTEND FORM pricelist
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT
    
    OBJECTS dt = DATETIME 
    FILTERS dt = startDateTime(p)
    
    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id,
                           nameCategory, level1, level2, level3, level4
    PROPERTIES READONLY overCost(i, dt) SHOWIF showCost(type(p)),
                        markup(t, i, dt) COLUMNS 'p' (t) HEADER name(t) + ', %' SHOWIF showMarkup(type(p), t),
                        priceB(t, i, dt) COLUMNS 'p' (t) HEADER name(t)
    ORDERS name(i)
    FILTERS level(category(i), c), canBeSold(i)
    
    FILTERGROUP noPrices
        FILTER 'No price' GROUP SUM 1 IF editIn(p, PriceType tp) AND NOT priceB(tp, i, dt)     
;

DESIGN pricelist {
    details {
        NEW search {
            caption = 'Search';
            horizontal = TRUE;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(i) { 
                fill = 2;
                PROPERTY(nameCategory(i)) { hide = TRUE; }
                PROPERTY(level1(i)) { hide = TRUE; }
                PROPERTY(level2(i)) { hide = TRUE; }
                PROPERTY(level3(i)) { hide = TRUE; }
                PROPERTY(level4(i)) { hide = TRUE; }
            }
            PROPERTY(markup(t, i, dt)) { background = RGB(212,242,252); }
            PROPERTY(priceB(t, i, dt)) { background = RGB(198,230,247); }
        }       
    }
}

selectAll 'Select all' (Pricelist pr, Category c) {
    FOR [FILTER pricelist.i](Item i) AND NOT in(i, pr) ORDER name(i) DO NEW l = PricelistLine {
        pricelist(l) <- pr;
        item(l) <- i;
    }
}

EXTEND FORM pricelist
    PROPERTIES in(i, p) ON CHANGE changeIn(i, p) BEFORE name(i) READONLYIF readonly(p), 
               selectAll(p, c) DRAW i TOOLBAR DISABLEIF readonly(p)
;
