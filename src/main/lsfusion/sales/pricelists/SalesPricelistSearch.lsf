MODULE SalesPricelistSearch;

REQUIRE SalesOrder, SalesPricelist, SalesPricelistValue;

NAMESPACE Sales;

// Подбор товаров
detail= GROUP SUM 1 BY item(PricelistLine pl), pricelist(pl);

in 'Вкл.' (Item item, Pricelist pr) = TRUE IF detail(item, pr);

changeIn(Item item, Pricelist pricelist)  { 
    INPUT b = BOOLEAN DO
        IF detail(item, pricelist) THEN {
            IF NOT b THEN
                DELETE PricelistLine pl WHERE item == item(pl) AND pricelist == pricelist(pl);
        } ELSE
            IF b THEN 
                NEW d = PricelistLine {
                   pricelist(d) <- pricelist;
                   item(d) <- item;
                }
}

markup 'Текущая надбавка' (Item i, PriceType t, DATETIME dt) = 
    round2(priceB(t, i, dt) / (OVERRIDE 100.0 / (100.0 + salesTax(i)) IF taxIncluded(t), 1.0) * 100.0 / overCost(i)) - 100.0;

EXTEND FORM pricelist
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDER name(c)
    
    OBJECTS dt = DATETIME 
    FILTERS dt = startDateTime(p)
    
    OBJECTS i = Item
    PROPERTIES READONLY name(i),
                        overCost(i) SHOWIF showCost(p),
                        markup(i, t, dt) COLUMNS 'p' (t) HEADER name(t) + ', %' SHOWIF showMarkup(p),
                        priceB(t, i, dt) COLUMNS 'p' (t) HEADER name(t)
    FILTERS level(category(i), c), canBeSold(i)
    
    FILTERGROUP noPrices
        FILTER 'Без цены' GROUP SUM 1 IF editIn(p, PriceType tp) AND NOT priceB(tp, i, dt)     
;

DESIGN pricelist {
    details {
        NEW search {
            caption = 'Подбор';
            type = SPLITH;
            fill = 1;
            MOVE BOX(TREE categories);
            MOVE BOX(i) { 
                fill = 2;
            }
            MOVE PROPERTY(overCost(i));
            MOVE PROPERTY(markup(i, t, dt));
        }       
    }
}

selectAll 'Подобрать все' (Pricelist pr, Category c) {
    FOR [FILTER pricelist.i](Item i) AND NOT in(i, pr) DO NEW l = PricelistLine {
        pricelist(l) <- pr;
        item(l) <- i;
    }
}

EXTEND FORM pricelist
    PROPERTIES in(i, p) ON CHANGE changeIn(i, p) BEFORE name(i), selectAll(p, c) DRAW i TOOLBAR 
;
