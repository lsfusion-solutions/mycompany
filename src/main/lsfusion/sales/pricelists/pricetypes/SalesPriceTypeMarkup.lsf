MODULE SalesPriceTypeMarkup;

REQUIRE SalesPriceType;

NAMESPACE Sales;

markup 'Надбавка по категории, %' = DATA NUMERIC[10,2] (PriceType, Category);
parentMarkup 'Надбавка (от верхней группы), %' (PriceType t, Category child) =
   GROUP LAST markup(t, Category parent) ORDER DESC level(child, parent) WHERE markup(t, parent);
   
dataMarkup 'Надбавка по товару, %' = DATA NUMERIC[10,2] (PriceType, Item);
markup 'Надбавка, %' (PriceType t, Item i) = OVERRIDE dataMarkup(t, i), parentMarkup(t, category(i));   

EXTEND FORM pricelistType
    TREE categories c = Category PARENT parent(c)
    PROPERTIES name(c) READONLY, markup(pt, c), parentMarkup(pt, c) READONLY

    OBJECTS i = Item
    PROPERTIES name(i) READONLY, dataMarkup(pt, i), markup(pt, i) READONLY
    FILTERS level(category(i), c)
;

DESIGN pricelistType {
    OBJECTS {
        NEW markup {
            caption = 'Надбавки';
            fill = 1;
            type = SPLITH;
            MOVE BOX(TREE categories);
            MOVE BOX(i);
        }
    }
}
