MODULE SalesPriceTypeMarkup;

REQUIRE SalesPriceType, BarCode;

NAMESPACE Sales;

markup '{Markup_p}' = DATA NUMERIC[10,2] (PriceType, Category);
parentMarkup 'Markup (result), %' (PriceType t, Category child) =
   GROUP LAST markup(t, Category parent) ORDER DESC level(child, parent) WHERE markup(t, parent);
   
dataMarkup '{Markup_p}' = DATA NUMERIC[10,2] (PriceType, Item);
markup 'Markup (result), %' (PriceType t, Item i) = OVERRIDE dataMarkup(t, i), parentMarkup(t, category(i));   

EXTEND FORM priceType
    TREE categories c = Category PARENT parent(c)
    PROPERTIES name(c) READONLY, markup(pt, c), parentMarkup(pt, c) READONLY
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT

    OBJECTS i = Item
    PROPERTIES(i) READONLY name, nameUom, idBarCode, id
    PROPERTIES             dataMarkup(pt, i), markup(pt, i) READONLY
    ORDERS name(i)
    FILTERS level(category(i), c)
;

DESIGN priceType {
    OBJECTS {
        NEW markup {
            caption = 'Markups';
            fill = 1;
            horizontal = TRUE;
            MOVE BOX(TREE categories);
            MOVE BOX(i) { caption = ''; }
        }
    }
}
