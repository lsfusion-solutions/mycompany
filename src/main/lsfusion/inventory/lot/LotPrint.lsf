MODULE LotPrint;

REQUIRE Lot, ItemPrint, QZTray;

count 'Qty' = DATA LOCAL INTEGER (Lot);

FORM printItemLot '{Label}'
    OBJECTS pt = PrintItemType PANEL
    PROPERTIES(pt) name, nameCompany, addressLinesCompany, addressCsvCompany, phoneCompany, emailCompany, websiteCompany
    REPORT OVERRIDE file(pt), file(RAWFILE(template(pt)), 'path')

    OBJECTS i = Item
    PROPERTIES(i) count, id, name, nameUom, idBarCode
    ORDERS name(i)
    FILTERS count(i), set(pt, i)

    OBJECTS l = Lot
    PROPERTIES(l) id
    FILTERS count(l), product(l) = i
;

@defineAttributeForm(printItemLot, i);

printLot (PrintItemType pt) {
    CASE EXCLUSIVE
        WHEN format(pt) = TemplateFormat.pdf THEN
            PRINT printItemLot OBJECTS pt = pt PDF;
        WHEN format(pt) = TemplateFormat.docx THEN
            PRINT printItemLot OBJECTS pt = pt DOCX;
        WHEN format(pt) = TemplateFormat.xlsx THEN
            PRINT printItemLot OBJECTS pt = pt XLSX;
        WHEN format(pt) = TemplateFormat.rtf THEN
            PRINT printItemLot OBJECTS pt = pt RTF;
        WHEN format(pt) = TemplateFormat.html THEN
            PRINT printItemLot OBJECTS pt = pt HTML;
        WHEN format(pt) = TemplateFormat.noPreview THEN {
            IF isWeb() THEN {
                LOCAL file = RAWFILE();
                PRINT printItemLot OBJECTS pt = pt PDF TO file;
                QZPrintDefaultPDF(file());
            } ELSE
                PRINT printItemLot OBJECTS pt = pt NOPREVIEW;
        } ELSE
            PRINT printItemLot OBJECTS pt = pt;
}

printLot () {
    LOCAL printType = PrintItemType();
    printType() <- NULL;
    IF (GROUP SUM 1 IF countItem(PrintItemType t)) = 1 THEN
        printType() <- GROUP MAX PrintItemType t IF countItem(t);
    ELSE
        DIALOG printItemParams OBJECTS t INPUT DOCKED DO {
            printType() <- t;
        }
    IF printType() THEN
        printLot(printType());
}

print '{Label}' (Lot l) {
    count(Item i) <- 1 IF i = product(l);
    count(l) <- 1;
    printLot();
}

EXTEND FORM lot
    PROPERTIES(l) print SHOWIF countPrintType(product(l))
;

DESIGN lot {
    secondaryActions {
        MOVE PROPERTY(print(l));
    } 
}