MODULE LocationImport;

REQUIRE Location, MasterData;

NAMESPACE Inventory;

locationExport 'Экспорт мест хранения' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Location l AND l IS Location) THEN {
            NEW l = Location {
                id(l) <- '123';
                name(l) <- 'Мой склад';
            }
        }
    
        LOCAL f = EXCELFILE ();
        EXPORT XLSX HEADER FROM 'Код' = id(Location l), 'Название' = name(l), 'Код родителя' = idParent(l), 
                                'Имя владельца' = ownerName(l) TO f;   
        open(f());
    }
}

locationImport 'Импорт мест хранения' () {
    LOCAL id = STRING[50] (INTEGER);
    LOCAL name = ISTRING[30] (INTEGER);
    LOCAL parent = ISTRING[50] (INTEGER);
    LOCAL owner = ISTRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {   
            IMPORT XLS HEADER FROM f TO id = A, name = B, parent = C, owner = D;
            
            FOR imported(INTEGER i) DO NEW l = Location {
                id(l) <- id(i);
                name(l) <- name(i);
                owner(l) <- partner(owner(i));
            }
            APPLY;
            
            IMPORT XLS HEADER FROM f TO id = A, name = B, parent = C, owner = D;
            FOR imported(INTEGER i) DO {
                FOR id(Location l) = id(i) DO {
                    parent(l) <- location(parent(i));
                }
            }
            APPLY;
            MESSAGE 'Импорт завершен';
        }
    }    
}

EXTEND FORM MasterData.options 
    PROPERTIES locationExport(), locationImport()
;

DESIGN MasterData.options {
    import {
        NEW location {
            type = CONTAINERH;
            caption = 'Места Хранения';
            MOVE PROPERTY(locationImport());
            MOVE PROPERTY(locationExport());
        }
    }
}
