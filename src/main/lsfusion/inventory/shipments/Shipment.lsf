MODULE Shipment;

REQUIRE System, Time, DateUtils, FileUtils,
        InventorySettings, LocationEmployee, Partner, Product, Department,
        ShipmentType, ItemSales, PartnerSales, BarCode, Doc, ResLedger;

NAMESPACE Inventory;

CLASS Shipment 'Shipment';

@defineDocStatus(shipment, 'Shipment status');

immediate 'Unplanned' = DATA BOOLEAN (Shipment);

// type
type 'Type' = DATA ShipmentType (Shipment) NONULL;
nameType 'Type' (Shipment s) = name(type(s));

WHEN LOCAL SET(Shipment s IS Shipment) AND NOT CHANGED(type(s)) DO type(s) <- defaultShipmentType(); 

scheduledDateTime 'Scheduled date' = DATA DATETIME (Shipment) NONULL IN id;
scheduledDateTime(Shipment s) <- currentDateTime() WHEN SET(s IS Shipment);
@defineDateTimeAggregation(Shipment, scheduled, '(scheduled)');

@defineNumberType(shipment);

numberDate 'Description' (Shipment s) = number(s) + ' (' + scheduledDateTime(s) + ')' IN id;

// locations
location 'Source location' = DATA Location (Shipment) NONULL INDEXED;
nameLocation 'Source location' (Shipment s) = name(location(s));
WHEN LOCAL SETCHANGED(type(Shipment s)) AND defaultLocation(type(s)) AND NOT CHANGED(location(s)) DO location(s) <- defaultLocation(type(s));
CONSTRAINT SETCHANGED(location(Shipment o)) AND internalUse(location(o)) CHECKED BY location[Shipment]
    MESSAGE 'The selected location is for internal use';

company 'Company' (Shipment s) = company(location(s));

toLocation 'Destination location' = DATA Location (Shipment);
nameToLocation 'Destination location' (Shipment s) = name(toLocation(s));
WHEN LOCAL SETCHANGED(type(Shipment s)) AND defaultToLocation(type(s)) AND NOT CHANGED(toLocation(s)) DO toLocation(s) <- defaultToLocation(type(s));
CONSTRAINT SETCHANGED(toLocation(Shipment o)) AND internalUse(location(o)) CHECKED BY toLocation[Shipment]
    MESSAGE 'The selected location is for internal use';

CONSTRAINT location(Shipment s) = toLocation(s)
    CHECKED BY toLocation[Shipment]
    MESSAGE 'Where and where storage locations cannot coincide';

// transfer
WHEN SETCHANGED(type(Shipment s)) AND NOT isTransfer(type(s)) DO toLocation(s) <- NULL;
CONSTRAINT isTransfer(type(Shipment s)) AND NOT toLocation(s) MESSAGE 'Destination location for transfer is not specified';

// customer
customer = DATA Partner (Shipment) INDEXED;
nameCustomer 'Customer' (Shipment s) = name(customer(s));
addressCsvCustomer 'Customer address' (Shipment s) = addressCsv(customer(s)); 

isCustomer = ABSTRACT VALUE BOOLEAN (Shipment);
isCustomer(Shipment s) += isCustomer(customer(s));
CONSTRAINT customer(Shipment s) AND NOT isCustomer(s) 
                CHECKED BY customer[Shipment]
                MESSAGE 'The partner must be a customer';

@defineDocDepartment(shipment, customer);

deliveryAddress 'Delivery address' = DATA STRING (Shipment) CHARWIDTH 20;
WHEN LOCAL (CHANGED(customer(Shipment s)) OR SETCHANGED(department(s))) AND NOT CHANGED(deliveryAddress(s)) DO 
    deliveryAddress(s) <- OVERRIDE addressCsv(department(s)), addressCsvCustomer(s);

customerReference 'Customer reference' = DATA STRING[30] (Shipment) INDEXED;
note 'Note' = DATA ISTRING (Shipment) CHARWIDTH 20;

// lines
CLASS ShipmentLine 'Shipment line';

shipment 'Document' = DATA Shipment (ShipmentLine) NONULL DELETE INDEXED IN id;

countLines 'Number of lines' (Shipment s) = GROUP SUM 1 IF shipment(ShipmentLine l) = s MATERIALIZED;
index 'â„–' = PARTITION SUM 1 ORDER ShipmentLine l BY shipment(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (ShipmentLine) NONULL INDEXED;
nameProduct 'Product' (ShipmentLine l) = name(product(l)) IN id;
idBarCodeProduct 'Barcode' (ShipmentLine l) = idBarCode(product(l));
idProduct '{ID}' (ShipmentLine l) = id(product(l));
referenceProduct 'Reference' (ShipmentLine l) = reference(product(l));

nameCategoryProduct 'Category' (ShipmentLine l) = nameCategory(product(l));
level1Product 'Category 1' (ShipmentLine l) = level1(product(l));
level2Product 'Category 2' (ShipmentLine l) = level2(product(l));
level3Product 'Category 3' (ShipmentLine l) = level3(product(l));
level4Product 'Category 4' (ShipmentLine l) = level4(product(l));

CONSTRAINT product(ShipmentLine l) AND NOT canBeSold(product(l))
            CHECKED BY product[ShipmentLine] 
            MESSAGE 'This product is not for sale';

@defineDocSingleLineForItem(shipment, product);

uom (ShipmentLine l) = uom(product(l));
nameUom 'UoM' (ShipmentLine l) = name(uom(l));

initialDemand 'Initial demand' = DATA NUMERIC[16,3] (ShipmentLine);

CONSTRAINT initialDemand(ShipmentLine l) < 0 OR initialDemand(l) > maxQuantity(type(shipment(l))) AND maxQuantity(type(shipment(l))) 
    MESSAGE 'The initial demand should be between 0 and the maximum allowed';

initialDemand 'Initial demand' (Shipment s) = GROUP SUM initialDemand(ShipmentLine l) IF shipment(l) = s;

// line properties
type (ShipmentLine l) = type(shipment(l));
imagedNameStatus 'Status' (ShipmentLine l) = imagedNameStatus(shipment(l));
scheduledDateTime 'Scheduled date' (ShipmentLine l) = scheduledDateTime(shipment(l)) IN id;
number 'Number' (ShipmentLine l) = number(shipment(l)) IN id;
numberDate 'Description' (ShipmentLine l) = numberDate(shipment(l));
customer (ShipmentLine l) = customer(shipment(l));
nameCustomer 'Customer' (ShipmentLine l) = nameCustomer(shipment(l));
location (ShipmentLine l) = location(shipment(l));
nameLocation 'Location' (ShipmentLine l) = nameLocation(shipment(l));

FORM shipmentLines 'Shipping lines'
    OBJECTS l = ShipmentLine
    PROPERTIES(l) READONLY scheduledDateTime, number, index, nameProduct

    LIST ShipmentLine OBJECT l
;

FORM shipment 'Shipment'
    OBJECTS s = Shipment PANEL 
    PROPERTIES(s) READONLYIF readonly(s)
                  nameType, scheduledDateTime, number, 
                  nameCustomer SHOWIF NOT isTransfer(type(s)), nameDepartment SHOWIF countDepartment(customer(s)), 
                  nameLocation, nameToLocation SHOWIF isTransfer(type(s)),
                  deliveryAddress, customerReference, note
                  
    OBJECTS l = ShipmentLine
    PROPERTIES(l) READONLYIF readonly(s)
                  index READONLY, nameProduct, nameUom, idBarCodeProduct, idProduct, referenceProduct, 
                  initialDemand SHOWIF NOT immediate(s) BACKGROUND IF status(shipment(l)) = ShipmentStatus.draft THEN RGB(198,230,247), 
                  NEW, DELETE
    FILTERS shipment(l) = s
             
    EDIT Shipment OBJECT s
;

DESIGN shipment {
    caption = badged('Shipment', number(s) + '(' + scheduledDateTime(s) + ')');
    OBJECTS {       
        NEW header {
            NEW headerCenter {
                alignment = STRETCH;
                horizontal = TRUE;
                NEW headerLeft {
                    MOVE PROPERTY(nameType(s)) { notNull = TRUE; }
                    MOVE PROPERTY(scheduledDateTime(s));
                    MOVE PROPERTY(number(s));
                    MOVE PROPERTY(customerReference(s));
                }
                NEW headerRight {
                    MOVE PROPERTY(nameCustomer(s));
                    MOVE PROPERTY(nameDepartment(s));
                    MOVE PROPERTY(nameLocation(s)) { notNull = TRUE; }           
                    MOVE PROPERTY(nameToLocation(s)) { notNull = TRUE; }           
                }
            }
            NEW headerBottom {
                alignment = STRETCH;
                MOVE PROPERTY(note(s));           
            }
        }
        NEW details {
            fill = 5;
            tabbed = TRUE;
            NEW lines {
                caption = 'Lines';
                MOVE BOX(l) {
                    caption = '';
                    PROPERTY(nameProduct(l)) { notNull = TRUE; }
                    PROPERTY(initialDemand(l)) { notNull = TRUE; }
                }
                NEW linesTab {
                    fill = 0.3;
                    tabbed = TRUE;
                }                
            }                  
            NEW otherInformation {
                caption = 'Other information';
                lines = 2;
                NEW shippingInformation {
                    caption = 'Shipping';
                    alignment = STRETCH;
                    MOVE PROPERTY(deliveryAddress(s));
                }
            }
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            horizontal = TRUE;
        }
    }
}

edit (ShipmentLine l) + { SHOW shipment OBJECTS s = shipment(l), l = l DOCKED; }

// shipments
background = ABSTRACT CASE COLOR (Shipment);

FORM shipments 'Shipments'
    OBJECTS s = Shipment BACKGROUND background(s)
    PROPERTIES(s) READONLYIF isReadonly()
                           number, scheduledDateTime, nameCustomer,
                           nameType,
                           nameLocation, nameToLocation,
                           note, countLines
    PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE DISABLEIF readonly(s)
;

@extendFormEditable(shipments);

@defineDocHistory(shipment, s, product, scheduledDateTime);
@defineDocHistoryLineProperty(shipment, ShipmentLine, initialDemand, 'Initial demand', nameProduct);

@defineDocObjectsForm(shipments, s, 'Shipments');
@defineDocStatusForm(shipment, s);
@defineDocLocationAccess(shipment, s);

@defineObjectComments(shipment, s, details);

@defineObjectFiles(shipment, s, 'Shipment file');

NAVIGATOR {
    operations {
        NEW shipments;
    }
}

// immediate
immediateShipment 'Immediate shipment' () {
    NEWSESSION {
        NEW s = Shipment {
            immediate(s) <- TRUE;
            DIALOG shipment OBJECTS s = s DOCKED DO seek(s);
        }
    }
} IMAGE 'add.png';

EXTEND FORM shipments 
    PROPERTIES immediateShipment() DRAW s TOOLBAR
;

// copy
cloneHeader (Shipment to, Shipment from) {
    type(to) <- type(from);
    location(to) <- location(from);
    toLocation(to) <- toLocation(from);
    note(to) <- note(from);
    immediate(to) <- immediate(from);
}

clone(Shipment to, Shipment from) {
    cloneHeader(to, from);
    FOR shipment(ShipmentLine l) = from INLINE NEW nl = ShipmentLine DO {
        shipment(nl) <- to;
        product(nl) <- product(l);
        initialDemand(nl) <- initialDemand(l);
    }
}

copy 'Copy' (Shipment from)  { 
    NEWSESSION {
        NEW to = Shipment {
            clone(to, from);
            SHOW shipment OBJECTS s = to DOCKED;
        }
    }
}

EXTEND FORM shipment
    PROPERTIES(s) copy
;

DESIGN shipment {
    secondaryActions {
        MOVE PROPERTY(copy(s)) { valueClass = 'btn-secondary'; }
    }
}