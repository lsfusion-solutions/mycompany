MODULE ShipmentReady;

REQUIRE Utils, ShipmentWaiting, ResLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    ready 'Ready'
}
ready 'Ready' = DATA BOOLEAN (Shipment);

status (Shipment s) += WHEN ready(s) THEN ShipmentStatus.ready;
color (ShipmentStatus s) += WHEN s = ShipmentStatus.ready THEN RGB(252,247,149);

// reserved
toReserve (Shipment s) = status(s) = ShipmentStatus.waiting OR status(s) = ShipmentStatus.ready MATERIALIZED;   

reserved 'Reserved' = DATA NUMERIC[16,3] (ShipmentLine);
countReserved (Shipment s) = GROUP SUM 1 IF reserved(ShipmentLine l) AND shipment(l) = s;
linesReserved (Shipment s) = GROUP SUM 1 IF reserved(ShipmentLine l) = initialDemand(l) AND shipment(l) = s;
reserved (Shipment s) = linesReserved(s) = countLines(s);

toReserve (ShipmentLine l) = initialDemand(l) (-) reserved(l);

onHand 'On hand' (ShipmentLine l) = prevOnHandRec(location(shipment(l)), product(l));
expected 'Expected' (ShipmentLine l) = prevExpectedRec(location(shipment(l)), product(l));
available 'Available' (ShipmentLine l) = prevAvailableRec(location(shipment(l)), product(l)) (+) PREV(reserved(l)); 

extraCheckAvailability ABSTRACT (Shipment);
precheckAvailability (Shipment s) {
    reserved(ShipmentLine l) <- max(min(initialDemand(l), available(l)), 0) WHERE shipment(l) = s;
    extraCheckAvailability(s);
    ready(s) <- reserved(s);
}

checkAvailability 'Check availability' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        precheckAvailability(s);
        APPLY;
    }
}

extraUnreserve ABSTRACT (Shipment); 
unreserve 'Unreserve' (Shipment s) {
    APPLY;
    IF canceled() THEN RETURN;

    NEWSESSION {
        reserved(ShipmentLine l) <- NULL WHERE shipment(l) = s;
        extraUnreserve(s); 
        ready(s) <- NULL;
        APPLY;
    }
}

overShipPartially ABSTRACT LIST (ShipmentLine, ShipmentLine);
shipPartially 'Ship partially' (Shipment s) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        NEW ns = Shipment {
            cloneHeader(ns, s);
            waiting(ns) <- TRUE;
            FOR shipment(ShipmentLine l) = s AND toReserve(l) > 0 INLINE NEW nl = ShipmentLine DO {
                shipment(nl) <- ns;
                product(nl) <- product(l);
                initialDemand(nl) <- toReserve(l);
                overShipPartially(nl, l);
            }
            DIALOG shipment OBJECTS s = ns DOCKED NOMANAGESESSION DO {
                ready(s) <- TRUE;
            }
            APPLY;
        }
    }
}

EXTEND FORM shipment
    PROPERTIES(s) checkAvailability SHOWIF toReserve(s) AND NOT reserved(s) AND NOT immediate(s),
                  shipPartially SHOWIF status(s) = ShipmentStatus.waiting AND countReserved(s),
                  unreserve SHOWIF toReserve(s) AND countReserved(s), 
                  ready 
       
    PROPERTIES(l) READONLY onHand, expected, available, reserved SHOWIF NOT immediate(s)
;

DESIGN shipment {
    primaryActions {
        MOVE PROPERTY(checkAvailability(s)) { valueClass = 'btn-primary'; }
    }
    secondaryActions {
        MOVE PROPERTY(shipPartially(s)) { valueClass = 'btn-warning'; }
        MOVE PROPERTY(unreserve(s)) { valueClass = 'btn-warning'; }
    }              
    statusGroup {
        MOVE PROPERTY(ready(s)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'Ready' status(s) = ShipmentStatus.ready    
;

@defineSelectionAction(shipments, Shipment, checkAvailability, 'Check availability', statuses);