MODULE ShipmentReady;

REQUIRE Utils, ShipmentWaiting, ResLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    ready 'Ready'
}
ready 'Ready' = DATA BOOLEAN (Shipment);

status (Shipment s) += WHEN ready(s) THEN ShipmentStatus.ready;
color (ShipmentStatus s) += WHEN s = ShipmentStatus.ready THEN RGB(252,247,149);

// reserved
toReserve (Shipment s) = status(s) = ShipmentStatus.waiting OR status(s) = ShipmentStatus.ready MATERIALIZED;   

reserved 'Reserved' = DATA NUMERIC[16,3] (ShipmentLine);
linesReserved (Shipment s) = GROUP SUM 1 IF reserved(ShipmentLine l) = initialDemand(l) AND shipment(l) = s;
reserved (Shipment s) = linesReserved(s) = countLines(s);

available 'Available' (ShipmentLine l) = prevAvailableRec(location(shipment(l)), product(l)) (+) PREV(reserved(l)); 

extraCheckAvailability ABSTRACT (Shipment);
precheckAvailability (Shipment s) {
    reserved(ShipmentLine l) <- max(min(initialDemand(l), available(l)), 0) WHERE shipment(l) = s;
    extraCheckAvailability(s);
    ready(s) <- reserved(s);
}

checkAvailability 'Check availability' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        precheckAvailability(s);
        APPLY;
    }
}

extraUnreserve ABSTRACT (Shipment); 
unreserve 'Unreserve' (Shipment s) {
    NEWSESSION {
        reserved(ShipmentLine l) <- NULL WHERE shipment(l) = s;
        extraUnreserve(s); 
        ready(s) <- NULL;
        APPLY;
    }
}

EXTEND FORM shipment
    PROPERTIES(s) checkAvailability SHOWIF toReserve(s) AND NOT reserved(s) AND NOT immediate(s), 
                  unreserve SHOWIF status(s) = ShipmentStatus.ready, 
                  ready 
       
    PROPERTIES(l) READONLY available, reserved SHOWIF NOT immediate(s)
;

DESIGN shipment {
    primaryActions {
        MOVE PROPERTY(checkAvailability(s)) { valueClass = 'btn-primary'; }
    }
    secondaryActions {
        MOVE PROPERTY(unreserve(s)) { valueClass = 'btn-warning'; }
    }              
    status {
        MOVE PROPERTY(ready(s));                               
    }    
}

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'Ready' status(s) = ShipmentStatus.ready    
;

@defineSelectionAction(shipments, Shipment, checkAvailability, 'Check availability', statuses);