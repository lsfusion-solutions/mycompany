MODULE ShipmentTotal;

REQUIRE Shipment, ShipmentSelection, ShipmentFilter,
        ShipmentSearch, ShipmentWaiting,
        ShipmentPeriod;

NAMESPACE Manufacturing;

initialDemand 'Планируемое кол-во' (Product p) = GROUP SUM initialDemand(ShipmentLine l) IF product(l) = p AND selectedShipments(shipment(l));

EXTEND FORM shipments
    OBJECTS ss = Shipment
    FILTERS selectedShipments(ss)

    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)

    OBJECTS p = Product
    PROPERTIES(p) READONLY name, nameUom, idBarCode, id
    PROPERTIES READONLY 'Доступно (откуда)' = prevAvailableRec(shipmentLocation(), p) SHOWIF shipmentLocation()

    PROPERTIES(p) READONLY 'Итого' = initialDemand BACKGROUND RGB(255,212,212) IF initialDemand(p) > OVERRIDE prevAvailableRec(shipmentLocation(), p), (0 IF shipmentLocation()) 
                           
    PROPERTIES initialDemand(ss, p) READONLYIF NOT (status(ss) = ShipmentStatus.draft OR status(ss) = ShipmentStatus.waiting)
                                    COLUMNS 'id' (ss) 
                                    HEADER CONCAT ' ', nameCustomer(ss), nameToLocation(ss), [FORMULA STRING[8] 'to_char(($1),\'DD.MM\')'](scheduledDate(ss))
                                    ON CHANGE changeInitialDemand(ss, p)
    
    PROPERTIES onHand 'Доступно' = prevAvailableRec(toLocation(ss), p) SHOWIF toLocation(ss) READONLY
                                  COLUMNS 'id' (ss)
    FILTERGROUP demand
        FILTER 'К отгрузке' initialDemand(p) DEFAULT
        FILTER 'Доступно' prevAvailableRec(shipmentLocation(), p)
    FILTERS level(category(p), c)
    FILTERGROUP active
        FILTER 'Активный' active(p) 'F10' DEFAULT  
    ORDERS name(p)
;

DESIGN shipments {
    tabbedPane {
        NEW total {
            showIf = countSelectedShipments();
            caption = 'Итоги';
            type = SPLITH;
            MOVE BOX(TREE categories);
            NEW products {
                fill = 3;
                NEW productParams { type = CONTAINERH; }
                MOVE BOX(p) {
                    PROPERTY(initialDemand(ss, p)) { charWidth = 6; background = #EEEEFF; }
                    PROPERTY(onHand) { charWidth = 6; }
                }
            }
        } 
    }
}

// period

EXTEND FORM shipments
    OBJECTS pFrom = DATE PANEL BEFORE p, pTo = DATE PANEL BEFORE p
    PROPERTIES pFrom 'Период с' = VALUE(pFrom), pTo 'по' = VALUE(pTo)
    
    PROPERTIES shipped 'Отгружено' = shippedPeriodicRec(toLocation(ss), p, pFrom, pTo) SHOWIF toLocation(ss) READONLY
                                     COLUMNS 'id' (ss)
    
    EVENTS ON INIT { 
        SEEK shipments.pFrom = subtract(currentDate(), 7);
        SEEK shipments.pTo = subtract(currentDate(), 1); 
    }
;

DESIGN shipments {
    productParams {
        MOVE PROPERTY(pFrom);
        MOVE PROPERTY(pTo);
        PROPERTY(shipped) { charWidth = 6; }
    }
}