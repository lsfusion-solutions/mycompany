MODULE ShipmentWaiting;

REQUIRE Shipment, ShipmentSelection, InvLedger;

NAMESPACE Inventory;

EXTEND CLASS ShipmentStatus {
    waiting 'Waiting'
}
waiting 'Waiting' = DATA BOOLEAN (Shipment);

status (Shipment s) += WHEN waiting(s) THEN ShipmentStatus.waiting;
color (ShipmentStatus s) += WHEN s = ShipmentStatus.waiting THEN RGB(250,150,157);

markAsToDo 'Mark as Todo' (Shipment s) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        waiting(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM shipment
    PROPERTIES(s) markAsToDo SHOWIF status(s) = ShipmentStatus.draft AND NOT immediate(s), waiting 
;

DESIGN shipment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(s)) { valueClass = 'btn-primary'; }
    }                
    statusGroup {
        MOVE PROPERTY(waiting(s)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }
}

EXTEND FORM shipments    
    EXTEND FILTERGROUP status
        FILTER 'Waiting' status(s) = ShipmentStatus.waiting    
;

@defineSelectionAction(shipments, Shipment, markAsToDo, 'Mark as Todo', statuses);

// total
waiting 'Awaiting shipment' (Location l, Product p) = 
    GROUP SUM initialDemand(ShipmentLine sl) IF status(shipment(sl)) = ShipmentStatus.waiting AND 
              location(sl) = l AND product(sl) = p;

waitingRec 'Awaiting shipment' (Location l, Product p) =
    GROUP SUM waiting(Location child, p) IF level(child, l);
