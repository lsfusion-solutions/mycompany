MODULE ReceiptType;

REQUIRE Numerator, Location;

NAMESPACE Inventory;

CLASS ReceiptType 'Тип приемки';
name 'Название' = DATA ISTRING[50] (ReceiptType) NONULL;

id 'Код' = DATA STRING[20] (ReceiptType);
receiptType = GROUP AGGR ReceiptType t BY id(t);

numerator = DATA Numerator (ReceiptType);
nameNumerator 'Нумератор' (ReceiptType o) = name(numerator(o));

defaultLocation = DATA Location (ReceiptType);
nameDefaultLocation 'Место хранения по умолчанию' (ReceiptType o) = name(defaultLocation(o));

maxQuantity 'Максимальное кол-во товара' = DATA NUMERIC[16,3] (ReceiptType);

FORM receiptType 'Тип приемки'
    OBJECTS o = ReceiptType PANEL 
    PROPERTIES(o) name, id, nameNumerator, nameDefaultLocation, maxQuantity
     
    EDIT ReceiptType OBJECT o;
;

DESIGN receiptType {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM receiptTypes 'Типы приемки'
    OBJECTS o = ReceiptType
    PROPERTIES(o) READONLY name, id
    LIST ReceiptType OBJECT o;
;

EXTEND FORM options
    OBJECTS rt = ReceiptType
    PROPERTIES(rt) READONLY name, id
    PROPERTIES(rt) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(rt);
    }
}

// default type
countReceiptTypes 'Кол-во типов' () = GROUP SUM 1 IF r IS ReceiptType; 
firstReceiptType = GROUP LAST ReceiptType r ORDER DESC r WHERE r IS ReceiptType;

defaultReceiptType = firstReceiptType() IF countReceiptTypes() = 1;