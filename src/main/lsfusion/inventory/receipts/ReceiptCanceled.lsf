MODULE ReceiptCanceled;

REQUIRE ReceiptDone;

NAMESPACE Inventory;
    
EXTEND CLASS ReceiptStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (Receipt);
active '{Active}' (Receipt r) = done(r) AND NOT canceled(r);

status(Receipt r) += WHEN canceled(r) THEN ReceiptStatus.canceled;
color(ReceiptStatus r) += WHEN r = ReceiptStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) cancel SHOWIF NOT (status(r) = ReceiptStatus.draft OR status(r) = ReceiptStatus.canceled), canceled 
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(cancel(r)) { valueClass = 'btn-danger'; }
    }             
    statusGroup {
        MOVE PROPERTY(canceled(r)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(r) = ReceiptStatus.canceled    
;

@defineSelectionAction(receipts, Receipt, cancel, 'Cancel', statuses);