MODULE ReceiptCanceled;

REQUIRE ReceiptDone;

NAMESPACE Inventory;
    
EXTEND CLASS ReceiptStatus {
    canceled 'Cancelled'
}
canceled 'Cancelled' = DATA BOOLEAN (Receipt);
active '{Active}' (Receipt r) = done(r) AND NOT canceled(r);

status(Receipt r) += WHEN canceled(r) THEN ReceiptStatus.canceled;
color(ReceiptStatus r) += WHEN r = ReceiptStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) cancel SHOWIF NOT (status(r) = ReceiptStatus.draft OR status(r) = ReceiptStatus.canceled), canceled 
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(cancel(r)) { valueClass = 'btn-danger'; }
    }             
    status {
        MOVE PROPERTY(canceled(r));                               
    }    
}

EXTEND FORM receipts    
    EXTEND FILTERGROUP status
        FILTER 'Cancelled' status(r) = ReceiptStatus.canceled    
;

@defineSelectionAction(receipts, Receipt, cancel, 'Cancel', statuses);