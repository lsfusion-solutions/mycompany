MODULE Receipt;

REQUIRE System, Time, DateUtils,
        InventorySettings, Location, Partner, Product, LocationEmployee,
        ReceiptType, ItemPurchase, PartnerPurchase, BarCode, Doc, ResLedger;

NAMESPACE Inventory;

CLASS Receipt 'Приемка';

@defineDocStatus(receipt, 'приемки');

immediate 'Незапланированное' = DATA BOOLEAN (Receipt);

// type
type 'Тип' = DATA ReceiptType (Receipt) NONULL;
nameType 'Тип' (Receipt r) = name(type(r));

WHEN LOCAL SET(Receipt r IS Receipt) AND NOT CHANGED(type(r)) DO type(r) <- defaultReceiptType(); 

scheduledDateTime 'Планируемая дата' = DATA DATETIME (Receipt) NONULL IN id;
scheduledDateTime(Receipt r) <- currentDateTime() WHEN (r IS Receipt); 
@defineDateTimeAggregation(Receipt, scheduled, ' планируемая');

@defineNumberType(receipt);
receipt (STRING[31] number) = GROUP LAST Receipt r ORDER r BY number(r);

numberDate 'Описание' (Receipt r) = number(r) + ' от ' + scheduledDateTime(r) IN id;

// location
location 'Место хранения' = DATA Location (Receipt) NONULL INDEXED;
nameLocation 'Место хранения' (Receipt r) = name(location(r));
WHEN LOCAL SETCHANGED(type(Receipt r)) AND defaultLocation(type(r)) AND NOT CHANGED(location(r)) DO location(r) <- defaultLocation(type(r));

company 'Компания' (Receipt r) = company(location(r));

// partner
vendor = DATA Partner (Receipt) INDEXED;
nameVendor 'Поставщик' (Receipt r) = name(vendor(r));

CONSTRAINT vendor(Receipt r) AND NOT isVendor(vendor(r)) 
                CHECKED BY vendor[Receipt]
                MESSAGE 'Контрагент должен быть поставщиком';

note 'Примечание' = DATA ISTRING[50] (Receipt);

// lines
CLASS ReceiptLine 'Строка приемки';

receipt 'Документ' = DATA Receipt (ReceiptLine) NONULL DELETE INDEXED;

countLines 'Кол-во строк' (Receipt r) = GROUP SUM 1 IF receipt(ReceiptLine l) = r MATERIALIZED;
index '№' = PARTITION SUM 1 ORDER ReceiptLine l BY receipt(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Товар' = DATA Product (ReceiptLine) NONULL INDEXED;
nameProduct 'Товар' (ReceiptLine l) = name(product(l)) IN id;
idBarCodeProduct 'Штрихкод' (ReceiptLine l) = idBarCode(product(l));
idProduct 'Код' (ReceiptLine l) = id(product(l));

CONSTRAINT product(ReceiptLine l) AND NOT canBePurchased(product(l))
            CHECKED BY product[ReceiptLine]
            MESSAGE 'Товар не предназначен для закупки';

uom (ReceiptLine l) = uom(product(l));
nameUom 'Ед. изм.' (ReceiptLine l) = name(uom(l));

initialDemand 'Планируемое кол-во' = DATA NUMERIC[16,3] (ReceiptLine);

CONSTRAINT initialDemand(ReceiptLine l) < 0 OR initialDemand(l) > maxQuantity(type(receipt(l))) AND maxQuantity(type(receipt(l))) 
    MESSAGE 'Планируемое количество должно быть от 0 до максимально разрешенного';
    
// Line properties
nameStatus 'Статус' (ReceiptLine l) = nameStatus(receipt(l));
number 'Номер' (ReceiptLine l) = number(receipt(l)) IN id;
numberDate 'Описание' (ReceiptLine l) = numberDate(receipt(l));
scheduledDateTime 'Планируемая дата' (ReceiptLine l) = scheduledDateTime(receipt(l)) IN id;
vendor (ReceiptLine l) = vendor(receipt(l));
nameVendor 'Поставщик' (ReceiptLine l) = nameVendor(receipt(l));
nameLocation 'Место хранения' (ReceiptLine l) = nameLocation(receipt(l));

FORM receiptLines 'Строки приемки'
    OBJECTS l = ReceiptLine
    PROPERTIES(l) READONLY scheduledDateTime, number, index, nameProduct

    LIST ReceiptLine OBJECT l
;

edit (ReceiptLine l) + { edit(receipt(l)); } 
           
FORM receipt 'Приемка'
    OBJECTS r = Receipt PANEL
    PROPERTIES(r) nameType, scheduledDateTime, number, 
                  nameVendor, nameLocation,
                  note

    OBJECTS l = ReceiptLine
    PROPERTIES(l) index READONLY, nameProduct, nameUom, idBarCodeProduct, idProduct,
                  initialDemand SHOWIF NOT immediate(r) BACKGROUND IF status(receipt(l)) = ReceiptStatus.draft THEN RGB(198,230,247)
    PROPERTIES(l) NEW, DELETE        
    FILTERS receipt(l) = r
   
    EDIT Receipt OBJECT r
;

DESIGN receipt {
    caption = (CONCAT ' ', 'Приемка', '№' + number(r), 'от ' + scheduledDateTime(r));
    OBJECTS {       
        NEW header {
            NEW headerCenter {
                alignment = STRETCH;
                type = CONTAINERH;
                NEW headerLeft {
                    MOVE PROPERTY(nameType(r)) { notNull = TRUE; }
                    MOVE PROPERTY(scheduledDateTime(r));
                    MOVE PROPERTY(number(r));
                }
                NEW headerRight {
                    MOVE PROPERTY(nameVendor(r));
                    MOVE PROPERTY(nameLocation(r)) { notNull = TRUE; }           
                }
            }
            NEW headerBottom {
                alignment = STRETCH;
                MOVE PROPERTY(note(r));           
            }
        }
        NEW details {
            fill = 5;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                type = SPLITH;
                MOVE BOX(l) {
                    PROPERTY(nameProduct(l)) { notNull = TRUE; }
                    PROPERTY(initialDemand(l)) { notNull = TRUE; }
                }
                NEW linesTab {
                    fill = 0.3;
                    type = TABBED;
                }
            }                  
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocHistory(receipt, r, product, initialDemand);

// receipts
background = ABSTRACT CASE COLOR (Receipt);

FORM receipts 'Приемки'
    OBJECTS r = Receipt BACKGROUND background(r)
    PROPERTIES(r) READONLYIF isReadonly()
                           nameType, scheduledDateTime, number, 
                           nameVendor, nameLocation,
                           note, countLines
    PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE
;

@extendFormEditable(receipts);

@defineDocObjectsForm(receipts, r, 'Приемки');
@defineDocStatusForm(receipt, r);
@defineDocLocationAccess(receipt, r);

NAVIGATOR {
    operations {
        NEW receipts;
    }
}

// immediate
immediateReceipt 'Незапланированная приемка' () {
    NEWSESSION {
        NEW r = Receipt {
            immediate(r) <- TRUE;
            SHOW receipt OBJECTS r = r DOCKED;
        }
    }
} IMAGE 'add.png';

EXTEND FORM receipts 
    PROPERTIES immediateReceipt() DRAW r TOOLBAR
;

// copy
clone(Receipt to, Receipt from) {
    type(to) <- type(from);
    location(to) <- location(from);
    vendor(to) <- vendor(from);
    note(to) <- note(from);
    immediate(to) <- immediate(from);
    FOR receipt(ReceiptLine l) = from INLINE NEW nl = ReceiptLine DO {
        receipt(nl) <- to;
        product(nl) <- product(l);
        initialDemand(nl) <- initialDemand(l);
    }
}

copy 'Копировать' (Receipt from) { 
    NEWSESSION {
        NEW to = Receipt {
            clone(to, from);
            SHOW receipt OBJECTS r = to DOCKED;
        }
    }
}

EXTEND FORM receipt
    PROPERTIES(r) copy 
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(copy(r));
    }
}
