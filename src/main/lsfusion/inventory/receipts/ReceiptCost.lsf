MODULE ReceiptCost;

REQUIRE ReceiptCanceled, CostLedger, CostLocation;

NAMESPACE Inventory;

cost 'Себестоимость' = ABSTRACT CASE NUMERIC[14,2] (ReceiptLine);

CLASS ReceiptCostLedger 'Приемка' : InCostLedger;
costLedger = AGGR ReceiptCostLedger WHERE done(ReceiptLine line) AND active(receipt(line));

type(ReceiptCostLedger l) += nameType(receipt(line(l)));

dateTime(ReceiptCostLedger l) += executionDateTime(receipt(line(l)));
number(ReceiptCostLedger l) += number(receipt(line(l)));

location(ReceiptCostLedger l) += costLocation(location(receipt(line(l))));

product(ReceiptCostLedger l) += product(line(l));
quantity(ReceiptCostLedger l) += done(line(l));

calcAmount (ReceiptCostLedger l) += cost(line(l));

edit(ReceiptCostLedger l) + { edit(line(l)); }

// data cost
showCost 'Показывать себестоимость' = DATA BOOLEAN (ReceiptType);

dataCost 'Себестоимость' = DATA NUMERIC[14,2] (ReceiptLine);
cost (ReceiptLine l) += WHEN dataCost(l) THEN dataCost(l);

EXTEND FORM receipt
    PROPERTIES(l) cost SHOWIF showCost(type(r)) ON CHANGE { INPUT c = cost(l) DO dataCost(l) <- c; }
;