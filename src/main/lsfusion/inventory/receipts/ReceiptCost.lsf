MODULE ReceiptCost;

REQUIRE ReceiptCanceled, CostLedger, CostLocation;

NAMESPACE Inventory;

cost 'Cost' = ABSTRACT CASE NUMERIC[14,2] (ReceiptLine);

CLASS ReceiptCostLedger 'Receipt' : InCostLedger;
costLedger = AGGR ReceiptCostLedger WHERE done(ReceiptLine line) AND active(receipt(line));

type(ReceiptCostLedger l) += nameType(receipt(line(l)));

dateTime(ReceiptCostLedger l) += executionDateTime(receipt(line(l)));
number(ReceiptCostLedger l) += number(receipt(line(l)));
note(ReceiptCostLedger l) += note(receipt(line(l)));

location(ReceiptCostLedger l) += costLocation(location(receipt(line(l))));

product(ReceiptCostLedger l) += product(line(l));
quantity(ReceiptCostLedger l) += done(line(l));

calcAmount (ReceiptCostLedger l) += cost(line(l));

edit(ReceiptCostLedger l) + { edit(line(l)); }

// data cost
showCost 'Show cost price' = DATA BOOLEAN (ReceiptType);
EXTEND FORM receiptType PROPERTIES(o) showCost;

dataCost 'Cost' = DATA NUMERIC[14,2] (ReceiptLine);
cost (ReceiptLine l) += WHEN dataCost(l) THEN dataCost(l);

EXTEND FORM receipt
    PROPERTIES(l) READONLYIF readonly(r) cost SHOWIF showCost(type(r)) ON CHANGE { INPUT c = cost(l) DO dataCost(l) <- c; }
;