MODULE ReceiptPutAway;

REQUIRE ReceiptDone, ReceiptInv, Input;

NAMESPACE Inventory;

// type
putAway 'Размещение' = DATA BOOLEAN (ReceiptType);
EXTEND FORM receiptType PROPERTIES(o) putAway;

// status
EXTEND CLASS ReceiptStatus {
    putAway 'Размещен'
}
putAway 'Размещен' = DATA BOOLEAN (Receipt);

status(Receipt r) += WHEN putAway(r) THEN ReceiptStatus.putAway;

markAsPutAway 'Размещен' (Receipt r) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        putAway(r) <- TRUE;
        APPLY;
    }
}

EXTEND FORM receipt
    PROPERTIES(r) markAsPutAway SHOWIF putAway(type(r)) AND status(r) = ReceiptStatus.done, putAway
;

DESIGN receipt {
    primaryActions {
        MOVE PROPERTY(markAsPutAway(r)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(putAway(r));
    }
}

// put away process
CLASS ReceiptPutAway 'Размещение';

receiptLine 'Строка приемки' = DATA ReceiptLine (ReceiptPutAway) NONULL DELETE;

location 'Место хранения' = DATA Location (ReceiptPutAway);
nameLocation 'Место хранения' (ReceiptPutAway p) = name(location(p));

CONSTRAINT location(ReceiptPutAway p) AND NOT level(location(p), location(receipt(receiptLine(p)))) 
           CHECKED BY location[ReceiptPutAway]
           MESSAGE 'Место хранения должно быть дочерним от места хранения из документа потупления';

quantity 'Кол-во' = DATA NUMERIC[16,3] (ReceiptPutAway); 

put 'Размещено' (ReceiptLine l) = GROUP SUM quantity(ReceiptPutAway p) IF receiptLine(p) = l;
ledgerDone(ReceiptLine l) += put(l);

CONSTRAINT put(ReceiptLine l) > done(l) MESSAGE 'Размещенное количество не может быть больше принятого';

EXTEND FORM receipt  
    OBJECTS p = ReceiptPutAway
    PROPERTIES(p) nameLocation, quantity, DELETE GRID
    FILTERS receiptLine(p) = l
;

FORM putAwayLoc 'Места хранения'
    OBJECTS loc = Location
    
    OBJECTS l = Location
    PROPERTIES(l) READONLY name, nameParent, canonicalName
    FILTERS level(l,loc)
;

DESIGN putAwayLoc {
    PROPERTY(formOk()) { changeKey = 'ENTER'; }
}

newPutAway 'Разместить (+)' (ReceiptLine ln) {
    DIALOG putAwayLoc OBJECTS l INPUT, loc = location(receipt(ln)) DO {
        DIALOG dialogQuantity OBJECTS q = done(ln) (-) put(ln) INPUT DO {
            IF q > 0 THEN {
                NEW p = ReceiptPutAway {
                    receiptLine(p) <- ln;
                    location(p) <- l;
                    quantity(p) <- q;
                    SEEK receipt.p = p;
                }
            } 
        }
    }
} CHANGEKEY 'ADD' HIDE;

toPutAway (Receipt r) = putAway(type(r)) AND (ready(r) OR done(r));

EXTEND FORM receipt
    PROPERTIES(l) put SHOWIF toPutAway(r) ON CHANGE newPutAway(l)
    
    PROPERTIES newPutAway(l) DRAW p TOOLBAR SHOWIF toPutAway(r)
;

DESIGN receipt {
    linesTab {
        NEW receiptPutAway {
            caption = 'Размещение';
            showIf = toPutAway(r);
            MOVE GRID(p);
            MOVE TOOLBARBOX(p);
        }
    }
}

// invledger
CLASS ReceiptPutAwayLedger 'Приемка' : InvLedger;
putAwayLedger = AGGR ReceiptPutAwayLedger WHERE quantity(ReceiptPutAway line);

type(ReceiptPutAwayLedger l) += nameType(receipt(receiptLine(line(l))));

active(ReceiptPutAwayLedger l) += active(receipt(receiptLine(line(l))));
dateTime(ReceiptPutAwayLedger l) += executionDateTime(receipt(receiptLine(line(l))));
toLocation(ReceiptPutAwayLedger l) += location(line(l));

product(ReceiptPutAwayLedger l) += product(receiptLine(line(l)));
quantity(ReceiptPutAwayLedger l) += quantity(line(l));

partner(ReceiptPutAwayLedger l) += vendor(receipt(receiptLine(line(l))));

number(ReceiptPutAwayLedger l) += number(receipt(receiptLine(line(l))));

edit (ReceiptPutAwayLedger l) + { edit(receipt(receiptLine(line(l)))); }