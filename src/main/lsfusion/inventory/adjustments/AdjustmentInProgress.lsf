MODULE AdjustmentInProgress;

REQUIRE Adjustment;

NAMESPACE Inventory;

EXTEND CLASS AdjustmentStatus {
     inProgress 'В процессе'
}
inProgress 'В процессе' = DATA BOOLEAN (Adjustment);

status(Adjustment a) += WHEN inProgress(a) THEN AdjustmentStatus.inProgress;
colorStatus(Adjustment a) += WHEN status(a) = AdjustmentStatus.inProgress THEN RGB(252,247,149);

aggregateRealQuantity = ABSTRACT VALUE BOOLEAN (AdjustmentLine);

realQuantity 'Фактическое кол-во' = DATA NUMERIC[16,3] (AdjustmentLine);

WHEN LOCAL CHANGED(theoreticalQuantity(AdjustmentLine l)) AND NOT CHANGED(realQuantity(l)) AND 
           status(adjustment(l)) = AdjustmentStatus.draft AND
           NOT aggregateRealQuantity(l) DO 
    realQuantity(l) <- theoreticalQuantity(l);

markAsToDo 'Начать' (Adjustment a) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        inProgress(a) <- TRUE;
        APPLY;
    }
}

background (AdjustmentLine l) = CASE WHEN theoreticalQuantity(l) < realQuantity(l) THEN RGB(152,251,152)
                                              WHEN theoreticalQuantity(l) > realQuantity(l) THEN RGB(252,184,205);

EXTEND FORM adjustment
    PROPERTIES(a) markAsToDo SHOWIF status(a) = AdjustmentStatus.draft, inProgress
    
    PROPERTIES(l) realQuantity READONLYIF aggregateRealQuantity(l) OR NOT status(adjustment(l)) = AdjustmentStatus.inProgress BACKGROUND background(l)
    
    FILTERGROUP adjustment
        FILTER 'Расхождения' NOT theoreticalQuantity(l) = realQuantity(l) 'F10'
        FILTER 'Излишки'  theoreticalQuantity(l) < realQuantity(l) 'F9'
        FILTER 'Недостачи'  theoreticalQuantity(l) > realQuantity(l) 'F8'
;

DESIGN adjustment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(a)) { fontStyle = 'bold'; }               
    }
    status {
        MOVE PROPERTY(inProgress(a));                               
    }
}