MODULE AdjustmentInProgress;

REQUIRE Adjustment;

NAMESPACE Inventory;

EXTEND CLASS AdjustmentStatus {
     inProgress 'In progress'
}
inProgress 'In progress' = DATA BOOLEAN (Adjustment);

status(Adjustment a) += WHEN inProgress(a) THEN AdjustmentStatus.inProgress;
color(AdjustmentStatus a) += WHEN a = AdjustmentStatus.inProgress THEN RGB(252,247,149);

aggregateRealQuantity = ABSTRACT VALUE BOOLEAN (AdjustmentLine);

realQuantity 'Real quantity' = DATA NUMERIC[16,3] (AdjustmentLine);
realQuantity 'Real quantity' (Adjustment a) = GROUP SUM realQuantity(AdjustmentLine al) IF adjustment(al) = a MATERIALIZED;

WHEN LOCAL CHANGED(theoreticalQuantity(AdjustmentLine l)) AND NOT CHANGED(realQuantity(l)) AND 
           status(adjustment(l)) = AdjustmentStatus.draft AND
           NOT aggregateRealQuantity(l) DO 
    realQuantity(l) <- theoreticalQuantity(l);

markAsToDo 'Mark as Todo' (Adjustment a) {
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        inProgress(a) <- TRUE;
        APPLY;
    }
}

background (AdjustmentLine l) = CASE WHEN theoreticalQuantity(l) < realQuantity(l) THEN RGB(152,251,152)
                                              WHEN theoreticalQuantity(l) > realQuantity(l) THEN RGB(252,184,205);

EXTEND FORM adjustment
    PROPERTIES(a) markAsToDo SHOWIF status(a) = AdjustmentStatus.draft, inProgress
    
    PROPERTIES(l) realQuantity READONLYIF aggregateRealQuantity(l) OR NOT status(adjustment(l)) = AdjustmentStatus.inProgress BACKGROUND background(l)
    
    FILTERGROUP adjustment
        FILTER 'Mismatch' NOT theoreticalQuantity(l) = realQuantity(l) 'F10'
        FILTER 'Surplus'  theoreticalQuantity(l) < realQuantity(l) 'F9'
        FILTER 'Missing'  theoreticalQuantity(l) > realQuantity(l) 'F8'
;

DESIGN adjustment {
    primaryActions {
        MOVE PROPERTY(markAsToDo(a)) { valueClass = 'btn-primary'; }               
    }
    statusGroup {
        MOVE PROPERTY(inProgress(a))  {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }
}

@defineDocHistoryLineProperty(adjustment, AdjustmentLine, realQuantity, 'Real quantity', nameProduct);

EXTEND FORM adjustments
    PROPERTIES(a) READONLY realQuantity
    
    EXTEND FILTERGROUP status
        FILTER 'In progress' status(a) = AdjustmentStatus.inProgress
;