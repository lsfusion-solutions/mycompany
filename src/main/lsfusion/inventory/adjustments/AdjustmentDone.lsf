MODULE AdjustmentDone;

REQUIRE AdjustmentInProgress, Utils;

NAMESPACE Inventory;

EXTEND CLASS AdjustmentStatus {
    done 'Done'
}
done 'Done' = DATA BOOLEAN (Adjustment);

status(Adjustment a) += WHEN done(a) THEN AdjustmentStatus.done;

validate 'Mark as Done' (Adjustment a) {
    APPLY;
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        done(a) <- TRUE;
        APPLY;
    }    
}

EXTEND FORM adjustment
    PROPERTIES(a) validate SHOWIF status(a) = AdjustmentStatus.inProgress, 
                  done READONLYIF readonly(a) 
;

DESIGN adjustment {                               
    primaryActions {                  
        MOVE PROPERTY(validate(a)) { valueClass = 'btn-primary'; }
    }
    statusGroup {
        MOVE PROPERTY(done(a))  {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                              
    }  
}

// inv adjustment
difference 'Difference' (AdjustmentLine l) = realQuantity(l) (-) theoreticalQuantity(l);
difference 'Difference' (Adjustment a) = realQuantity(a) (-) theoreticalQuantity(a);

createInAdjustment = ABSTRACT VALUE BOOLEAN (AdjustmentLine); 

CLASS InvAdjustment 'Adjustment';

invAdjustment = AGGR InvAdjustment WHERE (difference(AdjustmentLine line) OR createInAdjustment(line)) AND done(adjustment(line)) MATERIALIZED INDEXED;

adjustment (InvAdjustment a) = adjustment(line(a)) MATERIALIZED INDEXED;

quantity '{Quantity}' (InvAdjustment a) = difference(line(a));

location (InvAdjustment a) = location(line(a));
nameLocation 'Location' (InvAdjustment a) = canonicalName(location(a));

product (InvAdjustment a) = product(line(a));
nameProduct 'Product' (InvAdjustment a) = name(product(a));

edit (InvAdjustment a) + { edit(adjustment(a)); } 

EXTEND FORM adjustment
    OBJECTS i = InvAdjustment
    PROPERTIES(i) READONLY nameProduct, nameLocation, quantity
    FILTERS adjustment(i) = a
;

DESIGN adjustment {
    details {
        NEW adjustments {
            showIf = done(a);
            caption = 'Changes';
            MOVE BOX(i);
        }
    }
}

EXTEND FORM adjustments
    PROPERTIES(a) READONLY difference
    
    EXTEND FILTERGROUP status
        FILTER 'Done' status(a) = AdjustmentStatus.done
;