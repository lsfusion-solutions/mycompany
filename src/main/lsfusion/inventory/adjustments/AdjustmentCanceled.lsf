MODULE AdjustmentCanceled;

REQUIRE AdjustmentDone;

NAMESPACE Inventory;

EXTEND CLASS AdjustmentStatus {
    canceled 'Cancelled'
}
canceled 'Cancelled' = DATA BOOLEAN (Adjustment);
active '{Active}' (Adjustment a) = done(a) AND NOT canceled(a);

status(Adjustment a) += WHEN canceled(a) THEN AdjustmentStatus.canceled;
color(AdjustmentStatus a) += WHEN a = AdjustmentStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Adjustment a) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(a) <- TRUE;
        APPLY;
    }
}

EXTEND FORM adjustment
    PROPERTIES(a) cancel SHOWIF NOT (status(a) = AdjustmentStatus.draft OR status(a) = AdjustmentStatus.canceled), canceled 
;

DESIGN adjustment {
    secondaryActions {
        MOVE PROPERTY(cancel(a)) { valueClass = 'btn-danger'; }            
    }
    status {
        MOVE PROPERTY(canceled(a));                               
    }    
}

EXTEND FORM adjustments
    EXTEND FILTERGROUP status
        FILTER 'Cancelled' status(a) = AdjustmentStatus.canceled
;