MODULE AdjustmentCanceled;

REQUIRE AdjustmentDone;

NAMESPACE Inventory;

EXTEND CLASS AdjustmentStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (Adjustment);
active '{Active}' (Adjustment a) = done(a) AND NOT canceled(a);

status(Adjustment a) += WHEN canceled(a) THEN AdjustmentStatus.canceled;
color(AdjustmentStatus a) += WHEN a = AdjustmentStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Adjustment a) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(a) <- TRUE;
        APPLY;
    }
}

EXTEND FORM adjustment
    PROPERTIES(a) cancel SHOWIF NOT (status(a) = AdjustmentStatus.draft OR status(a) = AdjustmentStatus.canceled), canceled 
;

DESIGN adjustment {
    secondaryActions {
        MOVE PROPERTY(cancel(a)) { valueClass = 'btn-danger'; }            
    }
    statusGroup {
        MOVE PROPERTY(canceled(a)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM adjustments
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(a) = AdjustmentStatus.canceled
;