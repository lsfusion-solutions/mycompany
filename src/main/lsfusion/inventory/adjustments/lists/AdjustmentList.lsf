MODULE AdjustmentList;

REQUIRE AdjustmentInProgress;

NAMESPACE Inventory;

useList 'Use inventories' = DATA BOOLEAN (Adjustment);

EXTEND FORM adjustment
    PROPERTIES(a) useList READONLYIF readonly(a)
;

DESIGN adjustment {
    headerLeft {
        MOVE PROPERTY(useList(a));
    }
}

CLASS AdjustmentList 'Adjustment list';

adjustment 'Adjustment' = DATA Adjustment (AdjustmentList) NONULL DELETE INDEXED;

@defineNumber(adjustmentList, 'Adjustment lists', 'OPS');
note 'Note' = DATA ISTRING (AdjustmentList) CHARWIDTH 30;

readonly (AdjustmentList l) = l IS AdjustmentList AND NOT status(adjustment(l)) = AdjustmentStatus.inProgress;
location (AdjustmentList l) = location(adjustment(l));  

// lines
CLASS AdjustmentListLine 'Adjustment list line';

adjustmentList = DATA AdjustmentList (AdjustmentListLine) NONULL DELETE INDEXED;

countLines 'Number of lines' (AdjustmentList a) = GROUP SUM 1 IF adjustmentList(AdjustmentListLine l) = a MATERIALIZED;
index 'â„–' = PARTITION SUM 1 ORDER AdjustmentListLine l BY adjustmentList(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (AdjustmentListLine);
nameProduct 'Product' (AdjustmentListLine l) = name(product(l));
idBarCodeProduct 'Barcode' (AdjustmentListLine l) = idBarCode(product(l));
idProduct '{ID}' (AdjustmentListLine l) = id(product(l));

uom (AdjustmentListLine l) = uom(product(l));
nameUom 'UoM' (AdjustmentListLine l) = name(uom(l));

location 'Location' = DATA Location (AdjustmentListLine);
nameLocation 'Location' (AdjustmentListLine l) = canonicalName(location(l));

WHEN LOCAL SETCHANGED(location(adjustmentList(AdjustmentListLine l))) AND NOT location(l) DO location(l) <- location(adjustmentList(l));

CONSTRAINT location(AdjustmentListLine l) AND NOT level(location(l), location(adjustmentList(l))) CHECKED BY location[AdjustmentListLine]
    MESSAGE 'The adjustment line location is not a descendant of the document location';

lastAdjustmentListLine (AdjustmentList a, Product p, Location l) = 
    GROUP LAST AdjustmentListLine al ORDER al BY adjustmentList(al), product(al), location(al);

realQuantity 'Real quantity' = DATA NUMERIC[16,3] (AdjustmentListLine);
realQuantity 'Real quantity' (AdjustmentList a) = GROUP SUM realQuantity(AdjustmentListLine l) IF adjustmentList(l) = a MATERIALIZED;

// line properties
adjustment(AdjustmentListLine l) = adjustment(adjustmentList(l));
number 'Number' (AdjustmentListLine l) = number(adjustmentList(l)) IN id;
note 'Note' (AdjustmentListLine l) = note(adjustmentList(l));

// edit
FORM adjustmentList 'Adjustment list'
    OBJECTS a = AdjustmentList PANEL
    PROPERTIES(a) READONLYIF readonly(a) number, note
    
    OBJECTS l = AdjustmentListLine
    PROPERTIES(l) READONLYIF readonly(a) 
                  index READONLY, 
                  nameProduct, nameUom, idBarCodeProduct, idProduct,
                  nameLocation, 
                  realQuantity,
                  NEW, DELETE
    FILTERS adjustmentList(l) = a
    
    EDIT AdjustmentList OBJECT a
;

DESIGN adjustmentList {
    caption = badged('Adjustment list', CONCAT ' ', number(a), '(' + note(a) + ')');
}

// list
newList 'Add to' (Adjustment a) {
    NEWSESSION {
        NEW al = AdjustmentList {
            adjustment(al) <- a;
            DIALOG adjustmentList OBJECTS a = al DOCKED DO seek(al);
        }
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

edit (AdjustmentListLine l) + { edit(adjustmentList(l)); }

EXTEND FORM adjustment
    OBJECTS al = AdjustmentList
    PROPERTIES(al) READONLY number, note, countLines, realQuantity
    
    PROPERTIES(a) newList DRAW al TOOLBAR READONLYIF NOT PREV(a IS Adjustment) OR NOT status(a) = AdjustmentStatus.inProgress 
    PROPERTIES(al) EDIT NEWSESSION, DELETE READONLYIF NOT status(a) = AdjustmentStatus.inProgress
    FILTERS adjustment(al) = a
    
    OBJECTS all = AdjustmentListLine
    PROPERTIES(all) READONLY number, note, realQuantity
    PROPERTIES(all) EDIT
    FILTERS adjustment(all) = a,
            product(all) = product(l)
;

DESIGN adjustment {
    details {
        MOVE BOX(al) {
            showIf = useList(a);
            caption = 'Inventories';
        }
        linesTab {
            showIf = useList(a);
            MOVE BOX(all);
        }
    }
}

// change quantity
aggregateRealQuantity(AdjustmentLine l) += useList(adjustment(l));

listRealQuantity (Adjustment, Product, Location) = 
    GROUP SUM realQuantity(AdjustmentListLine l) BY adjustment(adjustmentList(l)), product(l), location(l);
    
WHEN SETCHANGED(listRealQuantity(Adjustment a, Product p, Location l)) AND useList(a) AND NOT lastAdjustmentLine(a, p, l) DO
    NEW al = AdjustmentLine {
        adjustment(al) <- a;
        product(al) <- p;
        location(al) <- l;
        realQuantity(al) <- listRealQuantity(a, p, l);
    } 

WHEN CHANGED(listRealQuantity(Adjustment a, Product p, Location l)) AND useList(a) AND AdjustmentLine al = lastAdjustmentLine(a, p, l) DO
    realQuantity(al) <- listRealQuantity(a, p, l);