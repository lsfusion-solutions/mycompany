MODULE AdjustmentPrint;

REQUIRE AdjustmentCanceled, LocationValuate, DocTemplate;

NAMESPACE Inventory;

company 'Компания' (Adjustment a) = company(location(a));
nameCompany 'Компания' (Adjustment a) = name(company(a));

companyAddress 'Адрес' (Adjustment a) = linesAddress(company(a)); 
companyPhone 'Телефон' (Adjustment a) = phone(company(a));
companyEmail 'Электронная почта' (Adjustment a) = email(company(a));
companyWebsite 'Веб сайт' (Adjustment a) = website(company(a));

FORM printAdjustment 'Результат инвентаризации'
    OBJECTS o = Adjustment PANEL 
    PROPERTIES(o) nameStatus, dateTime, nameType, nameCompany, companyAddress, companyPhone, companyEmail, companyWebsite
    
    OBJECTS l = AdjustmentLine
    PROPERTIES(l) nameLocation, nameProduct, realQuantity, theoreticalQuantity, realAmmount, theoreticalAmmount
    FILTERS adjustment(l) = o
;

CLASS AdjustmentTemplate 'Шаблон инвентаризации';
name 'Название' = DATA ISTRING[100] (AdjustmentTemplate) CHARWIDTH 15;
template 'Имя файла шаблона' = DATA STRING (AdjustmentTemplate) CHARWIDTH 50;

format = DATA TemplateFormat (AdjustmentTemplate);
nameFormat 'Формат' (AdjustmentTemplate t) = name(format(t));

// types
in 'Вкл' = DATA BOOLEAN (AdjustmentTemplate, AdjustmentType);
countAdjustmentTemplate (AdjustmentType tp) = GROUP SUM 1 IF in(AdjustmentTemplate tm, tp);

FORM adjustmentTemplate 'Шаблон инвентаризации'
    OBJECTS o = AdjustmentTemplate PANEL
    PROPERTIES(o) name, template, nameFormat
    
    OBJECTS t = AdjustmentType
    PROPERTIES             in(o, t)
    PROPERTIES(t) READONLY name
    
    EDIT AdjustmentTemplate OBJECT o
;

FORM adjustmentTemplates 'Шаблон инвентаризации'
    OBJECTS o = AdjustmentTemplate
    PROPERTIES(o) READONLY name
    
    LIST AdjustmentTemplate OBJECT o
;

FORM dialogTemplatesAdjustment 'Шаблон инвентаризации'
    OBJECTS itp = AdjustmentType
    
    OBJECTS itm = AdjustmentTemplate
    PROPERTIES(itm) READONLY name
    FILTERS in(itm, itp)
;

EXTEND FORM options
    OBJECTS tmAdjustment = AdjustmentTemplate
    PROPERTIES(tmAdjustment) READONLY name, template, nameFormat
    PROPERTIES(tmAdjustment) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(tmAdjustment);
    }
}

@defineDocTemplateForm(adjustment, a);
