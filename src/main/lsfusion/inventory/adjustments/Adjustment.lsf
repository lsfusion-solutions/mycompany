MODULE Adjustment;

REQUIRE InventorySettings, LocationEmployee, Comments, 
        Product, InvLedger, DateUtils,
        Company, BarCode, Doc;
                                  
NAMESPACE Inventory;

CLASS Adjustment 'Adjustment';

@defineDocStatus(adjustment, 'Adjustment status');

dateTime 'Date' = DATA DATETIME (Adjustment) IN id;
WHEN LOCAL SET(Adjustment a IS Adjustment) AND NOT CHANGED(dateTime(a)) DO dateTime(a) <- currentDateTime();
@defineDateTimeAggregation(Adjustment);

@defineNumber(adjustment, 'Adjustments', 'INV');

numberDate 'Description' (Adjustment a) = number(a) + ' (' + dateTime(a) + ')' IN id;

DESIGN options {
    commons {
        MOVE PROPERTY(nameNumeratorAdjustment());
    }
}

location 'Location' = DATA Location (Adjustment) NONULL;
nameLocation 'Location' (Adjustment a) = name(location(a));
CONSTRAINT SETCHANGED(location(Adjustment o)) AND internalUse(location(o)) CHECKED BY location[Adjustment]
    MESSAGE 'The selected location is for internal use';

// type
CLASS AdjustmentType 'Adjustment type' {
    all 'All',
    category 'By category',
    manual 'Manually'
}
name '{Name}' (AdjustmentType o) = ISTRING[100](staticCaption(o)) IF o IS AdjustmentType CHARWIDTH 15;

FORM adjustmentType 'Adjustment type'
    OBJECTS t = AdjustmentType PANEL
    PROPERTIES(t) name
    
    EDIT AdjustmentType OBJECT t  
;

FORM adjustmentTypes 'Adjustment types'
    OBJECTS t = AdjustmentType
    PROPERTIES(t) READONLY name

    LIST AdjustmentType OBJECT t
;

type 'Type' =  DATA AdjustmentType (Adjustment) NONULL;
nameType 'Type' (Adjustment a) = name(type(a));

category 'Category' = DATA Category (Adjustment);
nameCategory 'Category' (Adjustment a) = name(category(a));

note 'Note' = DATA ISTRING[50] (Adjustment);

// last
INDEX location(Adjustment a), dateTime(a), a;
lastAdjustment (Location l) = GROUP LAST Adjustment a ORDER dateTime(a), a WHERE location(a) = l;
dateTimeLastAdjustment 'Last adjustment date' (Location l) = dateTime(lastAdjustment(l));
statusLastAdjustment (Location l) = status(lastAdjustment(l));
imagedNameStatusLastAdjustment 'Last adjustment status' (Location l) = imagedNameStatus(lastAdjustment(l));
colorStatusLastAdjustment (Location l) = colorStatus(lastAdjustment(l));
nameTypeLastAdjustment 'Last adjustment type' (Location l) = nameType(lastAdjustment(l));

// lines
CLASS AdjustmentLine 'Adjustment line';

adjustment = DATA Adjustment (AdjustmentLine) NONULL DELETE;

index 'â„–' = PARTITION SUM 1 ORDER AdjustmentLine l BY adjustment(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (AdjustmentLine);
nameProduct 'Product' (AdjustmentLine l) = name(product(l));
idBarCodeProduct 'Barcode' (AdjustmentLine l) = idBarCode(product(l));
idProduct '{ID}' (AdjustmentLine l) = id(product(l));
referenceProduct 'Reference' (AdjustmentLine l) = reference(product(l));

uom (AdjustmentLine l) = uom(product(l));
nameUom 'UoM' (AdjustmentLine l) = name(uom(l));

location 'Location' = DATA Location (AdjustmentLine);
nameLocation 'Location' (AdjustmentLine l) = canonicalName(location(l));

WHEN LOCAL SETCHANGED(location(adjustment(AdjustmentLine l))) AND NOT location(l) DO location(l) <- location(adjustment(l));

CONSTRAINT location(AdjustmentLine l) AND NOT level(location(l), location(adjustment(l))) CHECKED BY location[AdjustmentLine]
    MESSAGE 'The adjustment line location is not a descendant of the document location';

lastAdjustmentLine (Adjustment a, Product p, Location l) = 
    GROUP LAST AdjustmentLine al ORDER al BY adjustment(al), product(al), location(al);

theoreticalQuantity 'Theoretical quantity' = DATA NUMERIC[16,3] (AdjustmentLine);
theoreticalQuantity 'Theoretical quantity' (Adjustment a) = GROUP SUM theoreticalQuantity(AdjustmentLine al) IF adjustment(al) = a MATERIALIZED; 

fillTheoreticalQuantity ABSTRACT LIST (AdjustmentLine);
fillTheoreticalQuantity(AdjustmentLine l) + {
    theoreticalQuantity(l) <- prevOnHandB(location(l), product(l), dateTime(adjustment(l)));
}
WHEN LOCAL (SETCHANGED (location(AdjustmentLine l)) OR SETCHANGED(product(l))) 
           AND NOT CHANGED(theoreticalQuantity(l)) DO
    fillTheoreticalQuantity(l); 

// line properties
nameStatus 'Status' (AdjustmentLine l) = nameStatus(adjustment(l)) IN id;
dateTime 'Date' (AdjustmentLine l) = dateTime(adjustment(l)) IN id;
number 'Number' (AdjustmentLine l) = number(adjustment(l)) IN id;

edit (AdjustmentLine l) + { edit(adjustment(l)); } 

FORM adjustment 'Adjustment'
    OBJECTS a = Adjustment PANEL 
    PROPERTIES(a) READONLYIF readonly(a)
                  dateTime, number, 
                  nameLocation, nameType, nameCategory SHOWIF type(a) = AdjustmentType.category,
                  note 
    
    OBJECTS l = AdjustmentLine
    PROPERTIES(l) READONLYIF readonly(a) 
                  index READONLY, 
                  nameProduct, nameUom, idBarCodeProduct, idProduct, referenceProduct,
                  nameLocation, 
                  theoreticalQuantity,
                  NEW, DELETE
    FILTERS adjustment(l) = a
    
    EDIT Adjustment OBJECT a
;

DESIGN adjustment {
    caption = badged('Adjustment', number(a) + '(' + dateTime(a) + ')');
    OBJECTS {
        NEW header {
            NEW headerCenter {
                alignment = STRETCH;
                horizontal = TRUE;
                NEW headerLeft {
                    MOVE PROPERTY(dateTime(a));
                    MOVE PROPERTY(number(a));
                }
                NEW headerRight {
                    MOVE PROPERTY(nameLocation(a))  { notNull = TRUE; }
                    MOVE PROPERTY(nameType(a)) { notNull = TRUE; }           
                    MOVE PROPERTY(nameCategory(a)) { notNull = TRUE; }
                }
            }
            NEW headerBottom {
                alignment = STRETCH;
                MOVE PROPERTY(note(a));           
            }
        }
        NEW details {
            fill = 1;
            tabbed = TRUE;
            NEW lines {
                caption = 'Lines';
                horizontal = TRUE;
                MOVE BOX(l) { caption = ''; }
                NEW linesTab {
                    fill = 0.3;
                    tabbed = TRUE;
                }
            }
        }
    }
}

edit (AdjustmentLine l) + { SHOW adjustment OBJECTS a = adjustment(l), l = l DOCKED; }

FORM adjustments 'Adjustments'
    OBJECTS a = Adjustment
    PROPERTIES(a) READONLY number, dateTime, nameType, nameLocation, nameCategory, note, theoreticalQuantity
    
    PROPERTIES(a) NEWSESSION NEW, EDIT, DELETE DISABLEIF readonly(a)
    
    LIST Adjustment OBJECT a
;

@defineDocObjectsForm(adjustments, a, 'Adjustments');

@defineDocHistory(adjustment, a, product);
@defineDocHistoryLineProperty(adjustment, AdjustmentLine, theoreticalQuantity, 'Theoretical quantity', nameProduct);

@defineDocStatusForm(adjustment, a);
@defineDocLinesCount(adjustment, a);
@defineDocLocationAccess(adjustment, a);

@defineObjectComments(adjustment, a, details);

NAVIGATOR {
    operations {
        NEW adjustments;
    }
}

// fill
fill 'Fill' (Adjustment a) {
    FOR adjustment(AdjustmentLine l) = a DO
        fillTheoreticalQuantity(l);

    FOR (level(Location loc, location(a)) AND onHand(loc, Product p)) AND 
        (level(category(p), category(a)) OR NOT type(a) = AdjustmentType.category) AND 
        NOT type(a) = AdjustmentType.manual AND NOT lastAdjustmentLine(a, p, loc) ORDER name(p), p DO NEW l = AdjustmentLine {
            adjustment(l) <- a;
            product(l) <- p;
            location(l) <- loc;
    }     
}

EXTEND FORM adjustment
    PROPERTIES(a) fill SHOWIF location(a) AND NOT readonly(a)
;

DESIGN adjustment {
    secondaryActions {
        MOVE PROPERTY(fill(a)) { valueClass = 'btn-secondary'; }
    }
}