MODULE ResLedger;

REQUIRE InvLedger;

NAMESPACE Inventory;

CLASS ResLedger 'Регистр резервирования';
class 'Вид' (ResLedger l) = objectClassName(l) IF l IS ResLedger CHARWIDTH 15;

type 'Тип' = ABSTRACT ISTRING[100] (ResLedger) CHARWIDTH 20 MATERIALIZED;

dateTime 'Время' = ABSTRACT DATETIME (ResLedger) MATERIALIZED;
number 'Номер' = ABSTRACT STRING[31] (ResLedger);

location 'Место хранения (откуда)' = ABSTRACT Location (ResLedger) MATERIALIZED;
nameLocation 'Место хранения (откуда)' (ResLedger r) = name(location(r));

product 'Товар' = ABSTRACT Product (ResLedger) MATERIALIZED;
nameProduct 'Товар' (ResLedger r) = name(product(r));

INDEX product(ResLedger l);
INDEX location(ResLedger l), product(l);

reserved 'Зарезервированое кол-во' = ABSTRACT NUMERIC[16,3] (ResLedger) MATERIALIZED;

partner 'Контрагент' = ABSTRACT Partner (ResLedger) MATERIALIZED;
namePartner 'Контрагент' (ResLedger l) = name(partner(l));

reserved 'Зарезервировано' (Location l, Product p) = 
    GROUP SUM reserved(ResLedger sl) IF location(sl) = l 
                                        AND product(sl) = p MATERIALIZED; 

reservedRec 'Зарезервировано (с учетом вложенности)' (Location l, Product p) = 
    GROUP SUM reserved(Location child, p) IF level(child, l) MATERIALIZED;
                                        
available 'Доступно' (Location ll, Product p) = onHand(ll,p) (-) reserved(ll,p);
prevAvailable 'Доступно' (Location ll, Product p) = PREV(available(ll, p)); 

availableRec 'Доступно' (Location ll, Product p) = onHandRec(ll,p) (-) reservedRec(ll,p);
prevAvailableRec 'Доступно' (Location ll, Product p) = PREV(availableRec(ll, p)); 

CONSTRAINT SET(available (Location ll, Product p) < 0) AND recPositiveOnHand(ll) 
            MESSAGE 'Доступный остаток не может быть отрицательным';