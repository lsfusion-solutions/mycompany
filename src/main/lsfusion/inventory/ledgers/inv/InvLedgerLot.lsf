MODULE InvLedgerLot;

REQUIRE InvLedger, InvLedgerItem, ProductLot;

NAMESPACE Inventory;
                                                                                        
quantity 'Qty' = ABSTRACT NUMERIC[16,3] (InvLedger, Lot) MATERIALIZED;
quantityLot 'Number of lots' (InvLedger rl) = GROUP SUM quantity(rl, Lot l);

inQuantity 'Received' (Location ll, Lot l) = GROUP SUM quantity(InvLedger il, Lot lt) 
                                                    IF active(il) AND toLocation(il) = ll AND lt = l MATERIALIZED;
                                                    
inQuantity 'Received' (Location ll, Lot l, DATETIME dt) = inQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt)  IF active(il) AND toLocation(il) = ll AND lt = l AND dateTime(il) > dt;
    
inQuantityA (Location ll, Lot l, DATETIME dt) = inQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt)  IF active(il) AND toLocation(il) = ll AND lt = l AND dateTime(il) > dt;
    
inQuantityB (Location ll, Lot l, DATETIME dt) = inQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt)  IF active(il) AND toLocation(il) = ll AND lt = l AND dateTime(il) >= dt;
                                   
outQuantity 'Quantity (sent)' (Location ll, Lot l) = GROUP SUM quantity(InvLedger il, Lot lt) 
                                                    IF active(il) AND fromLocation(il) = ll AND lt = l MATERIALIZED;

outQuantity 'Quantity (sent)' (Location ll, Lot l, DATETIME dt) = outQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt) IF active(il) AND  fromLocation(il) = ll AND lt = l AND dateTime(il) > dt;

outQuantityA 'Quantity (sent)' (Location ll, Lot l, DATETIME dt) = outQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt) IF active(il) AND  fromLocation(il) = ll AND lt = l AND dateTime(il) > dt;
    
outQuantityB 'Quantity (sent)' (Location ll, Lot l, DATETIME dt) = outQuantity(ll, l) (-)
    GROUP SUM quantity(InvLedger il, Lot lt) IF active(il) AND  fromLocation(il) = ll AND lt = l AND dateTime(il) >= dt;

onHand 'On hand' (Location ll, Lot l) = inQuantity(ll, l) (-) outQuantity(ll, l) MATERIALIZED;
prevOnHand (Location ll, Lot l) = PREV(onHand(ll, l));

onHand 'On hand' (Lot l) = GROUP SUM onHand(Location ll, l);

minLocationOnHand (Lot l) = GROUP MIN Location ll IF onHand(ll, l) > 0;
prevMinLocationOnHand (Lot l) = PREV(minLocationOnHand(l));

locations 'Locations' (Lot l) = GROUP CONCAT name(Location ll) IF onHand(ll, l) > 0, ', ' ORDER ll;

onHandLot 'On hand (lots)' (Location ll, Product p) = GROUP SUM onHand(ll, Lot l) IF product(l) = p;

onHand 'On hand' (Location ll, Lot l, DATETIME dt) = 
                                                            inQuantity(ll, l, dt) (-) outQuantity(ll, l, dt) IF dt IS DATETIME;

onHandLot 'On hand (lots)' (Location ll, Product p, DATETIME dt) = GROUP SUM onHand(ll, Lot l, dt) IF product(l) = p;                                                            

onHandA 'On hand' (Location ll, Lot l, DATETIME dt) = 
                                                            inQuantityA(ll, l, dt) (-) outQuantityA(ll, l, dt) IF dt IS DATETIME;
prevOnHandA (Location ll, Lot l, DATETIME dt) = PREV(onHandA(ll, l, dt));

onHandB 'On hand' (Location ll, Lot l, DATETIME dt) = 
                                                            inQuantityB(ll, l, dt) (-) outQuantityB(ll, l, dt) IF dt IS DATETIME;
prevOnHandB (Location ll, Lot l, DATETIME dt) = PREV(onHandB(ll, l, dt));

FORM invLedgerItemLot 'Lots'
    OBJECTS r = InvLedger
    PROPERTIES (r) READONLY nameProduct PANEL 
        
    OBJECTS lot = Lot  
    PROPERTIES (lot) id
    PROPERTIES (r, lot) READONLY quantity

    FILTERS quantity(r, lot)
;


changelots(InvLedger rl) { 
    IF useLot(product(rl)) THEN 
        DIALOG invLedgerItemLot OBJECTS r = rl;
}

EXTEND FORM item
    PROPERTIES(inv) AFTER quantity(inv) SHOWIF useLot(product(inv))  quantityLot     
    
    OBJECTS lotI = Lot  
    PROPERTIES(lotI) READONLY      id 
    PROPERTIES(inv, lotI) READONLY quantity
    PROPERTIES(lotI) NEWSESSION    EDIT
    FILTERS quantity(inv, lotI)
    
    PROPERTIES AFTER onHand(l, i) SHOWIF useLot(i) onHandLot(l, i)     

    OBJECTS lotH = Lot
    PROPERTIES(lotH) READONLY    id
    PROPERTIES(l, lotH) READONLY onHand
    PROPERTIES(lotH) NEWSESSION  EDIT
    FILTERS onHand(l, lotH) AND product(lotH) = i
;

DESIGN item {
    invledger {
        MOVE BOX (lotI) {
            fill = 0.4; 
            showIf = useLot(i);
        }
    }
    lines {
        MOVE BOX(lotH){
            fill = 0.4; 
            showIf = useLot(i);
        }
    }
}

EXTEND FORM lot
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, namePartner,
                            nameFromLocation, onHandAFromLocation,
                            nameToLocation, onHandAToLocation
    PROPERTIES(il, l) READONLY quantity
    PROPERTIES(il) NEWSESSION EDIT
    FILTERGROUP activeInv
        FILTER '{Active}' active(il) DEFAULT
    FILTERS quantity(il, l)
;

DESIGN lot {
    tabs {
        MOVE BOX(il) { caption = 'Movement'; }
    }
}

EXTEND FORM lots
    PROPERTIES(l) READONLY onHand
    
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, namePartner,
                            nameFromLocation, onHandAFromLocation,
                            nameToLocation, onHandAToLocation
    PROPERTIES(il, l) READONLY quantity
    PROPERTIES(il) NEWSESSION EDIT
    FILTERGROUP activeInv
        FILTER '{Active}' active(il) DEFAULT
    FILTERS quantity(il, l)
;

DESIGN lots {
    BOX(il) { 
        caption = 'Movement';
        fill = 0.3; 
    }
}