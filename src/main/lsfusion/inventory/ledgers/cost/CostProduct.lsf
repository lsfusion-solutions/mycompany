MODULE CostProduct;

REQUIRE Product;

NAMESPACE MasterData;

CLASS CostingMethod 'Методы расчета себестоимости' {
    standardPrice 'Плановая себестоимость',
    avco 'Средняя себестоимость',
    fifo 'FIFO'
}

name 'Метод расчета себестоимости' (CostingMethod o) = staticCaption(o) IF o IS CostingMethod CHARWIDTH 10;

FORM costingMethods 'Методы расчета себестоимости'
    OBJECTS c = CostingMethod
    PROPERTIES(c) READONLY 'Методы' = name
    
    LIST CostingMethod OBJECT c
;

dataCostingMethod = DATA CostingMethod (Category);
nameDataCostingMethod 'Метод расчета себестоимости' (Category c) = name(dataCostingMethod(c));

costingMethod 'Методы расчета себестоимости' (Category c) = 
        GROUP LAST dataCostingMethod(Category parent) ORDER DESC level(c, parent) MATERIALIZED;

costingMethod (Product p) = OVERRIDE costingMethod(category(p AS Product)), (CostingMethod.fifo IF p IS Product) MATERIALIZED;
nameCostingMethod 'Метод расчета себестоимости' (Product p) = name(costingMethod(p));

EXTEND FORM category
    PROPERTIES(c) nameDataCostingMethod
;
DESIGN category {
    params {
        MOVE PROPERTY(nameDataCostingMethod(c));
    }
}