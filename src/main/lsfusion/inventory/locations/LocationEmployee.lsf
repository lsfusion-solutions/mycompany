MODULE LocationEmployee;

REQUIRE Location, Employee;

NAMESPACE Inventory;

hasAccess 'Доступен' = DATA BOOLEAN (Employee, Location); 

hasParentAccess (Employee e, Location l) = GROUP LAST hasAccess(e, Location pl) ORDER DESC level(l, pl); 

countLocationAccess (Employee e) = GROUP SUM 1 IF hasAccess(e, Location l) MATERIALIZED PREREAD;

accessGranted (Employee e, Location l) = hasParentAccess(e, l) OR NOT countLocationAccess(e);

accessGranted (Location l) = l IS Location AND accessGranted(currentUser(), l);
accessDenied (Location l) = l IS Location AND currentUser() IS Employee AND NOT accessGranted(l);

firstLocation() = GROUP LAST Location l ORDER DESC l WHERE accessGranted(l); 
countLocation() = GROUP SUM 1 IF accessGranted(Location l);

EXTEND FORM employee
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES(lt) READONLY name, id
    PROPERTIES hasAccess(e, lt) BACKGROUND (GROUP SUM 1 IF hasAccess(e, Location cl) AND level(cl, lt)) 
    ORDERS name(lt)
;

DESIGN employee {
    details {
        MOVE BOX(TREE locations) {
            caption = 'Места хранения';
            PROPERTY(hasAccess(e, lt)) {
                width = 40;
            }
        }
    }
}

META defineDocLocationAccess(doc, let, prefix)
    WHEN LOCAL SET(###doc o IS ###doc) AND NOT CHANGED(prefix###location(o)) AND countLocation() = 1 DO
        prefix###location(o) <- firstLocation();

    CONSTRAINT SETCHANGED(prefix###location(###doc o)) AND accessDenied(prefix###location(o)) CHECKED BY prefix###location[###doc]
               MESSAGE 'У текущего пользователя нет прав доступа к выбранному месту хранения';
               
    EXTEND FORM doc##s
        FILTERS accessGranted(prefix###location(let)) OR NOT prefix###location(let) 
    ; 
END

META defineDocLocationAccess(doc, let)
    @defineDocLocationAccess(doc, let, );
END