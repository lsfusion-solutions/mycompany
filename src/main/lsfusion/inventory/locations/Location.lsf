MODULE Location;

REQUIRE InventorySettings, Company, MetaNumerator, Selection;

NAMESPACE Inventory;

CLASS Location 'Место хранения';

@defineID(location, 'Места хранения', 'МХ', 4);

name 'Название' = DATA ISTRING[30] (Location) IN id NONULL CHARWIDTH 15;

// hierarchy
parent 'Родитель' = DATA Location (Location);
idParent 'Код родителя' (Location l) = id(parent(l));
nameParent 'Родитель' (Location l) = name(parent(l));

CONSTRAINT DROPPED(Location l IS Location) AND PREV(parent(Location cl) = l)
        MESSAGE 'Нельзя удалить место хранения имеющее подчиненные склады';

level 'Уровень' (Location child, Location parent) =
   RECURSION 1l IF child IS Location AND parent = child
        STEP 2l IF parent = parent($parent) MATERIALIZED;

isChild (Location child, Location parent) = TRUE IF level(child, parent);

countChildren (Location parent) = GROUP SUM 1 IF parent(Location child) = parent;

canonicalName 'Каноническое имя' (Location l) =
   GROUP CONCAT name(Location parent), ' / ' ORDER DESC level(l, parent) CHARWIDTH 20 IN id;
canonicalNameParent 'Родитель' (Location l) = canonicalName(parent(l));

// company
company 'Владелец' = DATA Company (Location); 
idCompany 'Код владельца' (Location l) = id(company(l)); 
nameCompany 'Владелец' (Location l) = name(company(l)); 

WHEN LOCAL SET(Location l IS Location) AND NOT CHANGED(company(l)) DO company(l) <- defaultCompany();

// address
address 'Адрес' = DATA ISTRING[50] (Location);
city 'Город' = DATA ISTRING[30] (Location);
state 'Область' = DATA ISTRING[30] (Location);
zip 'Почтовый индекс' = DATA ISTRING[10] (Location);

hierarchyAddress (Location l) = GROUP LAST address(Location parent) ORDER DESC level(l,parent);
hierarchyCity (Location l) = GROUP LAST city(Location parent) ORDER DESC level(l,parent);
hierarchyState  (Location l) = GROUP LAST state(Location parent) ORDER DESC level(l,parent);
hierarchyZip  (Location l) = GROUP LAST zip(Location parent) ORDER DESC level(l,parent);

csvAddress 'Адрес' (Location l) = CONCAT ', ', hierarchyAddress(l), hierarchyCity(l), hierarchyState(l), hierarchyZip(l);
linesAddress 'Адрес' (Location l) = CONCAT '\n', hierarchyAddress(l), hierarchyCity(l), hierarchyState(l), hierarchyZip(l);
  
FORM location 'Место хранения'
    OBJECTS l = Location PANEL
    PROPERTIES(l) name, id, 
                  canonicalNameParent, nameCompany,
                  address, city, state, zip
     
    EDIT Location OBJECT l  
;

DESIGN location {
    OBJECTS {
        NEW pane {
            NEW id {
                type = CONTAINERH;
                MOVE PROPERTY(name(l));
                MOVE PROPERTY(id(l));
            }
            MOVE PROPERTY(canonicalNameParent(l));
            MOVE PROPERTY(nameCompany(l));
            NEW address {
                type = COLUMNS;
                columns = 2;
                MOVE PROPERTY(address(l));
                MOVE PROPERTY(city(l));
                MOVE PROPERTY(state(l));
                MOVE PROPERTY(zip(l));
            }
            NEW params {
                type = COLUMNS;
                columns = 2;
            }
            NEW tabbedPane {
                type = TABBED;
                fill = 1;
            }
        }
    }
}

background (Location l) = ABSTRACT CASE COLOR (Location);
FORM locations 'Места хранения'
    OBJECTS l = Location BACKGROUND background(l)
    PROPERTIES(l) READONLY name, id, 
                           canonicalNameParent, nameCompany,
                           address, city, state, zip
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE
    
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES(lt) READONLY name, id, canonicalNameParent, nameCompany, address, city, state, zip
    PROPERTIES(lt) NEWSESSION NEW, EDIT, DELETE
    ORDERS name(lt)
    
    LIST Location OBJECT l
    
    EVENTS ON CHANGE lt { SEEK locations.l = lt; }
;

DESIGN locations {
    OBJECTS {
        type = TABBED;
        NEW list {
            caption = 'Список';
            MOVE BOX(l);
        }
        NEW tree {
            caption = 'Дерево';
            MOVE BOX(TREE locations);
        }
    }
}

NAVIGATOR {
    settings {
        NEW locations;
    }
}