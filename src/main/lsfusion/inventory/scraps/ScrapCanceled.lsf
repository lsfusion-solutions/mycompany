MODULE ScrapCanceled;

REQUIRE ScrapDone;

NAMESPACE Inventory;
    
EXTEND CLASS ScrapStatus {
    canceled 'Canceled'
}
canceled 'Canceled' = DATA BOOLEAN (Scrap);
active '{Active}' (Scrap s) = done(s) AND NOT canceled(s);

status(Scrap s) += WHEN canceled(s) THEN ScrapStatus.canceled;
color(ScrapStatus s) += WHEN s = ScrapStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Scrap s) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM scrap
    PROPERTIES(s) cancel SHOWIF NOT (status(s) = ScrapStatus.draft OR status(s) = ScrapStatus.canceled), canceled 
;

DESIGN scrap {
    secondaryActions {
        MOVE PROPERTY(cancel(s)) { valueClass = 'btn-danger'; }
    }             
    statusGroup {
        MOVE PROPERTY(canceled(s)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }                               
    }    
}

EXTEND FORM scraps    
    EXTEND FILTERGROUP status
        FILTER 'Canceled' status(s) = ScrapStatus.canceled    
;
