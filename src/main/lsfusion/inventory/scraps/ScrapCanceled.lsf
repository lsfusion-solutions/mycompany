MODULE ScrapCanceled;

REQUIRE ScrapDone;

NAMESPACE Inventory;
    
EXTEND CLASS ScrapStatus {
    canceled 'Cancelled'
}
canceled 'Cancelled' = DATA BOOLEAN (Scrap);
active '{Active}' (Scrap s) = done(s) AND NOT canceled(s);

status(Scrap s) += WHEN canceled(s) THEN ScrapStatus.canceled;
color(ScrapStatus s) += WHEN s = ScrapStatus.canceled THEN RGB(181,195,247);

cancel 'Cancel' (Scrap s) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(s) <- TRUE;
        APPLY;
    }
}

EXTEND FORM scrap
    PROPERTIES(s) cancel SHOWIF NOT (status(s) = ScrapStatus.draft OR status(s) = ScrapStatus.canceled), canceled 
;

DESIGN scrap {
    secondaryActions {
        MOVE PROPERTY(cancel(s)) { valueClass = 'btn-danger'; }
    }             
    status {
        MOVE PROPERTY(canceled(s));                               
    }    
}

EXTEND FORM scraps    
    EXTEND FILTERGROUP status
        FILTER 'Cancelled' status(s) = ScrapStatus.canceled    
;
