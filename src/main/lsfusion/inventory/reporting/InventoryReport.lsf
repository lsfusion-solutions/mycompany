MODULE InventoryReport;

REQUIRE InvLedger, ResLedger, ItemImage, ItemAttribute, LocationEmployee, BarCode;

NAMESPACE Inventory;

singleLocation 'Single location' = DATA LOCAL BOOLEAN () PREREAD;
                                
FORM inventoryReport 'Inventory report'
    OBJECTS dt = DATETIME PANEL NULL
    PROPERTIES(dt) dateTime 'Select date' = VALUE
    PROPERTIES singleLocation()
    
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES READONLY name(lt)
    ORDERS name(lt)
    FILTERS accessChildGranted(lt)
    
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT
    
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Location' = canonicalName(l) SHOWIF NOT singleLocation() 
    PROPERTIES(p) READONLY 'Product' = name, nameUom, idBarCode, id, reference
    PROPERTIES(l, p) READONLY expected, reserved, available
    PROPERTIES(l, p) SHOWIF NOT dt IS DATETIME READONLY onHand, onHandRec
    PROPERTIES(l, p, dt) SHOWIF dt IS DATETIME READONLY onHandA 
    PROPERTIES(l, p) READONLY dateReceived, dateShipped
    PROPERTIES(p) READONLY image PANEL
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS canonicalName(l), name(p) 
    FILTERS IF singleLocation() THEN l = lt ELSE isChild(l, lt),
            level(category(p), c),
            accessGranted(l)

    FILTERGROUP rowFilter
        FILTER 'On hand'
            IF dt IS DATETIME THEN onHandA(l, p, dt) ELSE 
                (IF singleLocation() THEN onHandRec(l, p) ELSE onHand(l, p)) DEFAULT
        FILTER '{Received}' dateReceived(l, p)
        FILTER 'Expected' expected(l, p)
        FILTER 'Reserved' reserved(l, p)
        FILTER 'Available' available(l, p)
                
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, namePartner,
                            nameFromLocation, onHandAFromLocation, 
                            nameToLocation, onHandAToLocation,
                            quantity
    PROPERTIES(il) NEWSESSION EDIT
    FILTERGROUP activeInv
        FILTER '{Active}' active(il) DEFAULT
    FILTERS product(il) = p,
            IF singleLocation() THEN (level(fromLocation(il), l) OR level(toLocation(il), l))
                                ELSE (fromLocation(il) = l OR toLocation(il) = l),
            dateTime(il) <= dt OR NOT dt

    OBJECTS rl = ResLedger
    PROPERTIES(rl) READONLY class, type, dateTime, number, 
                            namePartner, nameLocation, 
                            reserved
    PROPERTIES(rl) NEWSESSION EDIT
    FILTERS product(rl) = p,
            IF singleLocation() THEN (TRUE IF level(location(rl), l))
                                ELSE location(rl) = l,
            dateTime(il) <= dt OR NOT dt
            
    OBJECTS a = ItemAttribute
    OBJECTS rlp = (ll = Location, rp = Product) PIVOT
    PROPERTIES READONLY 'Location' = canonicalName(ll), 'Product' = name(rp), value(rp, a) COLUMNS (a) HEADER name(a),
                        level1(rp), level2(rp), level3(rp), level4(rp), 'Canonical group' = canonicalNameCategory(rp)
    PROPERTIES(ll, rp) READONLY dateReceived, dateShipped
    PROPERTIES READONLY MEASURE expected(ll, rp), reserved(ll, rp), available(ll, rp), onHand 'On hand' = IF dt IS DATETIME THEN onHandA(ll, rp, dt) ELSE onHand(ll, rp)
    FILTERS IF dt IS DATETIME THEN onHandA(ll, rp, dt) ELSE onHand(ll, rp),
            accessGranted(ll)
;

DESIGN inventoryReport {
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            MOVE PROPERTY (dateTime);
            MOVE PROPERTY (singleLocation());
        }
        NEW invReport {
            fill = 1;
            tabbed = TRUE;
            NEW pane {
                caption = 'Inventory report';
                horizontal = TRUE;
                fill = 1;
                NEW filterTabs {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(TREE locations) { caption = 'Locations'; }
                    MOVE BOX(TREE categories) { caption = 'Categories'; }
                }
                NEW pane2 {
                    fill = 3;
                    MOVE BOX(lp) { caption = ''; }
                    NEW tabs {
                        tabbed = TRUE;
                        fill = 1;
                        MOVE BOX(il) { caption = 'Inventory ledger'; }
                        MOVE BOX(rl) { caption = 'Reservation ledger'; }
                        NEW image { 
                            caption = 'Picture';
                            MOVE PROPERTY(image(p)) { size = (100, 100); caption = ''; fill = 1; } 
                        }
                    }
                }               
            }
            MOVE BOX (rlp) { caption = '{Pivot}'; }
        }
    }
}

@defineBarCodeSeek(inventoryReport, p, lp);

@defineItemAttributeForm(inventoryReport, p, lp);

DESIGN inventoryReport {
    filterTabs {
        MOVE BOX(ia) { caption = 'Attributes'; }
    }
}

NAVIGATOR {
    reporting {
        NEW inventoryReport;
    }
}