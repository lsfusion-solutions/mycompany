MODULE InventoryReport;

REQUIRE InvLedger, ResLedger, ItemImage, ItemAttribute, LocationEmployee, BarCode;

NAMESPACE Inventory;

selectedDateTime 'Select date' = DATA LOCAL DATETIME () PREREAD;
singleLocation 'Single location' = DATA LOCAL BOOLEAN () PREREAD;
onHandDateTime 'Balance as of date' (Location l, Product p) = onHandA(l, p, selectedDateTime());
                                
FORM inventoryReport 'Inventory report'
    PROPERTIES selectedDateTime(), singleLocation()
    
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES READONLY name(lt)
    ORDERS name(lt)
    
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)
    
    OBJECTS lp = (l = Location, p = Product)
    PROPERTIES READONLY 'Location' = canonicalName(l) SHOWIF NOT singleLocation() 
    PROPERTIES(p) READONLY 'Product' = name, nameUom, idBarCode, id, reference
    PROPERTIES(l, p) READONLY reserved, available
    PROPERTIES(l, p) READONLY SHOWIF NOT selectedDateTime() onHand, onHandRec
    PROPERTIES(l, p) READONLY SHOWIF selectedDateTime() onHandDateTime 
    PROPERTIES(l, p) READONLY dateReceived, dateShipped
    PROPERTIES(p) READONLY image PANEL
    PROPERTIES(p) NEWSESSION EDIT
    ORDERS canonicalName(l), name(p) 
    FILTERS IF singleLocation() THEN l = lt ELSE isChild(l, lt),
            level(category(p), c),
            accessGranted(l)

    FILTERGROUP rowFilter
        FILTER 'On hand'
            IF selectedDateTime() THEN onHandDateTime(l, p) ELSE 
                (IF singleLocation() THEN onHandRec(l, p) ELSE onHand(l, p)) DEFAULT
        FILTER '{Received}' dateReceived(l, p)
                
    OBJECTS il = InvLedger
    PROPERTIES(il) READONLY class, type, dateTime, number, namePartner,
                            nameFromLocation, onHandAFromLocation, 
                            nameToLocation, onHandAToLocation,
                            quantity
    PROPERTIES(il) NEWSESSION EDIT
    FILTERGROUP activeInv
        FILTER '{Active}' active(il) DEFAULT
    FILTERS product(il) = p,
            IF singleLocation() THEN (level(fromLocation(il), l) OR level(toLocation(il), l))
                                ELSE (fromLocation(il) = l OR toLocation(il) = l),
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()

    OBJECTS rl = ResLedger
    PROPERTIES(rl) READONLY class, type, dateTime, number, 
                            namePartner, nameLocation, 
                            reserved
    PROPERTIES(rl) NEWSESSION EDIT
    FILTERS product(rl) = p,
            IF singleLocation() THEN (TRUE IF level(location(rl), l))
                                ELSE location(rl) = l,
            dateTime(il) <= selectedDateTime() OR NOT selectedDateTime()
            
    OBJECTS a = ItemAttribute
    OBJECTS rlp = (ll = Location, rp = Product) PIVOT
    PROPERTIES READONLY 'Location' = canonicalName(ll), 'Product' = name(rp), value(rp, a) COLUMNS (a) HEADER name(a),
                        level1(rp), level2(rp), level3(rp), level4(rp), 'Canonical group' = canonicalNameCategory(rp)
    PROPERTIES(ll, rp) READONLY dateReceived, dateShipped
    PROPERTIES READONLY MEASURE reserved(ll, rp), available(ll, rp), onHand 'On hand' = IF selectedDateTime() THEN onHandDateTime(ll, rp) ELSE onHand(ll, rp)
    FILTERS IF selectedDateTime() THEN onHandDateTime(ll, rp) ELSE onHand(ll, rp),
            accessGranted(ll)
;

DESIGN inventoryReport {
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            MOVE PROPERTY (selectedDateTime());
            MOVE PROPERTY (singleLocation());
        }
        NEW invReport {
            fill = 1;
            tabbed = TRUE;
            NEW pane {
                caption = 'Inventory report';
                type = SPLITH;
                fill = 1;
                NEW filterTabs {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(TREE locations) { caption = 'Locations'; }
                    MOVE BOX(TREE categories) { caption = 'Categories'; }
                }
                NEW pane2 {
                    fill = 3;
                    type = SPLITV;
                    MOVE BOX(lp) { caption = ''; }
                    NEW tabs {
                        tabbed = TRUE;
                        MOVE BOX(il) { caption = 'Inventory ledger'; }
                        MOVE BOX(rl) { caption = 'Reservation ledger'; }
                        NEW image { 
                            caption = 'Picture';
                            MOVE PROPERTY(image(p)) { caption = ''; fill = 1; } 
                        }
                    }
                }               
            }
            MOVE BOX (rlp) { caption = '{Pivot}'; }
        }
    }
}

@defineBarCodeSeek(inventoryReport, p, lp);

//@defineItemAttributeForm(inventoryReport, p, lp){
//    hasAttributeFilter (ItemAttribute ia, ItemAttributeValue v) = GROUP SUM 1 IF attribute(Item i, ia) = v AND [FILTER inventoryReport.p](i);
//    EXTEND FORM inventoryReport
//        OBJECTS ia = ItemAttribute BEFORE lp
//        PROPERTIES name(ia) READONLY, 
//                   value(ia) ON CHANGE { 
//                        DIALOG childValues OBJECTS a = ia, c = c, v = attributeFilter(ia) CHANGE LIST value(v) FILTERS hasAttributeFilter(ia, v);
//                   }
//        FILTERS NOT notMatchAttributeFilter(p)
//        FILTERS GROUP SUM 1 IF set(ia, Category cc) AND (level(cc, c) OR level(c, cc))
//    ;
//};
//DESIGN inventoryReport {
//    filterTabs {
//        MOVE BOX(ia) { caption = 'Attributes'; }
//    }
//}

NAVIGATOR {
    reporting {
        NEW inventoryReport;
    }
}