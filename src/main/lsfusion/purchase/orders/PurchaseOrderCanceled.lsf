MODULE PurchaseOrderCanceled;

REQUIRE PurchaseOrder, PurchaseOrderSelection;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus {
    canceled 'Cancelled'
}
canceled 'Cancelled' = DATA BOOLEAN (Order);
canceled 'Cancelled' (OrderLine l) = canceled(order(l));

status(Order o) += WHEN canceled(o) THEN OrderStatus.canceled;
color(OrderStatus o) += WHEN o = OrderStatus.canceled THEN RGB(252,247,149);

cancel 'Cancel' (Order o) {
    APPLY; 
    IF canceled() THEN RETURN;
           
    NEWSESSION {
        canceled(o) <- TRUE;
        APPLY;
    }    
}

EXTEND FORM order
     PROPERTIES(o) cancel SHOWIF (NOT status(o) = OrderStatus.draft AND NOT status(o) = OrderStatus.canceled), canceled
;

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(cancel(o)) { valueClass = 'btn-danger'; }
    }
    status {
        MOVE PROPERTY(canceled(o));    
    }
}

EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Cancelled' status(o) = OrderStatus.canceled    
;

@defineSelectionAction(orders, Order, cancel, 'Cancel', statuses);