MODULE PurchaseOrderConfirmed;

REQUIRE PurchaseOrderSent;

NAMESPACE Purchase;

EXTEND CLASS OrderStatus {
    confirmed 'Подтвержден'
}
confirmed 'Подтвержден' = DATA BOOLEAN (Order);

status(Order o) += WHEN confirmed(o) THEN OrderStatus.confirmed;
colorStatus(Order o) += WHEN status(o) = OrderStatus.confirmed THEN RGB(150,255,150);

confirm 'Подтвердить' (Order o){
    APPLY; 
    IF canceled() THEN RETURN;
    
    NEWSESSION {
        confirmed(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM order 
     PROPERTIES(o) confirmDraft = confirm SHOWIF (status(o) = OrderStatus.draft),
                   confirmSent = confirm SHOWIF (status(o) = OrderStatus.sent),
                   confirmed
;

DESIGN order {
    primaryActions {
        MOVE PROPERTY(confirmSent) { fontStyle = 'bold'; };
        MOVE PROPERTY(confirmDraft) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(confirmed(o));
    }
}


EXTEND FORM orders    
    EXTEND FILTERGROUP status
        FILTER 'Подтвержден' status(o) = OrderStatus.confirmed    
;

@defineSelectionAction(orders, Order, confirm, 'Подтвердить', statuses);