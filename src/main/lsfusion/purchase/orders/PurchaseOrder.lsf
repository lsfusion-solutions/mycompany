MODULE PurchaseOrder;

REQUIRE Time, MetaNumerator, Employee, Company, Currency, 
        FileUtils, Icon, Comments, Activity,
        MetaTax, TaxItem, BarCode, PurchaseOrderType,
        PurchaseSettings, PartnerPurchase, 
        ItemSales, ItemCost,
        PaymentTermsPurchase, Doc, DateUtils,
        DebtPartner, LocationEmployee;  

NAMESPACE Purchase;

CLASS Order 'Purchase order';

@defineDocStatus(order, 'Purchase order status');

@definePropertyObject(order);
@definePropertyValue(order, status, 'Status', nameStatus);

// type
type 'Type' = DATA OrderType (Order) NONULL;
nameType 'Type' (Order s) = name(type(s));
@definePropertyValue(order, type, 'Type', nameType);

WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(type(o)) DO type(o) <- defaultOrderType(); 

dateTime 'Date'  = DATA DATETIME (Order) IN id;
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(dateTime(o)) DO { dateTime(o) <- currentDateTime();}
@defineDateTimeAggregation(Order);
@definePropertyValue(order, date, 'Date');

@defineNumberType(order);
@definePropertyValue(order, number, 'Number');

numberDate 'Description' (Order o) = number(o) + ' (' + dateTime(o) + ')' IN id;

vendor 'Vendor' = DATA Partner (Order) NONULL;
nameVendor 'Vendor' (Order o) = name(vendor(o));
@definePropertyValue(order, vendor, 'Vendor', nameVendor);

numberDateVendor (Order o) = CONCAT ' ', numberDate(o), '(' + nameVendor(o) + ')';

CONSTRAINT vendor(Order o) AND NOT isVendor(vendor(o))
                CHECKED BY vendor[Order]
                MESSAGE 'The partner of the purchase order  must be a vendor';

addressCsvVendor 'Vendor address' (Order o) = addressCsv(vendor(o));

// Invoicing information
paymentTerms = DATA PaymentTerm (Order);
namePaymentTerms 'Payment terms' (Order o) = name(paymentTerms(o));

WHEN LOCAL CHANGED(vendor(Order o)) AND NOT CHANGED(paymentTerms(o)) DO paymentTerms(o) <- vendorPaymentTerms(vendor(o)); 

// Shipping information
scheduledDateTime 'Scheduled date' = DATA DATETIME (Order) IN id;
scheduledDateTime(Order o) <- currentDateTime() WHEN SET(o IS Order);
@definePropertyValue(order, scheduledDateTime, 'Scheduled date');

// company
company 'Company' = DATA Company (Order) NONULL;
nameCompany 'Company' (Order o) = name(company(o));
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(company(o)) DO company(o) <- defaultCompany();
@definePropertyValue(order, company, 'Company', nameCompany);

// location
location = DATA Location (Order);
nameLocation 'Location' (Order o) = name(location(o));
CONSTRAINT SETCHANGED(location(Order o)) AND internalUse(location(o)) CHECKED BY location[Order]
    MESSAGE 'The selected location is for internal use';
@definePropertyValue(order, location, 'Location', nameLocation);

// currency
currency = DATA Currency (Order);
nameCurrency 'Currency' (Order o) = name(currency(o));
@definePropertyValue(order, currency, 'Currency', nameCurrency);

WHEN LOCAL SETCHANGED(type(Order o)) AND currency(type(o)) AND NOT CHANGED(currency(o)) DO currency(o) <- currency(type(o));  

// Purchase information
representative 'Our representative' = DATA Employee (Order);
nameRepresentative 'Our representative' (Order o) = MasterData.name(representative(o));
@definePropertyValue(order, representative, 'Our representative', nameRepresentative);

representative(Order o) <- currentUser() IF currentUser() IS Employee WHEN SET(o IS Order);

vendorReference 'Vendor reference' = DATA STRING[30] (Order) INDEXED;
@definePropertyValue(order, vendorReference, 'Vendor reference');

note 'Note' = DATA ISTRING (Order) CHARWIDTH 20;

details 'Details' = DATA RICHTEXT (Order);

// Lines
CLASS OrderLine 'Order line';
order =  DATA Order (OrderLine) NONULL  DELETE;

index 'â„–' = PARTITION SUM 1 ORDER OrderLine l BY order(l) IN id MATERIALIZED CHARWIDTH 3;

item = DATA Item (OrderLine) NONULL;
nameItem '{Item}' (OrderLine l) = name(item(l));
idBarCodeItem 'Barcode' (OrderLine l) = idBarCode(item(l));
idItem '{ID}' (OrderLine l) = id(item(l));
referenceItem 'Reference' (OrderLine l) = reference(item(l));

nameCategoryItem 'Category' (OrderLine l) = nameCategory(item(l));
level1Item 'Category 1' (OrderLine l) = level1(item(l));
level2Item 'Category 2' (OrderLine l) = level2(item(l));
level3Item 'Category 3' (OrderLine l) = level3(item(l));
level4Item 'Category 4' (OrderLine l) = level4(item(l));

CONSTRAINT item(OrderLine l) AND NOT canBePurchased(item(l))
            CHECKED BY item[OrderLine] 
            MESSAGE 'The item is not for purchase';

description 'Description' = DATA ISTRING (OrderLine);

uom (OrderLine l) = uom(item(l));
nameUom 'UoM' (OrderLine l) = name(uom(l));

quantity '{Quantity}' = DATA NUMERIC[16,3] (OrderLine);

price 'Price' = DATA NUMERIC[10,2] (OrderLine);

defaultPrice = ABSTRACT CASE NUMERIC[10,2] (OrderLine);
WHEN LOCAL SETCHANGED(item(OrderLine l)) AND NOT CHANGED(price(l)) DO price(l) <- defaultPrice(l);

defaultPrice (OrderLine l) += WHEN cost(item(l), DATE(dateTime(order(l)))) THEN cost(item(l)); 

@defineTaxCalc(order, o, purchase);
taxIncluded (OrderLine l) += taxIncluded(type(order(l)));

WHEN LOCAL CHANGED (item(OrderLine ol)) DO {
    in(ol, Tax t) <- purchaseIn(item(ol),t); 
}

// Line properties
imagedNameStatus 'Status' (OrderLine l) = imagedNameStatus(order(l)); 
type (OrderLine l) = type(order(l)); 

dateTime 'Date' (OrderLine l) = dateTime(order(l));
number 'Number' (OrderLine l) = number(order(l)) IN id;
numberDate 'Description' (OrderLine l) = numberDate(order(l));

location 'Location' (OrderLine l) = location(order(l));
nameLocation 'Location' (OrderLine l) = nameLocation(order(l)) IN id;

vendor (OrderLine l) = vendor(order(l));
nameVendor 'Vendor' (OrderLine l) = nameVendor(order(l)) IN id;

edit (OrderLine l) + { edit(order(l)); } 

FORM order 'Purchase order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) READONLYIF readonly(o)
                  nameType, dateTime, number, 
                  nameVendor, nameCompany, nameLocation, nameCurrency,
                  namePaymentTerms,
                  scheduledDateTime,
                  nameRepresentative, vendorReference, note,
                  details
    PROPERTIES(o) READONLY untaxedAmount, taxAmount, amount
                 
    OBJECTS l = OrderLine
    PROPERTIES(l) READONLYIF readonly(o)
                  index, 
                  nameItem ON CHANGE {
                      INPUT i = item(l) LIST name(i) ACTIONS 'add' { newProduct(STRING(i)); } DO item(l) <- i;
                  },
                  description, nameUom, idBarCodeItem, idItem, referenceItem, 
                  quantity, price, untaxedAmount

    OBJECTS tg = TaxGroup
    PROPERTIES    READONLYIF readonly(o)
                  nameTax(l, tg) ON CHANGE changeTax(l, tg) COLUMNS (tg) HEADER name(tg)

    PROPERTIES(l) NEW, DELETE
    FILTERS order(l) = o
     
    EDIT Order OBJECT o    
;      

DESIGN order {
    caption = badgedDoc('Purchase order', number(o), dateTime(o));
    OBJECTS {
        NEW header {
            alignment = STRETCH;    
            horizontal = TRUE;
            NEW headerLeft {
                MOVE PROPERTY(nameType(o)) { notNull = TRUE; }
                MOVE PROPERTY(dateTime(o));
                MOVE PROPERTY(number(o));
            }
            NEW headerRight {
                MOVE PROPERTY(nameVendor(o)) { notNull = TRUE; } 
                MOVE PROPERTY(nameCompany(o)) { notNull = TRUE; }
                MOVE PROPERTY(nameLocation(o));
                MOVE PROPERTY(nameCurrency(o));
            }
            NEW relatedDoc {
                fill = 1;
                tabbed = TRUE;
            }                                            
        }
        NEW details {
            fill = 7;
            tabbed = TRUE;
            NEW lines {
                caption = 'Lines';
                MOVE BOX(l) { caption = ''; }
            }
            NEW otherInformation {
                caption = 'Other information';
                lines = 2;
                NEW invoicing {
                    caption = 'Invoicing';
                    alignment = STRETCH;
                    MOVE PROPERTY(namePaymentTerms(o));                         
                }
                NEW shippingInformation {
                    caption = 'Shipping';
                    alignment = STRETCH;
                    MOVE PROPERTY(scheduledDateTime(o));
                }
                NEW purchaseInformation {
                    caption = 'Others';
                    alignment = STRETCH;
                    MOVE PROPERTY(nameRepresentative(o));
                    MOVE PROPERTY(vendorReference(o));
                    MOVE PROPERTY(note(o));
                }
            }
            NEW detailsInformation {
                caption = 'Details';
                MOVE PROPERTY(details(o)) { 
                    caption = ''; 
                    fill = 1;
                    height = 300;
                }
            }
        }
        NEW footer {
            align = END;
            horizontal = TRUE; 
            NEW total {
                caption = 'Total';
                horizontal = TRUE;
                MOVE PROPERTY(untaxedAmount(o));
                MOVE PROPERTY(taxAmount(o));
                MOVE PROPERTY(amount(o));                     
            }
        }
    }        
}

edit (OrderLine l) + { SHOW order OBJECTS o = order(l), l = l DOCKED; }

showOrder (LONG id) {
    FOR LONG(Order o AS Order) = id DO
        SHOW order OBJECTS o = o DOCKED MANAGESESSION;
} @@api;

background = ABSTRACT CASE COLOR (Order);
FORM orders 'Purchase orders'
    OBJECTS o = Order BACKGROUND background(o)
    PROPERTIES(o) READONLYIF isReadonly()
                           number, dateTime, nameVendor,
                           nameType, nameCompany, namePaymentTerms,
                           scheduledDateTime, nameLocation,
                           nameRepresentative, vendorReference, note,
                           untaxedAmount, taxAmount, amount, nameCurrency     
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE DISABLEIF readonly(o)
;

@extendFormEditable(orders);

@defineDocHistoryLine(order, item);
@defineDocHistoryLineProperty(order, OrderLine, quantity, 'Qty', nameItem);
@defineHistoryForm(order, order, o);

@defineTaxData(order, o, headerRight);

@defineDocObjectsForm(orders, o, 'Purchase orders');
@defineDocStatusForm(order, o);
@defineDocLinesCount(order, o);
@defineDocLocationAccess(order, o);

@defineObjectActivity(order, o, details, numberDateVendor);

@defineObjectComments(order, o, details);
@defineObjectCommentsHistory(order);

@defineObjectCommentsActivity(order);

@defineObjectFiles(order, o, 'Purchase order file');

NAVIGATOR {
    operations {
        NEW orders FIRST;    
    }
}

clone (Order to, Order from) {
    type(to) <- type(from);
    vendor(to) <- vendor(from);
    paymentTerms(to) <- paymentTerms(from);
    company(to) <- company(from);
    location(to) <- location(from);
    currency(to) <- currency(from);
    representative(to) <- representative(from); 
    note(to) <- note(from);
    FOR order(OrderLine l) = from INLINE NEW nl = OrderLine DO {
        order(nl) <- to;
        item(nl) <- item(l);
        description(nl) <- description(l);
        
        quantity(nl) <- quantity(l);
        
        price(nl) <- price(l);
    }
}

copy 'Copy' (Order from)  { 
    NEWSESSION {
        NEW to = Order {
            clone(to, from);
            SHOW order OBJECTS o = to DOCKED;
        }
    }
}

EXTEND FORM order PROPERTIES copy(o);

DESIGN order {
    secondaryActions {
        MOVE PROPERTY(copy(o)) { valueClass = 'btn-secondary'; }
    }
}