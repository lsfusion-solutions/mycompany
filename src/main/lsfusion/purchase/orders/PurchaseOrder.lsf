MODULE PurchaseOrder;

REQUIRE Time, MetaNumerator, Individual, Company,
        MetaTax, TaxItem, BarCode, PurchaseOrderType,
        PurchaseSettings, PartnerPurchase, ItemSales,
        PaymentTermsPurchase, Doc, PurchasePricelistValue,
        DebtPartner, Location;  

NAMESPACE Purchase;

CLASS Order 'Заказ поставщику';

@defineStatus(order, 'заказа поставщику');

// type
type 'Тип' = DATA OrderType (Order) NONULL;
nameType 'Тип' (Order s) = name(type(s));

WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(type(o)) DO type(o) <- defaultOrderType(); 

dateTime 'Создан'  = DATA DATETIME (Order);
WHEN LOCAL SET(Order o IS Order) AND NOT CHANGED(dateTime(o)) DO { dateTime(o) <- currentDateTime();}

@defineNumberType(order);

numberDate 'Описание' (Order o) = number(o) + ' от ' + dateTime(o);

vendor 'Поставщик' = DATA Partner (Order) NONULL;
nameVendor 'Поставщик' (Order o) = name(vendor(o));

CONSTRAINT vendor(Order o) AND NOT isVendor(vendor(o))
                CHECKED BY vendor[Order]
                MESSAGE 'Контрагент заказа поставщику должен быть поставщиком';

addressVendor 'Адрес поставщика' (Order o) = csvAddress(vendor(o));

// Invoicing information
paymentTerms = DATA PaymentTerm (Order);
namePaymentTerms 'Условия оплаты' (Order o) = name(paymentTerms(o));

WHEN LOCAL CHANGED(vendor(Order o)) AND NOT CHANGED(paymentTerms(o)) DO paymentTerms(o) <- vendorPaymentTerms(vendor(o)); 

// Shipping information
scheduledDateTime 'Срок поставки' = DATA DATETIME (Order);
scheduledDateTime(Order o) <- currentDateTime() WHEN SET(o IS Order);   

location = DATA Location (Order);
nameLocation 'Место хранения' (Order o) = name(location(o));

// Purchase information
representative 'Наш представитель' = DATA Individual (Order);
nameRepresentative 'Наш представитель' (Order o)= name(representative(o));

CONSTRAINT representative(Order o) AND NOT type(legalEntity(representative(o))) = PartnerType.company 
    CHECKED BY representative MESSAGE 'Должен быть наш работник';

vendorReference 'Входящий номер поставщика' = DATA STRING[30] (Order);

// Lines
CLASS OrderLine 'Спецификация';
order =  DATA Order (OrderLine) NONULL  DELETE;

index '№' = PARTITION SUM 1 ORDER OrderLine l BY order(l) IN id MATERIALIZED CHARWIDTH 3;

item = DATA Item (OrderLine) NONULL;
nameItem 'Номенклатура' (OrderLine l) = name(item(l));
idBarCodeItem 'Штрихкод' (OrderLine l) = idBarCode(item(l));
idItem 'Код' (OrderLine l) = id(item(l));

CONSTRAINT item(OrderLine l) AND NOT canBePurchased(item(l))
            CHECKED BY item[OrderLine] 
            MESSAGE 'Номенклатура не предназначена для закупки';

description 'Описание' = DATA ISTRING (OrderLine);

uom = DATA Uom (OrderLine);
nameUom 'Ед. изм.' (OrderLine l) = name(uom(l));
WHEN LOCAL CHANGED(item(OrderLine l)) DO uom(l) <- uom(item(l));

quantity 'Количество' = DATA NUMERIC[16,3] (OrderLine);
price 'Цена' = DATA NUMERIC[10,2] (OrderLine);

@defineTaxCalc(order, o, purchase);
taxIncluded (OrderLine l) += taxIncluded(type(order(l)));

WHEN LOCAL CHANGED (item(OrderLine ol)) DO {
    description(ol) <- OVERRIDE nameItemVendor(pricelistLineA(vendor(order(ol)), item(ol), dateTime(order(ol)))),
                                nameItem(ol); 
    price(ol) <- OVERRIDE price(pricelistLineA(vendor(order(ol)), item(ol), dateTime(order(ol)))), 
                              cost(item(ol));
    uom(ol) <- uom(item(ol));
    in(ol, Tax t) <- purchaseIn(item(ol),t); 
}

// Line properties
nameStatus 'Статус' (OrderLine l) = nameStatus(order(l)); 
type (OrderLine l) = type(order(l)); 
dateTime 'Создан' (OrderLine l) = dateTime(order(l));
number 'Номер' (OrderLine l) = number(order(l));
numberDate 'Описание' (OrderLine l) = numberDate(order(l));
nameLocation 'Место хранения' (OrderLine l) = nameLocation(order(l));
nameVendor 'Поставщик' (OrderLine l) = nameVendor(order(l));

edit (OrderLine l) + { edit(order(l)); } 

FORM order 'Заказ поставщику'
    OBJECTS o = Order PANEL
    PROPERTIES(o) nameType, dateTime, number, nameVendor, nameLocation,
                  namePaymentTerms,
                  scheduledDateTime,
                  nameRepresentative, vendorReference
    PROPERTIES(o) READONLY untaxedAmount, totalTax, totalAmount
                 
    OBJECTS l = OrderLine
    PROPERTIES(l) index, nameItem, description, nameUom, idBarCodeItem, idItem, quantity, price, subtotal, taxes ON CHANGE changeTax(l)
    PROPERTIES(l) NEW, DELETE
    FILTERS order(l) = o
     
    EDIT Order OBJECT o    
;      

DESIGN order {
    OBJECTS {
        NEW header {
            alignment = STRETCH;    
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(o)) { notNull = TRUE; }
                MOVE PROPERTY(dateTime(o));
                MOVE PROPERTY(number(o));
            }
            NEW headerRight {
                MOVE PROPERTY(nameVendor(o)) { notNull = TRUE; } 
                MOVE PROPERTY(nameLocation(o));
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }                                            
        }
        NEW details {
            fill = 7;
            type = TABBED;
            MOVE BOX(l);
            NEW otherInformation {
                caption = 'Прочая информация';
                type = COLUMNS;
                columns = 2;
                NEW invoicing {
                    caption = 'Расчеты';
                    alignment = STRETCH;
                    MOVE PROPERTY(namePaymentTerms(o));                         
                }
                NEW shippingInformation {
                    caption = 'Поставка';
                    alignment = STRETCH;
                    MOVE PROPERTY(scheduledDateTime(o));
                }
                NEW purchaseInformation {
                    caption = 'Прочие';
                    alignment = STRETCH;
                    MOVE PROPERTY(nameRepresentative(o));
                    MOVE PROPERTY(vendorReference(o));
                }
            }            
        }
        NEW footer {
            align = END;
            type = CONTAINERH; 
            NEW total {
                caption = 'Итого';
                type = CONTAINERH;
                MOVE PROPERTY(untaxedAmount(o));
                MOVE PROPERTY(totalTax(o));
                MOVE PROPERTY(totalAmount(o));                     
            }
        }
    }        
}

@defineDocHistory(order, o, item, quantity);

FORM orders 'Заказы поставщикам'
    OBJECTS o = Order
    PROPERTIES(o) READONLY nameType, dateTime, number, nameVendor,
                           namePaymentTerms,
                           scheduledDateTime, nameLocation,
                           nameRepresentative, vendorReference,
                           untaxedAmount, totalTax, totalAmount     
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE 
;

@defineStatusForm(order, o);

NAVIGATOR {
    operations {
        NEW orders FIRST;    
    }
}

copyOrder 'Копировать' (Order o)  { 
    NEWSESSION {
        NEW no = Order {
            type(no) <- type(o);
            vendor(no) <- vendor(o);
            paymentTerms(no) <- paymentTerms(o);
            location(no) <- location(o);
            representative(no) <- representative(o); 
            FOR order(OrderLine l) = o INLINE NEW nl = OrderLine DO {
                order(nl) <- no;
                item(nl) <- item(l);
                description(nl) <- description(l);
                uom(nl) <- uom(l); 
                
                quantity(nl) <- quantity(l);
                
                price(nl) <- price(l);
            }
            SHOW order OBJECTS o = no DOCKED;
        }
    }
}

EXTEND FORM order PROPERTIES copyOrder(o);

DESIGN order {
    primaryActions {
        MOVE PROPERTY(copyOrder(o));
    }
}