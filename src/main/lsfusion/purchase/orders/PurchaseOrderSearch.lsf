MODULE PurchaseOrderSearch;

REQUIRE PurchaseOrderCanceled, ResLedger;

NAMESPACE Purchase;

@defineDocSearch(order, item, quantity, 'Quantity', o, canBePurchased);

// available
onHand 'On hand' (Order o, Product p) = prevOnHandRec(location(o), p);
expected 'Expected' (Order o, Product p) = prevExpectedRec(location(o), p);
available 'Available' (Order o, Product p) = prevAvailableRec(location(o), p); 

EXTEND FORM order
    PROPERTIES(o, itm) READONLY onHand, expected, available
;

// last
lastOrderLine (Partner p, Location l, Item i) = GROUP LAST OrderLine ol ORDER dateTime(ol), ol WHERE NOT canceled(order(ol)) BY vendor(ol), location(ol), item(ol);
lastQuantityOrderLine (Partner p, Location l, Item i) = quantity(lastOrderLine(p, l, i));
prevLastQuantityOrderLine (Partner p, Location l, Item i) = PREV(lastQuantityOrderLine(p, l, i));

lastOrderLine (Partner p, Item i) = GROUP LAST OrderLine ol ORDER dateTime(ol), ol WHERE NOT canceled(order(ol)) BY vendor(ol), item(ol);
lastQuantityOrderLine (Partner p, Item i) = quantity(lastOrderLine(p, i));
prevLastQuantityOrderLine (Partner p, Item i) = PREV(lastQuantityOrderLine(p, i));

prevLastQuantityLocation 'Previous order' (Order o, Item i) = prevLastQuantityOrderLine(vendor(o), location(o), i); 
prevLastQuantity 'Previous order' (Order o, Item i) = prevLastQuantityOrderLine(vendor(o), i); 

EXTEND FORM order
    PROPERTIES 'Previous order' = prevLastQuantity(o, itm) 

    FILTERGROUP line
        FILTER 'Was ordered for location' prevLastQuantityLocation(o, itm)
        FILTER 'Was ordered' prevLastQuantity(o, itm)
;

// barcode
@defineBarCodeSearch(order, o, l, quantity);
