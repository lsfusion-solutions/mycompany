MODULE PurchaseOrderBill;

REQUIRE PurchaseOrder, PurchaseOrderConfirmed,
        PurchaseOrderLocked,
        BillDone, BillCanceled, BillCurrency, Product;

NAMESPACE Purchase;

// type
billType = DATA BillType (OrderType);
nameBillType 'Bill type' (OrderType t) = name(billType(t));
EXTEND FORM orderType PROPERTIES nameBillType(o);

// invoicing policy
CLASS InvoicingPolicy 'Bill control' {
    ordered 'Ordered quantity'
}
name '{First_name}' (InvoicingPolicy p) = staticCaption(p) IF p IS InvoicingPolicy CHARWIDTH 20;

invoicingPolicy = DATA InvoicingPolicy (OrderType);
nameInvoicingPolicy 'Bill control' (OrderType t) = name(invoicingPolicy(t)); 
EXTEND FORM orderType PROPERTIES nameInvoicingPolicy(o);

forbidLockNotFullyPaid 'Forbid to lock orders that are not fully paid' = DATA BOOLEAN (OrderType);
EXTEND FORM orderType PROPERTIES(o) forbidLockNotFullyPaid;

// lines
@defineDocLineRelation(order, bill, '{Order}', 'Bill', o, b);

billed 'Billed' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND ready(bill(bl)) AND NOT canceled(bill(bl));
paid 'Paid' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND done(bill(bl)) AND NOT canceled(bill(bl));

CONSTRAINT SET(locked(Order o)) AND (GROUP SUM 1 IF order(OrderLine l) = o AND (OVERRIDE paid(l), 0.0) < quantity(l)) AND forbidLockNotFullyPaid(type(o))
    MESSAGE 'It is forbidden to lock orders that are not fully paid';

toBillOrdered 'To bill' (OrderLine l) = 
    max(quantity(l) (-) billed(l), 0) IF 
        (invoicingPolicy(type(l)) = InvoicingPolicy.ordered OR NOT invoicingPolicy(type(l)) OR NOT item(l) IS Product);
 
toBillOrdered (Order o) = GROUP SUM toBillOrdered(OrderLine ol) IF order(ol) = o;

toBill = ABSTRACT VALUE BOOLEAN (Order);
toBill (Order o) += toBillOrdered(o) > 0;

fill ABSTRACT LIST (BillLine, OrderLine);
fill ABSTRACT LIST (Bill, Order);
createBill 'Create Bill' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW nb = Bill {
            ready(nb) <- TRUE;
            type(nb) <- billType(type(o));
            
            company(nb) <- company(o);
                        
            vendor(nb) <- vendor(o);
            vendorReference(nb) <- vendorReference(o);
            note(nb) <- note(o);

            currency(nb) <- currency(o);
            
            paymentTerms(nb) <- paymentTerms(o);
                         
            FOR order(OrderLine ol) = o AND toBillOrdered(ol) > 0 
                NEW bl = BillLine DO {
                bill(bl) <- nb;
                item(bl) <- item(ol);
                description(bl) <- description(ol);
                quantity(bl) <- toBillOrdered(ol);
                price(bl) <- price(ol);
                orderLine(bl) <- ol;

                fill(bl, ol);
            }                            
            
            fill(nb, o);
            
            executeLocalEvents();
            FOR bill(BillLine bl) = nb DO {
                in(bl, Tax t) <- in(orderLine(bl), t);
            }                                
            SHOW bill OBJECTS b = nb DOCKED;
        }
    }
}

FORM orderBillShow 'Bills'
    OBJECTS ol = OrderLine
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY number, dateTime, imagedNameStatus, quantity
    FILTERS orderLine(bl) = ol
;

EXTEND FORM order   
    PROPERTIES(o) SHOWIF (status(o) = OrderStatus.confirmed AND toBill(o)) createBill
    PROPERTIES(l) SHOWIF confirmed(o) billed ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT billed(l) = quantity(l)
    PROPERTIES(l) SHOWIF confirmed(o) paid ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT paid(l) = quantity(l)

    OBJECTS bi = Bill
    PROPERTIES(bi) READONLY number, dateTime, imagedNameStatus BACKGROUND colorStatus(bi), nameType
    PROPERTIES(bi) NEWSESSION EDIT GRID   
    FILTERS in(o, bi)       
;
 
DESIGN order {
    primaryActions {
        MOVE PROPERTY(createBill(o)) { valueClass = 'btn-primary'; };
    }
    relatedDoc  {
        MOVE BOX(bi) {
            caption = badged('Bills', countBills(o));
            GRID(bi) { size = (100, -1); }
        }
        REMOVE TOOLBARBOX(bi);
    }
}

EXTEND FORM orders
    FILTERGROUP toBill
        FILTER 'Create Bill' status(o) = OrderStatus.confirmed AND toBill(o)
;

DESIGN orders {
    FILTERGROUP(toBill) { alignment = STRETCH; }
}