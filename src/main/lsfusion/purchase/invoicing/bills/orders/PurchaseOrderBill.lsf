MODULE PurchaseOrderBill;

REQUIRE PurchaseOrder, PurchaseOrderConfirmed,
        BillDone, BillCanceled, Product;

NAMESPACE Purchase;

// type
billType = DATA BillType (OrderType);
nameBillType 'Тип поступления' (OrderType t) = name(billType(t));
EXTEND FORM orderType PROPERTIES nameBillType(o);

// invoicing policy
CLASS InvoicingPolicy 'Политика оформления поступления' {
    ordered 'Заказанное количество'
}
name 'Имя' (InvoicingPolicy p) = staticCaption(p) IF p IS InvoicingPolicy CHARWIDTH 20;

invoicingPolicy = DATA InvoicingPolicy (OrderType);
nameInvoicingPolicy 'Политика оформления поступления' (OrderType t) = name(invoicingPolicy(t)); 
EXTEND FORM orderType PROPERTIES nameInvoicingPolicy(o);

// lines
@defineDocLineRelation(order, bill, 'Заказы', 'Поступления', o, b);

billed 'Поступило' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND ready(bill(bl)) AND NOT canceled(bill(bl));
paid 'Оплачено' (OrderLine ol) = 
    GROUP SUM quantity(BillLine bl) IF orderLine(bl) = ol AND done(bill(bl)) AND NOT canceled(bill(bl));
toBillOrdered 'К оформлению' (OrderLine l) = 
    max(quantity(l) (-) billed(l), 0) IF 
        (invoicingPolicy(type(l)) = InvoicingPolicy.ordered OR NOT invoicingPolicy(type(l)) OR NOT item(l) IS Product);
 
toBillOrdered (Order o) = GROUP SUM toBillOrdered(OrderLine ol) IF order(ol) = o;

toBill = ABSTRACT VALUE BOOLEAN (Order);
toBill (Order o) += toBillOrdered(o) > 0;

fill ABSTRACT LIST (BillLine, OrderLine);
fill ABSTRACT LIST (Bill, Order);
createBill 'Создать поступление' (Order o) {
    APPLY;
    IF canceled() THEN RETURN;
               
    NEWSESSION {
        NEW nb = Bill {
            ready(nb) <- TRUE;
            type(nb) <- billType(type(o));
            
            vendor(nb) <- vendor(o);
            vendorReference(nb) <- vendorReference(o);
            paymentTerms(nb) <- paymentTerms(o);
                         
            FOR order(OrderLine ol) = o AND toBillOrdered(ol) > 0 
                NEW bl = BillLine DO {
                bill(bl) <- nb;
                item(bl) <- item(ol);
                description(bl) <- description(ol);
                quantity(bl) <- toBillOrdered(ol);
                price(bl) <- price(ol);
                orderLine(bl) <- ol;

                fill(bl, ol);
            }                            
            
            fill(nb, o);
            
            executeLocalEvents();
            FOR bill(BillLine bl) = nb DO {
                in(bl, Tax t) <- in(orderLine(bl), t);
            }                                
            SHOW bill OBJECTS b = nb DOCKED;
        }
    }
}

FORM orderBillShow 'Выставлено счетов'
    OBJECTS ol = OrderLine
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) READONLY nameStatus, number, dateTime, quantity
    FILTERS orderLine(bl) = ol
;

EXTEND FORM order   
    PROPERTIES(o) SHOWIF (status(o) = OrderStatus.confirmed AND toBill(o)) createBill
    PROPERTIES(l) SHOWIF confirmed(o) billed ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT billed(l) = quantity(l)
    PROPERTIES(l) SHOWIF confirmed(o) paid ON CHANGE { DIALOG orderBillShow OBJECTS ol = l; } BACKGROUND NOT paid(l) = quantity(l)

    OBJECTS bi = Bill
    PROPERTIES(bi) READONLY nameType, dateTime, nameStatus BACKGROUND colorStatus(bi), number    
    PROPERTIES(bi) NEWSESSION EDIT GRID   
    FILTERS in(o, bi)       
;
 
DESIGN order {
    primaryActions {
        MOVE PROPERTY(createBill(o)) { fontStyle = 'bold'; };
    }
    relatedDoc  {
        MOVE BOX(bi) {
            caption = CONCAT ' ', 'Поступления', '(' + countBills(o) + ')';
            GRID(bi) { headerHeight = 24; }
        }
        REMOVE TOOLBARBOX(bi);
    }
}

EXTEND FORM orders
    FILTERGROUP toBill
        FILTER 'Создать поступление' status(o) = OrderStatus.confirmed AND toBill(o)
;

DESIGN orders {
    FILTERGROUP(toBill) { alignment = STRETCH; }
}