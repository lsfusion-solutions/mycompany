MODULE PurchasePricelist;

REQUIRE ItemPurchase, PartnerPurchase, PurchaseSettings, BarCode, Doc;

NAMESPACE Purchase; 

CLASS Pricelist 'Прайс-лист поставщика';

@defineDocStatus(pricelist, 'прайс-листа поставщика');

startDateTime 'Начало действия' = DATA DATETIME (Pricelist);
startDateTime(Pricelist l) <- currentDateTime() WHEN SET(l IS Pricelist);
 
endDateTime 'Окончание действия' = DATA DATETIME (Pricelist);

@defineNumber(pricelist, 'Прайс-листы поставщиков', 'ПЛП');

numberDate 'Описание' (Pricelist p) = number(p) + ' от ' + startDateTime(p) IN id;

note 'Примечание' = DATA STRING[50] (Pricelist);

vendor 'Поставщик ' = DATA Partner (Pricelist);
nameVendor 'Поставщик '(Pricelist p) = name(vendor(p));

// lines
CLASS PricelistLine 'Номенклатура';

pricelist 'Прайс-лист' = DATA Pricelist (PricelistLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER PricelistLine l BY pricelist(l) IN id MATERIALIZED CHARWIDTH 3;

item 'Номенклатура' = DATA Item (PricelistLine);
nameItem 'Номенклатура' (PricelistLine l) = name(item(l));
idBarCodeItem 'Штрихкод' (PricelistLine l) = idBarCode(item(l)); 
idItem 'Код' (PricelistLine l) = id(item(l)); 

uom (PricelistLine l) = uom(item(l));
nameUom 'Ед. изм.' (PricelistLine l) = name(uom(l)); 

nameItemVendor 'Наименование у поставщика' = DATA STRING[200] (PricelistLine);
codeItemVendor 'Код у поставщика' = DATA STRING[20] (PricelistLine);

price 'Цена' = DATA NUMERIC[10,2] (PricelistLine);

// Line properties
nameStatus 'Статус' (PricelistLine prl) = nameStatus(pricelist(prl));
number 'Номер' (PricelistLine prl) = number(pricelist(prl));
numberDate 'Описание' (PricelistLine l) = numberDate(pricelist(l));
startDateTime 'Начало действия' (PricelistLine prl) = startDateTime(pricelist(prl));
endDateTime 'Окончание действия' (PricelistLine prl) = endDateTime(pricelist(prl));
nameVendor 'Поставщик' (PricelistLine prl) = nameVendor(pricelist(prl));
note 'Примечание' (PricelistLine prl) = note(pricelist(prl));

FORM pricelist 'Прайс-лист поставщика'
    OBJECTS p = Pricelist PANEL
    PROPERTIES(p) number, startDateTime, endDateTime, nameVendor, note
    
    OBJECTS l = PricelistLine
    PROPERTIES(l) index READONLY, nameItem, nameUom READONLY, nameItemVendor, codeItemVendor,
                  idBarCodeItem, idItem, price
    PROPERTIES(l) NEW, DELETE 
    FILTERS pricelist(l) = p
    
    EDIT Pricelist OBJECT p
;

DESIGN pricelist {
    caption = (CONCAT ' ', 'Прайс-лист поставщика', '№' + number(p), 'от ' + startDateTime(p));
    OBJECTS {
        NEW header {
            type = CONTAINERH;
            alignment = STRETCH;
            NEW headerLeft {
                MOVE PROPERTY(number(p));
                NEW period {
                    alignment = STRETCH;
                    type = CONTAINERH;
                    MOVE PROPERTY(startDateTime(p)) { caption = 'Действует с'; }
                    MOVE PROPERTY(endDateTime(p)) { caption = 'по '; }                
                }
                MOVE PROPERTY(nameVendor(p));
                MOVE PROPERTY(note(p));
            }
            NEW relatedDoc {
                fill = 1;
                type = TABBED;
            }
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW lines {
                caption = 'Строки';
                MOVE BOX(l);
            }
        }
    }
    TOOLBARBOX {
        NEW footer FIRST {
            type = CONTAINERH;
        }
    }
}

@defineDocSearch(pricelist, item, price, 'Цена', p, canBePurchased);

FORM pricelists 'Прайс-листы поставщиков'
    OBJECTS p = Pricelist
    PROPERTIES(p) READONLY startDateTime, endDateTime, number, nameVendor, note
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE 

    LIST Pricelist OBJECT p
;  

@defineDocStatusForm(pricelist, p);
@defineDocLinesCount(pricelist, p);

NAVIGATOR {
    operations {
        NEW pricelists;       
    }
}

// copy
copy 'Копировать' (Pricelist pl) {
    NEWSESSION {
        NEW p = Pricelist {
            startDateTime(p) <- startDateTime(pl);
            endDateTime(p) <- endDateTime(pl);
            note(p) <- note(pl);
            vendor(p) <- vendor(pl);
            FOR pricelist(PricelistLine l)  = pl DO NEW nl = PricelistLine {
                pricelist(nl) <- p;
                item(nl) <- item(l);
                price(nl) <- price(l);
                nameItemVendor(nl) <- nameItemVendor(l);
                codeItemVendor(nl) <- codeItemVendor(l);
            }
            SHOW pricelist OBJECTS p = p;
        }
    }
}

EXTEND FORM pricelist
    PROPERTIES(p) copy 
;

DESIGN pricelist {
    secondaryActions {
        MOVE PROPERTY(copy(p)); 
    }
}