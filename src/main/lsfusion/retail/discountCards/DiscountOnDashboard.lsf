MODULE DiscountOnDashboard;

REQUIRE Invoice, DiscountCard, PosDashboard;

NAMESPACE Retail;

discountCard 'Дисконтная карта' = DATA DiscountCard(Invoice);
cardHolder (Invoice i) = customer(discountCard(i));
cardId 'Номер дисконтной карты' (Invoice i) = id(discountCard(i));

ON LOCAL {
    FOR CHANGED(discountCard(Invoice i)) DO 
        customer(i) <- customer(discountCard(i));
    FOR CHANGED(customer(Invoice i)) AND NOT CHANGED(discountCard(i)) DO 
        discountCard(i) <- NULL;
}

EXTEND FORM posDashboard
    PROPERTIES(i) cardId
;

DESIGN posDashboard {
    invoice {
            MOVE PROPERTY(cardId(i)) AFTER PROPERTY(nameCustomer(i));
    }
}

processBarcode (Invoice i, STRING[20] b) + {
    FOR DiscountCard dc = discountCardBarCode(b) DO {
        discountCard(i) <- discountCardBarCode(b);
        consumedBarcode() <- TRUE;
    }
}