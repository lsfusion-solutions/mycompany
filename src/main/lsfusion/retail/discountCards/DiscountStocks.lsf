MODULE DiscountStocks;

REQUIRE DiscountCategories, SalesDiscount, DiscountCardsSettings;

NAMESPACE Sales;

category = DATA DiscountCategory (Discount);
categoryName 'Категория карты' (Discount d) = name(category(d));
categoryVisible = DATA BOOLEAN (Discount);

EXTEND FORM discount
    OBJECTS dsc = Discount PANEL 
    PROPERTIES(dsc) categoryName SHOWIF categoryVisible(dsc)
;

DESIGN discount{
    general {
        NEW dscCards {
            MOVE PROPERTY(categoryName(dsc));
        }
    }
}

EXTEND FORM discounts
    FILTERS NOT categoryVisible(d);

newDiscountStock 'Новый' () {
    NEWSESSION {
        NEW dsc = Discount {
            categoryVisible(dsc) <- TRUE;
 //           APPLY;
            SHOW discount OBJECTS d = dsc;        
        }
    }
} IMAGE 'add.png';

FORM discountStocks 'Скидки по дисконтным картам'
    OBJECTS d = Discount
    PROPERTIES(d) READONLY name,
                           startDateTime, endDateTime,
                           fromAmount, fromQuantity,
                           fromAccumulatedPreviousMonth, fromAccumulated,
                           discount, namePriceType,
                           categories, items
    PROPERTIES(d) NEWSESSION EDIT, DELETE
    PROPERTIES newDiscountStock() DRAW d TOOLBAR
    
    FILTERS categoryVisible(d)
    FILTERGROUP active
        FILTER 'Активные' active(d) DEFAULT
    
    LIST Discount OBJECT d
;

NAVIGATOR {
    discount {
    NEW discountStocks;
    }
}