MODULE DiscountCard;

REQUIRE Partner, RetailSettings;

NAMESPACE Retail;

CLASS DiscountCard 'Дисконтная карта';

customer 'Владелец' = DATA Partner (DiscountCard) NONULL;
nameCustomer 'Владелец' (DiscountCard d) = name(customer(d)) IN id;

countDiscountCard (Partner p) = GROUP SUM 1 IF customer(DiscountCard d) = p;  

id 'Номер' = DATA STRING[20] (DiscountCard) IN id;
discountCard (STRING[20] id) = GROUP AGGR DiscountCard d BY id(d);

issuedDate 'Дата выпуска' = DATA DATE (DiscountCard) NONULL;
blockedDate 'Дата блокировки' = DATA DATE (DiscountCard);

issuedDate(DiscountCard d) <- currentDate() WHEN SET(d IS DiscountCard);

CONSTRAINT issuedDate(DiscountCard d) > blockedDate(d) 
    MESSAGE 'Дата блокировки дисконтной карты не может быть раньше даты выпуска';

FORM discountCard 'Дисконтная карта'
    OBJECTS d = DiscountCard PANEL
    PROPERTIES(d) id, nameCustomer, issuedDate, blockedDate
    
    EDIT DiscountCard OBJECT d
;

FORM discountCards 'Дисконтные карты'
    OBJECTS d = DiscountCard
    PROPERTIES(d) READONLY id, nameCustomer, issuedDate, blockedDate
    PROPERTIES(d) NEWSESSION NEW, EDIT, DELETE;
;

NAVIGATOR {
    settings {
        NEW discountCards;
    }
}