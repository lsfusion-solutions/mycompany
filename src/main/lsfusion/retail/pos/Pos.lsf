MODULE Pos;

REQUIRE RetailSettings, Company, Authentication;

NAMESPACE Retail;

CLASS Pos 'Касса ККМ';

id 'Код' = DATA STRING[20] (Pos) IN id;
pos (STRING[20] id) = GROUP AGGR Pos d BY id(d);

name 'Наименование' = DATA ISTRING[100] (Pos) NONULL CHARWIDTH 15;

company 'Компания' = DATA Company (Pos);
nameCompany 'Компания' (Pos p) = name(company(p));
WHEN LOCAL SET(Pos p IS Pos) AND NOT CHANGED(company(p)) DO company(p) <- defaultCompany();

in 'Вкл' = DATA BOOLEAN (Pos, Computer);
countPos (Computer c) = GROUP SUM 1 IF in(Pos p, c) MATERIALIZED;

FORM pos 'Касса ККМ'
    OBJECTS p = Pos PANEL
    PROPERTIES(p) name, id, nameCompany
    
    OBJECTS c = Computer
    PROPERTIES(p, c)       in
    PROPERTIES(c) READONLY hostname
    
    EDIT Pos OBJECT p
;

DESIGN pos {
    OBJECTS {
        NEW tabbedPane {
            fill = 1;
            type = TABBED;
            MOVE BOX(c);
        }
    }
}

FORM poss 'Кассы ККМ'
    OBJECTS p = Pos
    PROPERTIES(p) READONLY name, id
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE

    LIST Pos OBJECT p
;

NAVIGATOR {
    settings {
        NEW poss;
    }
}