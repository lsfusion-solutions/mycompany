MODULE PosDashboardPayment;

REQUIRE PosDashboard, PaymentMethod, IncomingPaymentDebt, PosPaymentMethod, SessionPayment;

NAMESPACE Retail;

amount '{Amount}' = DATA LOCAL NUMERIC[14,2] (PaymentMethod);
paymentAmount() = GROUP SUM amount(PaymentMethod pm); 
notCashPaymentAmount() = GROUP SUM amount(PaymentMethod pm) IF NOT isCash(pm); 

change (Invoice i) = paymentAmount() (-) amount(i); 

FORM posPayment 'Payment'
    OBJECTS i = Invoice PANEL
    PROPERTIES nameCustomer(i), amount(i), change '{Change}' = change(i) IF change(i) > 0
    
    OBJECTS pm = PaymentMethod
    PROPERTIES(pm) name READONLY, amount 
    FILTERS NOT disabled(pos(session(i)), pm)
    
    PROPERTIES overFormOk = formOk() READONLYIF (change(i) < 0 OR notCashPaymentAmount() > amount(i)) AND paymentAmount() 
;

DESIGN posPayment {
    OBJECTS {
        NEW top {
            alignment = STRETCH;
            MOVE PROPERTY(nameCustomer(i)) { fontSize = 24; alignment = STRETCH; changeKey = 'F5'; }
        }
        NEW pane {
            horizontal = TRUE;
            fill = 1;
            NEW center {
                fill = 1;
                MOVE PROPERTY(amount(i)) { fontSize = 24; alignment = STRETCH; }
                MOVE BOX(pm) {
                    GRID(pm) {
                        defaultComponent = TRUE;
                        PROPERTY(name(pm)) { fontSize = 24; charWidth = 10; focusable = FALSE; }
                        PROPERTY(amount(pm)) { fontSize = 24; charWidth = 6; }
                    }
                    REMOVE TOOLBARBOX(pm);
                }
                MOVE PROPERTY(change) { fontSize = 24; alignment = STRETCH; }
            }
            NEW right { alignment = STRETCH; }
        }
    }
    TOOLBARRIGHT {
        REMOVE PROPERTY(formRefresh());
        REMOVE PROPERTY(formOk());
        PROPERTY(formClose()) { fontSize = 24; fill = 1; }
        MOVE PROPERTY(overFormOk) { changeMouse = ''; fontSize = 24; fill = 1; }
    }
}

EXTEND FORM posPayment
    EVENTS ON INIT { ACTIVATE PROPERTY posPayment.amount(pm); }
;

@defineKeyboardDecimal(amount, posPayment, PaymentMethod, pm, right, 48);

afterPosPayment ABSTRACT LIST (Invoice);

posPayment 'Payment' (Session s, Invoice i) {
    amount(PaymentMethod pm) <- NULL;
    DIALOG posPayment OBJECTS i = i, pm INPUT NOMANAGESESSION DO {
        IF NOT paymentAmount() THEN amount(pm) <- amount(i);
        FOR am = (amount(PaymentMethod ppm) (-) (change(i) IF isCash(ppm))) DO NEW p = IncomingPayment {
            done(p) <- TRUE;
            
            paymentMethod(p, i) <- ppm;
            type(p) <- incomingPaymentType(ppm);
            
            company(p) <- company(pos(session(i)));
            companyAccount(p) <- account(pos(session(i)), ppm);
            
            partner(p) <- customer(i);
            
            amount(p) <- am;
            paid(p, i) <- am;
        }
        ready(i) <- TRUE;
        APPLY;
        IF canceled() THEN {
            ready(i) <- NULL;
            DELETE IncomingPayment p WHERE SET(p IS IncomingPayment);
        } ELSE {
            afterPosPayment(i);
            createInvoice(s);
        }
    }
} CHANGEKEY 'ctrl ENTER';

EXTEND FORM posDashboard
    PROPERTIES(s, i) posPayment READONLYIF NOT amount(i)
;

DESIGN posDashboard {
    actions {
        MOVE PROPERTY(posPayment(s, i)) { fill = 1; valueClass = 'btn-primary'; }
    }
}

// payment method total
EXTEND FORM posDashboard
    OBJECTS pm = PaymentMethod BEFORE si
    PROPERTIES READONLY paid(s, pm) COLUMNS (pm) HEADER name(pm) 

    PROPERTIES READONLY paid(si, pm) COLUMNS (pm) HEADER name(pm) 
;

DESIGN posDashboard {
    sessionTotal {
        MOVE PROPERTY(paid(s, pm));
    }
}