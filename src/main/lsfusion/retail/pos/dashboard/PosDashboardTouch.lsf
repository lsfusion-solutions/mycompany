MODULE PosDashboardTouch;

REQUIRE PosDashboardSearch, Utils, 
        CategoryImage, ItemImage;

NAMESPACE Retail;

hideTouch 'Hide' = DATA BOOLEAN (Category);
hideTouch 'Hide' = DATA BOOLEAN (Item);

EXTEND FORM options
    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    PROPERTIES hideTouch(c)
    ORDERS name(c)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT
    
    OBJECTS itm = Item
    PROPERTIES(itm) READONLY name, nameUom, idBarCode, id
    PROPERTIES hideTouch(itm)
    ORDERS name(itm)
    FILTERS level(category(itm), c)
    FILTERGROUP active
        FILTER '{Active}' active(itm) 'F10' DEFAULT  
;

DESIGN options {
    tabbedPane {
        NEW touch {
            caption = 'Touch';
            horizontal = TRUE;
            MOVE BOX(TREE categories);
            MOVE BOX(itm) { caption = ''; }
        }
    }
}

CLASS ABSTRACT TouchNode;

EXTEND CLASS Category : TouchNode;
EXTEND CLASS Item : TouchNode;

name (TouchNode n) = MULTI name[Category](n), name[Item](n);
parent (TouchNode n) = MULTI parent[Category](n), category[Item](n);
image (TouchNode n) = MULTI image[Category](n), image[Item](n);
hide (TouchNode n) = MULTI hideTouch[Category](n), hideTouch[Item](n);
active (TouchNode n) = MULTI active[Category](n), active[Item](n);

canonicalName 'Canonical name' (TouchNode n) =
   GROUP CONCAT name(TouchNode parent), ' / ' ORDER DESC level(n, parent) CHARWIDTH 50 IN id;

index (TouchNode n) = PARTITION SUM 1 IF NOT hide(n) ORDER name(n), n BY parent(n);

EXTEND FORM posDashboard
    OBJECTS tn = TouchNode PANEL
    PROPERTIES(tn) READONLY '' = canonicalName
;

back 'Back' (TouchNode tn) {
    SEEK posDashboard.tn = parent(tn);
}

drop 'Reset' () {
    SEEK posDashboard.tn = GROUP LAST Category c ORDER c WHERE c IS Category AND NOT parent(c);
}
EXTEND FORM posDashboard 
    PROPERTIES drop(), back(tn)
    EVENTS ON INIT drop()
;

select (Invoice i, TouchNode tn) {
    IF tn IS Category THEN
        SEEK posDashboard.tn = tn;
    ELSE
        selectPosSearch(i, tn);
}

EXTEND FORM posDashboard
    OBJECTS tn1 = TouchNode
    FILTERS parent(tn1) = tn, mod(index(tn1),3) = 1, NOT hide(tn1)
    PROPERTIES NOWAIT select(i, tn1) COLUMNS (tn1) HEADER name(tn1) IMAGE image(tn1)
    FILTERS active(tn1)
    
    OBJECTS tn2 = TouchNode
    FILTERS parent(tn2) = tn, mod(index(tn2),3) = 2, NOT hide(tn2)
    PROPERTIES NOWAIT select(i, tn2) COLUMNS (tn2) HEADER name(tn2) IMAGE image(tn2)
    FILTERS active(tn2)
    
    OBJECTS tn3 = TouchNode
    FILTERS parent(tn3) = tn, mod(index(tn3),3) = 0, NOT hide(tn3)
    PROPERTIES NOWAIT select(i, tn3) COLUMNS (tn3) HEADER name(tn3) IMAGE image(tn3)
    FILTERS active(tn3)
;

DESIGN posDashboard {
    tabPane {
        NEW touch BEFORE search {
            caption = 'Touch';
            showIf = openedSession(p) AND NOT hideInvoice();
            NEW current {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(canonicalName(tn)) { fill = 1; focusable = FALSE; }
                MOVE PROPERTY(drop()) { fontSize = 16; focusable = FALSE; }
                MOVE PROPERTY(back(tn)) { fontSize = 16; focusable = FALSE; }
            }
            NEW touchScroll {
                fill = 1;
                height = 100;
                NEW touchPane {
                    fill = 1;
                    horizontal = TRUE;
                    MOVE PROPERTY(select(i, tn1)) { fill = 1; valueClass = 'btn-outline-primary'; panelColumnVertical = TRUE; panelCaptionVertical = TRUE; focusable = FALSE; }
                    MOVE PROPERTY(select(i, tn2)) { fill = 1; valueClass = 'btn-outline-primary'; panelColumnVertical = TRUE; panelCaptionVertical = TRUE; focusable = FALSE; }
                    MOVE PROPERTY(select(i, tn3)) { fill = 1; valueClass = 'btn-outline-primary'; panelColumnVertical = TRUE; panelCaptionVertical = TRUE; focusable = FALSE; }
                }
            }
        }
    }
}