MODULE PosDashboardSearch;

REQUIRE PosDashboard, 
        SalesPricelistInvoice;

NAMESPACE Retail;

filterName 'Наименование' = DATA LOCAL ISTRING (); 
filterPrice 'Цена' = DATA LOCAL NUMERIC[14,2] ();

EXTEND FORM posDashboard
    OBJECTS it = Item
    PROPERTIES(it) READONLY name, idBarCode, id
    PROPERTIES(i, it) quantity ON CHANGE changeQuantity(i, it)
    PROPERTIES READONLY overListPrice(i, it)
    ORDERS name(it)

    FILTERS canBeSold(it),
            overListPrice(i, it),
            active(it)

    PROPERTIES() filterName, filterPrice
    FILTERS isISubstring(name(it), filterName()) OR NOT filterName(),
            overListPrice(i, it) = filterPrice() OR NOT filterPrice()

    FILTERGROUP searchFilter
        FILTER 'В чеке' quantity(i, it) 'shift F10'
;

DESIGN posDashboard {
    tabPane {
        NEW search FIRST {
            caption = 'Поиск';
            showIf = openedSession(p) AND NOT hideInvoice();
            NEW filters {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(filterName()) { changeKey = 'F6'; }                
                MOVE PROPERTY(filterPrice()) { changeKey = 'F7'; }                
            }
            NEW searchItems {
                fill = 1;
                REMOVE BOX(it);
                MOVE GRID(it);
                MOVE TOOLBARBOX(it);
                MOVE USERFILTER(it);
            }
        }
    }
}

// barcode add
processBarcode (Invoice i, STRING[20] b) + {
    FOR Item it = itemBarCode(b) DO {
        setQuantity(i, it, quantity(i, it) (+) 1);
        SEEK posDashboard.l = lastInvoiceLine(i, it);
        consumedBarcode() <- TRUE;
    }
}