MODULE PosDashboardReturn;

REQUIRE PosDashboard, CreditNote, BillReady;

NAMESPACE Retail;

EXTEND FORM posDashboard
    OBJECTS b = Bill PANEL
    PROPERTIES(b) 'To pay' = amount, nameVendor
    FILTERS session(b) = s
    
    OBJECTS bl = BillLine
    PROPERTIES(bl) index READONLY, nameItem, quantity, price, numberInvoiceLine,
                   DELETE GRID
    FILTERS bill(bl) = b
    
    PROPERTIES returnNewReceipt = newReceipt(s)

    OBJECTS sb = Bill
    PROPERTIES(sb) READONLY number, dateTime, nameVendor, 'To pay' = amount
    PROPERTIES(sb) NEWSESSION EDIT
    FILTERS PREV(session(sb)) = s
;

DESIGN posDashboard {
    leftPane {
        NEW billPane {
            fill = 1;
            showIf = openedSession(p) AND hideInvoice();
            NEW bill {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(nameVendor(b)) { changeKey = 'F5'; }
            }
            REMOVE BOX(bl);
            MOVE GRID(bl) {
                PROPERTY(nameItem(bl));
            }
            NEW billLinesFooter {
                horizontal = TRUE;
                alignment = STRETCH;
                NEW billInfo {
                    fill = 1;
                    MOVE PROPERTY(amount(b)) { charWidth = 8; panelCaptionVertical = TRUE; fontSize = 24; focusable = FALSE; }
                }
                NEW billActions {
                    alignment = STRETCH;
                    horizontal = TRUE;
                    MOVE PROPERTY(returnNewReceipt) { fill = 1; valueClass = 'btn-danger'; }
                }
            }
        }
        sessionDetails {
            MOVE BOX(sb) AFTER BOX(si) {
                caption = 'Refunds'; 
                showIf = openedSession(p); 
            }
        }
    }
}

return 'Return' (Session s, Invoice i) {
    IF s THEN { 
        CANCEL;
        
        hideInvoice() <- TRUE;
        NEW b = Bill {
            session(b) <- s;
            
            create(b, i);
            
            SEEK posDashboard.b = b;
        }
    }
}

EXTEND FORM posDashboard
    PROPERTIES(s, si) return TOOLBAR
;

DESIGN posDashboard {
    PROPERTY(return(s, si)) { valueClass = 'btn-warning'; }
}