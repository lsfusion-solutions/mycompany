MODULE PosDashboardCash;

REQUIRE PosDashboard, PosPaymentMethod, 
        IncomingPaymentDone, IncomingPaymentOutgoing, 
        OutgoingPaymentDone, OutgoingPaymentIncoming;

NAMESPACE Retail;

FORM inputMoney 'Внесение/изъятие денег'
    OBJECTS a = NUMERIC[14,2] PANEL
    PROPERTIES(a) amount 'Сумма' = VALUE
;
DESIGN inputMoney {
    BOX(a) {
        caption = '';
        PROPERTY(amount) { fontSize = 24; }
    }
}

// deposit
depositPaymentType = DATA IncomingPaymentType ();
nameDepositPaymentType 'Тип внесения денег' = name(depositPaymentType());
EXTEND FORM options PROPERTIES() nameDepositPaymentType;
DESIGN options {
    general {
        MOVE PROPERTY(nameDepositPaymentType());
    }
}

afterDeposit ABSTRACT LIST (Pos, IncomingPayment); 
deposit 'Внесение денег' (Pos p) {
    DIALOG inputMoney OBJECTS a INPUT DO {
        NEWSESSION {
            NEW ip = IncomingPayment {
                done(ip) <- TRUE;
                
                type(ip) <- depositPaymentType();
                
                partner(ip) <- company(p);
                partnerAccount(ip) <- account(p);
                
                company(ip) <- company(p);
                companyAccount(ip) <- cashAccount(p);
                
                amount(ip) <- a;
                
                APPLY;
                IF NOT canceled() THEN
                    afterDeposit(p, ip);
            }
        }
    } 
}

withdrawPaymentType = DATA OutgoingPaymentType ();
nameWithdrawPaymentType 'Тип изъятия денег' = name(withdrawPaymentType());
EXTEND FORM options PROPERTIES() nameWithdrawPaymentType;
DESIGN options {
    general {
        MOVE PROPERTY(nameWithdrawPaymentType());
    }
}

// withdraw
afterWithdraw ABSTRACT LIST (Pos, OutgoingPayment);
withdraw 'Выемка денег' (Pos p) {
    DIALOG inputMoney OBJECTS a INPUT DO {
        NEWSESSION {
            NEW op = OutgoingPayment {
                done(op) <- TRUE;
                
                type(op) <- withdrawPaymentType();
                
                partner(op) <- company(p);
                partnerAccount(op) <- account(p);

                company(op) <- company(p);
                companyAccount(op) <- cashAccount(p);
                
                amount(op) <- a;
                
                APPLY;
                IF NOT canceled() THEN 
                    afterWithdraw(p, op);
            }
        }
    } 
}


EXTEND FORM posDashboard
    PROPERTIES(p) cashBalance
    
    OBJECTS ip = IncomingPayment
    PROPERTIES(ip) READONLY dateTime, amount
    PROPERTIES(p)  deposit DRAW ip TOOLBAR
    PROPERTIES(ip) DELETE GRID
    FILTERS companyAccount(ip) = cashAccount(p), 
            type(ip) = depositPaymentType()
             

    OBJECTS op = OutgoingPayment
    PROPERTIES(op) READONLY dateTime, amount
    PROPERTIES(p)  withdraw DRAW op TOOLBAR
    PROPERTIES(op) DELETE GRID
    FILTERS companyAccount(op) = cashAccount(p),
            type(op) = withdrawPaymentType()
;

DESIGN posDashboard {
    posHeader {
        MOVE PROPERTY(cashBalance(p));
    }
    sessionDetails {
        MOVE BOX(ip) {
            caption = 'Внесение денег';
        }
        MOVE BOX(op) {
            caption = 'Выемка денег';
        }
    }
}