MODULE CashRegister;

REQUIRE Pos, Session, IncomingPayment, OutgoingPayment, Invoicing,
        PosDashboard, PosDashboardPayment, PosDashboardCash,
        CashRegisterMeta;

NAMESPACE Retail;

CLASS CashDriver 'Драйвер ККТ';

name 'Имя' (CashDriver d) = staticCaption(d) IF d IS CashDriver;

driver 'Драйвер' = DATA CashDriver (Pos);
nameDriver 'Драйвер' (Pos p) = name(driver(p));

dataHostname 'Хост' = DATA ISTRING[30] (Pos);
hostname 'Хост' (Pos p) = OVERRIDE dataHostname(p), 'localhost'; 

FORM cashDrivers 'Драйверы'
    OBJECTS d = CashDriver
    PROPERTIES(d) READONLY name
    
    LIST CashDriver OBJECT d
;

EXTEND FORM pos PROPERTIES(p) nameDriver, dataHostname SHOWIF driver(p);    
EXTEND FORM poss PROPERTIES(p) READONLY nameDriver;

session = DATA Session (Payment);

openShift 'Открыть смену' ABSTRACT CASE EXCLUSIVE (Session);
closeShift 'Закрыть смену' ABSTRACT CASE EXCLUSIVE (Session);
cashIn 'Внесение' ABSTRACT CASE EXCLUSIVE (Payment);
cashOut 'Выемка' ABSTRACT CASE EXCLUSIVE (Payment);
buy 'Чек расхода' ABSTRACT CASE EXCLUSIVE (Invoice);

continuePrint 'Допечатать' ABSTRACT CASE EXCLUSIVE (Pos);
reportX 'X-отчет' ABSTRACT CASE EXCLUSIVE (Pos);

afterOpen(Session s) + {openShift(s);}
afterClose(Session s) + {closeShift(s);}
afterPosPayment(Invoice i) + {buy(i);}
afterDeposit(Pos p, IncomingPayment i) + {
    session(i) <- openedSession(p);
    cashIn(i);
}
afterWithdraw(Pos p, OutgoingPayment o) + {
    session(o) <- openedSession(p);
    cashOut(o);
}

fiscalOpenSession 'Фискальный' = DATA BOOLEAN (Session);
fiscalCloseSession 'Фискальный' = DATA BOOLEAN (Session);

refiscalSession 'Фискализация' (Session s) {
    IF fiscalOpenSession(s) AND NOT fiscalCloseSession(s) AND closingDateTime(s) THEN
        closeShift(s);
    ELSE
        IF NOT fiscalOpenSession(s) AND NOT closingDateTime(s) THEN
            openShift(s);
}

EXTEND FORM session
    PROPERTIES(s) fiscalOpenSession SHOWIF NOT closingDateTime(s),
                  fiscalCloseSession SHOWIF closingDateTime(s),
                  refiscalSession SHOWIF NOT fiscalCloseSession(s) AND (closingDateTime(s) OR NOT fiscalOpenSession(s)) 
;

DESIGN session {
    header {
        NEW fiscal {
            type = CONTAINERH;
            MOVE PROPERTY(fiscalOpenSession(s));
            MOVE PROPERTY(fiscalCloseSession(s));
            MOVE PROPERTY(refiscalSession(s));
        }
    }
}

@defFiscal(incomingPayment, p, cashIn);
@defFiscal(outgoingPayment, p, cashOut);
@defFiscal(invoice, i, buy);

EXTEND FORM posDashboard PROPERTIES(p) continuePrint, reportX; 

DESIGN posDashboard {
    cashRegister {
        MOVE PROPERTY(continuePrint(p));
        MOVE PROPERTY(reportX(p));
    }
}