MODULE SessionInvoice;

REQUIRE Session, SalesDiscountInvoice, Bill;

NAMESPACE Retail;

// invoice
invoiceType = DATA InvoiceType (Pos) NONULL;
nameInvoiceType 'Invoice type' (Pos p) = name(invoiceType(p));

EXTEND FORM pos
    PROPERTIES(p) nameInvoiceType
;
DESIGN pos {
    PROPERTY(nameInvoiceType(p)) { notNull = TRUE; } 
}

session = DATA Session (Invoice) INDEXED;
numberSession '{Session}' (Invoice i) = number(session(i));

WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(type(i)) DO
    type(i) <- invoiceType(pos(session(i)));
    
WHEN LOCAL SET(session(Invoice i)) AND NOT CHANGED(company(i)) DO
    company(i) <- company(pos(session(i)));

EXTEND FORM session
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY number, dateTime, nameCustomer, imagedNameStatus BACKGROUND colorStatus(i), 
                           'Total' = fullAmount, 'Discount' = discountAmount, 'To pay' = amount
    PROPERTIES(i) NEWSESSION EDIT
    FILTERS session(i) = s
;

DESIGN session {
    tabPane {
        MOVE BOX(i) {
            caption = 'Cash receipts';
        }
    }
}

EXTEND FORM invoice
    PROPERTIES(i) READONLY numberSession SHOWIF session(i)
;

DESIGN invoice {
    otherInformation {
        NEW session {
            caption = '{Session}';
            MOVE PROPERTY(numberSession(i));
        }
    }
}

// bill
session = DATA Session (Bill);
numberSession '{Session}' (Bill i) = number(session(i));

EXTEND FORM session
    OBJECTS b = Bill
    PROPERTIES(b) READONLY number, dateTime, nameVendor, imagedNameStatus BACKGROUND colorStatus(i), 
                           'To pay' = amount
    PROPERTIES(b) NEWSESSION EDIT
    FILTERS session(b) = s
;

DESIGN session {
    tabPane {
        MOVE BOX(b) {
            caption = 'Refunds';
        }
    }
}

totalAmountInvoice 'Sold' (Session s) = GROUP SUM amount(Invoice i) IF session(i) = s MATERIALIZED ; 
countInvoices 'Sales' (Session s) = GROUP SUM 1 IF session(Invoice i) = s MATERIALIZED;

totalAmountBill 'Returned' (Session s) = GROUP SUM amount(Bill b) IF session(b) = s MATERIALIZED;
countBills 'Returns' (Session s) = GROUP SUM 1 IF  session(Bill b) = s MATERIALIZED;

amount 'Total' (Session s) = totalAmountInvoice(s) (-) totalAmountBill(s) MATERIALIZED;

EXTEND FORM sessions
    PROPERTIES(o) countInvoices, totalAmountInvoice,
                  countBills, totalAmountBill,
                  amount
;

EXTEND FORM bill
    PROPERTIES(b) READONLY numberSession SHOWIF session(b) READONLYIF readonly(b)
;

DESIGN bill {
    otherInformation {
        NEW session {
            caption = '{Session}';
            MOVE PROPERTY(numberSession(b));
        }
    }
}