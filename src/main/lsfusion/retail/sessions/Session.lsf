MODULE Session;

REQUIRE MetaNumerator, Time, DateUtils, 
        RetailSettings, Pos;

NAMESPACE Retail;

CLASS Session '{Session}';

openingDateTime 'Opened' = DATA DATETIME (Session) NONULL;
openingDateTime(Session s) <- currentDateTime() WHEN SET(s IS Session);
@defineDateTimeAggregation(Session, opening, '(opening)');

closingDateTime 'Closed' = DATA DATETIME (Session);
CONSTRAINT closingDateTime(Session s) < openingDateTime(s)
    MESSAGE 'The session cannot be closed earlier than it was open';

opened 'Opened' (Session s) = openingDateTime(s) AND NOT closingDateTime(s);

@defineNumber(session, 'Sessions', 'SM');

numberDate 'Description' (Session s) = number(s) + ' (' + openingDateTime(s) + ')' IN id;

pos 'POS' = DATA Pos (Session) NONULL;
namePos 'POS' (Session s) = name(pos(s));

// opened
openedSession (Pos p) = GROUP AGGR Session s WHERE opened(s) BY pos(s);

FORM session '{Session}'
    OBJECTS s = Session PANEL
    PROPERTIES(s) namePos, number, 
                  openingDateTime, closingDateTime
    
    EDIT Session OBJECT s
;

DESIGN session {
    OBJECTS {
        NEW header {
            NEW title {
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(namePos(s));
                MOVE PROPERTY(number(s));
            }
            NEW period {
                horizontal = TRUE;
                alignment = STRETCH;
                MOVE PROPERTY(openingDateTime(s));
                MOVE PROPERTY(closingDateTime(s));
            }
        }
        NEW tabPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM sessions 'Sessions'
    OBJECTS o = Session
    PROPERTIES(o) READONLY number, openingDateTime, closingDateTime, namePos
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST Session OBJECT o
;

DESIGN sessions {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDateFilterForm(sessions, o, opening);

NAVIGATOR {
    operations {
        NEW sessions;
    }
}

// actions
breakSession = DATA LOCAL BOOLEAN ();

beforeOpen ABSTRACT LIST (Session);
afterOpen ABSTRACT LIST (Session);
openSession 'Open session' (Pos p) {
    CANCEL;

    NEWSESSION {
        IF NOT openedSession(p) THEN
            NEW s = Session {
                pos(s) <- p;
                beforeOpen(s);
                IF NOT breakSession() THEN {
                    APPLY;
                    IF NOT canceled() THEN
                        afterOpen(s);
                }
            }
        ELSE
            MESSAGE 'There is already an open session';
    }
}

beforeClose ABSTRACT LIST (Session);
afterClose ABSTRACT LIST (Session);
close 'Close session' (Session s) {
    CANCEL;

    NEWSESSION {
        closingDateTime(s) <- currentDateTime();
        beforeClose(s);
        IF NOT breakSession() THEN {
            APPLY;
            IF NOT canceled() THEN
                afterClose(s);
        }
    }
} CONFIRM;