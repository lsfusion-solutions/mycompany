MODULE Session;

REQUIRE MetaNumerator, Time, DateUtils, 
        RetailSettings, Pos;

NAMESPACE Retail;

CLASS Session 'Смена';

openingDateTime 'Открыта' = DATA DATETIME (Session) NONULL;
openingDateTime(Session s) <- currentDateTime() WHEN SET(s IS Session);
@defineDateTimeAggregation(Session, opening, ' открытия');

closingDateTime 'Закрыта' = DATA DATETIME (Session);
CONSTRAINT closingDateTime(Session s) < openingDateTime(s)
    MESSAGE 'Смена не может быть закрыта раньше чем была открыта';

opened 'Открыта' (Session s) = openingDateTime(s) AND NOT closingDateTime(s);

@defineNumber(session, 'Смены', 'СМ');

numberDate 'Описание' (Session s) = number(s) + ' от ' + openingDateTime(s) IN id;

pos 'POS' = DATA Pos (Session) NONULL;
namePos 'POS' (Session s) = name(pos(s));

// opened
openedSession (Pos p) = GROUP AGGR Session s WHERE opened(s) BY pos(s);

FORM session 'Смена'
    OBJECTS s = Session PANEL
    PROPERTIES(s) namePos, number, 
                  openingDateTime, closingDateTime
    
    EDIT Session OBJECT s
;

DESIGN session {
    OBJECTS {
        NEW header {
            NEW title {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(namePos(s));
                MOVE PROPERTY(number(s));
            }
            NEW period {
                type = CONTAINERH;
                alignment = STRETCH;
                MOVE PROPERTY(openingDateTime(s));
                MOVE PROPERTY(closingDateTime(s));
            }
        }
        NEW tabPane {
            fill = 1;
            type = TABBED;
        }
    }
}

FORM sessions 'Смены'
    OBJECTS o = Session
    PROPERTIES(o) READONLY openingDateTime, closingDateTime, number, namePos
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST Session OBJECT o
;

@defineDateFilterForm(sessions, o, opening);

NAVIGATOR {
    operations {
        NEW sessions;
    }
}

// actions
afterOpen ABSTRACT LIST (Session);
openSession 'Открыть смену' (Pos p) {
    CANCEL;

    NEWSESSION {
        IF NOT openedSession(p) THEN
            NEW s = Session {
                pos(s) <- p;
                APPLY;
                IF NOT canceled() THEN
                    afterOpen(s);
            }
        ELSE
            MESSAGE 'Уже есть открытая смена';
    }
}

afterClose ABSTRACT LIST (Session);
close 'Закрыть смену' (Session s) {
    CANCEL;

    NEWSESSION {
        closingDateTime(s) <- currentDateTime();
        APPLY;
        IF NOT canceled() THEN
            afterClose(s);
    }
} CONFIRM;