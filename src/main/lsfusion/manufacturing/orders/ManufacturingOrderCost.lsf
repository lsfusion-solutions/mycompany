MODULE ManufacturingOrderCost;

REQUIRE ManufacturingOrderCanceled, CostLedger, CostLocation;

NAMESPACE Manufacturing;

// расходная проводка
CLASS ConsumedOutCostLedger 'Списание сырья' : OutCostLedger;
outCostLedger = AGGR ConsumedOutCostLedger WHERE consumed(ConsumedLine line) AND active(manufacturingOrder(line)) AND item(line) IS Product;

type(ConsumedOutCostLedger l) += nameType(manufacturingOrder(line(l)));

dateTime(ConsumedOutCostLedger l) += executionDateTime(manufacturingOrder(line(l)));
number(ConsumedOutCostLedger l) += number(manufacturingOrder(line(l)));

location(ConsumedOutCostLedger l) += costLocation(materialsLocation(manufacturingOrder(line(l))));

product(ConsumedOutCostLedger l) += item(line(l)) AS Product;
quantity(ConsumedOutCostLedger l) += consumed(line(l));

edit(ConsumedOutCostLedger l) + { edit(line(l)); }

cost 'Себестоимость' (ConsumedLine l) = amount(outCostLedger(l));

// приходная проводка
CLASS ConsumedInCostLedger 'Производство' : InCostLedger;
inCostLedger = AGGR ConsumedInCostLedger WHERE produced(FinishedLine line) AND active(manufacturingOrder(line)) AND item(line) IS Product;

type(ConsumedInCostLedger l) += nameType(manufacturingOrder(line(l)));

dateTime(ConsumedInCostLedger l) += executionDateTime(manufacturingOrder(line(l)));
number(ConsumedInCostLedger l) += number(manufacturingOrder(line(l)));

location(ConsumedInCostLedger l) += costLocation(productsLocation(manufacturingOrder(line(l))));

product(ConsumedInCostLedger l) += item(line(l)) AS Product;
quantity(ConsumedInCostLedger l) += produced(line(l));

cost 'Себестоимость' (ManufacturingOrder o) = GROUP SUM amount(ConsumedOutCostLedger l) IF manufacturingOrder(line(l)) = o;

calcAmount (ConsumedInCostLedger l) += NUMERIC[14,2](cost(manufacturingOrder(line(l))) * normalizedCostRatio(line(l)));

edit(ConsumedInCostLedger l) + { edit(line(l)); }

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLY cost
;