MODULE ManufacturingOrderReport;

REQUIRE ManufacturingOrderDone, ManufacturingOrderCost, ItemAttribute;

NAMESPACE Manufacturing;

attribute(ManufacturingOrder o, ItemAttribute a) = value(attribute(item(o), a));

FORM manufacturingOrderReport 'Order report'
    OBJECTS a = ItemAttribute
    
    OBJECTS o = ManufacturingOrder PIVOT
    PROPERTIES(o) READONLY number, scheduledDateTime, nameItem, imagedNameStatus, nameType, executionDateTime, 
                           nameCompany, nameMaterialsLocation, nameProductsLocation, 
                           nameUom, descriptionBom
    PROPERTIES READONLY 'Category 1' = level1(item(o)), 'Category 2' = level2(item(o)), 'Category 3' = level3(item(o)), 'Category 4' = level4(item(o)), 
                        'Canonical group' = canonicalNameCategory(item(o)) 
    PROPERTIES READONLY attribute(o, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(o) READONLY MEASURE toManufacture, manufactured,
                                   cost, extraCost, laborCost, totalCost
    FILTERS accessGranted(materialsLocation(o)) OR accessGranted(productsLocation(o))

    OBJECTS cl = (cl = ConsumedLine, co = ManufacturingOrder) PIVOT
    PROPERTIES(co) READONLY number, scheduledDateTime, nameItem, imagedNameStatus, nameType, executionDateTime,
                            nameCompany, nameMaterialsLocation, nameProductsLocation,
                            descriptionBom
    
    PROPERTIES(cl) READONLY 'Material' = nameItem, nameUom
    PROPERTIES READONLY 'Category 1' = level1(item(cl)), 'Category 2' = level2(item(cl)), 'Category 3' = level3(item(cl)), 'Category 4' = level4(item(cl)),
                        'Canonical group' = canonicalNameCategory(item(o))
    PROPERTIES READONLY attribute(co, a) COLUMNS (a) HEADER name(a)
    
    PROPERTIES(cl) READONLY MEASURE toConsume, reserved, consumed, cost
    FILTERS manufacturingOrder(cl) = co,
            accessGranted(materialsLocation(co)) OR accessGranted(productsLocation(co))
;

DESIGN manufacturingOrderReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
        NEW pane {
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(o) {
                caption = 'Manufacturing orders';
            }
            MOVE BOX(cl) {
                caption = 'Materials';
            }
        }
    }
}

@defineDateTimeAggregationForm(manufacturingOrderReport, o, scheduled);
@defineDateFilterForm(manufacturingOrderReport, o, scheduled);

@defineDateTimeAggregationForm(manufacturingOrderReport, co, scheduled);
EXTEND FORM manufacturingOrderReport
    FILTERS NOT scheduledDate(co) < from(dates), NOT scheduledDate(co) > to(dates)
;

NAVIGATOR {
    manufacturing {
        reporting {
            NEW manufacturingOrderReport;        
        }
    }
}