MODULE ManufacturingOrderDone;

REQUIRE ManufacturingOrderInProgress;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    done 'Done'
}

done 'Done' = DATA BOOLEAN (ManufacturingOrder);

executionDateTime 'Execution date' = DATA DATETIME (ManufacturingOrder);
WHEN SET(done(ManufacturingOrder o)) AND NOT CHANGED(executionDateTime(o)) DO executionDateTime(o) <- currentDateTime(); 

productsLocation 'Products location' = DATA Location (ManufacturingOrder);
nameProductsLocation 'Products location' (ManufacturingOrder m) = name(productsLocation(m));
CONSTRAINT SETCHANGED(productsLocation(ManufacturingOrder o)) AND internalUse(productsLocation(o)) CHECKED BY productsLocation[ManufacturingOrder]
    MESSAGE 'The selected location is for internal use';

@defineDocLocationAccess(manufacturingOrder, o, products);

CONSTRAINT done(ManufacturingOrder o) AND NOT productsLocation(o)
    MESSAGE 'No products location selected in completed production order'; 

prevOnHandAProductsLocation 'On hand after (destination)' (ManufacturingOrder o) = 
    prevOnHandA(productsLocation(o), item(o), executionDateTime(o));

markAsDone 'Mark as Done' (ManufacturingOrder m) {
    APPLY;
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        IF NOT produced(m) THEN
            setManufactured(m, toManufacture(m));
        
        ready(m) <- TRUE;
        inProgress(m) <- TRUE;
        done(m) <- TRUE;
        APPLY;
    }
}

status(ManufacturingOrder m) += WHEN done(m) THEN ManufacturingOrderStatus.done;

EXTEND FORM manufacturingOrder
    PROPERTIES(o) markAsDonePrimary = markAsDone SHOWIF status(o) = ManufacturingOrderStatus.inProgress,
                  markAsDoneSecondary = markAsDone SHOWIF (status(o) = ManufacturingOrderStatus.draft OR 
                                                           status(o) = ManufacturingOrderStatus.waiting OR 
                                                           status(o) = ManufacturingOrderStatus.ready), 
                  done, 
                  executionDateTime SHOWIF done(o),
                  
                  nameProductsLocation
;

DESIGN manufacturingOrder {
    primaryActions {
        MOVE PROPERTY(markAsDonePrimary) { valueClass = 'btn-primary'; }
    }
    secondaryActions {
        MOVE PROPERTY(markAsDoneSecondary) { valueClass = 'btn-secondary'; }
    }
    statusGroup {
        MOVE PROPERTY(done(o)) {
            valueClass = 'btn-check';
            captionClass = 'btn btn-outline-primary btn-arrow-right';
        }
    }
    headerLeft {
        MOVE PROPERTY(executionDateTime(o));
    }
    finishedHeader {
        MOVE PROPERTY(nameProductsLocation(o)) { notNull = TRUE; }
    }
}

@defineHistoryProperty(manufacturingOrder, executionDateTime, 'Execution date');

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLYIF isReadonly()
                           executionDateTime AFTER scheduledDateTime(o),
                           nameProductsLocation AFTER manufactured(o)
    PROPERTIES(o) READONLY prevOnHandAProductsLocation READONLY AFTER nameProductsLocation(o)  

    EXTEND FILTERGROUP status
        FILTER 'Done' status(o) = ManufacturingOrderStatus.done    
;

extraCopy (ManufacturingOrder o, ManufacturingOrder m) + {
    productsLocation(o) <- productsLocation(m);
}

@defineSelectionAction(manufacturingOrders, ManufacturingOrder, markAsDone, 'Mark as Done', statuses);
