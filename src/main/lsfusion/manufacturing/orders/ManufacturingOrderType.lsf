MODULE ManufacturingOrderType;

REQUIRE ManufacturingSettings, Location;

NAMESPACE Manufacturing;

CLASS ManufacturingOrderType 'Manufacturing order type';

name '{Name}' = DATA STRING[50] (ManufacturingOrderType) NONULL IN id;

id '{ID}'= DATA STRING[20] (ManufacturingOrderType);
manufacturingOrderType = GROUP AGGR ManufacturingOrderType t BY id(t);

numerator 'Numerator' = DATA Numerator (ManufacturingOrderType);
nameNumerator 'Numerator' (ManufacturingOrderType o) = name(numerator(o));

materialsLocation 'Materials location' = DATA Location (ManufacturingOrderType);
nameMaterialsLocation 'Materials location' (ManufacturingOrderType t) = name(materialsLocation(t)); 

unbuild 'Unbuild' = DATA BOOLEAN (ManufacturingOrderType);

FORM manufacturingOrderType 'Manufacturing order type'
    OBJECTS o = ManufacturingOrderType PANEL 
    PROPERTIES(o) name, id, nameNumerator, unbuild, nameMaterialsLocation
     
    EDIT ManufacturingOrderType OBJECT o;
;

DESIGN manufacturingOrderType {
    OBJECTS {
        NEW header {
            horizontal = TRUE;
            MOVE PROPERTY(name(o)) { charWidth = 30; }
            MOVE PROPERTY(id(o));
            MOVE PROPERTY(nameNumerator(o));
        }
        NEW pane {
            lines = 3;
            NEW default {
                caption = 'Default';
                MOVE PROPERTY(nameMaterialsLocation(o));
            }
            NEW otherInformation {
                caption = 'Other information';
                MOVE PROPERTY(unbuild(o));
            }
        }
        NEW tabbedPane {
            fill = 1;
            tabbed = TRUE;
        }
    }
}

FORM manufacturingOrderTypes 'Types of production orders'
    OBJECTS o = ManufacturingOrderType
    PROPERTIES(o) READONLY name, id, unbuild
    
    LIST ManufacturingOrderType OBJECT o;
;

EXTEND FORM options
    OBJECTS ot = ManufacturingOrderType
    PROPERTIES(ot) READONLY name, id
    PROPERTIES(ot) NEWSESSION NEW, EDIT, DELETE
;

DESIGN options {
    tabbedPane {
        MOVE BOX(ot);
    }
}

countManufacturingOrderTypes 'Number of types' () = GROUP SUM 1 IF r IS ManufacturingOrderType; 
firstManufacturingOrderType () = GROUP LAST ManufacturingOrderType t ORDER DESC t WHERE t IS ManufacturingOrderType;

defaultManufacturingOrderType () = firstManufacturingOrderType()
    IF countManufacturingOrderTypes() = 1;