MODULE ManufacturingOrderInProgress;

REQUIRE ManufacturingOrderReady, Input;

NAMESPACE Manufacturing;

EXTEND CLASS ManufacturingOrderStatus {
    inProgress 'В процессе'
}
inProgress 'В процессе' = DATA BOOLEAN (ManufacturingOrder);

status(ManufacturingOrder m) += WHEN inProgress(m) THEN ManufacturingOrderStatus.inProgress;
colorStatus(ManufacturingOrder m) += WHEN status(m) = ManufacturingOrderStatus.inProgress THEN RGB(252,247,149);

consumed 'Израсходовано' = DATA NUMERIC[16,3] (ConsumedLine);
consumed 'Израсходовано' (ManufacturingOrder o) = GROUP SUM consumed(ConsumedLine l) IF manufacturingOrder(l) = o AND item(l) = item(o) MATERIALIZED;

produced 'Произведено' = DATA NUMERIC[16,3] (FinishedLine);
produced 'Произведено' (ManufacturingOrder o) = GROUP SUM produced(FinishedLine l) IF manufacturingOrder(l) = o AND item(l) = item(o) MATERIALIZED;

// manufacture
manufactured 'Произведено' (ManufacturingOrder o) = IF unbuild(o) THEN consumed(o) ELSE produced(o);

setManufactured (ManufacturingOrder o, NUMERIC[16,3] q) {
    produced(FinishedLine l) <- NUMERIC[16,3](toProduce(l) * q / toManufacture(o)) WHERE manufacturingOrder(l) = o; 
    consumed(ConsumedLine l) <- NUMERIC[16,3](toConsume(l) * q / toManufacture(o)) WHERE manufacturingOrder(l) = o; 
}

changeManufactured (ManufacturingOrder o) {
    INPUT q = NUMERIC[16,3] DO
        setManufactured(o, q);
}

manufacture 'Произвести' (ManufacturingOrder m) {
    APPLY;
    IF canceled() THEN RETURN;
        
    NEWSESSION {
        DIALOG dialogQuantity OBJECTS q = toManufacture(m) INPUT DO {
            setManufactured(m, q);
            inProgress(m) <- TRUE;
            APPLY;
        }
    }
}

EXTEND FORM manufacturingOrder
    PROPERTIES(o) manufacture SHOWIF (status(o) = ManufacturingOrderStatus.ready OR status(o) = ManufacturingOrderStatus.inProgress), 
                  inProgress 
                  
    PROPERTIES SHOWIF inProgress(o) manufactured(o) ON CHANGE changeManufactured(o),
                                    produced(l),
                                    consumed(c) 
;

DESIGN manufacturingOrder {
    primaryActions {
        MOVE PROPERTY(manufacture(o)) { fontStyle = 'bold'; }
    }
    status {
        MOVE PROPERTY(inProgress(o));
    }
    quantity {
        MOVE PROPERTY(manufactured(o));
    }
}

EXTEND FORM manufacturingOrders
    PROPERTIES(o) READONLYIF isReadonly() manufactured AFTER toManufacture(o) ON CHANGE changeManufactured(o)

    EXTEND FILTERGROUP status
        FILTER 'В процессе' status(o) = ManufacturingOrderStatus.inProgress    
;

@defineSelectionAction(manufacturingOrders, ManufacturingOrder, manufacture, 'Произвести', statuses);