MODULE Bom;

REQUIRE Product, Company, ManufacturingSettings, MetaNumerator, BarCode;

NAMESPACE Manufacturing;

CLASS Bom 'Спецификация';

@defineNumber(bom, 'Спецификации', 'СП');
bom (STRING[50] id) = GROUP AGGR Bom b BY number(b);

name 'Название' = DATA ISTRING[50] (Bom);
description 'Описание' (Bom b) = CONCAT ' ', name(b), number(b) IN id;

archived 'Неактивная' = DATA BOOLEAN (Bom);
active 'Активная' (Bom b) = NOT archived(b);

item 'Номенклатура' = DATA Item (Bom) NONULL DELETE;
nameItem 'Номенклатура' (Bom b) = name(item(b));

quantity 'Кол-во' = DATA NUMERIC[16,3] (Bom) NONULL;

uom (Bom b) = uom(item(b));
nameUom 'Ед. изм.' (Bom b) = name(uom(b));

// default
dataDefaultBom = DATA Bom (Item);
calcDefaultBom (Item i) = GROUP LAST Bom b ORDER DESC b WHERE active(b) AND item(b) = i;
defaultBom (Item i) = OVERRIDE dataDefaultBom(i), calcDefaultBom(i);  

CONSTRAINT defaultBom(Item i) AND NOT item(defaultBom(i)) = i
    MESSAGE 'Номенклатура спецификации по умолчанию должна совпадать с номенклатурой';
WHEN DROPPED(item(defaultBom(Item i)) = i) DO dataDefaultBom(i) <- NULL;

CONSTRAINT dataDefaultBom(Item i) AND archived(dataDefaultBom(i))
    MESSAGE 'Спецификация по умолчанию должна быть активной';

default 'По умолчанию' (Bom b) = defaultBom(item(b)) = b;
changeDefault (Bom b) {
    INPUT l = BOOLEAN DO
        dataDefaultBom(Item i) <- b IF l WHERE i = item(b);
}

// lines
CLASS ComponentLine 'Строка компонентов';

bom 'Спецификация' = DATA Bom (ComponentLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER ComponentLine l BY bom(l) IN id MATERIALIZED CHARWIDTH 3;

item 'Номенклатура' = DATA Item (ComponentLine);
nameItem 'Номенклатура' (ComponentLine l) = name(item(l));
idBarCodeItem 'Штрихкод' (ComponentLine l) = idBarCode(item(l));
idItem 'Код' (ComponentLine l) = id(item(l));

uom (ComponentLine l) = uom(item(l));
nameUom 'Ед. изм.' (ComponentLine l) = name(uom(l));

quantity 'Кол-во' = DATA NUMERIC[16,3] (ComponentLine);

costRatio 'Доля стоимости' = DATA NUMERIC[5,2] (ComponentLine);

countComponentLine 'Компонентов' (Bom b) = GROUP SUM 1 IF bom(ComponentLine l) = b MATERIALIZED;

// byproduct
CLASS ByproductLine 'Строка производных номенклатур';

bom 'Спецификация' = DATA Bom (ByproductLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER ByproductLine l BY bom(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Товар' = DATA Product (ByproductLine);
nameProduct 'Товар' (ByproductLine l) = name(product(l));
idBarCodeProduct 'Штрихкод' (ByproductLine l) = idBarCode(product(l));
idProduct 'Код' (ByproductLine l) = id(product(l));

uom (ByproductLine l) = uom(product(l));
nameUom 'Ед. изм.' (ByproductLine l) = name(uom(l));

quantity 'Кол-во' = DATA NUMERIC[16,3] (ByproductLine);

countByproductLine 'Отходов' (Bom b) = GROUP SUM 1 IF bom(ByproductLine l) = b MATERIALIZED;

FORM bom 'Спецификация'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) number, nameItem, quantity, nameUom, name, 
                  default ON CHANGE changeDefault(b), archived
    
    OBJECTS c = ComponentLine
    PROPERTIES(c) index, nameItem, nameUom, idBarCodeItem, idItem, quantity, costRatio, NEW, DELETE 
    FILTERS bom(c) = b
    
    OBJECTS l = ByproductLine
    PROPERTIES(l) index, nameProduct, nameUom, idBarCodeProduct, idProduct, quantity, NEW, DELETE
    FILTERS bom(l) = b
    
    EDIT Bom OBJECT b
;

DESIGN bom {
    caption = (CONCAT ' ', 'Спецификация', '№' + number(b));
    OBJECTS {
        NEW actions {
            caption = 'Действия';
            type = CONTAINERH;
            alignment = STRETCH;
        }
        NEW header {
            alignment = STRETCH;
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(number(b))  { notNull = TRUE; }
                MOVE PROPERTY(nameItem(b)) { notNull = TRUE; }
                NEW quantity {
                    type = CONTAINERH;
                    MOVE PROPERTY(quantity(b));
                    MOVE PROPERTY(nameUom(b));
                }
                MOVE PROPERTY(name(b));
            }
            NEW headerRight {
                MOVE PROPERTY(default(b));           
                MOVE PROPERTY(archived(b));           
            }
        }
        NEW details {
            fill = 1;
            type = TABBED;
            NEW components {
                caption = 'Компоненты';
                MOVE BOX(c);
            }
            NEW byproducts {
                caption = 'Отходы';
                MOVE BOX(l);
            }
        }
    }
}

FORM boms 'Спецификации'
    OBJECTS b = Bom
    PROPERTIES(b) READONLY number, nameItem, quantity, nameUom, name, archived, countComponentLine, countByproductLine
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE
    
    FILTERGROUP active
        FILTER 'Активные' active(b) DEFAULT
    
    LIST Bom OBJECT b
;

NAVIGATOR {
    operations {
        NEW boms;
    }
}

FORM dialogItemBoms 'Выбор спецификации'
    OBJECTS i = Item PANEL
    
    OBJECTS b = Bom
    PROPERTIES(b) READONLY number, name
    FILTERS item(b) = i,
            active(b)
;

// copy
extraCopy ABSTRACT (Bom, Bom);
extraCopy ABSTRACT (ComponentLine, ComponentLine);

copy 'Копировать' (Bom b) {
    NEWSESSION {
        NEW nb = Bom {
            name(nb) <- name(b);
            item(nb) <- item(b);
            quantity(nb) <- quantity(b);
            
            FOR bom(ComponentLine l) = b DO NEW nl = ComponentLine {
                bom(nl) <- nb;
                item(nl) <- item(l);
                quantity(nl) <- quantity(l);
                costRatio(nl) <- costRatio(l);
                
                extraCopy(l, nl);
            }
            
            FOR bom(ByproductLine l) = b DO NEW nl = ByproductLine {
                bom(nl) <- nb;
                product(nl) <- product(l);
                quantity(nl) <- quantity(l);
            }
            
            extraCopy(b, nb);
            
            SHOW bom OBJECTS b = nb DOCKED;
        }
    }
}

EXTEND FORM bom PROPERTIES copy(b);

DESIGN bom {
    actions {
        MOVE PROPERTY(copy(b)); 
    }
}