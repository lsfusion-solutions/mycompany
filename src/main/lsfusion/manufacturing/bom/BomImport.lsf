MODULE BomImport;

REQUIRE Bom;

NAMESPACE Manufacturing;

bomExport 'Export BOMs' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF Bom b AND b IS Bom) THEN {
            NEW b = Bom {
                item(b) <- GROUP LAST Item i IF i IS Item ORDER i;
                quantity(b) <- 1;
            }
        }
        LOCAL f = EXCELFILE ();
        
        EXPORT XLSX HEADER FROM 'Number' = number(Bom b), 'ItemID' = id(item(b)), 
            'Quantity' = quantity(b), 'UoMID' = id(uom(b)) TO f;        
        open(f());
    }
}

componentLineExport 'Export components' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF ComponentLine l AND l IS ComponentLine) THEN {
            NEW l = ComponentLine {
                bom(l) <- GROUP LAST Bom b IF b IS Bom ORDER b;
                item(l) <- GROUP LAST Item i IF  i IS Item AND NOT i = item(bom(l)) ORDER i;
                quantity(l) <- 1;
            }
        }
    
        LOCAL f = EXCELFILE ();
        
        EXPORT XLSX HEADER FROM 'BoMNumber' = number(bom(ComponentLine l)), 'ItemID' = id(item(l)), 
            'Quantity' = quantity(l), 'UoMID' = id(uom(l)) TO f;
        open(f());
    }
}

byProductLineExport 'Export byproducts' () {
    NEWSESSION {
        IF NOT (GROUP SUM 1 IF ByproductLine l AND l IS ByproductLine) THEN {
            NEW l = ByproductLine {
                bom(l) <- GROUP LAST Bom b IF b IS Bom ORDER b;
                product(l) <- GROUP LAST Item i IF i IS Item AND NOT i = item(bom(l)) ORDER i;
                quantity(l) <- 1;
            }
        }
    
        LOCAL f = EXCELFILE ();
        
        EXPORT XLSX HEADER FROM 'BoMID' = number(bom(ByproductLine l)), 'ProductID' = id(product(l)), 
                'Quantity' = quantity(l), 'UoMID' = id(uom(l)) TO f;
        open(f());
    }
}

bomImport 'Import BOMs' () {
    LOCAL number = STRING[28] (INTEGER);
    LOCAL idItem = STRING[50] (INTEGER);
    LOCAL quantity = NUMERIC[16,3] (INTEGER);
    LOCAL idUom = STRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO number, idItem, quantity, idUom;
            
            FOR [GROUP SUM 1 BY idItem(INTEGER i)](num) AND NOT item[STRING](num) DO {
                MESSAGE 'There is no item the database with the code' + ' ' + num + '... Import canceled';
                RETURN;
            }
            
            FOR imported(INTEGER i) DO NEW b = Bom {
                number(b) <- number(i);
                item(b) <- item(idItem(i));
                quantity(b) <- quantity(i);
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

componentLineImport 'Import components' () {
    LOCAL id = STRING[28] (INTEGER);
    LOCAL idItem = STRING[50] (INTEGER );
    LOCAL quantity = NUMERIC[18,5] (INTEGER);
    LOCAL idUom = STRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id, idItem, quantity, idUom;
            
            FOR [GROUP SUM 1 BY id(INTEGER i)](num) AND NOT bom[STRING](num) DO {
                MESSAGE 'Missing BOM in database with code' + ' ' + num + '... Import canceled';
                RETURN;
            } 
            
            FOR imported(INTEGER i) DO NEW l = ComponentLine {
                bom(l) <- GROUP LAST Bom b IF number(b) = id(i) ORDER b;
                item(l) <- item(idItem(i));
                quantity(l) <- quantity(i);
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

byProductLineImport 'Import byproducts' () {
    LOCAL id = STRING[28] (INTEGER);
    LOCAL idItem = STRING[50] (INTEGER );
    LOCAL quantity = NUMERIC[16,3] (INTEGER);
    LOCAL idUom = STRING[50] (INTEGER);
    
    INPUT f = EXCELFILE DO {
        NEWSESSION {
            IMPORT XLS HEADER FROM f TO id, idItem, quantity, idUom;
            
            FOR [GROUP SUM 1 BY id(INTEGER i)](num) AND NOT bom[STRING](num) DO {
                MESSAGE 'Missing BOM in database with code' + ' ' + num + '... Import canceled';
                RETURN;
            }
            
            FOR imported(INTEGER i) DO NEW l = ByproductLine {
                bom(l) <- GROUP LAST Bom b IF number(b) = id(i) ORDER b;
                product(l) <- item(idItem(i));
                quantity(l) <- quantity(i);
            }
            
            APPLY;
            MESSAGE 'Import completed';
        }
    }
}

EXTEND FORM migrationData 
    PROPERTIES bomExport(), bomImport(), componentLineExport(), componentLineImport(), byProductLineExport(), byProductLineImport()
;

DESIGN migrationData {
    commons {
        NEW category {
            caption = 'Bills of Materials';
            NEW bom {
                horizontal = TRUE;
                MOVE PROPERTY(bomImport());
                MOVE PROPERTY(bomExport());
            }
            NEW componentLines {
                horizontal = TRUE;
                MOVE PROPERTY(componentLineImport());
                MOVE PROPERTY(componentLineExport());
            }
            NEW byProductLines {
                horizontal = TRUE;
                MOVE PROPERTY(byProductLineImport());
                MOVE PROPERTY(byProductLineExport());
            }
        }
    }
}
