MODULE BomStructureCost;

REQUIRE BomNested, ItemCost;

NAMESPACE Manufacturing;

nameProductIndent 'Номенклатура' (ComponentLine l, TEXT path) = 
    STRING(CONCAT '', repeat('    ', wordCount(path, '/') - 1), nameItem(l));

// cost
cost 'Стоимость изделия' (Bom b, ComponentLine l, TEXT path) = 
    quantity(b, l, path) * cost(item(l));
cost 'Стоимость изделия' (Bom b) = cost(item(b)) * quantity(b);

// components cost
componentsCost 'Стоимость компонентов' (Bom b, TEXT path) = 
    GROUP SUM quantity(b, ComponentLine ll, TEXT child) * cost(item(ll)) IF 
                startsWith(child, path) AND 
                NOT nestedBom(ll);
componentsCost 'Стоимость компонентов' (Bom b) = 
    GROUP SUM componentsCost(b, TEXT path) IF level(b, ComponentLine l, path) AND NOT nestedBom(l);

FORM structureAndCost 'Структура и стоимость'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) READONLY number, cost, componentsCost
       
    OBJECTS (l = ComponentLine, path = TEXT)
    PROPERTIES READONLY nameProductIndent(l, path), descriptionNestedBom(l), 
                        quantity(b, l, path), cost(b, l, path), componentsCost(b, path)
    PROPERTIES path = VALUE(path) SHOWIF NULL
    FILTERS level(b, l, path)   
    ORDERS path
;

structureAndCost 'Структура и стоимость' (Bom b) {
    SHOW structureAndCost OBJECTS b = b;
}

EXTEND FORM bom
    PROPERTIES(b) structureAndCost DRAW b TOOLBAR 
;

DESIGN bom {
    actions {
        MOVE PROPERTY(structureAndCost(b));
    }
}

// print
FORM printStructureAndCost 'Структура и стоимость'
    OBJECTS b = Bom PANEL 
    PROPERTIES(b) READONLY nameItem, componentsCost
       
    OBJECTS (l = ComponentLine, path = TEXT)
    PROPERTIES nameProductIndent(l, path), nameUom(l), descriptionNestedBom(l), 
               quantity(b, l, path), cost(b, l, path), componentsCost(b, path)
    PROPERTIES path = VALUE(path)
    FILTERS level(b, l, path)   
    ORDERS path
;

print 'Печать' (Bom b) {
    PRINT printStructureAndCost OBJECTS b = b; 
}

EXTEND FORM structureAndCost
    PROPERTIES(b) print
;