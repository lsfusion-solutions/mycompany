MODULE CRPTLot;

REQUIRE Utils, Product, InventorySettings, CategoryLot;

NAMESPACE CRPT;

NAVIGATOR {
    stock {
        NEW FOLDER CRPT 'Маркировка';
    }
}

CLASS LotType 'Товарная группа' {
    lp 'Одежда',
    perfumery 'Парфюмерия'
}

id '{ID}' (LotType t) = getWord(staticName(t),'.',2) IF t IS LotType CHARWIDTH 20;
name '{Name}' (LotType t) = staticCaption(t) IF t IS LotType CHARWIDTH 20;

FORM dialogLotTypes 'Выбор товарной группы'
    OBJECTS t = LotType
    PROPERTIES(t) READONLY name
    
    LIST LotType OBJECT t
;

// sku group

dataLotType = DATA LotType (Category);
nearestLotType (Category child) = GROUP LAST dataLotType(Category parent) ORDER DESC level(child, parent) WHERE dataLotType(parent);

lotType (Category c) = OVERRIDE dataLotType(c), nearestLotType(c) MATERIALIZED;
nameLotType 'Товарная группа (маркировка)' (Category c) = name(lotType(c));

lotType (Product p) = lotType(category(p)) MATERIALIZED;
nameLotType 'Товарная группа (маркировка)' (Product p) = name(lotType(p));

EXTEND FORM category
    PROPERTIES(c) nameLotType
;
DESIGN category {
    lot {
        MOVE PROPERTY(nameLotType(c));
    }
}

// barcode read

tail = DATA STRING[100] (Lot);

code (Lot l) = CONCAT '', id(l), tail(l);

barcodeToId (STRING[100] s) = STRING[100](left(s, 31));
barcodeToTail (STRING[100] s) = STRING[100](substrFrom(s, 32)); 

idToGtin (STRING[100] s) = substr(s, 3, 14);

translateBarcode = FORMULA STRING 'SELECT CASE WHEN ($1) ~* \'[а-яА-Я]\' THEN translate(left(($1), 31), \'ЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭ/ЯЧСМИТЬБЮ,йцукенгшщзхъфывапролджэячсмитьбю.\', \'QWERTYUIOP{}ASDFGHJKL:"|ZXCVBNM<>?qwertyuiop[]asdfghjkl;\'\'zxcvbnm,./\') ELSE left(($1), 31) END';

barCodeToLotId(STRING s) += WHEN length(s AS STRING) > 31 THEN translateBarcode(s);