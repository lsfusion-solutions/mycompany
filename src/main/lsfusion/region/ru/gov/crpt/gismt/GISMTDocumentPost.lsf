MODULE GISMTDocumentPost;

REQUIRE GISMTDocument;

NAMESPACE GISMT;

EXTEND CLASS DocumentStatus {
    posted 'Sent'
}
posted 'Sent' = DATA BOOLEAN (Document);

status (Document s) += WHEN posted(s) THEN DocumentStatus.posted;
color (DocumentStatus s) += WHEN s = DocumentStatus.posted THEN RGB(252,247,149);

request 'Запрос' = DATA TEXT (Document);

docCreateUrl (Document o) = url() + '/lk/documents/create?pg=' + idLotType(o);

stopCreate = DATA LOCAL BOOLEAN ();
export ABSTRACT LIST (Document, Source);

send (Document d) {
    export(d, source(d));
    IF stopCreate() THEN RETURN;
    IF NOT exportFile() THEN {
        MESSAGE 'Выбран неправильный тип документа ГИС МТ';
        RETURN;
    } 
    
    fileToString(exportFile());
    request(d) <- resultString();
    
    sign(exportFile(), TRUE, company(d));
    
    LOCAL sendRequest = FILE();
    EXPORT JSON FROM document_format = 'MANUAL', 
                     product_document = encode(RAWFILE(exportFile()), 'base64'),
                     type = idType(d),
                     signature = encodedSignature()
                TO sendRequest;
    
    getToken(company(d));
    LOCAL id = STRING();
    TRY {
        EXTERNAL HTTP POST docCreateUrl(d) HEADERS documentHeaders PARAMS sendRequest() TO id;
    } CATCH {
        MESSAGE 'Ошибка при оправке документа в СУЗ : \n' + (OVERRIDE messageCaughtException(), '') + '\n' + (OVERRIDE id(), '');
        RETURN;
    }
    
    id(d) <- STRING[100](id());
    posted(d) <- TRUE;
}

post 'Отправить в ГИС МТ' (Document d) {
    APPLY; 
    IF canceled() THEN RETURN;

    NEWSESSION {
        send(d);
        IF id(d) THEN
            APPLY;
    }
}

EXTEND FORM document
    PROPERTIES(d) post SHOWIF status(d) = DocumentStatus.draft, posted
    
    PROPERTIES(d) READONLY request 
;

DESIGN document {
    primaryActions {
        MOVE PROPERTY(post(d)) { valueClass = 'btn-primary'; }
    }                
    status {
        MOVE PROPERTY(posted(d));                               
    }
    
    OBJECTS {
        MOVE PROPERTY(request(d)) { fill = 1; panelCaptionVertical = TRUE; }
    }
}

EXTEND FORM documents    
    EXTEND FILTERGROUP status
        FILTER 'Sent' status(d) = DocumentStatus.posted    
;
