MODULE GISMTDocumentDone;

REQUIRE GISMTDocumentPost, GISMTDocumentCanceled;

NAMESPACE GISMT;

EXTEND CLASS DocumentStatus {
    done 'Done'
}
done 'Done' = DATA BOOLEAN (Document);

status(Document s) += WHEN done(s) THEN DocumentStatus.done;

gismtStatus 'Status' = DATA STRING (Document) CHARWIDTH 15;
gismtDownloadDesc 'Description' = DATA TEXT (Document) CHARWIDTH 50;

FORM getDocumentStatus
    OBJECTS results = INTEGER
    PROPERTIES(results) number = number, status = status, downloadDesc = downloadDesc 
;

checkStatus 'Проверить статус' (Document d) {
    APPLY; 
    IF canceled() THEN RETURN;

    NEWSESSION {
        getToken(company(d));
        
        LOCAL result = FILE();
        TRY {
            EXTERNAL HTTP GET url() + '/facade/doc/listV2?number=' + id(d) HEADERS documentHeaders TO result;
        } CATCH {
            fileToString(result());
            MESSAGE 'Ошибка при оправке документа в СУЗ : \n' + (OVERRIDE messageCaughtException(), '') + '\n' + (OVERRIDE resultString(), '');
            RETURN;
        }
        
        IMPORT getDocumentStatus JSON FROM result();
        gismtStatus(d) <- status(0);
        gismtDownloadDesc(d) <- downloadDesc(0);
        IF status(0) = 'CHECKED_OK' OR status(0) = 'ACCEPTED' THEN
            done(d) <- TRUE;
        ELSE IF status(0) != 'WAIT_ACCEPTANCE' THEN
            canceled(d) <- TRUE;
        
        APPLY;
    }
}

EXTEND FORM document
    PROPERTIES(d) checkStatus SHOWIF status(d) = DocumentStatus.posted, done

    PROPERTIES(d) READONLY gismtStatus, gismtDownloadDesc
;

DESIGN document {
    primaryActions {
        MOVE PROPERTY(checkStatus(d)) { valueClass = 'btn-primary'; }
    }                
    status {
        MOVE PROPERTY(done(d));                               
    }
    
    OBJECTS {
        NEW gismt {
            fill = 0.3;
            caption = 'Ответ ГИС МТ';
            alignment = STRETCH;
            MOVE PROPERTY(gismtStatus(d)) { alignment = STRETCH; };
            MOVE PROPERTY(gismtDownloadDesc(d)) { fill = 1; panelCaptionVertical = TRUE; }
        }
    }
}

EXTEND FORM documents
    PROPERTIES(d) READONLY gismtStatus
;

EXTEND FORM documents    
    EXTEND FILTERGROUP status
        FILTER 'Done' status(d) = DocumentStatus.done    
;
