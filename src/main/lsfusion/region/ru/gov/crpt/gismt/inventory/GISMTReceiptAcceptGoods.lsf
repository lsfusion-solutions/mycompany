MODULE GISMTReceiptAcceptGoods;

REQUIRE GISMTReceipt, GISMTDocumentIncoming;

NAMESPACE GISMT;

// release number
releaseNumber 'Документ на отгрузку товара' = DATA STRING[100] (Receipt, LotType);
releaseNumber 'Документ на отгрузку товара' (Receipt r) = GROUP CONCAT releaseNumber(r, LotType t) + '(' + name(t) + ')', ',' ORDER t CHARWIDTH 30;

FORM changeReleaseNumber 'Документ на отгрузку товара'
    OBJECTS r = Receipt
    
    OBJECTS t = LotType
    PROPERTIES name(t) READONLY, releaseNumber(r, t)
;
changeReleaseNumber (Receipt r) {
    NEWSESSION {
        DIALOG changeReleaseNumber OBJECTS r = r DO {
            APPLY;
        }
    }
}

EXTEND FORM receipt
    PROPERTIES(r) releaseNumber ON CHANGE changeReleaseNumber(r)
;
DESIGN receipt {
    gismt {
        MOVE PROPERTY(releaseNumber(r)) FIRST;
    }
}

// import
importLots 'Загрузить КМ' (Receipt r) {
    DIALOG dialogLotTypes OBJECTS t INPUT DO {
        getIncomingDocuments(company(r), vendor(r), t, 'WAIT_ACCEPTANCE');
        IF documentIndex() THEN {
            releaseNumber(r, t) <- number(documentIndex());

            getIncomingDocument(company(r), releaseNumber(r, t));
            FOR [GROUP SUM 1 BY product(INTEGER j)](Product p) AND NOT lastReceiptLine(r, p) NEW l = ReceiptLine DO {
                receipt(l) <- r;
                product(l) <- p;
            }
            
            FOR receipt(ReceiptLine l) = r AND product(INTEGER j) = product(l) AND NOT lot(uit_code(j)) ORDER l, j DO NEW lot = Lot {
                id(lot) <- uit_code(j);
                product(lot) <- product(j);
            }
    
            FOR receipt(ReceiptLine l) = r AND product(INTEGER j) = product(l) AND Lot lot = lot(uit_code(j)) ORDER l, j DO {
                done(l, lot) <- 1;
            }
            
            done(ReceiptLine l) <- doneLot(l) WHERE receipt(l) = r AND (GROUP SUM 1 IF product(l) = product(INTEGER j));
        }
    }
}

EXTEND FORM receipt
    PROPERTIES(r) importLots
;

DESIGN receipt {
    secondaryActions {
        MOVE PROPERTY(importLots(r)) { valueClass = 'btn-secondary'; }
    }
}

// export
FORM exportReceiptAcceptGoods
    OBJECTS d = Document, r = Receipt PANEL
    
    PROPERTIES acceptance_date = replace(toChar(executionDateTime(r), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''), 
               document_number = number(r),
               document_date = replace(toChar(executionDateTime(r), 'yyyy-MM-dd T HH24:MI:SS.MI0Z'),' ',''),
               request_type = 'ACCEPTANCE', 
               turnover_type = 'SELLING',
               trade_sender_inn = inn(vendor(r)),
               trade_recipient_inn = innCompany(d),
               release_order_number = releaseNumber(r, lotType(d))
    
    OBJECTS products = (l = ReceiptLine, lot = Lot)
    PROPERTIES accepted = TRUE IF done(l, lot), uit_code = id(lot), product_description = nameProduct(l)
    FILTERS receipt(l) = r, done(l, lot), lotType(product(l)) = lotType(d)
;

export (Document d, Receipt r) + {
    IF type(d) = DocumentType.LP_ACCEPT_GOODS THEN
        EXPORT exportReceiptAcceptGoods OBJECTS d = d, r = r; 
}