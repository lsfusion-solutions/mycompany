MODULE SUZLotOrder;

REQUIRE CRPTLot, ProductGtin, Company, Doc;

NAMESPACE SUZ;

CLASS LotOrder 'Заказ КМ';

@defineDocStatus(lotOrder, 'заказа КМ');

id '{ID}' = DATA STRING[100] (LotOrder) CHARWIDTH 36;

type 'Type' = DATA LotType (LotOrder) NONULL;
nameType 'Type' (LotOrder r) = name(type(r));
idType (LotOrder r) = id(type(r));

company 'Company' = DATA Company (LotOrder) NONULL;
nameCompany 'Company' (LotOrder o) = name(company(o));
WHEN LOCAL SET(LotOrder o IS LotOrder) AND NOT CHANGED(company(o)) DO company(o) <- defaultCompany(); 

note 'Note' = DATA ISTRING (LotOrder) CHARWIDTH 30;

// line
CLASS LotOrderLine 'Строка заказа КМ';

lotOrder 'Заказ КМ' = DATA LotOrder (LotOrderLine) NONULL DELETE;

index '№' = PARTITION SUM 1 ORDER LotOrderLine l BY lotOrder(l) IN id MATERIALIZED CHARWIDTH 3;

product 'Product' = DATA Product (LotOrderLine);
nameProduct 'Product' (LotOrderLine l) = name(product(l));
idBarCodeProduct 'Barcode' (LotOrderLine l) = idBarCode(product(l));
idProduct '{ID}' (LotOrderLine l) = id(product(l));
gtinProduct 'GTIN' (LotOrderLine l) = gtin(product(l));

uom (LotOrderLine l) = uom(product(l));
nameUom 'UoM' (LotOrderLine l) = name(uom(l));

quantity 'Qty' = DATA NUMERIC[16,3] (LotOrderLine);

quantityLotOrderLine (LotOrder o) = GROUP SUM quantity(LotOrderLine d) IF lotOrder(d) = o; 
quantity (LotOrder o, Product p) = GROUP SUM quantity(LotOrderLine d) IF lotOrder(d) = o AND p = product(d); 

FORM lotOrder 'Заказ КМ'
    OBJECTS o = LotOrder PANEL 
    PROPERTIES(o) id, nameType, nameCompany, note
    
    OBJECTS l = LotOrderLine
    PROPERTIES(l) index, nameProduct, nameUom, idBarCodeProduct, idProduct, gtinProduct, quantity, NEW, DELETE
    FILTERS lotOrder(l) = o
    
    EDIT LotOrder OBJECT o
;

DESIGN lotOrder {
    caption = badged('Заказ КМ', id(o));
    OBJECTS {
        PROPERTY(nameType(o)) { notNull = TRUE; }
        NEW details {
            fill = 1;
            tabbed = TRUE;
            NEW lines {
                caption = 'Lines';
                type = SPLITH;
                MOVE BOX(l) { caption = ''; }
                NEW linesTab {
                    fill = 0.3;
                    tabbed = TRUE;
                }    
            }
        }
    }
}

FORM lotOrders 'Заказы КМ'
    OBJECTS o = LotOrder 
    PROPERTIES(o) READONLY id, nameType, nameCompany, note
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    LIST LotOrder OBJECT o
;

@defineDocStatusForm(lotOrder, o);
@defineDocLinesCount(lotOrder, o);

NAVIGATOR {
    CRPT {
        NEW lotOrders;
    }
}

// copy
clone ABSTRACT LIST (LotOrder, LotOrder);
clone (LotOrder to, LotOrder from) + {
    type(to) <- type(from);
    company(to) <- company(from);
    FOR lotOrder(LotOrderLine l) = from INLINE NEW nl = LotOrderLine DO {
        lotOrder(nl) <- to;
        product(nl) <- product(l);
        quantity(nl) <- quantity(l);
    }
}

copy 'Copy' (LotOrder from) {
    NEWSESSION {
        NEW to = LotOrder {
            clone(to, from);
            SHOW lotOrder OBJECTS o = to DOCKED;
        }
    }
}

EXTEND FORM lotOrder
    PROPERTIES(o) copy
;

DESIGN lotOrder {
    secondaryActions {
        MOVE PROPERTY(copy(o));
    }
}