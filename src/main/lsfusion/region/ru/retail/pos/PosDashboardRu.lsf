MODULE PosDashboardRu;

REQUIRE PosDashboardPayment, CashRegisterFiscal, ItemMarkRu;

NAMESPACE Retail;

fiscalNumber 'Номер чека' = DATA INTEGER (Invoice);
EXTEND FORM posDashboard PROPERTIES(si) READONLY fiscalNumber AFTER fiscal(si);

emailOrPhone 'Отправить чек на' = DATA STRING (Invoice);

EXTEND FORM posPayment
    PROPERTIES(i) emailOrPhone ON CHANGE {
        INPUT s = OVERRIDE emailOrPhone(i), email(customer(i)), phone(customer(i)) DO
            emailOrPhone(i) <- s;
    }
;

DESIGN posPayment {
    top {
        MOVE PROPERTY(emailOrPhone(i)) { fontSize = 24; alignment = STRETCH; }
    }
}

// марки
dataMarkMandatory 'Обязательная КМ' = DATA BOOLEAN (Category);

markMandatory (Category c) =
    GROUP LAST dataMarkMandatory(Category parent) ORDER DESC level(c, parent) MATERIALIZED;

EXTEND FORM category PROPERTIES(c) dataMarkMandatory;
DESIGN category { pos { MOVE PROPERTY(dataMarkMandatory(c)); } }

mark 'Контрольная марка' = DATA STRING (InvoiceLine);

WHEN LOCAL FORMS posDashboard SETCHANGED(item(InvoiceLine l)) AND markMandatory(category(item(l))) AND NOT mark(l) DO {
    MESSAGE 'По выбранной позиции обязательно сканирование контрольной марки';
    DELETE l;
}

EXTEND FORM posDashboard
    PROPERTIES(l) READONLY SHOWIF mark(l) mark PANEL
;

DESIGN posDashboard {
    info {
        MOVE PROPERTY(mark(l)) FIRST { alignment = STRETCH; }
    }
}

// scan mark
invalidMark = DATA LOCAL BOOLEAN ();
checkMarkValidity ABSTRACT LIST (Pos, Item, STRING);

afterScanMark ABSTRACT (InvoiceLine, STRING);

processBarCode (Invoice i, InvoiceLine l, STRING b) + {
    IF length(b) > 20 THEN
        FOR Item it = itemSingleBarCode(ltrim(substr(b, 3, 14), '0')) DO {
            invalidMark() <- NULL;
            checkMarkValidity(pos(session(i)), it, translateBarcode(b));
            IF NOT invalidMark() THEN
                NEW nl = InvoiceLine {
                    invoice(nl) <- i;
                    item(nl) <- it;
                    quantity(nl) <- 1;
                    mark(nl) <- translateBarcode(b);
                    SEEK posDashboard.l = nl;
                    
                    afterScanMark(nl, translateBarcode(b));
                }
            consumedBarCode() <- TRUE;
        }
}