MODULE Mango;

REQUIRE System, SQLUtils, Utils;

NAMESPACE Mango;

// REQUIRE MangoPBX, MangoEventsCall, MangoEmployeeCall, MangoRecord
// Notify : https://<url to mycompany>/exec/mango
// Notice : must be melody on recieving calls in order to have time to route
// require pgcrypto extension for the database :

onStarted() +{
    runSQL('CREATE EXTENSION IF NOT EXISTS pgcrypto;');
}

customUrl 'Url' = DATA STRING ();
urlVPBX () = (OVERRIDE customUrl(), 'https://app.mango-office.ru') + '/vpbx/';

apiKey 'Key' = DATA STRING () CHARWIDTH 40;
apiSecret 'Secret' = DATA STRING () CHARWIDTH 40;

EXTEND FORM integrationData
    PROPERTIES() mangoCustomUrl = customUrl, mangoApiKey = apiKey, mangoApiSecret = apiSecret
;

DESIGN integrationData {
    pane {
        NEW mango {
            caption = 'Mango';
            NEW mangoHeader {
                lines = 3;
                MOVE PROPERTY(mangoCustomUrl);
                MOVE PROPERTY(mangoApiKey);
                MOVE PROPERTY(mangoApiSecret);
            }
            NEW mangoPane {
                fill = 1;
                tabbed = TRUE;
            }
        }
    }
}

apiSign (STRING key, STRING json, STRING secret) =
    FORMULA STRING 'encode(digest(($1)||($2)||($3),\'sha256\'),\'hex\')';

apiSign (JSON json) = apiSign(apiKey(), STRING(json), apiSecret());

httpHeaders(TEXT name) = CASE
    WHEN name = 'Content-Type' THEN 'multipart/form-data; boundary=&';

apiResponse = DATA FILE ();

apiGet (STRING method, JSON json) {
    IF apiKey() THEN {
        logToFile('mango', CONCAT ' : ', r'Get', method, STRING(json));
        EXTERNAL HTTP GET urlVPBX() + method
            BODYPARAMNAMES r'vpbx_api_key', r'sign', r'json'
            HEADERS httpHeaders
            PARAMS apiKey(), apiSign(json), STRING(json) TO apiResponse;
    }
}

apiPost (STRING method, JSON json) {
    IF apiKey() THEN {
        logToFile('mango', CONCAT ' : ', r'Post', method, STRING(json));
        EXTERNAL HTTP POST urlVPBX() + method
            BODYPARAMNAMES r'vpbx_api_key', r'sign', r'json'
            HEADERS httpHeaders
            PARAMS apiKey(), apiSign(json), STRING(json) TO apiResponse;
    }
}

extension = DATA LOCAL STRING();
call_id = DATA LOCAL STRING();
entry_id = DATA LOCAL STRING();
seq = DATA LOCAL STRING();
timestamp = DATA LOCAL STRING();
