MODULE KSeFFakturaFA3;

REQUIRE KSeFFaktura;

NAMESPACE KSeF;

// XML groups for export
GROUP Naglowek;
GROUP Podmiot1;
GROUP Podmiot2;
GROUP Fa;
GROUP KodFormularza : Naglowek;
GROUP Adnotacje : Fa;
GROUP Zwolnienie : Adnotacje;
GROUP NoweSrodkiTransportu : Adnotacje;
GROUP PMarzy : Adnotacje;
GROUP DaneFaKorygowanej : Fa;

// Nested groups
GROUP P1DaneId EXTID 'DaneIdentyfikacyjne' : Podmiot1;
GROUP P1Adres EXTID 'Adres' : Podmiot1;
GROUP P1Kontakt EXTID 'DaneKontaktowe' : Podmiot1;

GROUP P2DaneId EXTID 'DaneIdentyfikacyjne' : Podmiot2;
GROUP P2Adres EXTID 'Adres' : Podmiot2;
GROUP P2Kontakt EXTID 'DaneKontaktowe' : Podmiot2;

// export
FORM FA3XmlExport
    OBJECTS f = Faktura PANEL

    FORMEXTID '=http://crd.gov.pl/wzor/2025/06/25/13775/:Faktura'
    PROPERTIES ATTR = 'http://www.w3.org/2001/XMLSchema' EXTID 'xmlns:xsd'
    PROPERTIES ATTR = 'http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:xsi'
    PROPERTIES ATTR = 'http://crd.gov.pl/xml/schematy/dziedzinowe/mf/2022/01/05/eD/DefinicjeTypy/' EXTID 'xmlns:etd'

    // Naglowek
    PROPERTIES IN KodFormularza value = KodFormularza(f), = 'FA (3)' IF KodFormularza(f) EXTID 'kodSystemowy' ATTR, = '1-0E' IF KodFormularza(f) EXTID 'wersjaSchemy' ATTR
    
    PROPERTIES IN Naglowek WariantFormularza(f), DataWytworzeniaFa = toChar(DataWytworzeniaFa(f), 'yyyy-MM-ddThh24:mi:ss.msZ'), SystemInfo(f)

    // Podmiot1
    PROPERTIES(f) IN P1DaneId p1_NIP EXTID 'NIP', p1_Nazwa EXTID 'Nazwa'
    PROPERTIES(f) IN P1Adres p1_KodKraju EXTID 'KodKraju', p1_AdresL1 EXTID 'AdresL1', p1_AdresL2 EXTID 'AdresL2'
    PROPERTIES(f) IN P1Kontakt p1_Email EXTID 'Email', p1_Telefon EXTID 'Telefon'

    // Podmiot2
    PROPERTIES(f) IN P2DaneId p2_NIP EXTID 'NIP', p2_Nazwa EXTID 'Nazwa'
    PROPERTIES(f) IN P2Adres p2_KodKraju EXTID 'KodKraju', p2_AdresL1 EXTID 'AdresL1', p2_AdresL2 EXTID 'AdresL2'
    PROPERTIES(f) IN P2Kontakt p2_Email EXTID 'Email', p2_Telefon EXTID 'Telefon'
    PROPERTIES    IN Podmiot2 p2_NrKlienta(f) EXTID 'NrKlienta', = 2 EXTID 'JST', = 2 EXTID 'GV' 

    // Fa
    PROPERTIES(f) IN Fa KodWaluty, invoiceDate EXTID 'P_1', invoiceNumber EXTID 'P_2', deliveryDate EXTID 'P_6'
    PROPERTIES(f) IN Fa untaxedAmount1 EXTID 'P_13_1', taxAmount1 EXTID 'P_14_1'
    PROPERTIES(f) IN Fa untaxedAmount2 EXTID 'P_13_2', taxAmount2 EXTID 'P_14_2'
    PROPERTIES(f) IN Fa untaxedAmount3 EXTID 'P_13_3', taxAmount3 EXTID 'P_14_3'
    PROPERTIES(f) IN Fa untaxedAmount6 EXTID 'P_13_6', untaxedAmount7 EXTID 'P_13_7'
    PROPERTIES(f) IN Fa amount EXTID 'P_15'
    PROPERTIES(f) IN Fa KursWalutyZ

    PROPERTIES(f) IN Adnotacje metodaKasowa EXTID 'P_16', samofakturowanie EXTID 'P_17', odwrotneObciazenie EXTID 'P_18', mpp EXTID 'P_18A'
    
    PROPERTIES    IN Zwolnienie P_19N = 1
    PROPERTIES    IN NoweSrodkiTransportu P_22N = 1
    PROPERTIES(f) IN Adnotacje weUproszczona EXTID 'P_23'

    PROPERTIES    IN PMarzy P_PMarzyN = 1
    
    PROPERTIES(f) IN Fa RodzajFaktury

    PROPERTIES(f) IN Fa PrzyczynaKorekty
    PROPERTIES IN DaneFaKorygowanej = toChar(DataWystFaKorygowanej(f), 'yyyy-MM-dd') EXTID 'DataWystFaKorygowanej',
                  NrFaKorygowanej(f), NrKSeF = 1 IF NrFaKorygowanej(f), NrKSeFFaKorygowanej(f)

    // zaliczka
    OBJECTS z = FakturaZaliczkowa EXTID 'FakturaZaliczkowa' IN Fa
    PROPERTIES(z) number EXTID 'NrKSeFFaZaliczkowej'
    FILTERS faktura(z) = f

    // FaWiersz
    OBJECTS w = FaWiersz EXTID 'FaWiersz' IN Fa
    PROPERTIES(w) NrWierszaFa, description EXTID 'P_7', GTIN, nameUom EXTID 'P_8A', quantity EXTID 'P_8B', price EXTID 'P_9A', untaxedAmount EXTID 'P_11', taxes EXTID 'P_12'
    FILTERS faktura(w) = f
;

// import
KodFormularza = DATA LOCAL STRING();
WariantFormularza = DATA LOCAL INTEGER();
DataWytworzeniaFa = DATA LOCAL STRING();
SystemInfo = DATA LOCAL STRING();

p1_NIP = DATA LOCAL STRING();
p1_Nazwa = DATA LOCAL STRING();
p1_KodKraju = DATA LOCAL STRING();
p1_AdresL1 = DATA LOCAL STRING();
p1_AdresL2 = DATA LOCAL STRING();
p1_Email = DATA LOCAL STRING();
p1_Telefon = DATA LOCAL STRING();

p2_NIP = DATA LOCAL STRING();
p2_Nazwa = DATA LOCAL STRING();
p2_KodKraju = DATA LOCAL STRING();
p2_AdresL1 = DATA LOCAL STRING();
p2_AdresL2 = DATA LOCAL STRING();
p2_Email = DATA LOCAL STRING();
p2_Telefon = DATA LOCAL STRING();
NrKlienta = DATA LOCAL STRING();

KodWaluty = DATA LOCAL STRING();
P_1 = DATA LOCAL DATE();
P_2 = DATA LOCAL STRING();
P_6 = DATA LOCAL DATE();
P_13_1 = DATA LOCAL NUMERIC[14,2]();
P_13_2 = DATA LOCAL NUMERIC[14,2]();
P_13_3 = DATA LOCAL NUMERIC[14,2]();
P_13_6 = DATA LOCAL NUMERIC[14,2]();
P_13_7 = DATA LOCAL NUMERIC[14,2]();
P_14_1 = DATA LOCAL NUMERIC[14,2]();
P_14_2 = DATA LOCAL NUMERIC[14,2]();
P_14_3 = DATA LOCAL NUMERIC[14,2]();
P_15 = DATA LOCAL NUMERIC[14,2]();
P_16 = DATA LOCAL INTEGER();
P_17 = DATA LOCAL INTEGER();
P_18 = DATA LOCAL INTEGER();
P_18A = DATA LOCAL INTEGER();
P_23 = DATA LOCAL INTEGER();

KursWalutyZ = DATA NUMERIC[16,6] ();
RodzajFaktury = DATA LOCAL STRING();

PrzyczynaKorekty = DATA STRING ();
DataWystFaKorygowanej = DATA STRING ();
NrFaKorygowanej = DATA STRING[50] ();
NrKSeFFaKorygowanej = DATA STRING[36] ();

NrKSeFFaZaliczkowej = DATA LOCAL STRING[36](INTEGER);

NrWierszaFa = DATA LOCAL STRING(INTEGER);
UU_ID = DATA LOCAL STRING(INTEGER);
P_7 = DATA LOCAL STRING(INTEGER);
GTIN = DATA LOCAL STRING(INTEGER);
P_8A = DATA LOCAL STRING(INTEGER);
P_8B = DATA LOCAL NUMERIC[16,3](INTEGER);
P_9A = DATA LOCAL NUMERIC[10,2](INTEGER);
P_11 = DATA LOCAL NUMERIC[14,2](INTEGER);
P_12 = DATA LOCAL STRING(INTEGER);

FORM FA3XmlImport FORMEXTID '=http://crd.gov.pl/wzor/2025/06/25/13775/:Faktura'
    PROPERTIES ATTR ='http://crd.gov.pl/xml/schematy/dziedzinowe/mf/2022/01/05/eD/DefinicjeTypy/' EXTID 'xmlns:etd', ='http://www.w3.org/2001/XMLSchema' EXTID 'xmlns:xsd', ='http://www.w3.org/2001/XMLSchema-instance' EXTID 'xmlns:xsi'
    PROPERTIES() IN Naglowek KodFormularza, WariantFormularza, DataWytworzeniaFa, SystemInfo
    PROPERTIES() IN P1DaneId p1_NIP EXTID ':NIP', p1_Nazwa EXTID ':Nazwa'
    PROPERTIES() IN P1Adres p1_KodKraju EXTID ':KodKraju', p1_AdresL1 EXTID ':AdresL1', p1_AdresL2 EXTID ':AdresL2'
    PROPERTIES() IN P1Kontakt p1_Email EXTID ':Email', p1_Telefon EXTID ':Telefon'
    PROPERTIES() IN Podmiot2 NrKlienta
    PROPERTIES() IN P2DaneId p2_NIP EXTID ':NIP', p2_Nazwa EXTID ':Nazwa'
    PROPERTIES() IN P2Adres p2_KodKraju EXTID ':KodKraju', p2_AdresL1 EXTID ':AdresL1', p2_AdresL2 EXTID ':AdresL2'
    PROPERTIES() IN P2Kontakt p2_Email EXTID ':Email', p2_Telefon EXTID ':Telefon'
    PROPERTIES() IN Fa KodWaluty, P_1, P_2, P_6, P_13_1, P_13_2, P_13_3, P_13_6, P_13_7, P_14_1, P_14_2, P_14_3, P_15
    PROPERTIES() IN Adnotacje P_16, P_17, P_18, P_18A, P_23
    PROPERTIES() IN Fa KursWalutyZ, RodzajFaktury
    PROPERTIES() IN Fa PrzyczynaKorekty
    PROPERTIES() IN DaneFaKorygowanej DataWystFaKorygowanej, NrFaKorygowanej, NrKSeFFaKorygowanej

    OBJECTS z = INTEGER EXTID ':FakturaZaliczkowa' IN Fa
    PROPERTIES(z) NrKSeFFaZaliczkowej

    OBJECTS faWiersz = INTEGER EXTID ':FaWiersz' IN Fa
    PROPERTIES(faWiersz) NrWierszaFa, UU_ID, P_7, GTIN, P_8A, P_8B, P_9A, P_11, P_12
    FILTERS imported(faWiersz)
;

import (Faktura nf) {
    podmiot1(nf) <- legalEntityNip(p1_NIP());

    // Header
    KodFormularza(nf) <- KodFormularza();
    WariantFormularza(nf) <- WariantFormularza();
    DataWytworzeniaFa(nf) <- DATETIME(DataWytworzeniaFa());
    SystemInfo(nf) <- SystemInfo();
    // Parties
    podmiot1(nf) <- partnerNip(p1_NIP());
    p1_NIP(nf) <- p1_NIP(); p1_Nazwa(nf) <- p1_Nazwa(); p1_KodKraju(nf) <- p1_KodKraju(); 
    p1_AdresL1(nf) <- p1_AdresL1(); p1_AdresL2(nf) <- p1_AdresL2(); 
    p1_Email(nf) <- p1_Email(); p1_Telefon(nf) <- p1_Telefon();

    podmiot2(nf) <- partnerNip(p2_NIP());
    p2_NIP(nf) <- p2_NIP(); p2_Nazwa(nf) <- p2_Nazwa(); p2_KodKraju(nf) <- p2_KodKraju(); 
    p2_AdresL1(nf) <- p2_AdresL1(); p2_AdresL2(nf) <- p2_AdresL2(); 
    p2_Email(nf) <- p2_Email(); p2_Telefon(nf) <- p2_Telefon();
    p2_NrKlienta(nf) <- NrKlienta();
    // Fa
    KodWaluty(nf) <- KodWaluty(); invoiceDate(nf) <- P_1(); invoiceNumber(nf) <- P_2(); deliveryDate(nf) <- P_6();
    untaxedAmount1(nf) <- P_13_1(); taxAmount1(nf) <- P_14_1();
    untaxedAmount2(nf) <- P_13_2(); taxAmount2(nf) <- P_14_2();
    untaxedAmount3(nf) <- P_13_3(); taxAmount3(nf) <- P_14_3();
    untaxedAmount6(nf) <- P_13_6(); untaxedAmount7(nf) <- P_13_7();
    amount(nf) <- P_15();
    metodaKasowa(nf) <- P_16(); samofakturowanie(nf) <- P_17(); odwrotneObciazenie(nf) <- P_18(); mpp(nf) <- P_18A(); weUproszczona(nf) <- P_23();
    KursWalutyZ(nf) <- KursWalutyZ(); RodzajFaktury(nf) <- RodzajFaktury();
    PrzyczynaKorekty(nf) <- PrzyczynaKorekty(); DataWystFaKorygowanej(nf) <- DATE(DataWystFaKorygowanej());
    NrFaKorygowanej(nf) <- NrFaKorygowanej(); NrKSeFFaKorygowanej(nf) <- NrKSeFFaKorygowanej();

    FOR NrKSeFFaZaliczkowej(INTEGER fz) DO NEW z = FakturaZaliczkowa {
        faktura(z) <- nf;
        number(z) <- NrKSeFFaZaliczkowej(fz);
    }

    FOR imported(INTEGER faWiersz) DO NEW fw = FaWiersz {
        faktura(fw) <- nf;
        NrWierszaFa(fw) <- INTEGER(NrWierszaFa(faWiersz));
        GTIN(fw) <- GTIN(faWiersz);
        description(fw) <- P_7(faWiersz);
        nameUom(fw) <- P_8A(faWiersz);
        quantity(fw) <- P_8B(faWiersz);
        price(fw) <- P_9A(faWiersz);
        untaxedAmount(fw) <- P_11(faWiersz);
        taxes(fw) <- P_12(faWiersz);
    }
}